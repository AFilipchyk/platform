MODULE Hierarchy;

REQUIRE BaseLogicsModule; // todo [dale]: нужно только для того, чтобы BaseLogicsModule создавался раньше всех, надо пофиксать в будущем

META defineHierarchy (object)
    parent###object = DATA object (object);
    @defineHierarchyCustom(object);
END

META defineHierarchyAbstract (object)
    parent###object = ABSTRACT object (object);
    @defineHierarchyCustom(object);
END

META defineHierarchyCustom (object)
    TABLE object(object);
    TABLE object###object (object, object);

    nameParent###object 'Родительский объект' = name(parent###object(object));

    level###object###object 'Уровень' (child, parent) = RECURSION 1l AND child IS object AND parent == child
                                                                  STEP 2l AND parent == parent###object($parent) PERSISTENT;

    isParent###object###object 'Является потомком' (child, parent) = TRUE AND level###object###object(child, parent);

    childNumber###object 'Кол-во непосредственных потомков' (object) = GROUP SUM 1 BY parent###object(child) PERSISTENT;

    isLeaf###object 'Лист' (object) = object IS object AND NOT childNumber###object(object) PERSISTENT;

    isParentLeaf###object###object (child, parent) = isParent###object###object(child, parent) AND isLeaf###object(child);

    canonicalName###object 'Каноническое имя' (object) = castToString255(
                           [GROUP CONCAT name(parent), ' / ' BY child ORDER DESC level###object###object(child, parent)](object))
                           MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 PERSISTENT;
END