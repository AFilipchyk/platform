MODULE Profiling;

REQUIRE Service, Utils;

toString = FORMULA STRING[100] 'CAST($1 AS character(100))';
toBoolean(s) = IF s == 'true' THEN TRUE;

changeProfilingSettingsInteger(ReflectionProperty r, INTEGER value) = {
   baseValue(r) <- toString(value);
}
changeProfilingSettingsBoolean(ReflectionProperty r, BOOLEAN value) = {
    baseValue(r) <- IF value THEN 'true' ELSE 'false';
}

explainNoAnalyze 'Explain No Analyze' = DATA BOOLEAN ();
WHEN CHANGED(explainNoAnalyze()) DO {
    changeProfilingSettingsBoolean(reflectionProperty('explainNoAnalyze'), explainNoAnalyze());
}
explainJavaStack 'Explain Java Stack' = DATA BOOLEAN ();
WHEN CHANGED(explainJavaStack()) DO {
    changeProfilingSettingsBoolean(reflectionProperty('explainJavaStack'), explainJavaStack());
}
explainCompile 'Explain Compile' = DATA BOOLEAN ();
WHEN CHANGED(explainCompile()) DO {
    changeProfilingSettingsBoolean(reflectionProperty('explainCompile'), explainCompile());
}
explainThreshold 'Explain Threshold' = DATA INTEGER ();
WHEN CHANGED(explainThreshold()) DO {
    changeProfilingSettingsInteger(reflectionProperty('explainThreshold'), explainThreshold());
}

FORM profiler 'Профилировщик'
    EVENTS ON INIT { 
        NEWSESSION {
        explainNoAnalyze() <- toBoolean(value(reflectionProperty('explainNoAnalyze'), NULL));
        explainJavaStack() <- toBoolean(value(reflectionProperty('explainJavaStack'), NULL));
        explainCompile() <- toBoolean(value(reflectionProperty('explainCompile'), NULL));
        explainThreshold() <- toInteger(value(reflectionProperty('explainThreshold'), NULL));
        apply();
        }
    }
    OBJECTS cu = CustomUser
    PROPERTIES(cu) PANEL explainAnalyzeMode
    FILTERS cu == currentUser()
    PROPERTIES() explainNoAnalyze, explainJavaStack, explainCompile, explainThreshold
;

DESIGN profiler {
    NEW tab {
        type = TABBED;
        fill = 1;
        NEW sql {
            caption = 'SQL';
            NEW cubox {
                caption = 'Текущий пользователь';
                MOVE PROPERTY(explainAnalyzeMode(cu));
            }
            NEW settingsBox {
                caption = 'Настройки';
                MOVE PROPERTY(explainNoAnalyze());
                MOVE PROPERTY(explainJavaStack());
                MOVE PROPERTY(explainCompile());
                MOVE PROPERTY(explainThreshold());
             }
        }
    }
    MOVE TOOLBARBOX;
}

NAVIGATOR {
    systemEvents {
        ADD profiler;
    }
}