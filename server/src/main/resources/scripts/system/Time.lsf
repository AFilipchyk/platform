MODULE Time;

REQUIRE System, Utils;

extractHour = FORMULA INTEGER '(extract(hour from ($1)))'; 
extractDay = FORMULA INTEGER '(extract(day from ($1)))';
extractWeek = FORMULA INTEGER '(extract(week from ($1)))';
extractYear = FORMULA INTEGER '(extract(year from ($1)))';

currentDateTime = NATIVE DATETIME ();
currentMinute = NATIVE INTEGER ();
currentHour = NATIVE INTEGER ();
currentEpoch = NATIVE LONG ();

currentDate 'Тек. дата' = DATA DATE ();
dateDiffersCurrent(date) = date IS DATE AND date != currentDate();

currentYear 'Тек. год' = extractYear (currentDate());

toDate(dateTime) = [= FORMULA DATE '(CAST(($1) as date))'](dateTime AS DATETIME);
toTime(dateTime) = [= FORMULA TIME '(CAST(($1) as time))'](dateTime AS DATETIME);
toDateTime(date) = [= FORMULA DATETIME '(CAST(($1) as timestamp))'](date AS DATE);

currentTime 'Тек. время' = toTime(currentDateTime());

sumDate(date, days) = [= FORMULA DATE '(($1)+($2))'](date AS DATE, days AS LONG);
sumDateYear = FORMULA DATE '(CAST(($1) AS date) + interval \'1 year\')';

subtractDate(date, days) = [= FORMULA DATE '(($1)-($2))'](date AS DATE, days AS LONG);

sumTimeSeconds(time, seconds) = [= FORMULA TIME '(($1)+($2)*CAST(\'1 seconds\' AS INTERVAL))'](time AS TIME, seconds AS LONG);
sumTimeMinutes(time, minutes) = [= FORMULA TIME '(($1)+($2)*CAST(\'1 minutes\' AS INTERVAL))'](time AS TIME, minutes AS LONG);

sumDateTimeDay(dateTime, days) = [= FORMULA DATETIME '(($1)+($2)*CAST(\'1 days\' AS INTERVAL))'](dateTime AS DATETIME, days AS LONG);
sumDateTimeMinutes(dateTime, minutes) = [= FORMULA DATETIME '(($1)+($2)*CAST(\'1 minutes\' AS INTERVAL))'](dateTime AS DATETIME, minutes AS LONG);
sumDateTimeSeconds(dateTime, seconds) = [= FORMULA DATETIME '(($1)+($2)*CAST(\'1 seconds\' AS INTERVAL))'](dateTime AS DATETIME, seconds AS LONG);

subtractDateTimeSeconds(dateTime, seconds) = [= FORMULA DATETIME '(($1)-($2)*CAST(\'1 seconds\' AS INTERVAL))'](dateTime AS DATETIME, seconds AS LONG);

dateTimeToDateTime = FORMULA DATETIME 'to_timestamp(CAST($1 as char(10)) || CAST($2 as char(12)), \'YYYY-MM-DDHH24:MI:SS.MS\')';

daysInclBetweenDates (date1, date2) = subtractIntegerIncl (date2, date1) IF date1 IS DATE AND date2 IS DATE;
weeksInclBetweenDates(date1, date2) = divideInteger(daysInclBetweenDates(date1, date2), 7);
weeksNullInclBetweenDates (date1, date2) = weeksInclBetweenDates (date1, date2) != 0;

sumDateWeekFromFormula = FORMULA DATE '(($1)+($2)*7)';
sumDateWeekFrom (date, int) = sumDateWeekFromFormula(date, int) IF date IS DATE AND int IS INTEGER;
sumDateWeekToFormula = FORMULA DATE '(($1)+($2)*7+6)';
sumDateWeekTo (date, int) = sumDateWeekToFormula(date, int) IF date IS DATE AND int IS INTEGER;

// --------------------------------- Месяца ------------------------ //

CLASS Month 'Месяц' {
    january 'Январь',
    february 'Февраль',
    march 'Март',
    april 'Апрель',
    may 'Май',
    june 'Июнь',
    july 'Июль',
    august 'Август',
    september 'Сентябрь',
    october 'Октябрь',
    november 'Ноябрь',
    december 'Декабрь'
}

FORM months
    OBJECTS m = Month
    PROPERTIES(m) staticCaption
    DIALOG Month OBJECT m
;

TABLE month (Month);

numberMonth 'Номер месяца' = PARTITION SUM 1 IF m IS Month ORDER m PERSISTENT;
monthNumber 'Месяц (ИД)' (month) = GROUP AGGR month BY numberMonth(month);

extractMonthNumber = FORMULA INTEGER '(extract(month from ($1)))';
extractMonth 'Месяц (ИД)' (date) = monthNumber(extractMonthNumber(date));
extractMonthName 'Месяц' (date) = staticCaption(extractMonth(date));

currentMonth 'Тек. месяц' = extractMonthNumber (currentDate());

// --------------------------------- Дни недели ------------------------ //

CLASS DOW 'День недели' {
    sunday 'Воскресенье',
    monday 'Понедельник',
    tuesday 'Вторник',
    wednesday 'Среда',
    thursday 'Четверг',
    friday 'Пятница',
    saturday 'Суббота'
}
FORM DOWs
    OBJECTS d = DOW
    PROPERTIES(d) staticCaption
    DIALOG DOW OBJECT d
;

TABLE dow (DOW);

numberDOW 'Номер дня недели' = (PARTITION SUM 1 IF d IS DOW ORDER d) - 1 PERSISTENT;
DOWNumber 'День недели (ИД)' (dow) = GROUP AGGR dow BY numberDOW(dow);

extractDOWNumber = FORMULA INTEGER '(extract(dow from ($1)))';
extractDOW 'День недели (ИД)' (date) = DOWNumber(extractDOWNumber(date));
extractDOWName 'День недели' (date) = staticCaption(extractDOW(date));