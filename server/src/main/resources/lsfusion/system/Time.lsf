MODULE Time;

REQUIRE System;

extractHour = FORMULA INTEGER PG '(extract(hour from ($1)))' MS 'datepart(hh, $1)'; 
extractDay = FORMULA INTEGER PG '(extract(day from ($1)))' MS 'datepart(dd, $1)';
extractWeek = FORMULA INTEGER PG '(extract(week from ($1)))' MS 'datepart(isoww, $1)';
extractYear = FORMULA INTEGER PG '(extract(year from ($1)))' MS 'datepart(yy, $1)';

currentDateTime = NATIVE DATETIME ();
currentMinute = NATIVE INTEGER ();
currentHour = NATIVE INTEGER ();
currentEpoch = NATIVE LONG ();

currentDate 'Тек. дата' = DATA DATE ();
dateDiffersCurrent(date) = date IS DATE AND date != currentDate();

currentYear 'Тек. год' = extractYear (currentDate());

toDate(dateTime) = DATE(dateTime AS DATETIME);
toTime(dateTime) = TIME(dateTime AS DATETIME);
toDateTime(date) = DATETIME(date AS DATE);

toDateDDMMYY = FORMULA VARSTRING[8] 'to_char(($1),\'DD.MM.YY\')';
toDateDDMMYYYY = FORMULA VARSTRING[10] 'to_char(($1),\'DD.MM.YYYY\')';

currentTime 'Тек. время' = toTime(currentDateTime());

sumDate(date, days) = [= FORMULA DATE PG '(($1)+($2))' MS 'DATEADD(dd, $2, $1)'](date AS DATE, days AS LONG);
sumDateMonth (date, months) = FORMULA DATE PG '(CAST(($1) AS date) + ($2)*(interval \'1 month\'))' MS 'DATEADD(mm, $2, $1)';
sumDateYear (date, months) = FORMULA DATE PG '(CAST(($1) AS date) + ($2)*(interval \'1 year\'))' MS 'DATEADD(yy, $2, $1)';

subtractDate(date, days) = [= FORMULA DATE PG '(($1)-($2))' MS 'DATEADD(dd, -($2), $1)'](date AS DATE, days AS LONG);

sumTimeSeconds(time, secs) = [= FORMULA TIME PG '(($1)+($2)*CAST(\'1 seconds\' AS INTERVAL))' MS 'DATEADD(ss, $2, $1)'](time AS TIME, secs AS LONG);
sumTimeMinutes(time, mins) = [= FORMULA TIME PG '(($1)+($2)*CAST(\'1 minutes\' AS INTERVAL))' MS 'DATEADD(mm, $2, $1)'](time AS TIME, mins AS LONG);

sumDateTimeDay(dateTime, days) = [= FORMULA DATETIME PG '(($1)+($2)*CAST(\'1 days\' AS INTERVAL))' MS 'DATEADD(dd, $2, $1)'](dateTime AS DATETIME, days AS LONG);
sumDateTimeMinutes(dateTime, mins) = [= FORMULA DATETIME PG '(($1)+($2)*CAST(\'1 minutes\' AS INTERVAL))' MS 'DATEADD(mm, $2, $1)'](dateTime AS DATETIME, mins AS LONG);
sumDateTimeSeconds(dateTime, secs) = [= FORMULA DATETIME PG '(($1)+($2)*CAST(\'1 seconds\' AS INTERVAL))' MS 'DATEADD(ss, $2, $1)'](dateTime AS DATETIME, secs AS LONG);

subtractDateTimeSeconds(dateTime, secs) = [= FORMULA DATETIME PG '(($1)-($2)*CAST(\'1 seconds\' AS INTERVAL))' MS 'DATEADD(ss, -($2), $1)'](dateTime AS DATETIME, secs AS LONG);

subtractDateTimeDay(dateTime, days) = [= FORMULA DATETIME PG '(($1)-($2)*CAST(\'1 days\' AS INTERVAL))' MS 'DATEADD(dd, -($2), $1)'](dateTime AS DATETIME, days AS LONG);

dateTimeToDateTime = FORMULA DATETIME PG 'to_timestamp(CAST($1 as char(10)) || CAST($2 as char(12)), \'YYYY-MM-DDHH24:MI:SS.MS\')'
                                      MS 'CAST($1 AS DATETIME) + CAST($2 AS DATETIME)';

daysInclBetweenDates (date1, date2) = subtractInteger(date2, date1) + 1;

sumDateWeekFrom (date, int) = sumDate(date, int * 7) IF date IS DATE AND int IS INTEGER;
sumDateWeekTo (date, int) = sumDate(date, int * 7 + 6) IF date IS DATE AND int IS INTEGER;

// --------------------------------- Месяца ------------------------ //

CLASS Month 'Месяц' {
    january 'Январь',
    february 'Февраль',
    march 'Март',
    april 'Апрель',
    may 'Май',
    june 'Июнь',
    july 'Июль',
    august 'Август',
    september 'Сентябрь',
    october 'Октябрь',
    november 'Ноябрь',
    december 'Декабрь'
}

FORM months
    OBJECTS m = Month
    PROPERTIES(m) staticCaption
    DIALOG Month OBJECT m
;

TABLE month (Month);

numberMonth 'Номер месяца' = DATA INTEGER (Month);
onStarted() += ACTION (){
    numberMonth(d) <- 1 WHERE d == Month.january; 
    numberMonth(d) <- 2 WHERE d == Month.february; 
    numberMonth(d) <- 3 WHERE d == Month.march; 
    numberMonth(d) <- 4 WHERE d == Month.april; 
    numberMonth(d) <- 5 WHERE d == Month.may; 
    numberMonth(d) <- 6 WHERE d == Month.june; 
    numberMonth(d) <- 7 WHERE d == Month.july; 
    numberMonth(d) <- 8 WHERE d == Month.august; 
    numberMonth(d) <- 9 WHERE d == Month.september; 
    numberMonth(d) <- 10 WHERE d == Month.october; 
    numberMonth(d) <- 11 WHERE d == Month.november; 
    numberMonth(d) <- 12 WHERE d == Month.december; 
}

monthNumber 'Месяц (ИД)' (number) = GROUP AGGR month BY numberMonth(month);

extractMonthNumber = [= FORMULA INTEGER PG '(extract(month from ($1)))' MS 'DATEPART(mm, $1)'](date AS DATE);
extractMonth 'Месяц (ИД)' (date) = monthNumber(extractMonthNumber(date));
extractMonthName 'Месяц' (date) = staticCaption(extractMonth(date));

currentMonth 'Тек. месяц' = extractMonthNumber (currentDate());

// --------------------------------- Дни недели ------------------------ //

CLASS DOW 'День недели' {
    sunday 'Воскресенье',
    monday 'Понедельник',
    tuesday 'Вторник',
    wednesday 'Среда',
    thursday 'Четверг',
    friday 'Пятница',
    saturday 'Суббота'
}
TABLE dow (DOW);

numberDOW 'Номер дня недели' = DATA INTEGER (DOW);
onStarted() += ACTION (){
    numberDOW(d) <- 0 WHERE d == DOW.sunday; 
    numberDOW(d) <- 1 WHERE d == DOW.monday; 
    numberDOW(d) <- 2 WHERE d == DOW.tuesday; 
    numberDOW(d) <- 3 WHERE d == DOW.wednesday; 
    numberDOW(d) <- 4 WHERE d == DOW.thursday; 
    numberDOW(d) <- 5 WHERE d == DOW.friday; 
    numberDOW(d) <- 6 WHERE d == DOW.saturday; 
}

FORM DOWs 'Дни недели'
    OBJECTS d = DOW
    PROPERTIES(d) READONLY staticCaption, numberDOW
    ORDER BY numberDOW(d)
    DIALOG DOW OBJECT d
;

DOWNumber 'День недели (ИД)' (number) = GROUP AGGR dow BY numberDOW(dow);

extractDOWNumber = [= FORMULA INTEGER PG '(extract(dow from ($1)))' MS '(DATEPART(dw, $1)-1)'](date AS DATE);
extractDOW 'День недели (ИД)' (date) = DOWNumber(extractDOWNumber(date));
extractDOWName 'День недели' (date) = staticCaption(extractDOW(date));

distanceDOWDOW (d1, d2) = (numberDOW(d2) - numberDOW(d1)) (+) (7 IF numberDOW(d2) < numberDOW(d1));     