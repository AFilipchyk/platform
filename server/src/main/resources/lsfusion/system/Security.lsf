MODULE Security;

REQUIRE System, Reflection, Email;

CLASS UserRole 'Роль';
TABLE userRole (UserRole);
TABLE customUserRole (CustomUser, UserRole);
TABLE userRoleProperty (UserRole, Property);
TABLE userRolePropertyGroup (UserRole, PropertyGroup);
TABLE userProperty (User, Property);
TABLE userRoleNavigatorElement (UserRole, NavigatorElement);

nameUserRole 'Наименование' = DATA VARISTRING[100](UserRole);

sidUserRole 'Идентификатор' = DATA VARSTRING[30] (UserRole);
userRoleSID (sid) = GROUP AGGR userRole BY sidUserRole (userRole) WHERE userRole IS UserRole;

mainRoleUser = DATA UserRole (User);
mainRoleCustomUser (user) = mainRoleUser (user) IF user IS CustomUser;
sidMainRoleCustomUser 'Идентификатор главной роли' (user) = sidUserRole (mainRoleCustomUser(user));
nameMainRoleUser 'Главная роль' (user) = nameUserRole (mainRoleUser(user));

defaultMainRole 'Роль по умолчанию' = DATA UserRole ();
nameDefaultMainRole 'Роль по умолчанию' = nameUserRole(defaultMainRole());
EXTEND FORM customUsers
    PROPERTIES() nameDefaultMainRole
;
mainRoleUser(u) <- defaultMainRole() WHEN SET(u IS CustomUser);

inUserRole 'Вкл.' = DATA BOOLEAN (CustomUser, UserRole);

inMainRoleCustomUser(customUser, userRole) = mainRoleCustomUser(customUser) == userRole OR inUserRole(customUser, userRole);

permitViewAllPropertyUserRole 'Разрешить просмотр всех свойств' = DATA BOOLEAN (UserRole);
permitViewAllPropertyUser 'Разрешить просмотр всех свойств' (user) = permitViewAllPropertyUserRole (mainRoleUser(user));
forbidViewAllPropertyUserRole 'Запретить просмотр всех свойств' = DATA BOOLEAN (UserRole);
forbidViewAllPropertyUser 'Запретить просмотр всех свойств' (user) = forbidViewAllPropertyUserRole (mainRoleUser(user));

transactTimeoutUserRole 'Таймаут для транз. (сек.)' = DATA INTEGER (UserRole);
transactTimeoutUser 'Таймаут для транз. (сек.)' (user) = transactTimeoutUserRole (mainRoleUser(user));

permitChangeAllPropertyUserRole 'Разрешить изменение всех свойств' = DATA BOOLEAN (UserRole);
permitChangeAllPropertyUser 'Разрешить изменение всех свойств' (user) = permitChangeAllPropertyUserRole(mainRoleUser(user));
forbidChangeAllPropertyUserRole 'Запретить изменение всех свойств' = DATA BOOLEAN (UserRole);
forbidChangeAllPropertyRole 'Запретить изменение всех свойств' (user) = forbidChangeAllPropertyUserRole(mainRoleUser(user));

dataPermitViewPropertyGroup 'Разрешить просмотр группы свойств' = DATA BOOLEAN (PropertyGroup);

levelPermitViewParentPropertyGroup (propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataPermitViewPropertyGroup(parent)
          BY propertyGroup PERSISTENT;

levelPermitViewPropertyGroup = TRUE IF levelPermitViewParentPropertyGroup (propertyGroup);
permitViewPropertyGroup 'Разрешить просмотр группы свойств' (propertyGroup) = OVERRIDE levelPermitViewPropertyGroup (propertyGroup), dataPermitViewPropertyGroup (propertyGroup);

dataPermitViewProperty 'Разрешить просмотр свойства' = DATA BOOLEAN (Property);
permitViewProperty 'Разрешить просмотр свойства' (property) = OVERRIDE levelPermitViewPropertyGroup (parentProperty(property)), dataPermitViewProperty (property) PERSISTENT;

dataForbidViewPropertyGroup 'Запретить просмотр группы свойств' = DATA BOOLEAN (PropertyGroup);

levelForbidViewParentPropertyGroup (propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataForbidViewPropertyGroup(parent)
          BY propertyGroup PERSISTENT;

levelForbidViewPropertyGroup = TRUE IF levelForbidViewParentPropertyGroup (propertyGroup);
forbidViewPropertyGroup 'Запретить просмотр группы свойств' (propertyGroup) = OVERRIDE levelForbidViewPropertyGroup (propertyGroup), dataForbidViewPropertyGroup (propertyGroup);

dataForbidViewProperty 'Запретить просмотр свойства' = DATA BOOLEAN (Property);
forbidViewProperty 'Запретить просмотр свойства' (property) = OVERRIDE levelForbidViewPropertyGroup (parentProperty(property)), dataForbidViewProperty (property) PERSISTENT;

dataPermitChangePropertyGroup 'Разрешить изменение группы свойств' = DATA BOOLEAN (PropertyGroup);

levelPermitChangeParentPropertyGroup (propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataPermitChangePropertyGroup(parent)
          BY propertyGroup PERSISTENT;

levelPermitChangePropertyGroup = TRUE IF levelPermitChangeParentPropertyGroup (propertyGroup);
permitChangePropertyGroup 'Разрешить изменение группы свойств' (propertyGroup) = OVERRIDE levelPermitChangePropertyGroup (propertyGroup), dataPermitChangePropertyGroup (propertyGroup);

dataPermitChangeProperty 'Разрешить изменение свойства' = DATA BOOLEAN (Property);
permitChangeProperty 'Разрешить изменение свойства' (property) = OVERRIDE levelPermitChangePropertyGroup (parentProperty(property)), dataPermitChangeProperty (property) PERSISTENT;

dataForbidChangePropertyGroup 'Запретить изменение группы свойств' = DATA BOOLEAN (PropertyGroup);

levelForbidChangeParentPropertyGroup (propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataForbidChangePropertyGroup(parent)
          BY propertyGroup PERSISTENT;

levelForbidChangePropertyGroup = TRUE IF levelForbidChangeParentPropertyGroup (propertyGroup);
forbidChangePropertyGroup 'Запретить изменение группы свойств' (propertyGroup) = OVERRIDE levelForbidChangePropertyGroup (propertyGroup), dataForbidChangePropertyGroup (propertyGroup);

dataForbidChangeProperty 'Запретить изменение свойства' = DATA BOOLEAN (Property);
forbidChangeProperty 'Запретить изменение свойства' (property) = OVERRIDE levelForbidChangePropertyGroup (parentProperty(property)), dataForbidChangeProperty (property) PERSISTENT;

notNullPermissionProperty (property) = OVERRIDE permitViewProperty(property), forbidViewProperty(property), permitChangeProperty(property), forbidChangeProperty(property) PERSISTENT INDEXED;

dataPermitViewUserRolePropertyGroup 'Разрешить просмотр группы свойств' = DATA BOOLEAN (UserRole, PropertyGroup);

levelPermitViewParentUserRolePropertyGroup (userRole, propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataPermitViewUserRolePropertyGroup(userRole, parent)
          BY userRole, propertyGroup PERSISTENT;

levelPermitViewUserRolePropertyGroup = TRUE IF levelPermitViewParentUserRolePropertyGroup (userRole, propertyGroup);
permitViewUserRolePropertyGroup 'Разрешить просмотр группы свойств' (userRole, propertyGroup) = OVERRIDE levelPermitViewUserRolePropertyGroup (userRole, propertyGroup), dataPermitViewUserRolePropertyGroup (userRole, propertyGroup) PERSISTENT;

dataPermitViewUserRoleProperty 'Разрешить просмотр свойства' = DATA BOOLEAN (UserRole, Property);
permitViewUserRoleProperty 'Разрешить просмотр свойства' (userRole, property) = OVERRIDE levelPermitViewUserRolePropertyGroup (userRole, parentProperty(property)), dataPermitViewUserRoleProperty (userRole, property) PERSISTENT;
permitViewUserProperty 'Разрешить просмотр свойства' (user, property) = permitViewUserRoleProperty(mainRoleUser(user), property) PERSISTENT;

dataForbidViewUserRolePropertyGroup 'Запретить просмотр группы свойств' = DATA BOOLEAN (UserRole, PropertyGroup);

levelForbidViewParentUserRolePropertyGroup (userRole, propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataForbidViewUserRolePropertyGroup(userRole, parent)
          BY userRole, propertyGroup PERSISTENT;

levelForbidViewUserRolePropertyGroup = TRUE IF levelForbidViewParentUserRolePropertyGroup (userRole, propertyGroup);
forbidViewUserRolePropertyGroup 'Запретить просмотр группы свойств' (userRole, propertyGroup) = OVERRIDE levelForbidViewUserRolePropertyGroup (userRole, propertyGroup), dataForbidViewUserRolePropertyGroup (userRole, propertyGroup) PERSISTENT;

dataForbidViewUserRoleProperty 'Запретить просмотр свойства' = DATA BOOLEAN (UserRole, Property);
forbidViewUserRoleProperty 'Запретить просмотр свойства' (userRole, property) = OVERRIDE levelForbidViewUserRolePropertyGroup (userRole, parentProperty(property)), dataForbidViewUserRoleProperty (userRole, property) PERSISTENT;
forbidViewUserProperty 'Запретить просмотр свойства' (user, property) = forbidViewUserRoleProperty(mainRoleUser(user), property) PERSISTENT;

dataPermitChangeUserRolePropertyGroup 'Разрешить изменение группы свойств' = DATA BOOLEAN (UserRole, PropertyGroup);

levelPermitChangeParentUserRolePropertyGroup (userRole, propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataPermitChangeUserRolePropertyGroup(userRole, parent)
          BY userRole, propertyGroup PERSISTENT;

levelPermitChangeUserRolePropertyGroup = TRUE IF levelPermitChangeParentUserRolePropertyGroup (userRole, propertyGroup);
permitChangeUserRolePropertyGroup 'Разрешить изменение группы свойств' (userRole, propertyGroup) = OVERRIDE levelPermitChangeUserRolePropertyGroup (userRole, propertyGroup), dataPermitChangeUserRolePropertyGroup (userRole, propertyGroup) PERSISTENT;

dataPermitChangeUserRoleProperty 'Разрешить изменение свойства' = DATA BOOLEAN (UserRole, Property);
permitChangeUserRoleProperty 'Разрешить изменение свойства' (userRole, property) = OVERRIDE levelPermitChangeUserRolePropertyGroup (userRole, parentProperty(property)), dataPermitChangeUserRoleProperty (userRole, property) PERSISTENT;
permitChangeUserProperty 'Разрешить изменение свойства' (user, property) = permitChangeUserRoleProperty (mainRoleUser(user), property) PERSISTENT;

dataForbidChangeUserRolePropertyGroup 'Запретить изменение группы свойств' = DATA BOOLEAN (UserRole, PropertyGroup);

levelForbidChangeParentUserRolePropertyGroup (userRole, propertyGroup) =
    GROUP MIN levelPropertyGroupPropertyGroup(propertyGroup, parent) IF dataForbidChangeUserRolePropertyGroup(userRole, parent)
          BY userRole, propertyGroup PERSISTENT;

levelForbidChangeUserRolePropertyGroup = TRUE IF levelForbidChangeParentUserRolePropertyGroup (userRole, propertyGroup);
forbidChangeUserRolePropertyGroup 'Запретить изменение группы свойств' (userRole, propertyGroup) = OVERRIDE levelForbidChangeUserRolePropertyGroup (userRole, propertyGroup), dataForbidChangeUserRolePropertyGroup (userRole, propertyGroup) PERSISTENT;

dataForbidChangeUserRoleProperty 'Запретить изменение свойства' = DATA BOOLEAN (UserRole, Property);
forbidChangeUserRoleProperty 'Запретить изменение свойства' (userRole, property) = OVERRIDE levelForbidChangeUserRolePropertyGroup (userRole, parentProperty(property)), dataForbidChangeUserRoleProperty (userRole, property) PERSISTENT;
forbidChangeUserProperty 'Запретить изменение свойства' (user, property) = forbidChangeUserRoleProperty (mainRoleUser(user), property) PERSISTENT;

notNullPermissionUserProperty (user, property) = OVERRIDE permitViewUserProperty(user, property), forbidViewUserProperty(user, property), permitChangeUserProperty(user, property), forbidChangeUserProperty(user, property);

CLASS DefaultForms 'Отображение форм при запуске' {
    none 'Не восстанавливать формы',
    restore 'Отображать сохранённые формы',
    default 'Отображать формы по умолчанию'
}
FORM defaultForms 'Отображение форм при запуске'
    OBJECTS df = DefaultForms
    PROPERTIES(df) READONLY staticCaption
    DIALOG DefaultForms OBJECT df
;

defaultFormsUserRole 'Отображение форм при запуске' = DATA DefaultForms (UserRole);
nameDefaultFormsUserRole 'Отображение форм при запуске' (userRole) = staticCaption(defaultFormsUserRole(userRole));
defaultFormsUser 'Отображение форм при запуске' (user) = defaultFormsUserRole(mainRoleUser(user));

defaultNumberUserRoleNavigatorElement 'Номер по умолчанию' = DATA INTEGER (UserRole, NavigatorElement);
defaultNumberUserNavigatorElement 'Номер по умолчанию' (user, navigatorElement) = defaultNumberUserRoleNavigatorElement(mainRoleUser(user), navigatorElement);

permitNavigatorElement 'Разрешить форму' = DATA BOOLEAN (NavigatorElement);
forbidNavigatorElement 'Запретить форму' = DATA BOOLEAN (NavigatorElement);
permitExportNavigatorElement 'Разрешить экспорт формы' = DATA BOOLEAN (NavigatorElement);

permitAllFormsUserRole 'Разрешить отображение всех форм' = DATA BOOLEAN (UserRole);
permitAllFormsUser 'Разрешить отображение всех форм' (user) = permitAllFormsUserRole(mainRoleUser(user));
forbidAllFormsUserRole 'Запретить отображение всех форм' = DATA BOOLEAN (UserRole);
forbidAllFormsUser 'Запретить отображение всех форм' (user) = forbidAllFormsUserRole(mainRoleUser(user));

permitUserRoleNavigatorElement 'Разрешить форму' = DATA BOOLEAN (UserRole, NavigatorElement);
permitUserNavigatorElement 'Разрешить форму' = permitUserRoleNavigatorElement(mainRoleUser(user), navigatorElement);
forbidUserRoleNavigatorElement 'Запретить форму' = DATA BOOLEAN (UserRole, NavigatorElement);
forbidUserNavigatorElement 'Запретить форму' = forbidUserRoleNavigatorElement(mainRoleUser(user), navigatorElement);

CLASS Policy 'Политика безопасности';
TABLE policy (Policy);
TABLE userRolePolicy (UserRole, Policy);

namePolicy 'Наименование' = DATA VARISTRING[100](Policy);

policyName 'Политика' (name) = GROUP AGGR policy BY namePolicy(policy) WHERE policy IS Policy;
descriptionPolicy 'Описание' = DATA VARSTRING[100] (Policy);
orderUserRolePolicy 'Порядок политики' = DATA INTEGER (UserRole, Policy);
orderUserPolicy 'Порядок политики' (user, policy) = orderUserRolePolicy (mainRoleUser(user), policy);

EXTEND FORM customUser
    PROPERTIES(u) nameMainRoleUser BEFORE emailContact(u)

    OBJECTS ur=UserRole
    PROPERTIES(ur) READONLY nameUserRole, sidUserRole
    PROPERTIES(u,ur) inUserRole
;

EXTEND DESIGN customUser {
    container {
        ADD ur.box {
            flex = 1.0;
        }
    }
}

EXTEND FORM customUsers
    PROPERTIES(u) READONLY nameMainRoleUser BEFORE nameContact(u)
;

FORM userRole 'Роль'
    OBJECTS ur = UserRole FIXED PANEL
    PROPERTIES(ur) nameUserRole, sidUserRole
    EDIT UserRole OBJECT ur
;

// ---------------------------------------- Копирование роли ----------------------- //
overCopyUserRole = ABSTRACT ACTION LIST (UserRole, UserRole);

copyDataUserRole = ACTION (userRole, ur) {
    ASSIGN nameUserRole(ur) <- nameUserRole(userRole);
    ASSIGN permitViewAllPropertyUserRole(ur) <- permitViewAllPropertyUserRole(userRole);
    ASSIGN forbidViewAllPropertyUserRole(ur) <- forbidViewAllPropertyUserRole(userRole);
    ASSIGN permitChangeAllPropertyUserRole(ur) <- permitChangeAllPropertyUserRole(userRole);
    ASSIGN forbidChangeAllPropertyUserRole(ur) <- forbidChangeAllPropertyUserRole(userRole);
    ASSIGN defaultFormsUserRole(ur) <- defaultFormsUserRole(userRole);
    ASSIGN permitAllFormsUserRole(ur) <- permitAllFormsUserRole(userRole);
    ASSIGN forbidAllFormsUserRole(ur) <- forbidAllFormsUserRole(userRole);

    ASSIGN dataPermitViewUserRoleProperty(ur, property) <- dataPermitViewUserRoleProperty(userRole, property);
    ASSIGN dataForbidViewUserRoleProperty(ur, property) <- dataForbidViewUserRoleProperty(userRole, property);
    ASSIGN dataPermitChangeUserRoleProperty(ur, property) <- dataPermitChangeUserRoleProperty(userRole, property);
    ASSIGN dataForbidChangeUserRoleProperty(ur, property) <- dataForbidChangeUserRoleProperty(userRole, property);
    
    ASSIGN dataPermitViewUserRolePropertyGroup(ur, propertyGroup) <- dataPermitViewUserRolePropertyGroup(userRole, propertyGroup);
    ASSIGN dataForbidViewUserRolePropertyGroup(ur, propertyGroup) <- dataForbidViewUserRolePropertyGroup(userRole, propertyGroup);
    ASSIGN dataPermitChangeUserRolePropertyGroup(ur, propertyGroup) <- dataPermitChangeUserRolePropertyGroup(userRole, propertyGroup);
    ASSIGN dataForbidChangeUserRolePropertyGroup(ur, propertyGroup) <- dataForbidChangeUserRolePropertyGroup(userRole, propertyGroup);
    
    ASSIGN defaultNumberUserRoleNavigatorElement(ur, navigatorElement) <- defaultNumberUserRoleNavigatorElement(userRole, navigatorElement);
    ASSIGN permitUserRoleNavigatorElement(ur, navigatorElement) <- permitUserRoleNavigatorElement(userRole, navigatorElement);
    ASSIGN forbidUserRoleNavigatorElement(ur, navigatorElement) <- forbidUserRoleNavigatorElement(userRole, navigatorElement);
    
    ASSIGN orderUserRolePolicy(ur, userPolicy) <- orderUserRolePolicy(userRole, userPolicy);
            
    EXEC overCopyUserRole(userRole, ur);
}

copyUserRole 'Копировать' = ACTION (userRole) NEWSESSION {
    FOR ADDOBJ ur = UserRole DO {
        EXEC copyDataUserRole(userRole, ur);
        FORM userRole OBJECTS ur = ur MANAGESESSION DOCKEDMODAL;
    }
} TOOLBAR;

FORM securityPolicy 'Политика безопасности'

    OBJECTS ur=UserRole
    PROPERTIES(ur) READONLY nameUserRole, sidUserRole
    PROPERTIES(ur) transactTimeoutUserRole, permitViewAllPropertyUserRole, forbidViewAllPropertyUserRole, permitChangeAllPropertyUserRole,
                   forbidChangeAllPropertyUserRole, nameDefaultFormsUserRole, permitAllFormsUserRole, forbidAllFormsUserRole

    OBJECTS p=Policy
    PROPERTIES(p)  namePolicy, descriptionPolicy

    OBJECTS f=NavigatorElement
    PROPERTIES(f) READONLY captionNavigatorElement, canonicalNameNavigatorElement
    PROPERTIES(f) numberNavigatorElement
    PROPERTIES(ur, f) permitUserRoleNavigatorElement, forbidUserRoleNavigatorElement, defaultNumberUserRoleNavigatorElement

    OBJECTS pr=Property
    PROPERTIES(pr) captionProperty, canonicalNameProperty
    PROPERTIES(ur, pr) permitViewUserRoleProperty, forbidViewUserRoleProperty, permitChangeUserRoleProperty, forbidChangeUserRoleProperty
    ORDER BY canonicalNameProperty(pr)

    OBJECTS df=NavigatorElement
    PROPERTIES(df) READONLY captionNavigatorElement, canonicalNameNavigatorElement
    PROPERTIES(df) numberNavigatorElement, permitNavigatorElement, forbidNavigatorElement, permitExportNavigatorElement
    ORDER BY canonicalNameProperty(pr)

    OBJECTS dpr=Property
    PROPERTIES(dpr) captionProperty, canonicalNameProperty, permitViewProperty, forbidViewProperty, permitChangeProperty, forbidChangeProperty

    TREE treeFormObject tf = NavigatorElement PARENT parentNavigatorElement
    PROPERTIES(tf) READONLY captionNavigatorElement, canonicalNameNavigatorElement
    PROPERTIES(tf) numberNavigatorElement
    PROPERTIES(ur, tf) permitUserRoleNavigatorElement, forbidUserRoleNavigatorElement, defaultNumberUserRoleNavigatorElement
    ORDER BY numberNavigatorElement(tf)

    TREE treePropertyObject tprs = PropertyGroup PARENT parentPropertyGroup, prs = Property
    PROPERTIES(tprs) captionPropertyGroup, SIDPropertyGroup, numberPropertyGroup
    ORDER BY numberPropertyGroup(tprs)
    PROPERTIES(prs) captionProperty, canonicalNameProperty, numberProperty
    PROPERTIES(ur, prs) permitViewUserRoleProperty, forbidViewUserRoleProperty, permitChangeUserRoleProperty, forbidChangeUserRoleProperty
    PROPERTIES(ur, tprs) permitViewUserRolePropertyGroup, forbidViewUserRolePropertyGroup, permitChangeUserRolePropertyGroup, forbidChangeUserRolePropertyGroup
    ORDER BY numberProperty(prs)

    TREE treeDefaultForm tdf = NavigatorElement PARENT parentNavigatorElement
    PROPERTIES(tdf) READONLY captionNavigatorElement, canonicalNameNavigatorElement
    PROPERTIES(tdf) numberNavigatorElement, permitNavigatorElement, forbidNavigatorElement, permitExportNavigatorElement
    ORDER BY numberNavigatorElement(tdf)

    TREE treeDefaultProperty tdprs = PropertyGroup PARENT parentPropertyGroup, dprs = Property
    PROPERTIES (tdprs) captionPropertyGroup, SIDPropertyGroup, numberPropertyGroup, permitViewPropertyGroup, forbidViewPropertyGroup, permitChangePropertyGroup, forbidChangePropertyGroup
    ORDER BY numberPropertyGroup(tdprs)

    PROPERTIES (dprs)  captionProperty, canonicalNameProperty,      numberProperty,      permitViewProperty,          forbidViewProperty,          permitChangeProperty,          forbidChangeProperty
    ORDER BY numberProperty(dprs)

    PROPERTIES(ur, p) orderUserRolePolicy COLUMNS (ur) HEADER sidUserRole(ur)

    FILTERS parentProperty(prs)==tprs, parentProperty(dprs)==tdprs
;

EXTEND DESIGN securityPolicy {
    NEW container {
        fill = 1;
        type = TABBED;

        NEW defaultPolicy {
            type = TABBED;
            caption = 'Политика по умолчанию';

            NEW defaultForms {
                type = TABBED;
                caption = 'Формы';
                ADD treeDefaultForm.tree.box;
                ADD df.box {
                    caption = 'Таблица';
                }
            }

            NEW defaultPropertyContainer {
                type = TABBED;
                caption = 'Свойства';
                ADD treeDefaultProperty.tree.box;
                ADD dpr.box {
                    caption = 'Таблица';
                };
            }
        }

        NEW roles {
            caption = 'Роли';
            type = SPLITV;
            ADD ur.box;

            NEW rolePolicyContainer {
                fill = 2;
                type = TABBED;

                NEW formsContainer {
                    type = TABBED;
                    caption = 'Формы';
                    ADD treeFormObject.tree.box;
                    ADD f.box {
                        caption = 'Таблица';
                    }
                }

                NEW propertiesContainer {
                    type = TABBED;
                    caption = 'Свойства';
                    ADD treePropertyObject.tree.box;
                    ADD pr.box {
                        caption = 'Таблица';
                    }
                }
            }
        }

        ADD p.box {
            caption = 'Дополнительные политики';
        }
    }
    
    ADD functions.box;
}

FORM userRoles 'Роли'
    OBJECTS ur=UserRole
    PROPERTIES(ur) READONLY nameUserRole, sidUserRole
    PROPERTIES(ur) copyUserRole FORCE PANEL
    PROPERTIES(ur) ADDFORM, EDITFORM, DELETE
    DIALOG UserRole OBJECT ur
;

FORM propertyPolicy 'Политика безопасности для свойства'
    OBJECTS p=Property FIXED PANEL, ur=UserRole
    PROPERTIES(p) READONLY captionProperty, canonicalNameProperty
    PROPERTIES(ur) READONLY nameUserRole, sidUserRole
    PROPERTIES(p) permitViewProperty, forbidViewProperty, permitChangeProperty, forbidChangeProperty, userLoggableProperty, isSetNotNullProperty
    PROPERTIES(ur, p) permitViewUserRoleProperty, forbidViewUserRoleProperty, permitChangeUserRoleProperty, forbidChangeUserRoleProperty
;

EXTEND DESIGN propertyPolicy {
    NEW property {
        type = CONTAINERH;
        caption = 'Свойства';
        ADD PROPERTY(captionProperty(p));
        ADD PROPERTY(canonicalNameProperty(p));
    }
    NEW permission {
        type = CONTAINERH;
        caption = 'Доступ';
        ADD PROPERTY(permitViewProperty(p));
        ADD PROPERTY(forbidViewProperty(p));
        ADD PROPERTY(permitChangeProperty(p));
        ADD PROPERTY(forbidChangeProperty(p));
        ADD PROPERTY(userLoggableProperty(p));
        ADD PROPERTY(isSetNotNullProperty(p));
    }
    ADD ur.box;
    ADD functions.box;
}

NAVIGATOR {
    security {
        ADD Security.userRoles;
        ADD Security.securityPolicy;
    }
}
