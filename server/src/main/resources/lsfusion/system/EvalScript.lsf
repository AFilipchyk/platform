MODULE EvalScript;

REQUIRE System, Time;

NAMESPACE System;

scriptStorage 'Скрипт' () = DATA TEXT ();

scriptExecution 'Выполнить скрипт' () = ACTION EVAL scriptStorage();

actionBodyExecution 'Выполнить действие' () = ACTION EVAL CONCAT ' ', 'run() = ACTION {', scriptStorage(), ';\n};';
formExecution 'Показать форму' () = ACTION EVAL CONCAT ' ', 'FORM form', scriptStorage(), ';\nrun() = ACTION FORM form;';

CLASS Script 'Скрипт';
TABLE script(Script);

nameScript 'Наименование' = DATA VARSTRING[100] (Script);
textScript 'Текст' = DATA TEXT (Script);
countTextScript() = GROUP MAX script IF textScript(script) == scriptStorage(); 
dateTimeScript 'Дата запуска' = DATA DATETIME (Script);

updateScriptStorage(script) = ACTION {
    scriptStorage() <- textScript(script);
}

FORM chooseScriptName 'Сохранить как'
    OBJECTS name = STRING[100] FIXED PANEL
    PROPERTIES valueName = OBJVALUE(name)
;

EXTEND DESIGN chooseScriptName {
    main{
        caption = 'Сохранить как';
        PROPERTY(valueName){
            caption = 'Наименование';
            font = 'Tahoma 24';
        }
    REMOVE leftControls;
    REMOVE PROPERTY(formRefresh());
    }
}

saveAsScriptAction 'Сохранить как' = ACTION () {
    FORM chooseScriptName MODAL;
    IF formResult() == FormResult.ok THEN {
        FOR ADDOBJ s = Script DO {
        nameScript(s) <- chosenString('name');
        textScript(s) <- scriptStorage();
        dateTimeScript(s) <- currentDateTime();
        }
    }
}

saveScriptAction 'Сохранить' = ACTION (script) {
    textScript(script) <- scriptStorage();
    dateTimeScript(script) <- currentDateTime();
}

// Универсальная таблица для последующего импорта
CLASS Row 'Ряд';
TABLE row(Row);

string1Row 'Строка 1' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string2Row 'Строка 2' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string3Row 'Строка 3' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string4Row 'Строка 4' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string5Row 'Строка 5' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string6Row 'Строка 6' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string7Row 'Строка 7' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string8Row 'Строка 8' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string9Row 'Строка 9' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;
string10Row 'Строка 10' = DATA VARSTRING[1000] (Row) MINCHARWIDTH 10 PREFCHARWIDTH 20;

numeric1Row 'Число 1' = DATA NUMERIC[20,7] (Row);
numeric2Row 'Число 2' = DATA NUMERIC[20,7] (Row);
numeric3Row 'Число 3' = DATA NUMERIC[20,7] (Row);
numeric4Row 'Число 4' = DATA NUMERIC[20,7] (Row);
numeric5Row 'Число 5' = DATA NUMERIC[20,7] (Row);

date1Row 'Дата 1' = DATA DATE (Row);
date2Row 'Дата 2' = DATA DATE (Row);
date3Row 'Дата 3' = DATA DATE (Row);

FORM script 'Скрипт'
    OBJECTS s = Script FIXED PANEL
    PROPERTIES(s) nameScript SELECTOR EVENTS ON CHANGE s updateScriptStorage(s) 
    PROPERTIES() scriptStorage,
                 scriptExecution, actionBodyExecution, formExecution, saveAsScriptAction
    PROPERTIES(s) saveScriptAction
                 
    OBJECTS r = Row FIXED GRID
    PROPERTIES(r) string1Row, string2Row, string3Row, string4Row, string5Row, string6Row, string7Row, string8Row, string9Row, string10Row 
    PROPERTIES(r) numeric1Row, numeric2Row, numeric3Row, numeric4Row, numeric5Row, date1Row, date2Row, date3Row
    PROPERTIES(r) ADDOBJ, DELETESESSION
;

DESIGN script FROM DEFAULT {
    NEW pane {
        fill = 1;
        type = TABBED;
        NEW scriptPane {
            fill = 1;        
            caption = 'Скрипт';
            NEW script {
                caption = 'Сохранённые скрипты';
                type = CONTAINERH;
                ADD PROPERTY(nameScript(s)) { font = 'bold 24'; alignment = STRETCH; }
                ADD PROPERTY(saveScriptAction(s)) { font = 'bold 24'; alignment = STRETCH; }
                ADD PROPERTY(saveAsScriptAction()) { font = 'bold 24'; alignment = STRETCH; }
            }
            ADD PROPERTY(scriptStorage()) {
                panelLabelAbove = TRUE;    
                fill = 1;
            }
            NEW run {
                caption = 'Запуск';
                type = CONTAINERH;
                ADD PROPERTY(scriptExecution()) { font = 'bold 24'; }
                ADD PROPERTY(actionBodyExecution()) { font = 'bold 24'; }
                ADD PROPERTY(formExecution()) { font = 'bold 24'; }
            }
        }
        ADD r.box {
            caption = 'Данные';
        }
    }
    ADD functions.box;
}

FORM scriptLog 'Скрипт'
    OBJECTS s = Script FIXED PANEL
    PROPERTIES(s) nameScript, textScript, dateTimeScript

    EDIT Script OBJECT s
;

FORM scriptsLog 'Скрипты'
    
    OBJECTS s=Script
    PROPERTIES(s) nameScript, textScript READONLY, dateTimeScript READONLY 
    PROPERTIES(s) DELETE FORCE PANEL TOOLBAR
    ORDER BY dateTimeScript(s) DESC

    FILTERGROUP user FILTER 'Только пользовательские' 'F9' nameScript(s)

    DIALOG Script OBJECT s
;

NAVIGATOR {
    configuration {
        ADD script;
    }
}

