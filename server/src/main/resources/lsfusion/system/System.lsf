MODULE System;

NAMESPACE System;

CLASS FormResult 'Результат вызова формы' {
    drop 'Сбросить',
    ok 'Принять',
    close 'Закрыть'
}

CLASS NATIVE Object;
CLASS NATIVE CustomObjectClass;
objectClass = NATIVE CustomObjectClass (Object);
random = NATIVE DOUBLE ();

statCustomObjectClass 'Статистика' = DATA INTEGER (CustomObjectClass);

GROUP root 'Корневая группа';
GROUP public 'Пользовательские свойства' : root;
GROUP private 'Внутренние свойства' : root;

GROUP base 'Атрибуты' : public;
GROUP recognize 'Идентифицирующие свойства' : base;
GROUP drillDown 'Действия детализации' : root;
GROUP propertyPolicy 'Свойства политики безопасности' : root;

canceled 'Canceled' = DATA SESSION BOOLEAN ();

apply = ACTION APPLY {};
cancel = ACTION CUSTOM 'lsfusion.server.logics.property.actions.flow.CancelActionProperty';
onStarted = ABSTRACT ACTION LIST ();

upper = FORMULA 'upper($1)';

subtractInteger(dateFrom, dateTo) = [= FORMULA INTEGER PG '(($1)-($2))' MS 'DATEDIFF(dd, $2, $1)'](dateFrom AS DATE, dateTo AS DATE);

formApply 'Сохранить' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.FormApplyActionProperty';
formCancel 'Отменить' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.FormCancelActionProperty';
formPrint 'Печать' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.PrintActionProperty';
formEdit 'Редактировать' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.EditActionProperty';
formXls 'Экспорт в xls' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.XlsActionProperty';
formDrop 'Сбросить' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.DropActionProperty';
formRefresh 'Обновить' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.RefreshActionProperty';
formOk 'OK' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.OkActionProperty';
formClose 'Закрыть' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.form.CloseActionProperty';

seek 'Найти объект' = ACTION CUSTOM 'lsfusion.server.logics.SeekActionProperty';

sleep 'Sleep' = ACTION CUSTOM 'lsfusion.server.logics.property.actions.SleepActionProperty';

applyOnlyWithoutRecalc = ACTION CUSTOM 'lsfusion.server.logics.property.actions.ApplyOnlyWithoutRecalcActionProperty';
applyOnlyCalc = ACTION CUSTOM 'lsfusion.server.logics.property.actions.ApplyOnlyCalcActionProperty';
applyOnlyData = ACTION CUSTOM 'lsfusion.server.logics.property.actions.ApplyOnlyDataActionProperty';
applyAll = ACTION CUSTOM 'lsfusion.server.logics.property.actions.ApplyAllActionProperty';

staticName 'Статическое имя' = DATA STRING[250] (Object);
nameStatic (name) = GROUP MAX object BY staticName(object);

staticCaption 'Статический заголовок' = DATA ISTRING[100] (Object);

objectClassName 'Класс объекта' (o) = staticCaption(objectClass(o));;

defaultBackgroundColor 'Цвет фона по умолчанию при выделении' = DATA COLOR ();
defaultOverrideBackgroundColor 'Цвет фона по умолчанию при выделении' = OVERRIDE RGB(255, 255, 0), defaultBackgroundColor() PERSISTENT;
defaultForegroundColor 'Цвет шрифта по умолчанию при выделении' = DATA COLOR ();
defaultOverrideForegroundColor 'Цвет шрифта по умолчанию при выделении' = OVERRIDE RGB(255, 0, 0), defaultForegroundColor() PERSISTENT;
  
customReportCharWidth 'Ширина символа в пикселах в отчетах' = DATA INTEGER ();
customReportRowHeight 'Высота поля в пикселах в отчетах' = DATA INTEGER ();
   
defaultReportCharWidth = 8;
defaultReportRowHeight = 18;    
   
reportCharWidth 'Ширина символа в пикселах в отчетах' = OVERRIDE defaultReportCharWidth(), customReportCharWidth();      
reportRowHeight 'Высота поля в пикселах в отчетах' = OVERRIDE defaultReportRowHeight(), customReportRowHeight();
reportNotToStretch 'Не растягивать поле по вертикали' = DATA BOOLEAN (); 
reportToStretch 'Растягивать поле по вертикали' = NOT reportNotToStretch(); 
  
addedObject() = DATA SESSION Object ();
confirmed() = DATA SESSION BOOLEAN ();
requestCanceled() = DATA SESSION BOOLEAN ();
formResult() = DATA SESSION FormResult ();

META defineAnyValue(name, param) 
    name##Object = DATA SESSION Object (param);
    name##String = DATA SESSION STRING[2000] (param);
    name##Integer = DATA SESSION INTEGER (param);
    name##Long = DATA SESSION LONG (param);
    name##Double = DATA SESSION DOUBLE (param);
    name##Numeric = DATA SESSION NUMERIC[20,7] (param);
    name##Year = DATA SESSION YEAR (param);
    name##DateTime = DATA SESSION DATETIME (param);
    name##Logical = DATA SESSION BOOLEAN (param);
    name##Date = DATA SESSION DATE (param);
    name##Time = DATA SESSION TIME (param);
    name##Color = DATA SESSION COLOR (param);
    name##WordFile = DATA SESSION WORDFILE (param);
    name##ImageFile = DATA SESSION IMAGEFILE (param);
    name##PdfFile = DATA SESSION PDFFILE (param);
    name##CustomFile = DATA SESSION CUSTOMFILE (param);
    name##ExcelFile = DATA SESSION EXCELFILE (param);
END

@defineAnyValue(requested, );
@defineAnyValue(chosen, STRING[100]);

META applicationForm(sid, name)
    FORM sid name;
    
    DESIGN sid FROM DEFAULT {
        main {
            NEW pane BEFORE functions.box {
                type = TABBED;
                flex = 1;
                align = STRETCH;
                NEW commons {
                    caption = 'Общие';
                }
            }
        }
    }
END

@applicationForm(options, 'Настройки');
@applicationForm(integrationData, 'Интеграция');
@applicationForm(migrationData, 'Миграция');

WINDOW TOOLBAR root 'Корень' HORIZONTAL POSITION(0, 0, 100, 6) VALIGN (CENTER) HIDETITLE HIDESCROLLBARS;
WINDOW TOOLBAR toolbar 'Тулбар' VERTICAL POSITION(0, 6, 20, 64) HIDETITLE;
WINDOW TREE tree 'Дерево' POSITION(0, 6, 20, 64) HIDETITLE;

NAVIGATOR {
    NEW root 'Формы' TO root {
        NEW administration 'Администрирование' TO toolbar IMAGE '/images/tools.png' {
            NEW application 'Приложение' {
                ADD options;
                ADD integrationData;
                ADD migrationData;
            }
            NEW configuration 'Система';
            NEW systemEvents 'Просмотр событий';
            NEW objects 'Объекты' TO tree; 
        }
    }
}

// Tests

randInt(max) = INTEGER(random() * ((max AS INTEGER) - 1)) + 1;  

META defineRandom1(cls, name, fnc)
     randNumber###name###cls (prm1) = PARTITION SUM 1 IF fnc(prm1) ORDER prm1;
     cls###name###fromRandNumber (number) = GROUP NAGGR prm1 BY randNumber###name###cls(prm1);
     maxRand###name###cls = GROUP SUM 1 IF fnc(prm1);
     random###name###cls(max) = cls###name###fromRandNumber(MIN maxRand###name###cls(), max AS INTEGER);     
END;

META defineRandom2(cls1, cls2, name, fnc)
     randNumber###name###cls1###cls2 (prm1, prm2) = PARTITION SUM 1 IF fnc(prm1, prm2) BY prm2 ORDER prm1;
     cls1###name###fromRandNumber###cls2 (number, prm2) = GROUP NAGGR prm1 BY randNumber###name###cls1###cls2(prm1, prm2), prm2;
     maxRand###name###cls1###cls2 = GROUP SUM 1 IF fnc(prm1, prm2) BY prm2;
     random###name###cls1###cls2(max, prm2) = cls1###name###fromRandNumber###cls2((MIN maxRand###name###cls1###cls2(prm2), max AS INTEGER), prm2);     
END;
