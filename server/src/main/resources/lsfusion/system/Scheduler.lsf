MODULE Scheduler;

REQUIRE System, Reflection, Service;


CLASS SchedulerStartType 'Отсчёт времени' {
    afterStart 'От запуска предыдущего',
    afterFinish 'От окончания предыдущего'
}

FORM schedulerStartTypes 'Способ отсчета времени'
    OBJECTS s = SchedulerStartType
    PROPERTIES(s) READONLY staticCaption
    DIALOG SchedulerStartType OBJECT s
;

DESIGN schedulerStartTypes {
    main {
        PROPERTY(staticCaption(s)) {
            caption = 'Отсчёт времени';
        }
    }
}

CLASS ScheduledTask 'Задание планировщика';
TABLE scheduledTask (ScheduledTask);

nameScheduledTask 'Имя' = DATA VARSTRING[100] (ScheduledTask);
scheduledTaskName (name) = GROUP AGGR task BY nameScheduledTask(task) WHERE task IS ScheduledTask;

runAtStartScheduledTask 'Выполнить при старте' = DATA BOOLEAN (ScheduledTask);
timeFromScheduledTask 'Время с' = DATA TIME (ScheduledTask);
timeToScheduledTask 'Время по' = DATA TIME (ScheduledTask);
startDateScheduledTask 'Дата начала' = DATA DATETIME (ScheduledTask);
periodScheduledTask 'Повторять каждые (секунд)' = DATA INTEGER (ScheduledTask);
schedulerStartTypeScheduledTask 'Отсчёт времени' = DATA SchedulerStartType (ScheduledTask);
nameSchedulerStartTypeScheduledTask 'Отсчёт времени' = staticCaption(schedulerStartTypeScheduledTask(ScheduledTask));
activeScheduledTask 'Активно' = DATA BOOLEAN (ScheduledTask);
daysOfMonthScheduledTask 'Выполнять по дням (через запятую)' = DATA VARSTRING[255] (ScheduledTask);

TABLE DOWScheduledTask(DOW, ScheduledTask);
inDOWScheduledTask 'Вкл' = DATA BOOLEAN (DOW, ScheduledTask); 
daysOfWeekScheduledTask (t)= GROUP CONCAT VARSTRING[3](numberDOW(d)) IF inDOWScheduledTask (d,t), ', ' BY t ORDER d;   

CLASS ScheduledTaskDetail 'Строка задания планировщика';
TABLE scheduledTaskDetail (ScheduledTaskDetail);

activeScheduledTaskDetail 'Активно' = DATA BOOLEAN (ScheduledTaskDetail);
orderScheduledTaskDetail 'Порядок' = DATA INTEGER (ScheduledTaskDetail);
scriptScheduledTaskDetail 'Скрипт' = DATA TEXT (ScheduledTaskDetail);

scheduledTaskScheduledTaskDetail = DATA ScheduledTask (ScheduledTaskDetail) NOT NULL DELETE;
propertyScheduledTaskDetail = DATA Property (ScheduledTaskDetail);
captionPropertyScheduledTaskDetail 'Свойство' (scheduledTaskDetail) = captionProperty(propertyScheduledTaskDetail(scheduledTaskDetail));
canonicalNamePropertyScheduledTaskDetail 'Код свойства' (scheduledTaskDetail) = canonicalNameProperty(propertyScheduledTaskDetail(scheduledTaskDetail));
parameterScheduledTaskDetail 'Параметр свойства' = DATA TEXT (ScheduledTaskDetail);
classPropertyScheduledTaskDetail 'Класс свойства' (scheduledTaskDetail) = classProperty(propertyScheduledTaskDetail(scheduledTaskDetail));
returnPropertyScheduledTaskDetail 'Возвращаемый класс' (scheduledTaskDetail) = returnProperty(propertyScheduledTaskDetail(scheduledTaskDetail));
timeoutScheduledTaskDetail 'Выполнять не дольше (секунд)' = DATA INTEGER (ScheduledTaskDetail);
ignoreExceptionsScheduledTaskDetail 'Игнорировать ошибки' = DATA BOOLEAN (ScheduledTaskDetail);

CLASS ScheduledTaskLog 'Лог планировщика';
TABLE scheduledTaskLog (ScheduledTaskLog);
TABLE scheduledTaskScheduledTaskLog (ScheduledTask, ScheduledTaskLog);

resultScheduledTaskLog 'Результат' = DATA VARSTRING[200] (ScheduledTaskLog);
exceptionOccurredScheduledTaskLog 'Ошибка' = DATA BOOLEAN (ScheduledTaskLog);
propertyScheduledTaskLog 'Свойство' = DATA VARSTRING[200] (ScheduledTaskLog);
dateScheduledTaskLog 'Время' = DATA DATETIME (ScheduledTaskLog);
toDateScheduledTaskLog 'Дата' (d) = DATE(dateScheduledTaskLog(d));
scheduledTaskScheduledTaskLog 'Задание планировщика' = DATA ScheduledTask (ScheduledTaskLog);

@defineLog (ScheduledTaskLog, 'планировщика', log, toDate);

CLASS ScheduledClientTaskLog 'Сообщения клиента';
TABLE scheduledClientTaskLog (ScheduledClientTaskLog);

scheduledTaskLogScheduledClientTaskLog 'Лог планировщика'= DATA ScheduledTaskLog (ScheduledClientTaskLog) NOT NULL DELETE;
messageScheduledClientTaskLog 'Сообщение'= DATA TEXT (ScheduledClientTaskLog);
dateScheduledClientTaskLog 'Время'= DATA DATETIME (ScheduledClientTaskLog);

isMessagesScheduledTaskLog 'Сообщения' (stl)= TRUE IF [=GROUP SUM 1 IF messageScheduledClientTaskLog(stcl) BY scheduledTaskLogScheduledClientTaskLog(stcl)](stl) AND NOT exceptionOccurredScheduledTaskLog(stl);

stopScheduler 'Остановить планировщик' = ACTION CUSTOM 'lsfusion.server.StopSchedulerActionProperty' ();
stopSchedulerApply 'Остановить планировщик' = ACTION {
    stopScheduler();
    apply();
}
runSetupScheduler 'Запустить планировщик' = ACTION CUSTOM 'lsfusion.server.SetupSchedulerActionProperty' ();
runSetupSchedulerApply 'Запустить планировщик' = ACTION {
    runSetupScheduler();
    apply();
}
isStartedScheduler 'Запущен'  = DATA BOOLEAN ();
isStoppedScheduler 'Остановлен' () = NOT isStartedScheduler();

onStarted() += ACTION runSetupScheduler();

scriptText 'Скрипт' () = DATA LOCAL TEXT ();
evalScript 'Выполнить скрипт' () = ACTION EVAL scriptText();

changedScheduler () = GROUP SUM 1 IF (((CHANGED (runAtStartScheduledTask(t)) OR CHANGED (startDateScheduledTask(t)) OR CHANGED (timeFromScheduledTask(t)) 
                                         OR CHANGED (timeToScheduledTask(t)) OR CHANGED (periodScheduledTask(t)) OR CHANGED (daysOfMonthScheduledTask(t))
                                         OR CHANGED (schedulerStartTypeScheduledTask(t)) OR CHANGED (schedulerStartTypeScheduledTask(t))) AND activeScheduledTask(t)
                                         OR CHANGED (activeScheduledTask(t))) OR
                                                ((CHANGED (activeScheduledTaskDetail(td)) OR CHANGED (orderScheduledTaskDetail(td)) OR CHANGED(scriptScheduledTaskDetail(td)) 
                                         OR CHANGED (scheduledTaskScheduledTaskDetail(td)) OR CHANGED (propertyScheduledTaskDetail(td)) 
                                         OR CHANGED (ignoreExceptionsScheduledTaskDetail(td)) OR CHANGED (timeoutScheduledTaskDetail(td)) OR CHANGED (parameterScheduledTaskDetail(td)))
                                         AND activeScheduledTask(scheduledTaskScheduledTaskDetail(td))) OR 
                                                CHANGED(inDOWScheduledTask(dow, t))) AND isStartedScheduler() AND t IS ScheduledTask AND td IS ScheduledTaskDetail AND dow IS DOW;
                                         ;
WHEN changedScheduler() DO EXEC runSetupScheduler();
WHEN DROPPED (td IS ScheduledTaskDetail) IF PREV(activeScheduledTaskDetail(td)) AND isStartedScheduler() DO EXEC runSetupScheduler();
           
FORM scheduledTask 'Планировщик'

    OBJECTS t=ScheduledTask
    PROPERTIES (t) activeScheduledTask, nameScheduledTask, timeFromScheduledTask, timeToScheduledTask, startDateScheduledTask, periodScheduledTask,
                    nameSchedulerStartTypeScheduledTask, runAtStartScheduledTask, daysOfMonthScheduledTask FORCE PANEL
    PROPERTIES (t) ADDOBJ, DELETESESSION
         
    OBJECTS td=ScheduledTaskDetail
    PROPERTIES (td) activeScheduledTaskDetail, ignoreExceptionsScheduledTaskDetail, orderScheduledTaskDetail, captionPropertyScheduledTaskDetail, 
                    canonicalNamePropertyScheduledTaskDetail, parameterScheduledTaskDetail, classPropertyScheduledTaskDetail, 
                    returnPropertyScheduledTaskDetail, timeoutScheduledTaskDetail, scriptScheduledTaskDetail FORCE PANEL
    PROPERTIES (td) ADDOBJ, DELETESESSION
    
    OBJECTS tl=ScheduledTaskLog
    PROPERTIES (tl) READONLY propertyScheduledTaskLog, resultScheduledTaskLog, exceptionOccurredScheduledTaskLog, isMessagesScheduledTaskLog, dateScheduledTaskLog
    
    FILTERGROUP stlFilters 
        FILTER 'Ошибка или сообщение' exceptionOccurredScheduledTaskLog(tl) OR isMessagesScheduledTaskLog(tl)

    OBJECTS ctl=ScheduledClientTaskLog
    PROPERTIES (ctl) READONLY messageScheduledClientTaskLog, dateScheduledClientTaskLog
    
    OBJECTS dow=DOW
    PROPERTIES (dow) numberDOW, staticCaption 
    PROPERTIES (dow, t) inDOWScheduledTask 
    
    PROPERTIES () stopSchedulerApply SHOWIF isStartedScheduler() TOOLBAR, runSetupSchedulerApply SHOWIF isStoppedScheduler() TOOLBAR
    
    ORDER BY numberDOW(dow)
    
    FILTERS scheduledTaskScheduledTaskDetail(td)==t,
            scheduledTaskScheduledTaskLog(tl) == t,
            scheduledTaskLogScheduledClientTaskLog(ctl) == tl

    FILTERGROUP filtersScheduler
            FILTER 'Только активные' activeScheduledTaskDetail(td) 'F9'
;

DESIGN scheduledTask {
    NEW specContainer {
        fill = 1; 
        type = SPLITV;
        MOVE t.box;
        NEW south {
            fill = 2;
            type = TABBED;
            MOVE td.box {
                caption = 'Свойства';
                MOVE PROPERTY(scriptScheduledTaskDetail(td)) {
                    panelCaptionAbove = TRUE;
                    fill = 0.5;
                }
                PROPERTY(parameterScheduledTaskDetail(td)) {
                    preferredSize = (-1, 18);
                    minimumSize = (-1, 18);
                }
            }
            NEW logContainer {
                caption = 'Лог';
                type = CONTAINERH;
                MOVE tl.box;
                MOVE ctl.box;
            }
            NEW dowContainer {
                caption = 'Фильтр по дням';
                MOVE dow.box;
                PROPERTY(numberDOW(dow)) {
                    hide = TRUE;
                }
                MOVE PROPERTY(daysOfMonthScheduledTask(t));
            }        
        }
    }
    NEW bottom {
        type = CONTAINERH;
        align = TRAILING;
        MOVE PROPERTY(runSetupSchedulerApply());
        MOVE PROPERTY(stopSchedulerApply());
    }
    MOVE functions.box;
}

FORM scheduledTaskProperties 'Свойства'
    OBJECTS p = Property
    PROPERTIES(p) READONLY captionProperty, canonicalNameProperty, classProperty, returnProperty
    FILTERS returnProperty(p) == 'ActionClass'
;

NAVIGATOR {
    configuration {
        ADD Scheduler.scheduledTask;
    }
}