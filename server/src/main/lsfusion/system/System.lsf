MODULE System;

NAMESPACE System;

CLASS NATIVE Object;

GROUP NATIVE root 'Корневая группа';
GROUP NATIVE public 'Пользовательские свойства' : root;
GROUP NATIVE private 'Внутренние свойства' : root;

CLASS NATIVE StaticObject;
TABLE staticObject (StaticObject) NODEFAULT;

CLASS NATIVE CustomObjectClass : StaticObject;
TABLE customObjectClass (CustomObjectClass);
objectClass = NATIVE CustomObjectClass (Object);

random = NATIVE DOUBLE ();
//watch = NATIVE (); 

stat 'Статистика' = DATA INTEGER (CustomObjectClass);

GROUP NATIVE base 'Атрибуты' : public;
GROUP NATIVE id 'Идентифицирующие свойства' : base;

GROUP drillDown 'Действия детализации' : root;
GROUP propertyPolicy 'Свойства политики безопасности' : root;

canceled = DATA LOCAL BOOLEAN ();
applyMessage = DATA LOCAL TEXT ();
empty()  { };
empty(Object o)  { };

apply  { APPLY; }
cancel  { CANCEL; }
onStarted  ABSTRACT LIST ( );

messageCaughtException = DATA LOCAL TEXT();
javaStackTraceCaughtException = DATA LOCAL TEXT();
lsfStackTraceCaughtException = DATA LOCAL TEXT();

upper = FORMULA 'upper($1)';    
subtractSeconds(DATETIME dateTimeFrom, DATETIME dateTimeTo) = [ FORMULA INTEGER PG 'extract (\'epoch\' from (($1)-($2)))', MS 'DATEDIFF(s, $2, $1)'](dateTimeTo AS DATETIME, dateTimeFrom AS DATETIME);

formApply '{action.form.save}'  INTERNAL  'lsfusion.server.logics.property.actions.form.FormApplyActionProperty' ();
formCancel '{action.form.cancel}'  INTERNAL  'lsfusion.server.logics.property.actions.form.FormCancelActionProperty' ();
formEditReport '{action.form.edit}'  INTERNAL  'lsfusion.server.logics.property.actions.form.EditReportActionProperty' ();
formDrop '{action.form.drop}'  INTERNAL  'lsfusion.server.logics.property.actions.form.DropActionProperty' ();
formRefresh '{action.form.refresh}'  INTERNAL  'lsfusion.server.logics.property.actions.form.RefreshActionProperty' ();
formOk '{action.form.ok}'  INTERNAL  'lsfusion.server.logics.property.actions.form.OkActionProperty' ();
formClose '{action.form.close}'  INTERNAL  'lsfusion.server.logics.property.actions.form.CloseActionProperty' ();

seek 'Найти объект'  INTERNAL  'lsfusion.server.logics.SeekScriptingActionProperty' (Object);

sleep 'Sleep'  INTERNAL  'lsfusion.server.logics.property.actions.SleepActionProperty' (LONG);

sessionOwners = DATA LOCAL NESTED MANAGESESSION INTEGER ();
manageSession = NOT sessionOwners() > 1;

applyOnlyWithoutRecalc  INTERNAL  'lsfusion.server.logics.property.actions.ApplyOnlyWithoutRecalcActionProperty' ();
applyOnlyCalc  INTERNAL  'lsfusion.server.logics.property.actions.ApplyOnlyCalcActionProperty' ();
applyOnlyCheck  INTERNAL  'lsfusion.server.logics.property.actions.ApplyOnlyCheckActionProperty' ();
applyOnlyData  INTERNAL  'lsfusion.server.logics.property.actions.ApplyOnlyDataActionProperty' ();
applyAll  INTERNAL  'lsfusion.server.logics.property.actions.ApplyAllActionProperty' ();

throwException  INTERNAL  'lsfusion.server.logics.property.actions.ThrowExceptionActionProperty' (TEXT);

setNoCancelInTransaction()  INTERNAL  <{ context.getSession().setNoCancelInTransaction(true); }>;
dropNoCancelInTransaction()  INTERNAL  <{ context.getSession().setNoCancelInTransaction(false); }>;

setNoEventsInTransaction()  INTERNAL  <{ context.getSession().setNoEventsInTransaction(true); }>;
dropNoEventsInTransaction()  INTERNAL  <{ context.getSession().setNoEventsInTransaction(false); }>;

check()  { 
    applyOnlyCheck();
    
    TRY { 
        apply();
    } FINALLY {
        applyAll();
    }    
}

executeLocalEvents  INTERNAL  'lsfusion.server.logics.property.actions.ExecuteLocalEventsActionProperty' (TEXT);
executeLocalEvents()  { executeLocalEvents(NULL); }

staticName 'Статическое имя' = DATA STRING[250] (Object); // temporary for migration
staticName 'Статическое имя' (StaticObject object) = staticName[Object](object) MATERIALIZED TABLE staticObject;
nameStatic (name) = GROUP MAX StaticObject object BY staticName(object);

staticCaption 'Статическое имя' = DATA ISTRING[100] (Object); // temporary for migration
staticCaption 'Статический заголовок' (StaticObject object) = staticCaption[Object](object) MATERIALIZED TABLE staticObject IN id;

objectClassName 'Класс объекта' (Object o) = staticCaption(objectClass(o));
prevObjectClassName 'Класс объекта' (Object o) = staticCaption(PREV(objectClass(o)));

defaultBackgroundColor 'Цвет фона по умолчанию при выделении' = DATA COLOR ();
defaultOverrideBackgroundColor 'Цвет фона по умолчанию при выделении' = OVERRIDE defaultBackgroundColor(), RGB(255, 255, 0) MATERIALIZED;
defaultForegroundColor 'Цвет шрифта по умолчанию при выделении' = DATA COLOR ();
defaultOverrideForegroundColor 'Цвет шрифта по умолчанию при выделении' = OVERRIDE defaultForegroundColor(), RGB(255, 0, 0) MATERIALIZED;

selectedRowBackgroundColor 'Цвет фона ряда при выделении' = DATA COLOR ();
selectedRowBorderColor 'Цвет границы ряда при выделении' = DATA COLOR ();
selectedCellBackgroundColor 'Цвет фона ячейки при выделении' = DATA COLOR ();
focusedCellBackgroundColor 'Цвет фона ячейки при фокусировании' = DATA COLOR ();
focusedCellBorderColor 'Цвет границы ячейки при фокусировании' = DATA COLOR ();
  
customReportCharWidth 'Ширина символа в пикселах в отчетах' = DATA INTEGER ();
customReportRowHeight 'Высота поля в пикселах в отчетах' = DATA INTEGER ();
   
defaultReportCharWidth = 8;
defaultReportRowHeight = 18;    
   
reportCharWidth 'Ширина символа в пикселах в отчетах' = OVERRIDE customReportCharWidth(), defaultReportCharWidth();      
reportRowHeight 'Высота поля в пикселах в отчетах' = OVERRIDE customReportRowHeight(), defaultReportRowHeight();
reportNotToStretch 'Не растягивать поле по вертикали' = DATA BOOLEAN (); 
reportToStretch 'Растягивать поле по вертикали' = NOT reportNotToStretch();

hashModules 'Хэш модулей' = DATA VARSTRING[200] ();

// ----------------------------------- open files ----------------------------------- //

//source path, name
open 'Открыть файл'  INTERNAL  'lsfusion.utils.system.OpenPathActionProperty' (VARISTRING[255], VARISTRING[255]);
open 'Открыть файл' (VARSTRING[255] source)  { open(source, NULL); }
//source file, name
open 'Открыть файл'  INTERNAL  'lsfusion.utils.system.OpenFileActionProperty' (FILE, VARISTRING[255]);
open 'Открыть файл' (FILE source)  { open(source, NULL); }
//source rawFile, name
open 'Открыть файл'  INTERNAL  'lsfusion.utils.system.OpenRawFileActionProperty' (RAWFILE, VARISTRING[255]);
open 'Открыть файл' (RAWFILE source)  { open(source, NULL); }
//source link
open 'Открыть ссылку'  INTERNAL  'lsfusion.utils.system.OpenLinkActionProperty' (LINK);
//source rawLink
open 'Открыть ссылку'  INTERNAL  'lsfusion.utils.system.OpenRawLinkActionProperty' (RAWLINK);
  
addedObject() = DATA LOCAL Object ();
formPageCount() = DATA LOCAL INTEGER ();
exportFile() = DATA LOCAL FILE ();
importFile() = DATA LOCAL FILE ();

imported = DATA LOCAL BOOLEAN (INTEGER);
importedString = DATA LOCAL BOOLEAN (VARSTRING[10]);

readFile = DATA LOCAL FILE ();

getFileExtension(FILE file) = FORMULA TEXT 'get_extension($1)';
getExtension = getFileExtension(FILE(file));

ignorePrintType 'Игнорировать опции PRINT' = ABSTRACT BOOLEAN ();

downloadFile 'Скопировать файл на клиент'  INTERNAL  'lsfusion.server.lib.DownloadFileActionProperty' (VARSTRING[100]); 

loadDownloadedLibrary 'Загрузить скачанную библиотеку'  INTERNAL  'lsfusion.server.lib.LoadDownloadedLibraryActionProperty' (VARSTRING[100]);
loadDownloadedFont 'Загрузить шрифт'  INTERNAL  'lsfusion.server.lib.LoadDownloadedFontActionProperty' (VARSTRING[100]);

loadLibrary 'Загрузить библиотеку' (path)   { 
    downloadFile(path);
    loadDownloadedLibrary(path);
}

loadFont 'Загрузить шрифт' (path)   { 
    downloadFile(path);
    loadDownloadedFont(path);
}

applyCanceled() = DATA LOCAL NESTED BOOLEAN ();
requestCanceled() = DATA LOCAL NESTED BOOLEAN ();
requestPushed() = DATA LOCAL NESTED BOOLEAN ();

requestedObject = DATA LOCAL NESTED Object ();
requestedString = DATA LOCAL NESTED STRING[4000] ();
requestedText = DATA LOCAL NESTED TEXT ();
requestedInteger = DATA LOCAL NESTED INTEGER ();
requestedLong = DATA LOCAL NESTED LONG ();
requestedDouble = DATA LOCAL NESTED DOUBLE ();
requestedNumeric = DATA LOCAL NESTED NUMERIC[20,7] ();
requestedYear = DATA LOCAL NESTED YEAR ();
requestedDateTime = DATA LOCAL NESTED DATETIME ();
requestedLogical = DATA LOCAL NESTED BOOLEAN ();
requestedDate = DATA LOCAL NESTED DATE ();
requestedTime = DATA LOCAL NESTED TIME ();
requestedColor = DATA LOCAL NESTED COLOR ();
requestedWordFile = DATA LOCAL NESTED WORDFILE ();
requestedImageFile = DATA LOCAL NESTED IMAGEFILE ();
requestedPdfFile = DATA LOCAL NESTED PDFFILE ();
requestedRawFile = DATA LOCAL NESTED RAWFILE ();
requestedFile = DATA LOCAL NESTED FILE ();
requestedExcelFile = DATA LOCAL NESTED EXCELFILE ();
requestedCsvFile = DATA LOCAL NESTED CSVFILE ();
requestedHtmlFile = DATA LOCAL NESTED HTMLFILE ();
requestedJsonFile = DATA LOCAL NESTED JSONFILE ();
requestedXmlFile = DATA LOCAL NESTED XMLFILE ();
requestedWordLink = DATA LOCAL NESTED WORDLINK ();
requestedImageLink = DATA LOCAL NESTED IMAGELINK ();
requestedPdfLink = DATA LOCAL NESTED PDFLINK ();
requestedRawLink = DATA LOCAL NESTED RAWLINK ();
requestedLink = DATA LOCAL NESTED LINK ();
requestedExcelLink = DATA LOCAL NESTED EXCELLINK ();    
requestedCsvLink = DATA LOCAL NESTED CSVLINK ();    
requestedHtmlLink = DATA LOCAL NESTED HTMLLINK ();    
requestedJsonLink = DATA LOCAL NESTED JSONLINK ();    
requestedXmlLink = DATA LOCAL NESTED XMLLINK ();    

isActiveForm = DATA LOCAL BOOLEAN ();

edit 'Редактировать'  ABSTRACT MULTI OVERRIDE FIRST ( Object) IMAGE 'edit.png' CHANGEKEY 'BACK_SPACE' HIDE TOOLBAR;
edit(Object o) + {  DIALOG EDIT Object =o DOCKED; }
formEdit(Object o)  { edit(o); }

// private, only for interactive usage (for other purposes use operator DELETE)
delete 'Удалить'  ABSTRACT MULTI OVERRIDE FIRST ( Object) IMAGE 'delete.png' CHANGEKEY 'DELETE' HIDE TOOLBAR;
delete(Object o) + {  DELETE o; } 
formDelete(Object o)  { 
    IF sessionOwners() THEN
        delete(o);
    ELSE 
        ASK '{form.instance.do.you.really.want.to.take.action} \'{logics.delete}\'' DO {
            delete(o);
            APPLY;
        }
}

META applicationForm(sid, name)
    FORM sid name;
    
    DESIGN sid {
        BOX {
            NEW pane BEFORE TOOLBARBOX {
                type = TABBED;
                flex = 1;
                align = STRETCH;
                NEW commons {
                    caption = 'Общие';
                }
            }
        }
    }
END

@applicationForm(options, '{navigator.options}');
@applicationForm(integrationData, '{navigator.integration}');
@applicationForm(migrationData, '{navigator.migration}');

networkPath 'Сетевой путь' = DATA LOCAL NESTED VARSTRING[500] ();
loadNetworkPath 'Загрузить'  INTERNAL  'lsfusion.server.logics.property.actions.LoadLinkActionProperty' ();
FORM networkPathForm 'Выбор сетевого пути'
    PROPERTIES() networkPath, loadNetworkPath;

META defineLinkDialog (object, class)
    dialog###object(class o)  { 
        networkPath() <- VARSTRING[500] (object(o));
        DIALOG networkPathForm DO {
            object(o) <- IMAGELINK (networkPath()); 
        }
    } ASON CHANGE object[class];
END

META defineLinkDialog (object)
    dialog###object()  { 
        networkPath() <- VARSTRING[500] (object());
        DIALOG networkPathForm DO {
            object() <- LINK (networkPath()); 
        }
    } ASON CHANGE object;
END

WINDOW root '{window.root}' TOOLBAR HORIZONTAL POSITION(0, 0, 100, 6) VALIGN (CENTER) HIDETITLE HIDESCROLLBARS;
WINDOW toolbar '{window.toolbar}' TOOLBAR VERTICAL POSITION(0, 6, 20, 64) HIDETITLE;
WINDOW tree '{window.tree}' TOOLBAR POSITION(0, 6, 20, 64) HIDETITLE;

NAVIGATOR {
    NEW FOLDER root '{navigator.forms}' WINDOW root {
        NEW FOLDER administration '{navigator.administration}' WINDOW toolbar IMAGE '/images/tools.png' {
            NEW FOLDER application '{navigator.application}' {
                NEW options;
                NEW integrationData;
                NEW migrationData;
            }
            
            NEW FOLDER system '{navigator.system}' {
                NEW FOLDER performance '{navigator.performance}';
                NEW FOLDER scheduler '{navigator.scheduler}';    
                NEW FOLDER logs '{navigator.logs}';            
            }
            
            NEW FOLDER objects '{navigator.objects}' WINDOW tree; 
        }
    }
}

// Tests

randInt(INTEGER max) = INTEGER(random() * ((max AS INTEGER) - 1)) + 1;  

META defineRandom1(cls, name, fnc)
     randNumber###name###cls (prm1) = PARTITION SUM 1 IF fnc(prm1) ORDER prm1;
     cls###name###fromRandNumber (number) = GROUP NAGGR prm1 BY randNumber###name###cls(prm1);
     maxRand###name###cls = GROUP SUM 1 IF fnc(prm1);
     random###name###cls(max) = cls###name###fromRandNumber(MIN maxRand###name###cls(), max AS INTEGER);     
END;

META defineRandom2(cls1, cls2, name, fnc)
     randNumber (###cls1 prm1, ###cls2 prm2) = PARTITION SUM 1 IF prevCurrentBalance(prm1, prm2) ORDER prm1 BY prm2;
     cls1##FromRand (number, ###cls2 prm2) = GROUP NAGGR ###cls1 prm1 BY randNumber(prm1, prm2);
     maxRand###cls1(###cls2 prm2) = GROUP SUM 1 IF prevCurrentBalance(###cls1 prm1, prm2);
     random###cls1(INTEGER max, ###cls2 prm2) = cls1##FromRand((MIN maxRand###cls1(prm2), max AS INTEGER), prm2);     
END;

// Options
META defineOption(property, caption, container)
    @defineOption(property, caption, BOOLEAN, container);
END

META defineOption(property, caption, type, container)
    property caption = DATA type () COMPLEX;
    
    EXTEND FORM options PROPERTIES() property;
    DESIGN options { container { MOVE PROPERTY(property()); } }
END