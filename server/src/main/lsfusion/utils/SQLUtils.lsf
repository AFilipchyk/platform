MODULE SQLUtils;

REQUIRE System, Authentication, Service, Utils, Time, ProcessMonitor;

runSQL 'Выполнить SQL'  INTERNAL  'lsfusion.utils.RunSQLScriptActionProperty' (TEXT);

runJDBC 'Выполнить JDBC'  INTERNAL  'lsfusion.utils.RunJDBCStatementActionProperty' (VARSTRING[100], TEXT);

vacuumTableAction 'Упаковать таблицу'(Table table)  { 
    runSQL('VACUUM FULL ' + sid(table));
}

vacuumTablesAction 'Упаковать БД потаблично' ()  { 
    FOR table IS Table AND NOT skipVacuum(table) ORDER rows(table) DO {
        vacuumTableAction(table);
    }
    
    //vacuum части системных таблиц
    runSQL('VACUUM FULL pg_catalog.pg_statistic');
    runSQL('VACUUM FULL pg_catalog.pg_attribute');
    runSQL('VACUUM FULL pg_catalog.pg_class');
    runSQL('VACUUM FULL pg_catalog.pg_depend');
    runSQL('VACUUM FULL pg_catalog.pg_type');
    runSQL('VACUUM FULL pg_catalog.pg_constraint');
    runSQL('VACUUM FULL pg_catalog.pg_index');
    runSQL('VACUUM FULL pg_catalog.pg_proc');
    
    MESSAGE 'Упаковка БД была успешно завершена';
}

alterTableColumnsToLong 'Alter Table Columns To Long'  INTERNAL  'lsfusion.utils.AlterTableColumnsToLongActionProperty' (Table);

EXTEND FORM physicalModel PROPERTIES(t) BEFORE skipVacuum(t) GRID vacuumTableAction, alterTableColumnsToLong;
EXTEND FORM maintenance PROPERTIES() vacuumTablesAction;
DESIGN maintenance {
    database {
        MOVE PROPERTY(vacuumTablesAction());
    }
}

changeAllDates 'Сместить время всех свойств и ключей'  INTERNAL  'lsfusion.utils.ChangeAllDatesActionProperty' () CONFIRM;
secondsChangeAllDates 'Интервал в секундах' = DATA INTEGER ();

EXTEND FORM maintenance PROPERTIES() changeAllDates, secondsChangeAllDates;
DESIGN maintenance {
    rightColumn {
        NEW changeDates {
            caption = 'Сместить время';
            MOVE PROPERTY(changeAllDates());
            MOVE PROPERTY(secondsChangeAllDates());
        }
    }
}
