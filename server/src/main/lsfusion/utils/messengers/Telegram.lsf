MODULE Telegram;

REQUIRE Messenger;

EXTEND CLASS Messenger { telegram 'Telegram' }
isTelegram(Account a) = messenger(a) == Messenger.telegram;

setWebHookTelegramResult = DATA LOCAL STRING();
setWebHookTelegram(Account account) {
    LOCAL result = JSONFILE();
        
    EXTERNAL HTTP POST 'https://api.telegram.org/bot' + token(account) + '/setWebhook'
        BODYURL 'url=' + webServerUrl() + '/exec/webHookTelegram?botid=' + LONG(account) TO result;
    
    LOCAL ok = BOOLEAN();
    LOCAL description = STRING();
    IMPORT FROM result() TO() ok, description;
    IF NOT ok() THEN {
        setWebHookTelegramResult() <- description();
    }
}
WHEN SET (Account a IS Account) AND isTelegram(a) DO {
    setWebHookTelegram(a);
    IF setWebHookTelegramResult() THEN {
        MESSAGE setWebHookTelegramResult() NOWAIT;
    }
}

GROUP message;
GROUP chat : message;
id = DATA LOCAL STRING();
type = DATA LOCAL STRING();
first_name = DATA LOCAL STRING();
username = DATA LOCAL STRING();
title = DATA LOCAL STRING();

FORM webHookTelegram
PROPERTIES() IN chat id, type, first_name, username, title;

webHookTelegram(JSONFILE f) {
    printToLog(STRING(f));
    
    IMPORT webHookTelegram JSON FROM f;
    
    LOCAL chat = Chat();
    chat() <- chatIdAccount(account(LONG(params('botid'))), id());
    IF NOT chat() THEN {
        NEW chat = Chat {
            chat() <- chat;
            account(chat) <- account(LONG(params('botid')));
            id(chat()) <- id();
        }
    }
    username(chat()) <-username();
    title(chat()) <- CASE WHEN type() == 'private' THEN first_name()
                          WHEN type() == 'group' THEN title()
                          WHEN type() == 'channel' THEN title();
    chatType(chat()) <- CASE WHEN type() == 'private' THEN ChatType.private 
                             WHEN type() == 'group' THEN ChatType.group
                             WHEN type() == 'channel' THEN ChatType.channel;
 
    APPLY;
} @@noauth;

sendMessageTelegramResult = DATA LOCAL STRING();
sendMessageTelegram(Chat chat, STRING message) {
    sendMessageTelegramResult() <- NULL;
    LOCAL result = JSONFILE();
    urlEncode(message);
    EXTERNAL HTTP GET 'https://api.telegram.org/bot' + token(account(chat)) + '/sendMessage?chat_id=' + id(chat) + '&text=' + urlEncoded() TO result;
    
    LOCAL ok = BOOLEAN();
    LOCAL description = STRING();
    IMPORT FROM result() TO() ok, description;
    IF NOT ok() THEN {
        sendMessageTelegramResult() <- description();
    }
}

sendMessage(Chat chat, STRING message) + WHEN isTelegram(account(chat)) AND message IS STRING THEN {
    sendMessageTelegram(chat, message);
    sendMessageResult() <- sendMessageTelegramResult();
}