MODULE Backup;

REQUIRE System, Reflection, SchedulerDefaultData;

makeBackup 'Копировать'= ACTION CUSTOM 'lsfusion.erp.utils.backup.BackupActionProperty' () TOOLBAR;
saveBackup 'Скачать'= ACTION CUSTOM 'lsfusion.erp.utils.backup.SaveBackupActionProperty' (Backup) TOOLBAR;
deleteBackup 'Удалить'= ACTION CUSTOM 'lsfusion.erp.utils.backup.DeleteBackupActionProperty' (Backup) CONFIRM;
decimateBackups 'Проредить'= ACTION CUSTOM 'lsfusion.erp.utils.backup.DecimateBackupsActionProperty' () TOOLBAR CONFIRM;

customRestore 'Восстановить таблицы' = ACTION CUSTOM 'lsfusion.erp.utils.backup.CustomRestoreActionProperty' (Backup);
inCustomRestoreTable 'Вкл' = DATA BOOLEAN (Table);
restoreObjectsTable 'Восстанавливать удалённые объекты' = DATA BOOLEAN (Table);
inCustomRestoreTableColumn 'Вкл' = DATA BOOLEAN (TableColumn);
replaceOnlyNullTableColumn 'Не замещать' = DATA BOOLEAN (TableColumn);

CLASS Backup 'Резервная копия';
TABLE backup(Backup);

dateBackup 'Дата' = DATA DATE (Backup);
timeBackup 'Время' = DATA TIME (Backup);
fileBackup 'Адрес файла' = DATA VARSTRING[200] (Backup);
nameBackup 'Имя файла' = DATA VARSTRING[100] (Backup);
fileLogBackup 'Адрес лога файла' = DATA VARSTRING[200] (Backup);
fileDeletedBackup 'Файл удалён' = DATA BOOLEAN (Backup);
logBackup 'Лог' = DATA TEXT (Backup);

backupFileName 'Имя файла резервной копии' = DATA LOCAL VARSTRING[100] ();
backupFilePath 'Файл резервной копии' = DATA LOCAL VARSTRING[200]();

excludeTable 'Исключить' = DATA LOCAL BOOLEAN (Table);

TABLE backupTable(Backup, Table);
excludeBackupTable 'Исключена' = DATA BOOLEAN (Backup, Table);

FORM backup 'Резервное копирование'
    OBJECTS b = Backup
    PROPERTIES() decimateBackups TODRAW b FORCE PANEL, makeBackup TODRAW b FORCE PANEL
    PROPERTIES(b) saveBackup TODRAW b FORCE PANEL
    PROPERTIES(b) READONLY dateBackup, timeBackup, fileBackup, fileDeletedBackup, logBackup FORCE PANEL
    PROPERTIES(b) deleteBackup
    
    OBJECTS t1 = Table
    PROPERTIES(t1) sidTable, excludeTable
    
    OBJECTS t2 = Table
        PROPERTIES(t2) sidTable
        PROPERTIES(b,t2) excludeBackupTable
;

DESIGN backup {
    NEW pane {
        fill = 1;
        type = CONTAINERH;
        NEW leftPane {
            fill = 1;
            MOVE b.box;
            MOVE t1.box;
        }
        NEW rightPane {
            fill = 1;
            MOVE PROPERTY(logBackup(b)) {
                fill = 1;
                panelCaptionAbove = TRUE;
            }
            MOVE t2.box {
                caption = 'Скопированные таблицы';
            }
        }
    }
    MOVE functions.box;
}

FORM backups 'Резервные копии'
    OBJECTS b=Backup
    PROPERTIES(b) READONLY nameBackup, fileBackup, fileDeletedBackup
    FILTERS NOT fileDeletedBackup(b)
    DIALOG Backup OBJECT b
;

FORM customRestore 'Восстановление данных'
    OBJECTS t = Table
    PROPERTIES(t) inCustomRestoreTable, restoreObjectsTable, sidTable READONLY
    FILTERGROUP active FILTER 'Только включенные' inCustomRestoreTable(t) DEFAULT   
        
    OBJECTS tc=TableColumn
    PROPERTIES(tc) inCustomRestoreTableColumn, replaceOnlyNullTableColumn
    PROPERTIES(tc) READONLY sidTableColumn, captionTableColumn, canonicalNameTableColumn
    FILTERS tableTableColumn(tc) == t

    
    OBJECTS b = Backup FIXED PANEL
    PROPERTIES(b) nameBackup SELECTOR, customRestore
    ORDER BY nameBackup(b) DESC
;

NAVIGATOR {
    configuration {
        ADD backup;
        ADD customRestore;
    }
}

loadDefaultScheduledTasks () += ACTION ()  {    
    loadDefaultScheduledTask ('Резервное копирование', 2014_07_01_01:00, 86400, SchedulerStartType.afterStart);
    loadDefaultScheduledTaskDetail ('Резервное копирование', 1, 'Backup.makeBackup[]');
    loadDefaultScheduledTaskDetail ('Резервное копирование', 2, 'Backup.decimateBackups[]');        
}
