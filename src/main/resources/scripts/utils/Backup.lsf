MODULE Backup;

REQUIRE System, Reflection, SchedulerDefaultData;

makeBackup 'Копировать'= ACTION CUSTOM 'lsfusion.erp.utils.backup.BackupActionProperty' TOOLBAR;
saveBackup 'Скачать'= ACTION CUSTOM 'lsfusion.erp.utils.backup.SaveBackupActionProperty' TOOLBAR;
deleteBackup 'Удалить'= ACTION CUSTOM 'lsfusion.erp.utils.backup.DeleteBackupActionProperty' CONFIRM;
decimateBackups 'Проредить'= ACTION CUSTOM 'lsfusion.erp.utils.backup.DecimateBackupsActionProperty' TOOLBAR CONFIRM;

CLASS Backup 'Резервная копия';
TABLE backup(Backup);

dateBackup 'Дата' = DATA DATE (Backup);
timeBackup 'Время' = DATA TIME (Backup);
fileBackup 'Адрес файла' = DATA VARSTRING[200] (Backup);
nameBackup 'Имя файла' = DATA VARSTRING[100] (Backup);
fileLogBackup 'Адрес лога файла' = DATA VARSTRING[200] (Backup);
fileDeletedBackup 'Файл удалён' = DATA BOOLEAN (Backup);
logBackup 'Лог' = DATA TEXT (Backup);

backupFileName 'Имя файла резервной копии' = DATA SESSION VARSTRING[100] ();
backupFilePath 'Файл резервной копии' = DATA SESSION VARSTRING[200]();

excludeTable 'Исключить' = DATA SESSION BOOLEAN (Table);

TABLE backupTable(Backup, Table);
excludeBackupTable 'Исключена' = DATA BOOLEAN (Backup, Table);

FORM backup 'Резервное копирование'
    OBJECTS b = Backup
    PROPERTIES() decimateBackups TODRAW b FORCE PANEL, makeBackup TODRAW b FORCE PANEL
    PROPERTIES(b) saveBackup TODRAW b FORCE PANEL
    PROPERTIES(b) READONLY dateBackup, timeBackup, fileBackup, fileDeletedBackup, logBackup FORCE PANEL
    PROPERTIES(b) deleteBackup
    
    OBJECTS t1 = Table
    PROPERTIES(t1) sidTable, excludeTable
    
    OBJECTS t2 = Table
        PROPERTIES(t2) sidTable
        PROPERTIES(b,t2) excludeBackupTable
;

DESIGN backup FROM DEFAULT {
    NEW pane {
        fill = 1;
        type = CONTAINERH;
        NEW leftPane {
            fill = 1;
            ADD b.box;
            ADD t1.box;
        }
        NEW rightPane {
            fill = 1;
            ADD PROPERTY(logBackup(b)) {
                fill = 1;
                panelLabelAbove = TRUE;
            }
            ADD t2.box {
                caption = 'Скопированные таблицы';
            }
        }
    }
    ADD functions.box;
}

NAVIGATOR {
    configuration {
        ADD backup;
    }
}

loadDefaultScheduledTasks () += ACTION ()  {    
    loadDefaultScheduledTask ('Резервное копирование', 2014_07_01_01:00, 86400, SchedulerStartType.afterStart);
    loadDefaultScheduledTaskDetail ('Резервное копирование', 1, 'Backup.makeBackup[]');
    loadDefaultScheduledTaskDetail ('Резервное копирование', 2, 'Backup.decimateBackups[]');        
}
