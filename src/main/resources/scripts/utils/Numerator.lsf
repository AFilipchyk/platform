MODULE Numerator;

REQUIRE System, MasterData, DefaultData;

// ------------------------------------------- Нумераторы ----------------------------------------------- //

CLASS Numerator 'Нумератор';
TABLE numerator (Numerator);

nameNumerator 'Наименование' = DATA VARISTRING[100](Numerator) MINCHARWIDTH 20 PREFCHARWIDTH 20;

seriesNumerator 'Серия' = DATA STRING[2] (Numerator) IN recognize FIXEDCHARWIDTH 3;

minValueNumerator 'Мин. значение' = DATA LONG (Numerator) IN recognize MINCHARWIDTH 15;
maxValueNumerator 'Макс. значение' = DATA LONG (Numerator) IN recognize MINCHARWIDTH 15;

stringLengthNumerator 'Длина' = DATA INTEGER (Numerator) IN base;

curValueNumerator 'Тек. значение' = DATA LONG (Numerator) IN recognize MINCHARWIDTH 15;
curValueNumerator(numerator) <- minValueNumerator(numerator) WHEN SET(numerator IS Numerator);

lpadStringWithZero = FORMULA STRING[18] PG 'lpad(CAST($1 AS text),$2,\'0\')' MS 'RIGHT(REPLICATE(\'0\', $2) + $1, $2)';

curStringValueNumerator 'Тек. значение (строка)' (numerator) = lpadStringWithZero(curValueNumerator(numerator), stringLengthNumerator(numerator)) IN base;

incrementValueNumerator 'Увеличить значение' = ACTION (numerator) NEWSESSION {
    IF curValueNumerator(numerator) >= maxValueNumerator(numerator) THEN
        MESSAGE 'Счетчик нумератора ' + nameNumerator(numerator) + ' достиг максимального значения. Обратитесь к администратору.'
    ELSE {
        curValueNumerator(numerator) <- curValueNumerator(numerator) + 1;
        apply();
    };
};

incrementValueSessionNumerator 'Увеличить значение' = ACTION (numerator) {
    IF curValueNumerator(numerator) >= maxValueNumerator(numerator) THEN
        MESSAGE 'Счетчик нумератора достиг максимального значения. Обратитесь к администратору.'
    ELSE
        ASSIGN curValueNumerator(numerator) <- curValueNumerator(numerator) + 1;
}

FORM numerator 'Нумератор'
    OBJECTS n = Numerator FIXED PANEL
    PROPERTIES(n) nameNumerator, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator

    EDIT Numerator OBJECT n
;

FORM numerators 'Нумераторы'
    OBJECTS n = Numerator
    PROPERTIES(n) READONLY nameNumerator, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator
    PROPERTIES(n)          ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR

    DIALOG Numerator OBJECT n
;

FORM defaultNumerators 'Нумераторы'
    OBJECTS n = Numerator
    PROPERTIES(n) READONLY nameNumerator, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator
    PROPERTIES(n)          ADDFORM, EDITFORM, DELETE FORCE PANEL TOOLBAR
;

DESIGN defaultNumerators FROM DEFAULT {
    main {
        NEW defaultContainer AFTER n.box {
            type = COLUMNS;
            columns = 3;
            caption = 'Нумераторы по умолчанию';
        }
    }
}

NAVIGATOR {
    masterData {
        ADD defaultNumerators;
    }
}

// --------------------------------------------- Макрос по объявлению нумерируемого объекта ---------------------------- //

GROUP numbered 'Нумератор' : recognize;

META defineNumberedAbstract(class)
    number###class 'Номер' = ABSTRACT STRING[18] (class) IN numbered PERSISTENT MINCHARWIDTH 7;
    series###class 'Серия' = ABSTRACT STRING[2] (class) IN numbered PERSISTENT FIXEDCHARWIDTH 3; 

    seriesNumber###class 'Серия/Номер' (o) = 
        CONCAT '', series###class(o), number###class(o) 
        MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;
        
END

useUpperedSeries 'Использовать верхний регистр для серии' = DATA BOOLEAN (); 

EXTEND FORM options
    PROPERTIES () useUpperedSeries
;
EXTEND DESIGN options{
    commons {
        ADD PROPERTY (useUpperedSeries());
    }    
}

META defineNumbered(class)
    number###class 'Номер' = DATA STRING[18] (class) IN numbered MINCHARWIDTH 7;
    series###class 'Серия' = DATA STRING[2] (class) IN numbered FIXEDCHARWIDTH 3;
     
    WHEN useUpperedSeries() AND CHANGED(series###class(o))
         DO series###class(o) <- upper(series###class(o));

    seriesNumber###class 'Серия/Номер' (o) = 
        CONCAT '', series###class(o), number###class(o) 
        MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;
        
END

META defineNumerated(class)
    numerator###class 'Нумератор (ИД)' = DATA Numerator (class);
    nameNumerator###class 'Нумератор' (o) = nameNumerator(numerator###class(o)) IN numbered MINCHARWIDTH 7 PREFCHARWIDTH 15;

    generateSeriesNumber###class 'Сгенерировать номер' = ACTION (o) {
        number###class(o) <- curStringValueNumerator(numerator###class(o));
        series###class(o) <- seriesNumerator(numerator###class(o));
        incrementValueSessionNumerator(numerator###class(o));
    }
    
    WHEN SETCHANGED(numerator###class(o)) AND
         NOT CHANGED(number###class(o)) AND
         NOT CHANGED(series###class(o))
         DO generateSeriesNumber###class(o) ;
END

META defineNumeratedDefault(class, captionV, seriesV)
    @defineNumerated(class);
    @defineNumeratedDefault(class, captionV, seriesV, );
END

GROUP defaultNumerator 'Нумераторы по умолчанию' : public;

META defineNumeratedDefault(class, captionV, seriesV, prefix)

    prefix###defaultNumerator###class = DATA Numerator ();
    name###prefix##DefaultNumerator###class captionV = nameNumerator(prefix###defaultNumerator###class()) IN defaultNumerator;

    numerator###class(o) <- prefix###defaultNumerator###class() WHEN SET(o IS class);

//    initValueNumeratorObject(object) += prefix###defaultNumerator###class() IF object IS class;
//    initWhenNumeratorObject(object) += SET(object IS class);

    EXTEND FORM defaultNumerators
        PROPERTIES() name###prefix##DefaultNumerator###class
    ;
    
    loadDefaultNumerators() += ACTION () {
        FOR ADDOBJ n = Numerator DO {
            ASSIGN nameNumerator(n) <- captionV;
            ASSIGN seriesNumerator(n) <- seriesV;
            ASSIGN minValueNumerator(n) <- 1L;
            ASSIGN maxValueNumerator(n) <- 99999L;
            ASSIGN stringLengthNumerator(n) <- 5;

            ASSIGN prefix###defaultNumerator###class() <- n;
        }
    }

END

//------------------------------------------ Для двух объектов ---------------------------------------------//

META defineNumerated(object1, object2)

    number###object1###object2 'Номер' = DATA STRING[18] (###object1, ###object2) MINCHARWIDTH 7;
    series###object1###object2 'Серия' = DATA STRING[2] (###object1, ###object2) FIXEDCHARWIDTH 3;
    seriesNumber###object1###object2 'Серия/Номер' (object1, object2) = CONCAT '', series###object1###object2(object1, object2), number###object1###object2(object1, object2) MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

    numerator###object1###object2 = DATA Numerator (###object1, ###object2);
    nameNumerator###object1###object2 'Нумератор' (object1, object2) = nameNumerator(numerator###object1###object2(object1, object2)) MINCHARWIDTH 7 PREFCHARWIDTH 15;

    setValueSessionNumerated###object1###object2 'Сгенерировать номер' = ACTION (object1, object2) {
        ASSIGN number###object1###object2(object1, object2) <- curStringValueNumerator(numerator###object1###object2(object1, object2));
        ASSIGN series###object1###object2(object1, object2) <- seriesNumerator(numerator###object1###object2(object1, object2));
        EXEC incrementValueSessionNumerator(numerator###object1###object2(object1, object2));
    } SHORTCUT number###object1###object2;

    WHEN CHANGED(numerator###object1###object2(object1, object2)) AND
         NOT CHANGED(number###object1###object2(object1, object2)) AND
         NOT CHANGED(series###object1###object2(object1, object2))
         DO EXEC setValueSessionNumerated###object1###object2(object1, object2) ;

END

META defineNumeratedDefault (object1, object2, property, captionV, seriesV)

    defaultNumerator###object1###object2 = DATA Numerator ();
    nameDefaultNumerator###object1###object2 captionV = nameNumerator(defaultNumerator###object1###object2());

    EXTEND FORM defaultNumerators
        PROPERTIES() nameDefaultNumerator###object1###object2
    ;
    EXTEND DESIGN defaultNumerators {
        defaultContainer {
            ADD PROPERTY(nameDefaultNumerator###object1###object2);
        }
    }

    loadDefaultNumerators() += ACTION () {
        FOR ADDOBJ n = Numerator DO {
            nameNumerator(n) <- captionV;
            seriesNumerator(n) <- seriesV;
            minValueNumerator(n) <- 1L;
            maxValueNumerator(n) <- 99999L;
            stringLengthNumerator(n) <- 5;

            defaultNumerator###object1###object2() <- n;
        }
    };

    numerator###object1###object2 (object1, object2) <- defaultNumerator###object1###object2 () WHEN SET(property(object1, object2));
END

META defineNumeratedID(object, caption)
    defaultNumerator##object = DATA Numerator ();
    nameDefaultNumerator##object caption = nameNumerator(defaultNumerator##object()) IN defaultNumerator;
    WHEN SET(o IS object) AND defaultNumerator##object() AND NOT id##object(o) DO {
        id##object(o) <- CONCAT '', seriesNumerator(defaultNumerator##object()), curStringValueNumerator(defaultNumerator##object());  
        incrementValueSessionNumerator(defaultNumerator##object());
    } 
    
    EXTEND FORM defaultNumerators PROPERTIES() nameDefaultNumerator##object;
END
// --------------------------------------------- Стандартные значения ---------------------------- //

loadDefaultNumerators 'Загрузить стандартные нумераторы' = ABSTRACT ACTION LIST ();
@implementLoadDefaultData(loadDefaultNumerators);
