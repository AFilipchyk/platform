MODULE Word;

REQUIRE System, MasterData;

CLASS Template 'Шаблон';
TABLE template(Template);

name 'Имя' = DATA VARSTRING[100] (Template);
file 'Файл шаблона' (Template) = DATA WORDFILE (Template);
loadFile 'Загрузить шаблон'(Template t) = ACTION LOADFILE file(t);
openFile 'Открыть шаблон'(Template t) = ACTION OPENFILE file(t);
id 'Идентификатор' = DATA VARISTRING[100] (Template) MINCHARWIDTH 5 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

process = ACTION CUSTOM 'lsfusion.erp.utils.word.ProcessTemplateActionProperty' (Template);

resultTemplate = DATA LOCAL WORDFILE ();
openResultTemplate () = ACTION OPENFILE resultTemplate();

openProcess 'Сгенерировать документ'(Template template) = ACTION  {
    process(template);
    openResultTemplate();
}

templateId (string1) = GROUP AGGR Template template BY id(template) WHERE template IS Template;

CLASS TemplateEntry 'Строка шаблона';
TABLE templateEntry(TemplateEntry);

template 'Шаблон' = DATA Template (TemplateEntry);
isTable 'Таблица' = DATA BOOLEAN(TemplateEntry);
key 'Параметр' = DATA VARSTRING[100](TemplateEntry);
value 'Значение' = DATA LOCAL TEXT (TemplateEntry);

templateEntryKeyEntryIdTemplate (string1, string2) = GROUP AGGR TemplateEntry templateEntry BY  key(templateEntry), id(template(templateEntry)) WHERE templateEntry IS TemplateEntry;

FORM template 'Шаблон Word'

    OBJECTS t = Template FIXED PANEL
    PROPERTIES (t) name, id
    PROPERTIES (t) loadFile, openFile

    OBJECTS te = TemplateEntry
    PROPERTIES (te) isTable, key, value, ADDOBJ, DELETESESSION
    PROPERTIES (t) TODRAW te FORCE PANEL TOOLBAR openProcess

    FILTERS template (te) == t

    EDIT Template OBJECT t
;

DESIGN template {
    main{
        preferredSize = (1024, 768);
        type = CONTAINERV;
        MOVE t.box {
            type = CONTAINERH;
            NEW row1 {
                MOVE PROPERTY(name(t));
                MOVE PROPERTY(id(t));
            };
            NEW row2 {
                type = CONTAINERH;
                MOVE PROPERTY(loadFile(t));
                MOVE PROPERTY(openFile(t));
            };
        }
        MOVE te.box;
    }
    MOVE functions.box;
}


FORM templates 'Шаблоны Word'

    OBJECTS t = Template
    PROPERTIES (t) READONLY name, id
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    OBJECTS te = TemplateEntry
    PROPERTIES (te) READONLY isTable, key, value
    FILTERS template (te) == t
;

FORM dialogTemplates 'Шаблоны Word'

    OBJECTS t = Template
    PROPERTIES (t) READONLY name, id
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    DIALOG Template OBJECT t
;
DESIGN dialogTemplates {
    main {
        preferredSize = (1024, 768);
    }
}


NAVIGATOR {
    masterData {
        templatesNavigator  {
            ADD templates;
        }
    }
}