MODULE Word;

REQUIRE System, MasterData;

CLASS Template 'Шаблон';
TABLE template(Template);

nameTemplate 'Имя' = DATA VARSTRING[100] (Template);
fileTemplate 'Файл шаблона' (Template) = DATA WORDFILE (Template);
loadFileTemplate 'Загрузить шаблон'(t) = ACTION LOADFILE fileTemplate(t);
openFileTemplate 'Открыть шаблон'(t) = ACTION OPENFILE fileTemplate(t);
idTemplate 'Идентификатор' = DATA VARSTRING[20] (Template) MINCHARWIDTH 5 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

processTemplate = ACTION CUSTOM 'lsfusion.erp.utils.word.ProcessTemplateActionProperty' (Template);

resultTemplate = DATA LOCAL WORDFILE ();
openResultTemplate () = ACTION OPENFILE resultTemplate();

openProcessTemplate 'Сгенерировать документ' = ACTION (template) {
    processTemplate(template);
    openResultTemplate();
}

templateId (string1) = GROUP AGGR template BY idTemplate(template) WHERE template IS Template;

CLASS TemplateEntry 'Строка шаблона';
TABLE templateEntry(TemplateEntry);

templateTemplateEntry 'Шаблон' = DATA Template (TemplateEntry);
keyTemplateEntry 'Параметр' = DATA VARSTRING[100](TemplateEntry);
valueTemplateEntry 'Значение' = DATA LOCAL TEXT (TemplateEntry);

templateEntryKeyEntryIdTemplate (string1, string2) = GROUP AGGR templateEntry BY  keyTemplateEntry(templateEntry), idTemplate(templateTemplateEntry(templateEntry)) WHERE templateEntry IS TemplateEntry;

FORM template 'Шаблон Word'

    OBJECTS t = Template FIXED PANEL
    PROPERTIES (t) nameTemplate, idTemplate
    PROPERTIES (t) loadFileTemplate, openFileTemplate

    OBJECTS te = TemplateEntry
    PROPERTIES (te) keyTemplateEntry, valueTemplateEntry, ADDOBJ, DELETESESSION
    PROPERTIES (t) TODRAW te FORCE PANEL TOOLBAR openProcessTemplate

    FILTERS templateTemplateEntry (te) == t

    EDIT Template OBJECT t
;

DESIGN template {
    main{
        preferredSize = (1024, 768);
        type = CONTAINERV;
        MOVE t.box {
            type = CONTAINERH;
            NEW row1 {
                MOVE PROPERTY(nameTemplate(t));
                MOVE PROPERTY(idTemplate(t));
            };
            NEW row2 {
                type = CONTAINERH;
                MOVE PROPERTY(loadFileTemplate(t));
                MOVE PROPERTY(openFileTemplate(t));
            };
        }
        MOVE te.box;
    }
    MOVE functions.box;
}


FORM templates 'Шаблоны Word'

    OBJECTS t = Template
    PROPERTIES (t) READONLY nameTemplate, idTemplate
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    OBJECTS te = TemplateEntry
    PROPERTIES (te) READONLY keyTemplateEntry, valueTemplateEntry
    FILTERS templateTemplateEntry (te) == t
;

FORM dialogTemplates 'Шаблоны Word'

    OBJECTS t = Template
    PROPERTIES (t) READONLY nameTemplate, idTemplate
    PROPERTIES (t) ADDFORM, EDITFORM, DELETE

    DIALOG Template OBJECT t
;
DESIGN dialogTemplates {
    main {
        preferredSize = (1024, 768);
    }
}


NAVIGATOR {
    masterData {
        templatesNavigator  {
            ADD templates;
        }
    }
}