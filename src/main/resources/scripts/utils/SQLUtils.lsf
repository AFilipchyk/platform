MODULE SQLUtils;

REQUIRE System, Authentication, Utils, Time;

CLASS ProcessType 'Фильтр процессов' {
    all 'Все',
    activeAll 'Все активные',
    activeSQL 'Активные (SQL)',
    activeJava 'Активные (Java)'
}

getActiveTasksAction 'Обновить список активных процессов' = ACTION CUSTOM 'lsfusion.erp.utils.GetActiveTasksActionProperty' () TOOLBAR;
getAllTasksAction 'Обновить список всех процессов' = ACTION CUSTOM 'lsfusion.erp.utils.GetAllTasksActionProperty' () TOOLBAR;
cancelActiveTaskAction 'Снять процесс' = ACTION CUSTOM 'lsfusion.erp.utils.CancelActiveTaskActionProperty' (INTEGER);
killActiveTaskAction 'Убить процесс' = ACTION CUSTOM 'lsfusion.erp.utils.KillActiveTaskActionProperty' (INTEGER);

previousCountActiveTask =  DATA LOCAL INTEGER();
idActiveTask 'ID процесса' = DATA LOCAL INTEGER (INTEGER);
queryActiveTask 'Запрос' = DATA LOCAL TEXT (INTEGER);
computerActiveTask 'Компьютер' = DATA LOCAL VARSTRING[100] (INTEGER);
userActiveTask 'Пользователь' = DATA LOCAL VARSTRING[100] (INTEGER);
addressUserActiveTask 'Адрес пользователя' = DATA LOCAL VARSTRING[100] (INTEGER);
dateTimeActiveTask 'Время запуска' = DATA LOCAL DATETIME (INTEGER);

FORM activeTasks 'Активные процессы'
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY idActiveTask, queryActiveTask, computerActiveTask, userActiveTask, addressUserActiveTask, dateTimeActiveTask
    PROPERTIES(i) TOOLBAR cancelActiveTaskAction, killActiveTaskAction
    PROPERTIES() getActiveTasksAction TOOLBAR TODRAW i, getAllTasksAction TOOLBAR TODRAW i
    FILTERS idActiveTask(i)
;

getActiveBlocksAction 'Обновить список блокировок' = ACTION CUSTOM 'lsfusion.erp.utils.GetActiveBlocksActionProperty' () TOOLBAR;

previousCountActiveBlock =  DATA LOCAL INTEGER();
processIdActiveBlock 'ID процесса блокировки' = DATA LOCAL INTEGER (INTEGER);
typeLockedObjectActiveBlock 'Тип заблокированного объекта' = DATA LOCAL VARSTRING[100] (INTEGER);
idLockedObjectActiveBlock 'ID заблокированного объекта' = DATA LOCAL VARSTRING[100] (INTEGER);
modeActiveBlock 'Уровень блокировки' = DATA LOCAL VARSTRING[100] (INTEGER);
grantedActiveBlock 'Granted' = DATA LOCAL BOOLEAN (INTEGER);

FORM activeBlocks 'Активные блокировки'
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY processIdActiveBlock, typeLockedObjectActiveBlock, idLockedObjectActiveBlock, modeActiveBlock, grantedActiveBlock
    PROPERTIES() getActiveBlocksAction TOOLBAR TODRAW i
    FILTERS processIdActiveBlock(i)
;

CLASS StateProcess 'Статус процесса' {
    active 'Active',
    idle 'Idle in transaction'
}

updateProcessMonitorAction 'Обновить монитор процессов' = ACTION CUSTOM 'lsfusion.erp.utils.UpdateProcessMonitorActionProperty' () TOOLBAR;
updateThreadAllocatedBytesAction = ACTION CUSTOM 'lsfusion.erp.utils.UpdateThreadAllocatedBytesActionProperty' ();
killJavaProcess 'Убить java-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.KillJavaProcessActionProperty' (VARSTRING[10]);
killSQLProcess 'Убить SQL-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.KillSQLProcessActionProperty' (VARSTRING[10]);
cancelJavaProcess 'Снять java-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.CancelJavaProcessActionProperty' (VARSTRING[10]);
cancelSQLProcess 'Снять SQL-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.CancelSQLProcessActionProperty' (VARSTRING[10]);

readAllocatedBytes 'Показывать выделенную память' = DATA BOOLEAN ();
WHEN CHANGED (readAllocatedBytes()) DO updateThreadAllocatedBytesAction();
onStarted() += ACTION updateThreadAllocatedBytesAction();

processType 'Фильтр процессов'= DATA LOCAL ProcessType();
nameProcessType 'Фильтр процессов' () = staticName(processType()) PREFCHARWIDTH 15;
captionProcessType 'Фильтр процессов' () = staticCaption(processType());
WHEN SESSION CHANGED (processType()) DO EXEC updateProcessMonitorAction();
 
idThreadProcess 'ID потока процесса' = DATA LOCAL VARSTRING[10] (VARSTRING[10]);

nameComputerJavaProcess 'Компьютер (Java)' = DATA LOCAL VARSTRING[100] (VARSTRING[10]);
computerProcess 'Компьютер (SQL)' = DATA LOCAL INTEGER (VARSTRING[10]);
objValueComputer (i) = INTEGER(i AS Computer);
computerObject (value) = GROUP AGGR computer BY objValueComputer(computer);
nameComputerSQLProcess 'Компьютер (SQL)' (process) = hostnameComputer(computerObject(computerProcess(process)));
nameComputerProcess 'Компьютер' (process) = OVERRIDE nameComputerJavaProcess(process), nameComputerSQLProcess(process);
  
nameUserJavaProcess 'Пользователь (Java)' = DATA LOCAL VARSTRING[100] (VARSTRING[10]);
userProcess 'Пользователь (SQL)' = DATA LOCAL INTEGER (VARSTRING[10]);
objValueUser (i) = INTEGER(i AS User);
userObject (value) = GROUP AGGR user BY objValueUser(user);
nameUserSQLProcess 'Пользователь (SQL)' (process) = nameUser(userObject(userProcess(process)));
nameUserProcess 'Пользователь' (process) = OVERRIDE nameUserJavaProcess(process), nameUserSQLProcess(process);
dateTimeCallProcess 'Время начала вызова' = DATA LOCAL DATETIME (VARSTRING[10]);

idSQLProcess 'ID процесса (SQL)' = DATA LOCAL INTEGER (VARSTRING[10]);
querySQLProcess 'Запрос (SQL)' = DATA LOCAL TEXT (VARSTRING[10]);
fullQuerySQLProcess 'Полный запрос (SQL)' = DATA LOCAL TEXT (VARSTRING[10]);
addressUserSQLProcess 'Адрес пользователя (SQL)' = DATA LOCAL VARSTRING[100] (VARSTRING[10]);
dateTimeSQLProcess 'Время запуска (SQL)' = DATA LOCAL DATETIME (VARSTRING[10]);
isActiveSQLProcess 'Активный (SQL)' = DATA LOCAL BOOLEAN (VARSTRING[10]);
inTransactionSQLProcess 'В транзакции (SQL)' = DATA LOCAL BOOLEAN (VARSTRING[10]);
startTransactionSQLProcess 'Время начала транзакции (SQL)' = DATA LOCAL DATETIME (VARSTRING[10]);
attemptCountSQLProcess 'Номер попытки (SQL)' = DATA LOCAL VARSTRING[20] (VARSTRING[10]);
statusMessageSQLProcess 'Статус (SQL)' = DATA LOCAL TEXT (VARSTRING[10]);

lsfStackTraceProcess 'LSF след потока' = DATA LOCAL TEXT (VARSTRING[10]);
lastThreadAllocatedBytesProcess 'Память за последний интервал' = DATA LOCAL LONG (VARSTRING[10]);
threadAllocatedBytesProcess 'Выделенная память' = DATA LOCAL LONG (VARSTRING[10]);

stackTraceJavaProcess 'След java-потока (Java)' = DATA LOCAL TEXT (VARSTRING[10]);
hasStackTraceJavaProcess (i) = stackTraceJavaProcess(i) AND i IS VARSTRING[10];
nameJavaProcess 'Имя java-потока (Java)' = DATA LOCAL VARSTRING[100] (VARSTRING[10]);
statusJavaProcess 'Статус (Java)' = DATA LOCAL VARSTRING[100] (VARSTRING[10]);
lockNameJavaProcess 'Блокировка (Java)' = DATA LOCAL VARSTRING[100] (VARSTRING[10]);

lockOwnerIdProcess 'ID блокирующего потока' = DATA LOCAL VARSTRING[10] (VARSTRING[10]);
lockOwnerNameProcess 'Блокирующий поток' = DATA LOCAL VARSTRING[100] (VARSTRING[10]);
isDisabledNestLoopProcess 'Disabled nested loop' = DATA LOCAL BOOLEAN (VARSTRING[10]);
queryTimeoutProcess 'Query Timeout' = DATA LOCAL INTEGER (VARSTRING[10]);
   
processIdThread (id) = GROUP AGGR process BY idThreadProcess(process) WHERE process IS VARSTRING[10];
threadOwnerProcess (i) = processIdThread(lockOwnerIdProcess(i));
                                                                                                                                                                                                                                                          
blockingProcess 'Уровень' (child, parent) = RECURSION 1l IF idThreadProcess(child) AND parent == child
                                                        STEP 1l IF parent == threadOwnerProcess($parent);                                                                                                                                                                                                                                                       
isBlockingProcess 'Блокируется' (child, parent) = TRUE IF blockingProcess(child, parent);
sumPlusOneBlockedProcess  (process) = GROUP SUM 1 IF isBlockingProcess(child, parent) BY parent;
sumBlockedProcess 'Кол-во заблокированных процессов' (process) = sumPlusOneBlockedProcess(process) - 1;
deadlockProcess (process) = GROUP SUM 1 IF isBlockingProcess(parent, child) AND statusJavaProcess(child) != 'BLOCKED' BY parent;
isDeadlockProcess 'Deadlock' (process) = NOT deadlockProcess(process) AND process IS VARSTRING[10];
sumPlusOneBlockingProcess 'Кол-во блокирующих процессов' (process) = GROUP SUM 1 IF isBlockingProcess(parent, child) BY parent;
sumBlockingProcess 'Глубина блокировки' (process) = sumPlusOneBlockingProcess(process) - 1;

activeSQLProcess (i) = querySQLProcess(i) AND isActiveSQLProcess(i);
activeJavaBlockingProcess (i) = statusJavaProcess(i) == 'RUNNABLE' AND hasStackTraceJavaProcess(i);
activeJavaProcess (i) = activeJavaBlockingProcess(i)
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.DualStackPlainSocketImpl') == 1)
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'sun.awt.windows.WToolkit.eventLoop') == 1)
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.SocketInputStream.socketRead0') == 1)
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'sun.management.ThreadImpl.dumpThreads0') == 1)
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.SocketOutputStream.socketWrite') == 1)                                        
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.PlainSocketImpl') == 1)
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'java.io.FileInputStream.readBytes') == 1)
                                   AND NOT (startsWith(stackTraceJavaProcess(i), 'java.lang.UNIXProcess.waitForProcessExit') == 1);
activeBlockingProcess 'Активный' (i) = activeSQLProcess(i) OR activeJavaBlockingProcess(i);
activeProcess 'Активный' (i) = activeSQLProcess(i) OR activeJavaProcess(i);

blockedProcess 'Заблокированный' (i) = statusJavaProcess(i) == 'BLOCKED' OR lockOwnerIdProcess(i);             

intToColor = FORMULA '(256*256*($1)+256*($2)+($3))::integer';

deltaDateTimeProcess(i) = subtractSeconds(dateTimeSQLProcess(i), currentDateTime());
deltaTransactionProcess(i) = subtractSeconds(startTransactionSQLProcess(i), currentDateTime());

red(i) = 255 IF i IS VARSTRING[10];
green(i) = CASE
                        WHEN deltaTransactionProcess(i) > 90 THEN 105
                        WHEN deltaTransactionProcess(i) > 80 THEN 125
                        WHEN deltaTransactionProcess(i) > 70 THEN 145
                        WHEN deltaTransactionProcess(i) > 60 THEN 165
                        WHEN deltaTransactionProcess(i) > 50 THEN 185
                        WHEN deltaTransactionProcess(i) > 40 THEN 205
                        WHEN deltaTransactionProcess(i) > 30 THEN 225
                        WHEN deltaTransactionProcess(i) > 20 THEN 245
                        WHEN i IS VARSTRING[10] THEN 255;
blue(i) = CASE
                        WHEN deltaDateTimeProcess(i) > 90 THEN 0
                        WHEN deltaDateTimeProcess(i) > 80 THEN 30
                        WHEN deltaDateTimeProcess(i) > 70 THEN 60
                        WHEN deltaDateTimeProcess(i) > 60 THEN 90
                        WHEN deltaDateTimeProcess(i) > 50 THEN 120
                        WHEN deltaDateTimeProcess(i) > 40 THEN 150
                        WHEN deltaDateTimeProcess(i) > 30 THEN 180
                        WHEN deltaDateTimeProcess(i) > 20 THEN 210
                        WHEN i IS VARSTRING[10] THEN 240;
sqlColorProcess(i) = COLOR(intToColor(red(i), green(i), blue(i)));
backgroundProcess(i) = IF isActiveSQLProcess(i) OR startsWith(idThreadProcess(i), 's') THEN sqlColorProcess(i) ELSE RGB(240, 255, 255);  
                   
cancelProcess 'Снять процесс' (i) = ACTION {
    IF idSQLProcess(i) THEN cancelSQLProcess(i) ELSE cancelJavaProcess(i);
    updateProcessMonitorAction();
}           
killProcess 'Убить процесс' (i) = ACTION { 
    IF isActiveSQLProcess(i) THEN killSQLProcess(i) ELSE killJavaProcess(i);
    updateProcessMonitorAction();
}
showExtraButtons(i) = idSQLProcess(i) AND NOT startsWith(idThreadProcess(i), 's');
               
//test1Action = ACTION CUSTOM 'lsfusion.erp.utils.TestActionProperty'();
//test2Action = ACTION CUSTOM 'lsfusion.erp.utils.Test2ActionProperty'();
//test3Action = ACTION CUSTOM 'lsfusion.erp.utils.Test3ActionProperty'();
//test4Action = ACTION CUSTOM 'lsfusion.erp.utils.Test4ActionProperty'();
              
setDefaultProcessType = ACTION () {
    processType() <- ProcessType.activeSQL;
}               
               
FORM processMonitor 'Монитор процессов'
    //PROPERTIES() test1Action, test2Action, test3Action, test4Action
    OBJECTS i = VARSTRING[10]
    PROPERTIES(i) READONLY BACKGROUND backgroundProcess(i) activeProcess, idThreadProcess, nameComputerProcess, nameUserProcess,
                           dateTimeCallProcess, querySQLProcess, dateTimeSQLProcess, isActiveSQLProcess, inTransactionSQLProcess, 
                           startTransactionSQLProcess, attemptCountSQLProcess, statusMessageSQLProcess,
                           addressUserSQLProcess, lsfStackTraceProcess, threadAllocatedBytesProcess SHOWIF readAllocatedBytes(), 
                           lastThreadAllocatedBytesProcess SHOWIF readAllocatedBytes(), nameJavaProcess, statusJavaProcess, lockNameJavaProcess, 
                           stackTraceJavaProcess, lockOwnerIdProcess, lockOwnerNameProcess, isDisabledNestLoopProcess, queryTimeoutProcess, 
                           idSQLProcess, fullQuerySQLProcess
    PROPERTIES(i) TOOLBAR cancelProcess, killProcess, cancelJavaProcess SHOWIF showExtraButtons(i), killJavaProcess SHOWIF showExtraButtons(i)                       
    FILTERS idThreadProcess(i), NOT statusJavaProcess(i) OR statusJavaProcess(i) != 'BLOCKED'
    EVENTS ON INIT setDefaultProcessType()
    
    TREE blocking i2 = VARSTRING[10] PARENT threadOwnerProcess
    PROPERTIES(i2) READONLY BACKGROUND backgroundProcess(i2) sumBlockedProcess,
                            activeProcess, idThreadProcess, nameComputerProcess, nameUserProcess, dateTimeCallProcess,
                            querySQLProcess, dateTimeSQLProcess, isActiveSQLProcess, inTransactionSQLProcess, 
                            startTransactionSQLProcess, attemptCountSQLProcess, statusMessageSQLProcess,
                            addressUserSQLProcess, lsfStackTraceProcess, threadAllocatedBytesProcess SHOWIF readAllocatedBytes(), 
                            lastThreadAllocatedBytesProcess SHOWIF readAllocatedBytes(), nameJavaProcess, statusJavaProcess, lockNameJavaProcess, 
                            stackTraceJavaProcess, lockOwnerIdProcess, lockOwnerNameProcess, isDisabledNestLoopProcess, queryTimeoutProcess, 
                            idSQLProcess, fullQuerySQLProcess
    PROPERTIES(i2) TOOLBAR cancelProcess, killProcess, cancelJavaProcess SHOWIF isActiveSQLProcess(i2), killJavaProcess SHOWIF isActiveSQLProcess(i2)  
    FILTERS idThreadProcess(i2)
    FILTERGROUP filter FILTER 'Фильтр' (activeBlockingProcess(i2) AND sumBlockedProcess(i2) > 0) OR (lockOwnerIdProcess(i2) AND lockOwnerIdProcess(i2) != '-1') DEFAULT   
    
    OBJECTS blocked = VARSTRING[10], blocking = VARSTRING[10]
    PROPERTIES(blocked) READONLY idThreadProcess, isDeadlockProcess, 
                                 sumBlockingProcess, nameComputerProcess, nameUserProcess, dateTimeCallProcess,
                                 querySQLProcess, dateTimeSQLProcess, isActiveSQLProcess, inTransactionSQLProcess, 
                                 startTransactionSQLProcess, attemptCountSQLProcess, statusMessageSQLProcess,
                                  lsfStackTraceProcess, threadAllocatedBytesProcess SHOWIF readAllocatedBytes(), 
                                 lastThreadAllocatedBytesProcess SHOWIF readAllocatedBytes(), nameJavaProcess, statusJavaProcess, 
                                 lockNameJavaProcess, stackTraceJavaProcess, lockOwnerIdProcess, lockOwnerNameProcess, isDisabledNestLoopProcess, 
                                 queryTimeoutProcess, idSQLProcess, fullQuerySQLProcess
    PROPERTIES(blocking) READONLY activeProcess, idThreadProcess, sumBlockingProcess, nameComputerProcess, nameUserProcess, dateTimeCallProcess,
                                 querySQLProcess, dateTimeSQLProcess, isActiveSQLProcess, inTransactionSQLProcess, 
                                 startTransactionSQLProcess, attemptCountSQLProcess, statusMessageSQLProcess,
                                 addressUserSQLProcess, lsfStackTraceProcess, threadAllocatedBytesProcess SHOWIF readAllocatedBytes(), 
                                 lastThreadAllocatedBytesProcess SHOWIF readAllocatedBytes(), nameJavaProcess, statusJavaProcess,
                                 lockNameJavaProcess, stackTraceJavaProcess, lockOwnerIdProcess, lockOwnerNameProcess, isDisabledNestLoopProcess, 
                                 queryTimeoutProcess, idSQLProcess, fullQuerySQLProcess
    PROPERTIES(blocking) TOOLBAR cancelProcess, killProcess, cancelJavaProcess SHOWIF isActiveSQLProcess(blocking), killJavaProcess SHOWIF isActiveSQLProcess(blocking)                                   
    FILTERS idThreadProcess(blocked), sumBlockingProcess(blocked) > 0, sumBlockedProcess(blocking) > 0, isBlockingProcess(blocked, blocking) AND blocked != blocking
    FILTERGROUP nonBlocking FILTER 'Только не блокирующие' sumBlockedProcess(blocked) == 0 OR isDeadlockProcess(blocked) DEFAULT 
    ORDER BY sumBlockingProcess(blocked) DESC, sumBlockingProcess(blocking) DESC
    
    PROPERTIES() readAllocatedBytes, captionProcessType, updateProcessMonitorAction
;

DESIGN processMonitor {
    NEW top {
        type = TABBED;
        fill = 1;
        NEW allProcesses {
            fill = 1;
            caption = 'Все процессы';
            MOVE i.box {
            caption = 'Процессы';
            }
        }
        NEW blockingProcesses {
            fill = 1;
            caption = 'Блокирующие';
            MOVE blocking.tree.box {
                caption = 'Блокирующие процессы';
            }
        }
        NEW blockedProcesses {
            fill = 1;
            caption = 'Блокированные';
            MOVE blocked.box {
                caption = 'Блокированные процессы';
            }
            MOVE blocking.box {
                caption = 'Блокирующие процессы';
            }
        }
    } 
    NEW bottom {
        type = CONTAINERH;
        align = TRAILING;
        MOVE PROPERTY(readAllocatedBytes());
        MOVE PROPERTY(captionProcessType());
        MOVE PROPERTY(updateProcessMonitorAction());
    }
    MOVE functions.box;
}

NAVIGATOR {
    systemEvents {
        ADD activeTasks;
        ADD activeBlocks;
        ADD processMonitor;
    }
}
