MODULE SQLUtils;

REQUIRE System, Authentication, Utils;

getActiveTasksAction 'Обновить список активных процессов' = ACTION CUSTOM 'lsfusion.erp.utils.GetActiveTasksActionProperty' () TOOLBAR;  
getAllTasksAction 'Обновить список всех процессов' = ACTION CUSTOM 'lsfusion.erp.utils.GetAllTasksActionProperty' () TOOLBAR;  
cancelActiveTaskAction 'Снять процесс' = ACTION CUSTOM 'lsfusion.erp.utils.CancelActiveTaskActionProperty' (INTEGER);  
killActiveTaskAction 'Убить процесс' = ACTION CUSTOM 'lsfusion.erp.utils.KillActiveTaskActionProperty' (INTEGER);  

previousCountActiveTask =  DATA LOCAL INTEGER();
idActiveTask 'ID процесса' = DATA LOCAL INTEGER (INTEGER);
queryActiveTask 'Запрос' = DATA LOCAL TEXT (INTEGER);
computerActiveTask 'Компьютер' = DATA LOCAL VARSTRING[100] (INTEGER);
userActiveTask 'Пользователь' = DATA LOCAL VARSTRING[100] (INTEGER);
addressUserActiveTask 'Адрес пользователя' = DATA LOCAL VARSTRING[100] (INTEGER);
dateTimeActiveTask 'Время запуска' = DATA LOCAL DATETIME (INTEGER);

FORM activeTasks 'Активные процессы' 
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY idActiveTask, queryActiveTask, computerActiveTask, userActiveTask, addressUserActiveTask, dateTimeActiveTask
    PROPERTIES(i) TOOLBAR cancelActiveTaskAction, killActiveTaskAction
    PROPERTIES() getActiveTasksAction TOOLBAR TODRAW i, getAllTasksAction TOOLBAR TODRAW i
    FILTERS idActiveTask(i)
;

getActiveBlocksAction 'Обновить список блокировок' = ACTION CUSTOM 'lsfusion.erp.utils.GetActiveBlocksActionProperty' () TOOLBAR;  

previousCountActiveBlock =  DATA LOCAL INTEGER();
processIdActiveBlock 'ID процесса блокировки' = DATA LOCAL INTEGER (INTEGER);
typeLockedObjectActiveBlock 'Тип заблокированного объекта' = DATA LOCAL VARSTRING[100] (INTEGER);
idLockedObjectActiveBlock 'ID заблокированного объекта' = DATA LOCAL VARSTRING[100] (INTEGER);
modeActiveBlock 'Уровень блокировки' = DATA LOCAL VARSTRING[100] (INTEGER);
grantedActiveBlock 'Granted' = DATA LOCAL BOOLEAN (INTEGER);

FORM activeBlocks 'Активные блокировки' 
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY processIdActiveBlock, typeLockedObjectActiveBlock, idLockedObjectActiveBlock, modeActiveBlock, grantedActiveBlock
    PROPERTIES() getActiveBlocksAction TOOLBAR TODRAW i
    FILTERS processIdActiveBlock(i)
;

CLASS StateProcess 'Статус процесса' {
    active 'Active',
    idle 'Idle in transaction'
}

updateProcessMonitorAction 'Обновить монитор процессов' = ACTION CUSTOM 'lsfusion.erp.utils.UpdateProcessMonitorActionProperty' () TOOLBAR; 
testAction 'Test' = ACTION CUSTOM 'lsfusion.erp.utils.TestActionActionProperty' () TOOLBAR;
previousCountProcess =  DATA LOCAL INTEGER();
idThreadProcess 'ID потока процесса' = DATA LOCAL VARSTRING[10] (INTEGER);
computerProcess 'Компьютер' = DATA LOCAL VARSTRING[100] (INTEGER);
userProcess 'Пользователь' = DATA LOCAL VARSTRING[100] (INTEGER);

querySQLProcess 'Запрос (SQL)' = DATA LOCAL TEXT (INTEGER);
addressUserSQLProcess 'Адрес пользователя (SQL)' = DATA LOCAL VARSTRING[100] (INTEGER);
dateTimeSQLProcess 'Время запуска (SQL)' = DATA LOCAL DATETIME (INTEGER);
stateSQLProcess 'Статус процесса (SQL)' = DATA StateProcess (INTEGER);
nameStateSQLProcess 'Статус процесса (SQL)' (i) = staticCaption(stateSQLProcess(i));

stackTraceJavaProcess 'След java-потока (Java)' = DATA LOCAL TEXT (INTEGER);
hasStackTraceJavaProcess (i) = stackTraceJavaProcess(i) AND i IS INTEGER;
nameJavaProcess 'Имя java-потока (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);
statusJavaProcess 'Статус (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);
lockNameJavaProcess 'Блокировка (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);     
lockOwnerIdJavaProcess 'ID блокирующего потока (Java)' = DATA LOCAL VARSTRING[10] (INTEGER);     
lockOwnerNameJavaProcess 'Имя блокирующего потока (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);
   
processIdThread (id) = GROUP AGGR process BY idThreadProcess(process) WHERE process IS INTEGER;  
threadOwnerProcess (i) = processIdThread(lockOwnerIdJavaProcess(i));                                                             
                                                                                                                                                                                                                                                          
blockingProcess 'Уровень' (child, parent) = RECURSION 1l IF idThreadProcess(child) AND parent == child
                                                        STEP 1l IF parent == threadOwnerProcess($parent);                                                                                                                                                                                                                                                               
isBlockingProcess 'Блокируется' (child, parent) = TRUE IF blockingProcess(child, parent);
sumPlusOneBlockedProcess  (process) = GROUP SUM 1 IF isBlockingProcess(child, parent) BY parent;
sumBlockedProcess 'Кол-во заблокированных процессов' (process) = sumPlusOneBlockedProcess(process) - 1;
sumPlusOneBlockingProcess 'Кол-во блокирующих процессов' (process) = GROUP SUM 1 IF isBlockingProcess(parent, child) BY parent;
sumBlockingProcess 'Глубина блокировки' (process) = sumPlusOneBlockingProcess(process) - 1;

activeProcess 'Активный' (i) = (querySQLProcess(i) AND stateSQLProcess(i) == StateProcess.active)
                                          OR (statusJavaProcess(i) == 'RUNNABLE' AND hasStackTraceJavaProcess(i) AND i IS INTEGER
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.DualStackPlainSocketImpl') == 1)
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'sun.awt.windows.WToolkit.eventLoop') == 1)
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.SocketInputStream.socketRead0') == 1)
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'sun.management.ThreadImpl.dumpThreads0') == 1));
FORM processMonitor 'Монитор процессов' 
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY idThreadProcess, computerProcess, userProcess, 
                           querySQLProcess, addressUserSQLProcess, dateTimeSQLProcess, nameStateSQLProcess,
                           stackTraceJavaProcess, nameJavaProcess, statusJavaProcess, lockNameJavaProcess, 
                           lockOwnerIdJavaProcess, lockOwnerNameJavaProcess, threadOwnerProcess
    FILTERS idThreadProcess(i)
    FILTERGROUP active FILTER 'Только активные' activeProcess(i) DEFAULT
    //ORDER BY sumBlockedProcess(i)
    
    TREE blocking i2 = INTEGER PARENT threadOwnerProcess
    PROPERTIES(i2) READONLY sumBlockedProcess,
                            idThreadProcess, computerProcess, userProcess, 
                            querySQLProcess, addressUserSQLProcess, dateTimeSQLProcess, nameStateSQLProcess,
                            stackTraceJavaProcess, nameJavaProcess, statusJavaProcess, lockNameJavaProcess, 
                            lockOwnerIdJavaProcess, lockOwnerNameJavaProcess, threadOwnerProcess                                                

    FILTERS idThreadProcess(i2)
    FILTERGROUP active2 FILTER 'Только активные' activeProcess(i2) OR sumPlusOneBlockingProcess(i2) > 1 DEFAULT
    //ORDER BY sumBlockedProcess(i2)
    
//    TREE blocked i3 = INTEGER PARENT threadSlaveProcess
//    PROPERTIES(i3) READONLY idThreadSlaveProcess, sumBlockingProcess,
//                            idThreadProcess, computerProcess, userProcess, 
//                            querySQLProcess, addressUserSQLProcess, dateTimeSQLProcess, nameStateSQLProcess,
//                            stackTraceJavaProcess, nameJavaProcess, statusJavaProcess, lockNameJavaProcess, 
//                            lockOwnerIdJavaProcess, lockOwnerNameJavaProcess, threadOwnerProcess                                                
//    FILTERS idThreadProcess(i3)
//    FILTERGROUP active3 FILTER 'Только активные' activeProcess(i3) OR sumPlusOneBlockingProcess(i3) > 1 DEFAULT
    //ORDER BY sumBlockingProcess(i3)
    
    PROPERTIES() updateProcessMonitorAction TODRAW i TOOLBAR, testAction TODRAW i TOOLBAR
;

DESIGN processMonitor {
    NEW top {
        type = TABBED;
        fill = 1;
        NEW allProcesses {
            fill = 1;
            caption = 'Все процессы';
            MOVE i.box;
        }
        NEW blockingProcesses {
            fill = 1;
            caption = 'Блокирующие';
            MOVE blocking.tree.box;
        }
//        NEW blockedProcesses {
//            fill = 1;
//            caption = 'Блокированные';
//            MOVE blocked.tree.box;
//        }
    }    
    MOVE PROPERTY(updateProcessMonitorAction()) {
        align = TRAILING;
    }
    MOVE PROPERTY(testAction()) {
        align = TRAILING; 
    }
    MOVE functions.box;
}

NAVIGATOR {
    systemEvents {
        ADD activeTasks;
        ADD activeBlocks;
        ADD processMonitor;
    }
}
