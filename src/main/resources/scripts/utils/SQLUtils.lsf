MODULE SQLUtils;

REQUIRE System, Authentication, Utils;

getActiveTasksAction 'Обновить список активных процессов' = ACTION CUSTOM 'lsfusion.erp.utils.GetActiveTasksActionProperty' () TOOLBAR;
getAllTasksAction 'Обновить список всех процессов' = ACTION CUSTOM 'lsfusion.erp.utils.GetAllTasksActionProperty' () TOOLBAR;
cancelActiveTaskAction 'Снять процесс' = ACTION CUSTOM 'lsfusion.erp.utils.CancelActiveTaskActionProperty' (INTEGER);
killActiveTaskAction 'Убить процесс' = ACTION CUSTOM 'lsfusion.erp.utils.KillActiveTaskActionProperty' (INTEGER);

previousCountActiveTask =  DATA LOCAL INTEGER();
idActiveTask 'ID процесса' = DATA LOCAL INTEGER (INTEGER);
queryActiveTask 'Запрос' = DATA LOCAL TEXT (INTEGER);
computerActiveTask 'Компьютер' = DATA LOCAL VARSTRING[100] (INTEGER);
userActiveTask 'Пользователь' = DATA LOCAL VARSTRING[100] (INTEGER);
addressUserActiveTask 'Адрес пользователя' = DATA LOCAL VARSTRING[100] (INTEGER);
dateTimeActiveTask 'Время запуска' = DATA LOCAL DATETIME (INTEGER);

FORM activeTasks 'Активные процессы'
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY idActiveTask, queryActiveTask, computerActiveTask, userActiveTask, addressUserActiveTask, dateTimeActiveTask
    PROPERTIES(i) TOOLBAR cancelActiveTaskAction, killActiveTaskAction
    PROPERTIES() getActiveTasksAction TOOLBAR TODRAW i, getAllTasksAction TOOLBAR TODRAW i
    FILTERS idActiveTask(i)
;

getActiveBlocksAction 'Обновить список блокировок' = ACTION CUSTOM 'lsfusion.erp.utils.GetActiveBlocksActionProperty' () TOOLBAR;

previousCountActiveBlock =  DATA LOCAL INTEGER();
processIdActiveBlock 'ID процесса блокировки' = DATA LOCAL INTEGER (INTEGER);
typeLockedObjectActiveBlock 'Тип заблокированного объекта' = DATA LOCAL VARSTRING[100] (INTEGER);
idLockedObjectActiveBlock 'ID заблокированного объекта' = DATA LOCAL VARSTRING[100] (INTEGER);
modeActiveBlock 'Уровень блокировки' = DATA LOCAL VARSTRING[100] (INTEGER);
grantedActiveBlock 'Granted' = DATA LOCAL BOOLEAN (INTEGER);

FORM activeBlocks 'Активные блокировки'
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY processIdActiveBlock, typeLockedObjectActiveBlock, idLockedObjectActiveBlock, modeActiveBlock, grantedActiveBlock
    PROPERTIES() getActiveBlocksAction TOOLBAR TODRAW i
    FILTERS processIdActiveBlock(i)
;

CLASS StateProcess 'Статус процесса' {
    active 'Active',
    idle 'Idle in transaction'
}

updateProcessMonitorAction 'Обновить монитор процессов' = ACTION CUSTOM 'lsfusion.erp.utils.UpdateProcessMonitorActionProperty' () TOOLBAR;
killJavaProcess 'Убить java-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.KillJavaProcessActionProperty' (INTEGER);
killSQLProcess 'Убить SQL-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.KillSQLProcessActionProperty' (INTEGER);
cancelJavaProcess 'Снять java-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.CancelJavaProcessActionProperty' (INTEGER);
cancelSQLProcess 'Снять SQL-процесс' = ACTION CUSTOM 'lsfusion.erp.utils.CancelSQLProcessActionProperty' (INTEGER);

//test1Action 'test1' = ACTION CUSTOM 'lsfusion.erp.utils.Test1ActionActionProperty' () TOOLBAR;
//test2Action 'test2' = ACTION CUSTOM 'lsfusion.erp.utils.Test2ActionActionProperty' () TOOLBAR;
//test3Action 'test3' = ACTION CUSTOM 'lsfusion.erp.utils.Test3ActionActionProperty' () TOOLBAR;
//test4Action 'test4' = ACTION CUSTOM 'lsfusion.erp.utils.Test4ActionActionProperty' () TOOLBAR;
previousCountProcess =  DATA LOCAL INTEGER();
idThreadProcess 'ID потока процесса' = DATA LOCAL VARSTRING[10] (INTEGER);
computerProcess 'Компьютер' = DATA LOCAL VARSTRING[100] (INTEGER);
userProcess 'Пользователь' = DATA LOCAL VARSTRING[100] (INTEGER);

querySQLProcess 'Запрос (SQL)' = DATA LOCAL TEXT (INTEGER);
addressUserSQLProcess 'Адрес пользователя (SQL)' = DATA LOCAL VARSTRING[100] (INTEGER);
dateTimeSQLProcess 'Время запуска (SQL)' = DATA LOCAL DATETIME (INTEGER);
stateSQLProcess 'Статус процесса (SQL)' = DATA StateProcess (INTEGER);
nameStateSQLProcess 'Статус процесса (SQL)' (i) = staticCaption(stateSQLProcess(i));

stackTraceJavaProcess 'След java-потока (Java)' = DATA LOCAL TEXT (INTEGER);
hasStackTraceJavaProcess (i) = stackTraceJavaProcess(i) AND i IS INTEGER;
nameJavaProcess 'Имя java-потока (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);
statusJavaProcess 'Статус (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);
lockNameJavaProcess 'Блокировка (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);
lockOwnerIdJavaProcess 'ID блокирующего потока (Java)' = DATA LOCAL VARSTRING[10] (INTEGER);
lockOwnerNameJavaProcess 'Имя блокирующего потока (Java)' = DATA LOCAL VARSTRING[100] (INTEGER);
   
processIdThread (id) = GROUP AGGR process BY idThreadProcess(process) WHERE process IS INTEGER;
threadOwnerProcess (i) = processIdThread(lockOwnerIdJavaProcess(i));
                                                                                                                                                                                                                                                          
blockingProcess 'Уровень' (child, parent) = RECURSION 1l IF idThreadProcess(child) AND parent == child
                                                        STEP 1l IF parent == threadOwnerProcess($parent);                                                                                                                                                                                                                                                       
isBlockingProcess 'Блокируется' (child, parent) = TRUE IF blockingProcess(child, parent);
sumPlusOneBlockedProcess  (process) = GROUP SUM 1 IF isBlockingProcess(child, parent) BY parent;
sumBlockedProcess 'Кол-во заблокированных процессов' (process) = sumPlusOneBlockedProcess(process) - 1;
sumPlusOneBlockingProcess 'Кол-во блокирующих процессов' (process) = GROUP SUM 1 IF isBlockingProcess(parent, child) BY parent;
sumBlockingProcess 'Глубина блокировки' (process) = sumPlusOneBlockingProcess(process) - 1;

activeProcess 'Активный' (i) = (querySQLProcess(i) AND stateSQLProcess(i) == StateProcess.active)
                                          OR (statusJavaProcess(i) == 'RUNNABLE' AND hasStackTraceJavaProcess(i) AND i IS INTEGER
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.DualStackPlainSocketImpl') == 1)
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'sun.awt.windows.WToolkit.eventLoop') == 1)
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'java.net.SocketInputStream.socketRead0') == 1)
                                          AND NOT (startsWith(stackTraceJavaProcess(i), 'sun.management.ThreadImpl.dumpThreads0') == 1));
blockedProcess 'Заблокированный' (i) = statusJavaProcess(i) == 'BLOCKED' AND i IS INTEGER;             

isSQLProcess(i) = startsWith(idThreadProcess(i), 's');// OR (querySQLProcess(i) AND stateSQLProcess(i) == StateProcess.active);
isJavaProcess(i) = NOT isSQLProcess(i);
backgroundProcess(i) = IF isSQLProcess(i) THEN RGB(255, 255, 240) ELSE RGB(240, 255, 255);  
                   
cancelProcess 'Снять процесс' (i) = ACTION {
    IF isSQLProcess(i) THEN cancelSQLProcess(i) ELSE cancelJavaProcess(i);
    updateProcessMonitorAction();
}           
killProcess 'Убить процесс' (i) = ACTION { 
    IF isSQLProcess(i) THEN killSQLProcess(i) ELSE killJavaProcess(i);
    updateProcessMonitorAction();
}
                             
FORM processMonitor 'Монитор процессов'
    OBJECTS i = INTEGER
    PROPERTIES(i) READONLY BACKGROUND backgroundProcess(i) activeProcess, idThreadProcess, computerProcess, userProcess,
                           querySQLProcess, dateTimeSQLProcess, nameStateSQLProcess, addressUserSQLProcess,
                           stackTraceJavaProcess, nameJavaProcess, statusJavaProcess, lockNameJavaProcess,
                           lockOwnerIdJavaProcess, lockOwnerNameJavaProcess
    PROPERTIES(i) TOOLBAR cancelProcess, killProcess                       
    FILTERS idThreadProcess(i)
    FILTERGROUP javaSQL 
        FILTER 'Java-процессы' isJavaProcess(i)
        FILTER 'SQL-процессы' isSQLProcess(i)
    FILTERGROUP active FILTER 'Только активные' activeProcess(i) DEFAULT
    
    TREE blocking i2 = INTEGER PARENT threadOwnerProcess
    PROPERTIES(i2) READONLY BACKGROUND backgroundProcess(i) sumBlockedProcess,
                            activeProcess, idThreadProcess, computerProcess, userProcess,
                            querySQLProcess, dateTimeSQLProcess, nameStateSQLProcess, addressUserSQLProcess,
                            stackTraceJavaProcess, nameJavaProcess, statusJavaProcess, lockNameJavaProcess,
                            lockOwnerIdJavaProcess, lockOwnerNameJavaProcess

    FILTERS idThreadProcess(i2), (activeProcess(i2) AND sumBlockedProcess(i2) > 0) OR blockedProcess(i2)
    
    OBJECTS blocked = INTEGER, blocking = INTEGER
    PROPERTIES(blocked) READONLY idThreadProcess, sumBlockingProcess, computerProcess, userProcess,
                                 querySQLProcess, dateTimeSQLProcess, nameStateSQLProcess, addressUserSQLProcess,
                                 stackTraceJavaProcess, nameJavaProcess, statusJavaProcess, lockNameJavaProcess,
                                 lockOwnerIdJavaProcess, lockOwnerNameJavaProcess
    PROPERTIES(blocking) READONLY activeProcess, idThreadProcess, sumBlockingProcess, computerProcess, userProcess,
                                 querySQLProcess, dateTimeSQLProcess, nameStateSQLProcess, addressUserSQLProcess,
                                 stackTraceJavaProcess, nameJavaProcess, statusJavaProcess, lockNameJavaProcess,
                                 lockOwnerIdJavaProcess, lockOwnerNameJavaProcess
    FILTERS idThreadProcess(blocked), sumBlockingProcess(blocked) > 0, sumBlockedProcess(blocking) > 0, isBlockingProcess(blocked, blocking) AND blocked != blocking
    FILTERGROUP nonBlocking FILTER 'Только не блокирующие' sumBlockedProcess(blocked) == 0 DEFAULT
    ORDER BY sumBlockingProcess(blocked) DESC, sumBlockingProcess(blocking) DESC
    
    PROPERTIES() updateProcessMonitorAction//, test1Action, test2Action, test3Action,test4Action
;

DESIGN processMonitor {
    NEW top {
        type = TABBED;
        fill = 1;
        NEW allProcesses {
            fill = 1;
            caption = 'Все процессы';
            MOVE i.box {
            caption = 'Процессы';
            }
        }
        NEW blockingProcesses {
            fill = 1;
            caption = 'Блокирующие';
            MOVE blocking.tree.box {
                caption = 'Блокирующие процессы';
            }
        }
        NEW blockedProcesses {
            fill = 1;
            caption = 'Блокированные';
            MOVE blocked.box {
                caption = 'Блокированные процессы';
            }
            MOVE blocking.box {
                caption = 'Блокирующие процессы';
            }
        }
    }    
    MOVE PROPERTY(updateProcessMonitorAction()) {
        align = TRAILING;
    }
    //MOVE PROPERTY(test1Action());
    //MOVE PROPERTY(test2Action());
    //MOVE PROPERTY(test3Action());
    //MOVE PROPERTY(test4Action());
    MOVE functions.box;
}

NAVIGATOR {
    systemEvents {
        ADD activeTasks;
        ADD activeBlocks;
        ADD processMonitor;
    }
}
