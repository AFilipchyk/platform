MODULE Paas;

REQUIRE System, Authentication, Contact;

CLASS Project 'Проект' : Named;
projectDescription 'Описание' = DATA STRING[100] (Project);
projectOwner 'Владелец' = DATA CustomUser (Project);
projectOwnerName 'Имя владельца' (project) = nameContact(projectOwner(project));
projectOwnerLogin 'Логин владельца' (project) = loginCustomUser(projectOwner(project));

CLASS Module 'Модуль' : Named;
moduleInProject 'Модуль в проекте' = DATA BOOLEAN (Project, Module);
moduleSource 'Исходный код модуля' = DATA TEXT (Module);

CLASS Database 'База данных' : Named;

CLASS Status 'Статус конфигурации' {
    init 'Стартует',
    started 'Работает',
    stopping 'Останавливается',
    stopped 'Остановлен'
} : Named;

CLASS Configuration 'Конфигурация' : Named;
configurationProject 'Проект' = DATA Project (Configuration);
configurationDatabase 'База данных' = DATA Database (Configuration);
configurationDatabaseName 'Имя базы данных' (configuration) = name(configurationDatabase(configuration));
configurationPort 'Порт для запуска' = DATA INTEGER (Configuration);
configurationExportName 'Имя для экспорта через RMI' = DATA STRING[40] (Configuration);
configurationStatus 'Статус' = DATA Status (Configuration);
configurationStatusName 'Статус' (configuration) = name(configurationStatus(configuration));

databaseConfiguration 'Конфигурация' (database) = GROUP AGGR configuration BY configurationDatabase(configuration);

configurationDelete 'Удалить' (configuration) = delete(configuration) IF configurationStatus(configuration) == Status.stopped IMAGE 'delete.png';

configurationStart 'Запустить' = [ACTION (c) CUSTOM 'paas.properties.StartConfigurationActionProperty'](c)
                                    IF configurationStatus(c) == Status.stopped IMAGE 'start.png';
configurationStop 'Остановить' = [ACTION (c) CUSTOM 'paas.properties.StopConfigurationActionProperty'](c)
                                    IF configurationStatus(c) == Status.started IMAGE 'stop.png';

refreshStatuses 'Обновить' = ACTION CUSTOM 'paas.properties.RefreshStatusActionProperty' IMAGE 'refresh.png';

FORM selectModulesForm 'Выбор модулей'
    OBJECTS p=Project FIXED PANEL, m=Module FIXED GRID
    PROPERTIES(p) name, projectDescription, projectOwnerName
    PROPERTIES(m) name
    PROPERTIES(p, m) moduleInProject
    FILTERGROUP filters1
        FILTER 'Невыбранные объекты' 'F9' p IS Project AND m IS Module AND NOT moduleInProject(p, m) DEFAULT
        FILTER 'Выбранные объекты' 'F10' moduleInProject(p, m)
;

selectProjectModules 'Выбрать модули' (project) = ACTION FORM selectModulesForm OBJECTS p = project MODAL;

FORM modulesForm 'Модули'
    OBJECTS m=Module
    PROPERTIES(m) name
    PROPERTIES(m) FORCE PANEL moduleSource, ADDOBJ, delete
;

DESIGN modulesForm FROM DEFAULT {
    m.grid {
        fillVertical = 0.5;
    }

    m.panel {
        fillVertical = 1.5;
    }

    PROPERTY(moduleSource) {
        panelLabelAbove = TRUE;
        fillHorizontal = 1;
        fillVertical = 1;
    }
}


FORM projectsForm 'Проекты'
    OBJECTS p=Project FIXED PANEL
    PROPERTIES(p) name READONLY, projectOwnerName, ADDOBJ, delete

    OBJECTS m=Module
    PROPERTIES(m) name, moduleSource FORCE PANEL, ADDOBJ, delete
    PROPERTIES(p) TODRAW m FORCE PANEL selectProjectModules

    OBJECTS c=Configuration
    PROPERTIES(c) name, configurationDatabaseName, configurationPort, configurationExportName, configurationStatusName, configurationStart, configurationStop, ADDOBJ, configurationDelete
    PROPERTIES(p) TODRAW c FORCE PANEL refreshStatuses

    FILTERS configurationProject(c) == p;
;

DESIGN projectsForm FROM DEFAULT {
    m.grid {
        fillVertical = 0.5;
    }

    m.panel {
        fillVertical = 1.5;
    }

    PROPERTY(moduleSource) {
        panelLabelAbove = TRUE;
        fillHorizontal = 1;
        fillVertical = 1;
    }
}

NAVIGATOR {
    ADD modulesForm;
    ADD projectsForm;
}




