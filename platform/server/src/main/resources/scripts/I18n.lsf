MODULE I18n;

REQUIRE System;

// ------------------------------------------------ Язык ---------------------------------------------- //

CLASS dictionary 'Словарь' : named;
TABLE dictionary (dictionary);

insensitiveDictionary 'Нечувствительный к регистру' = DATA BOOLEAN (dictionary);

CLASS dictionaryEntry 'Слова';
TABLE dictionaryEntry (dictionaryEntry);

dictionaryDictionaryEntry 'Словарь' = DATA dictionary (dictionaryEntry);
termDictionaryEntry 'Термин' = DATA STRING[50] (dictionaryEntry);
insensitiveTermDictionaryEntry(dictionaryEntry) = upper (termDictionaryEntry(dictionaryEntry));
translationDictionaryEntry 'Перевод' = DATA STRING[50] (dictionaryEntry);

dictionaryEntryDictionaryTerm = GROUP UNIQUE dictionaryEntry BY dictionaryDictionaryEntry(dictionaryEntry), termDictionaryEntry(dictionaryEntry) WHERE dictionaryEntry IS dictionaryEntry;
translationDictionaryEntryDictionaryTerm 'Перевод' (dictionary, term) = translationDictionaryEntry(dictionaryEntryDictionaryTerm(dictionary, term));
nameDictionaryDictionaryEntry 'Словарь' (dictionaryEntry) = name (dictionaryDictionaryEntry(dictionaryEntry));
insensitiveDictionaryEntryDictionaryTerm = GROUP MAX dictionaryEntry BY dictionaryDictionaryEntry(dictionaryEntry), insensitiveTermDictionaryEntry(dictionaryEntry) WHERE dictionaryEntry IS dictionaryEntry;
insensitiveTranslationDictionaryEntryDictionaryTerm 'Нечувствительный к регистру перевод' = translationDictionaryEntry (dictionaryEntryDictionaryTerm(dictionary, term));

CLASS language 'Язык' : named;
TABLE language(language);

localeLanguage 'Locale' = DATA STRING[5] (language);

dictionaryCompositionLanguage = DATA dictionary(language);
nameDictionaryCompositionLanguage 'Словарь для составов' = name(dictionaryCompositionLanguage(language)) IN base;

dictionaryNameLanguage = DATA dictionary(language);
nameDictionaryNameLanguage 'Словарь для наименований' = name(dictionaryNameLanguage(language)) IN base;

CLASS multiLanguageNamed 'Мультиязычный объект';

FORM language 'Язык'
    OBJECTS l = language FIXED PANEL
    PROPERTIES(l) name, localeLanguage, nameDictionaryCompositionLanguage, nameDictionaryNameLanguage

    EDIT language OBJECT l
;

FORM languages 'Языки'
    OBJECTS l = language
    PROPERTIES(l) READONLY name, localeLanguage, nameDictionaryCompositionLanguage, nameDictionaryNameLanguage
    PROPERTIES(l)          ADDFORM, EDITFORM, delete

    DIALOG language OBJECT l
;

NAVIGATOR {
    catalogs {
        ADD languages;
    }
}

// ---------------------------------------------- Мультиязычный объект -------------------------------- //


TABLE multiLanguageNamedLanguage(multiLanguageNamed, language);
languageName 'Название (иностр.)' = DATA ISTRING[110] (multiLanguageNamed, language);

// ---------------------------------------------- Словари --------------------------------------------- //

FORM dictionary 'Словарь'
    OBJECTS d = dictionary FIXED PANEL
    PROPERTIES(d) name, insensitiveDictionary

    OBJECTS e = dictionaryEntry
    PROPERTIES(e) termDictionaryEntry, translationDictionaryEntry, ADDOBJ, delete
    FILTERS       dictionaryDictionaryEntry(e) == d

    EDIT dictionary OBJECT d
;

FORM dictionaries 'Словари'
    OBJECTS d = dictionary
    PROPERTIES(d) READONLY name, insensitiveDictionary

    PROPERTIES(d) ADDFORM, EDITFORM, delete

    DIALOG dictionary OBJECT d
;

NAVIGATOR {
    catalogs {
        ADD dictionaries;
    }
}
