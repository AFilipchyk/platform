IMPORT BaseLogicsModule;

CLASS game 'Игра';
CLASS team 'Команда' : named;

CLASS conference 'Конференция' : named;

CLASS STATIC gameResult 'Результат матча'
{
    win 'В основное время',
    winOT 'В овертайме',
    winSO 'По буллитам'
};

city 'Город' (team) = DATA STRING[100] (team) IN baseGroup;

conference(team) = DATA conference (team);
conferenceName 'Конференция' (team) = name(conference(team)) IN baseGroup;

hostTeam(game) = DATA team (game);
guestTeam(game) = DATA team (game);

gameDate 'Дата' (game) = DATA DATE (game) IN baseGroup;

hostTeamName 'Хозяева' = name(hostTeam(game)) IN baseGroup;
guestTeamName 'Гости' = name(guestTeam(game)) IN baseGroup;

hostGoals 'Голы хозяев' (game) = DATA INTEGER (game) IN baseGroup;
guestGoals 'Голы гостей' (game) = DATA INTEGER (game) IN baseGroup;

gameWinner(game) = UNION EXCLUSIVE (hostTeam(game) IF hostGoals(game) > guestGoals(game),
                                    guestTeam(game) IF hostGoals(game) < guestGoals(game));

gameLooser(game) = UNION OVERRIDE (hostTeam(game), guestTeam(game) IF gameWinner(game) != guestTeam(game));

gameResultData(game) = DATA gameResult (game);

gameResult(game) = UNION OVERRIDE (gameResult.win IF game IS game, gameResultData(game));

gameResultName 'Завершился' (game) = name(gameResult(game)) IN baseGroup;

hostGamesPlayed(team) = GROUP SUM 1 BY hostTeam(game);
guestGamesPlayed(team) = GROUP SUM 1 BY guestTeam(game);

gamesPlayed 'И' (team) = UNION SUM (hostGamesPlayed(team), guestGamesPlayed(team));

zeroIfTeamResult(obj1, obj2) = 0 IF obj1 IS team AND obj2 IS gameResult;

gamesWonByResult(team, type) = UNION SUM ([GROUP SUM 1 BY gameWinner(game), gameResult(game)](team, type),
                                          zeroIfTeamResult(team, type));
gamesLostByResult(team, type) = UNION SUM ([GROUP SUM 1 BY gameLooser(game), gameResult(game)](team, type),
                                          zeroIfTeamResult(team, type));

gamesWon 'В' (team) = gamesWonByResult(team, gameResult.win);
gamesWonOT 'ВО' (team) = gamesWonByResult(team, gameResult.winOT);
gamesWonSO 'ВБ' (team) = gamesWonByResult(team, gameResult.winSO);

gamesLost 'П' (team) = gamesLostByResult(team, gameResult.win);
gamesLostOT 'ПО' (team) = gamesLostByResult(team, gameResult.winOT);
gamesLostSO 'ПБ' (team) = gamesLostByResult(team, gameResult.winSO);

points 'О' (team) = gamesWon(team) * 3 + (gamesWonSO(team) + gamesWonOT(team)) * 2 + gamesLostOT(team) + gamesLostSO(team);

hostGoalsScored(team) = GROUP SUM hostGoals(game) BY hostTeam(game);
guestGoalsScored(team) = GROUP SUM guestGoals(game) BY guestTeam(game);
goalsScored 'ШЗ' (team) = UNION SUM (hostGoalsScored(team), guestGoalsScored(team));

hostGoalsConceded(team) = GROUP SUM guestGoals(game) BY hostTeam(game);
guestGoalsConceded(team) = GROUP SUM hostGoals(game) BY guestTeam(game);
goalsConceded 'ШП' (team) = UNION SUM (hostGoalsConceded(team), guestGoalsConceded(team));

extendedGame(game) = gameResult(game) != gameResult.win;

CONSTRAINT CHECKED hostTeam(team) == guestTeam(team) MSG 'Команда не может играть сама с собой';
CONSTRAINT extendedGame(game) AND hostGoals(game) > guestGoals(game) + 1 MSG 'При таком счете матч должен закончиться в основное время';
CONSTRAINT extendedGame(game) AND guestGoals(game) > hostGoals(game) + 1 MSG 'При таком счете матч должен закончиться в основное время';
CONSTRAINT hostGoals(team) == guestGoals(team) MSG 'Матч не может закончиться вничью';

FORM MainForm 'Турнирная таблица'
OBJECTS game, team
PROPERTIES gameDate(game), hostTeamName(game), hostGoals(game), guestGoals(game), guestTeamName(game), gameResultName(game)
PROPERTIES name(team), gamesPlayed(team), gamesWon(team), gamesWonOT(team), gamesWonSO(team), gamesLostSO(team),
           gamesLostOT(team), gamesLost(team), goalsScored(team), goalsConceded(team), points(team)
PROPERTIES ADDOBJ(game), ADDOBJ(team);
           
