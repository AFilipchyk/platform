IMPORT BaseLogicsModule;

CLASS game 'Game';
CLASS team 'Team' : named;

CLASS conference 'Conference' : named;

CLASS STATIC gameResult 'S/O'
{
    win '',
    winOT 'OT',
    winSO 'SO'
};

city 'City' (team) = DATA STRING[100] (team) IN baseGroup;
teamName 'Team' (team) = name(team) IF team IS team;

conference(team) = DATA conference (team);
conferenceName 'Conference' (team) = name(conference(team)) IN baseGroup;

hostTeam(game) = DATA team (game);
guestTeam(game) = DATA team (game);

gameDate 'Date' (game) = DATA DATE (game) IN baseGroup;

hostTeamName 'Home' = name(hostTeam(game)) IN baseGroup;
guestTeamName 'Visitor' = name(guestTeam(game)) IN baseGroup;

hostGoals 'H Score' (game) = DATA INTEGER (game) IN baseGroup;
guestGoals 'V Score' (game) = DATA INTEGER (game) IN baseGroup;

//guestGoals(game) <- OLD hostGoals(game) ON CHANGE hostGoals(game);

gameWinner(game) = UNION EXCLUSIVE hostTeam(game) IF hostGoals(game) > guestGoals(game),
                                   guestTeam(game) IF hostGoals(game) < guestGoals(game);

gameLooser(game) = UNION OVERRIDE hostTeam(game), guestTeam(game) IF gameWinner(game) != guestTeam(game);

gameResultData(game) = DATA gameResult (game);

gameResult(game) = UNION OVERRIDE gameResult.win IF game IS game, gameResultData(game);

pureWin(game) = deltaDouble2(hostGoals(game), guestGoals(game)) > 1;

gameResult(game) <- gameResult.win ON ASSIGN pureWin(game);

gameResultName 'O/S' (game) = name(gameResult(game)) IN baseGroup;

hostGamesPlayed(team) = GROUP SUM 1 BY hostTeam(game);
guestGamesPlayed(team) = GROUP SUM 1 BY guestTeam(game);

gamesPlayed 'GP' (team) = UNION SUM hostGamesPlayed(team), guestGamesPlayed(team);

zeroIfTeamResult(obj1, obj2) = 0 IF obj1 IS team AND obj2 IS gameResult;

gamesWonByResult(team, type) = UNION SUM [GROUP SUM 1 BY gameWinner(game), gameResult(game)](team, type),
                                         zeroIfTeamResult(team, type);
gamesLostByResult(team, type) = UNION SUM [GROUP SUM 1 BY gameLooser(game), gameResult(game)](team, type),
                                          zeroIfTeamResult(team, type);

gamesWon 'W' (team) = gamesWonByResult(team, gameResult.win);
gamesWonOT 'OTW' (team) = gamesWonByResult(team, gameResult.winOT);
gamesWonSO 'SOW' (team) = gamesWonByResult(team, gameResult.winSO);

gamesLost 'L' (team) = gamesLostByResult(team, gameResult.win);
gamesLostOT 'OTL' (team) = gamesLostByResult(team, gameResult.winOT);
gamesLostSO 'SOL' (team) = gamesLostByResult(team, gameResult.winSO);

points 'PTS' (team) = gamesWon(team) * 3 + (gamesWonSO(team) + gamesWonOT(team)) * 2 + gamesLostOT(team) + gamesLostSO(team);

hostGoalsScored(team) = GROUP SUM hostGoals(game) BY hostTeam(game);
guestGoalsScored(team) = GROUP SUM guestGoals(game) BY guestTeam(game);
goalsScored 'GF' (team) = UNION SUM hostGoalsScored(team), guestGoalsScored(team);

hostGoalsConceded(team) = GROUP SUM guestGoals(game) BY hostTeam(game);
guestGoalsConceded(team) = GROUP SUM hostGoals(game) BY guestTeam(game);
goalsConceded 'GA' (team) = UNION SUM hostGoalsConceded(team), guestGoalsConceded(team);

extendedGame(game) = gameResult(game) != gameResult.win;

place 'PL' (team) = PARTITION SUM 1 ORDER DESC points(team), gamesWon(team), gamesWonOT(team), gamesWonSO(team),
                                               goalsScored(team) - goalsConceded(team), goalsScored(team);
                                    
conferencePlace 'Conf. PL' (team) = PARTITION SUM 1 BY conference(team)
                                              ORDER DESC points(team), gamesWon(team), gamesWonOT(team), gamesWonSO(team),
                                                         goalsScored(team) - goalsConceded(team), goalsScored(team);
                                    

CONSTRAINT CHECKED hostTeam(team) == guestTeam(team) MSG 'Home and Visitor teams should be different';
CONSTRAINT extendedGame(game) AND hostGoals(game) > guestGoals(game) + 1 MSG 'Overtime is impossible';
CONSTRAINT extendedGame(game) AND guestGoals(game) > hostGoals(game) + 1 MSG 'Overtime is impossible';
CONSTRAINT hostGoals(game) == guestGoals(game) MSG 'The game cannot end in a draw';


FORM MainForm 'Scoreboard'
OBJECTS game, team
PROPERTIES gameDate(game), hostTeamName(game), hostGoals(game), guestGoals(game), guestTeamName(game), gameResultName(game)
PROPERTIES place(team), conferencePlace(team),
           teamName(team), gamesPlayed(team), gamesWon(team), gamesWonOT(team), gamesWonSO(team),
           gamesLostSO(team), gamesLostOT(team), gamesLost(team), goalsScored(team), goalsConceded(team), points(team)
PROPERTIES ADDOBJ(game), ADDOBJ(team);
           
