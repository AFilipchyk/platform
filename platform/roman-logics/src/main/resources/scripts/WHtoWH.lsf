//From WareHouse to Store
MODULE WHtoWH;

REQUIRE System,

        Stock,
        Numerator,
        Document,
        RomanDocument,
        Consignment,
        Declaration,
        WholesalePrice,
        MasterData,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, Stock;

CLASS WHtoWH 'Расход со склада на склад' : historyObject, numberedObject, consignment;
CLASS WHtoWHDetail 'Строка расхода со склада на склад' : outAutoSkuLedger, consignmentDetail;
CLASS WHtoWHPosted 'Закрытый расход со склада на склад' : WHtoWH, postedObject, consignment;


skuWHtoWHDetail 'Товар (ИД)' (WHtoWHDetail) = DATA sku (WHtoWHDetail)IN idGroup;
nameSkuWHtoWHDetail 'Наименование товара' (WHtoWHDetail) = nameSku(skuWHtoWHDetail(WHtoWHDetail));

@defineRBCorrespondingDocumentWithRetailPrices(WHtoWH, warehouse, 'Склад-отправитель', sku, 'Расход со склада на склад', warehouse, 'Склад-получатель');
@defineDocumentDetailPackWeightSku(WHtoWH);
@defineDocumentHeaderSkuQuantity(WHtoWH);


quantityOutAutoSkuLedger (ledger) += quantityWHtoWHDetail (ledger);

wholesalePriceWHtoWHDetail 'Оптовая цена' (WHtoWHDetail) =  priceWholesaleArticleDateTime(articleWHtoWHDetail(WHtoWHDetail), dateTimeWHtoWHDetail(WHtoWHDetail));
supplierPriceWHtoWHDetail (WHtoWHDetail) <- wholesalePriceWHtoWHDetail(WHtoWHDetail) WHEN CHANGED(skuWHtoWHDetail(WHtoWHDetail)) OR CHANGED (dateTimeWHtoWHDetail(WHtoWHDetail));

contractSkuCompanyWHtoWH 'Договор ИД' (WHtoWH) = DATA contractSkuCompany(WHtoWH);
numberContractSkuCompanyWHtoWH 'Номер договора' (WHtoWH) = numberContract(contractSkuCompanyWHtoWH(WHtoWH)) IN documentPrmGroup;

isCompanyABWHtoWH (WHtoWH)= companyAContractSkuCompany(contractSkuCompanyWHtoWH(WHtoWH))== companyWarehouse(warehouseWHtoWH(WHtoWH)) AND
    companyBContractSkuCompany(contractSkuCompanyWHtoWH(WHtoWH))== companyWarehouse(corrWarehouseWHtoWH(WHtoWH)) AND
    activeContract(contractSkuCompanyWHtoWH(WHtoWH), dateWHtoWH(WHtoWH));

CONSTRAINT contractSkuCompanyWHtoWH(WHtoWH) AND NOT isCompanyABWHtoWH(WHtoWH)
    CHECKED BY contractSkuCompanyWHtoWH MESSAGE 'Договор не является договором между выбранными контрагентами или не действующий';

contractSkuCompanyWHtoWH(WHtoWH) <- contractSkuCompanyCompanyCompany(companyWarehouse(warehouseWHtoWH(WHtoWH)), companyWarehouse(corrWarehouseWHtoWH(WHtoWH)))
    IF countContractPartyAPartyB(companyWarehouse(warehouseWHtoWH(WHtoWH)), companyWarehouse(corrWarehouseWHtoWH(WHtoWH))) == 1
    WHEN CHANGED(warehouseWHtoWH(WHtoWH)) OR CHANGED(corrWarehouseWHtoWH(WHtoWH)) ;


FORM WHtoWH 'Расход со склада на склад'
    OBJECTS w = WHtoWH FIXED PANEL

    PROPERTIES (w) numberObject, seriesObject, dateWHtoWH, timeWHtoWH, nameWarehouseWHtoWH, nameCorrWarehouseWHtoWH,
                   quantityWHtoWHDetailWHtoWH, supplierSumWHtoWHDetailWHtoWH, invoiceVATSumWHtoWHDetailWHtoWH,
                   invoiceSumWHtoWHDetailWHtoWH, noteWHtoWH, numberContractSkuCompanyWHtoWH

    OBJECTS d = WHtoWHDetail

    PROPERTIES(d) indexWHtoWHDetail, barcodeWHtoWHDetail, nameSkuWHtoWHDetail, shortNameUOMWHtoWHDetail, sidSizeWHtoWHDetail, nameBrandWHtoWHDetail,
                  sidArticleWHtoWHDetail, nameCategoryWHtoWHDetail, sidColorWHtoWHDetail, nameColorWHtoWHDetail, balanceBSkuWHtoWHDetail,
                  quantityWHtoWHDetail, importerPriceWHtoWHDetail, supplierPriceWHtoWHDetail, supplierSumWHtoWHDetail, numberSupplierVATWHtoWHDetail,
                  valueSupplierVATWHtoWHDetail, supplierVATISumWHtoWHDetail, invoiceSumWHtoWHDetail

    PROPERTIES(w) TODRAW d addDetailDialogSkuWHtoWHDetailWHtoWH, addDetailDialogBarcodeWHtoWHDetailWHtoWH,
                  deleteWHtoWHDetailWHtoWH

    PROPERTIES(d) ADDOBJ, delete

    FILTERS inWHtoWHWHtoWHDetail(w, d)

    EDIT WHtoWH OBJECT w
;

DESIGN WHtoWH FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        ADD w.box {
            childConstraints = TO THE RIGHT;
            NEW row1 {
                childConstraints = TO THE BOTTOM;
                ADD w.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (nameWarehouseWHtoWH);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateWHtoWH);
                    ADD PROPERTY (timeWHtoWH);
                }
                NEW case {
                childConstraints = TO THE BOTTOM;
                    title = 'Контрагент';
                    ADD PROPERTY (nameCorrWarehouseWHtoWH);
                }
                ADD w.documentPrmGroup;
            }
            NEW row2 {
                ADD w.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        };
        ADD d.box;
        ADD functions.box;
    }
}

FORM WHtoWHs 'Расходы со склада на склад'
    OBJECTS w = WHtoWH
    PROPERTIES (w) READONLY objectClassName, numberObject, seriesObject, dateWHtoWH, timeWHtoWH, nameWarehouseWHtoWH, nameCorrWarehouseWHtoWH,
                   numberContractSkuCompanyWHtoWH, countWHtoWHDetailWHtoWH, quantityWHtoWHDetailWHtoWH, supplierSumWHtoWHDetailWHtoWH, invoiceVATSumWHtoWHDetailWHtoWH,
                   invoiceSumWHtoWHDetailWHtoWH, noteWHtoWH

    PROPERTIES (w) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed


    PROPERTIES (w) ADDFORM, EDITFORM SHOWIF isDraftWHtoWH(w), delete FORCE PANEL SHOWIF isDraftWHtoWH(w),
                   postWHtoWH SHOWIF isDraftWHtoWH(w), unpostWHtoWH SHOWIF isPostedWHtoWH(w)

    PROPERTIES (w ) FORCE PANEL printConsignmentVerticalA, printConsignmentHorizontalA,
                   printConsignmentVerticalB, printConsignmentHorizontalB,
                   printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                   printConsignmentSimpleVertical, printConsignmentSimpleAttach

    OBJECTS d = WHtoWHDetail
    PROPERTIES(d) READONLY indexWHtoWHDetail, barcodeWHtoWHDetail, nameSkuWHtoWHDetail, shortNameUOMWHtoWHDetail, sidSizeWHtoWHDetail, nameBrandWHtoWHDetail,
                  sidArticleWHtoWHDetail, nameCategoryWHtoWHDetail, sidColorWHtoWHDetail, nameColorWHtoWHDetail, balanceBSkuWHtoWHDetail,
                  quantityWHtoWHDetail, importerPriceWHtoWHDetail, supplierPriceWHtoWHDetail, supplierSumWHtoWHDetail, numberSupplierVATWHtoWHDetail,
                  valueSupplierVATWHtoWHDetail, supplierVATISumWHtoWHDetail, invoiceSumWHtoWHDetail

    FILTERS inWHtoWHWHtoWHDetail(w, d)
;

DESIGN WHtoWHs FROM DEFAULT {
    ADD w.box{
        PROPERTY (delete(w)) {
            panelLocation = TOOLBAR;
            askConfirm = TRUE;
        PROPERTY (objectClassName(w)) {
            preferredCharWidth = 15;
        }
        }
    }
    ADD d.box;

    NEW footer.container {
        childConstraints = TO THE BOTTOM;

        NEW cont3 {
            childConstraints = TO THE RIGHT;
            ADD w.historyGroup {
                childConstraints = TO THE BOTTOM;
            }

            ADD w.postedGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        ADD w.printGroup {
            childConstraints = TO THE BOTTOM;
            NEW case55{
                childConstraints = TO THE RIGHT;

                NEW contOne {
                    title = 'Накладная';
                    ADD PROPERTY(editConsignment);
                }
                NEW tn{
                    childConstraints = TO THE RIGHT;
                    title = 'ТН-2';
                    ADD PROPERTY(printConsignmentSimpleVertical);
                    ADD PROPERTY(printConsignmentSimpleHorizontal);
                    ADD PROPERTY(printConsignmentSimpleAttach);
                }
            }
            NEW ttn1{
                childConstraints = TO THE RIGHT;
                title = 'ТТН-1';
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentHorizontalB);
                ADD PROPERTY(printConsignmentAttach);
            }

        }

    }
    ADD functions.box;
}

@defineConsignment(WHtoWH);
@implementConsignmentHeader(WHtoWH, warehouse);

senderConsignment (consignment) += companyWarehouse(warehouseWHtoWH(consignment));
recipientConsignment (consignment) += companyStore(corrWarehouseWHtoWH(consignment));

consignmentConsignmentDetail (consignmentDetail) += WHtoWHWHtoWHDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuWHtoWHDetail (consignmentDetail);
shortNameUOMConsignmentDetail (consignmentDetail) += shortNameUOMWHtoWHDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantityWHtoWHDetail (consignmentDetail);
priceConsignmentDetail (consignmentDetail) += supplierPriceWHtoWHDetail (consignmentDetail);
vatConsignmentDetail (consignmentDetail) += valueSupplierVATWHtoWHDetail (consignmentDetail);
sumVATConsignmentDetail (consignmentDetail) += supplierVATISumWHtoWHDetail (consignmentDetail);
sumConsignmentDetail (consignmentDetail) += supplierSumWHtoWHDetail (consignmentDetail);
sumInvoiceConsignmentDetail (consignmentDetail) += invoiceSumWHtoWHDetail (consignmentDetail);
grossWeightConsignmentDetail (consignmentDetail) += grossWeightWHtoWHDetail (consignmentDetail);
packQuantityConsignmentDetail (consignmentDetail) += packQuantityWHtoWHDetail(consignmentDetail);
//currencyConsignment(consignment) += currencyWHtoWH(consignment);
//isCustomsFlowConsignment (consignment) += consignment IS WHtoWH;
noteConsignment(consignment) += noteWHtoWH(consignment);

//noteConsignmentDetail

//todo: Надо наполнить свойствами накладную. (УНП...)



