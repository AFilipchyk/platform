//From WareHouse to Store
MODULE WHtoWH;

REQUIRE System,

        Stock,
        Numerator,
        Document,
        RomanDocument,
        Consignment,
        Declaration,
        WholesalePrice,
        MasterData,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, Stock;

CLASS WHtoWH 'Расход со склада на склад' : historyObject, numberedObject, consignment;
CLASS WHtoWHDetail 'Строка расхода со склада на склад' : outAutoSkuLedger, consignmentDetail;
CLASS WHtoWHPosted 'Закрытый расход со склада на склад' : WHtoWH, postedObject, consignment;


skuWHtoWHDetail 'Товар (ИД)' (WHtoWHDetail) = DATA sku (WHtoWHDetail)IN idGroup;
nameSkuWHtoWHDetail 'Наименование товара' (WHtoWHDetail) = nameSku(skuWHtoWHDetail(WHtoWHDetail));

@defineRBCorrespondingDocument(WHtoWH, warehouse, 'Оптовый склад-отправитель', sku, 'Расход со склада на склад', store, 'Оптовый склад-получатель');

quantityOutAutoSkuLedger (ledger) += quantityWHtoWHDetail (ledger);

importerPriceWHtoWHDetail 'Цена изготовителя/импортера' (WHtoWHDetail) = DATA NUMERIC[14,3] (WHtoWHDetail);
supplierPriceWHtoWHDetail 'Цена поставщика без НДС' (WHtoWHDetail) = DATA NUMERIC[14,3] (WHtoWHDetail);

importerPriceWHtoWHSku 'Цена без НДС' (WHtoWH, sku) = GROUP MAX importerPriceWHtoWHDetail(WHtoWHDetail) BY WHtoWHWHtoWHDetail(WHtoWHDetail), skuWHtoWHDetail(WHtoWHDetail);

wholesalePriceWHtoWHDetail 'Оптовая цена' (WHtoWHDetail) =  priceWholesaleArticleDateTime(articleWHtoWHDetail(WHtoWHDetail), dateTimeWHtoWHDetail(WHtoWHDetail));
supplierPriceWHtoWHDetail (WHtoWHDetail) <- wholesalePriceWHtoWHDetail(WHtoWHDetail) WHEN CHANGED(skuWHtoWHDetail(WHtoWHDetail)) OR CHANGED (dateTimeWHtoWHDetail(WHtoWHDetail));

supplierSumWHtoWHDetail 'Стоимость без НДС' (WHtoWHDetail) = supplierPriceWHtoWHDetail(WHtoWHDetail) * quantityWHtoWHDetail(WHtoWHDetail);

supplierSumWHtoWHDetailWHtoWH 'Стоимость без НДС' (WHtoWH)= GROUP SUM supplierSumWHtoWHDetail(WHtoWHDetail) BY WHtoWHWHtoWHDetail(WHtoWHDetail) IN documentSumGroup;


supplierVATWHtoWHDetail(WHtoWHDetail) = DATA range (WHtoWHDetail);
numberSupplierVATWHtoWHDetail 'НДС, номер' (WHtoWHDetail) = numberRange(supplierVATWHtoWHDetail(WHtoWHDetail));
valueSupplierVATWHtoWHDetail 'НДС, %' (WHtoWHDetail) = valueRateRangeDate
    (supplierVATWHtoWHDetail(WHtoWHDetail), dateWHtoWHDetail(WHtoWHDetail));

CONSTRAINT taxRange(supplierVATWHtoWHDetail(WHtoWHDetail)) != tax.taxVAT CHECKED BY supplierVATWHtoWHDetail MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';


sumSupplierVATWHtoWHDetail 'Сумма НДС' (WHtoWHDetail) = [X*Y/100](
    supplierSumWHtoWHDetail(WHtoWHDetail), valueSupplierVATWHtoWHDetail(WHtoWHDetail));

sumSupplierVATWHtoWHDetailWHtoWH 'Сумма НДС' (WHtoWH) = GROUP SUM sumSupplierVATWHtoWHDetail(WHtoWHDetail) BY WHtoWHWHtoWHDetail(WHtoWHDetail) IN documentSumGroup;

sumInvoiceWHtoWHDetail 'Стоимость с НДС' (WHtoWHDetail) = supplierSumWHtoWHDetail(WHtoWHDetail) (+) sumSupplierVATWHtoWHDetail(WHtoWHDetail);

sumInvoiceWHtoWHDetailWHtoWH 'Стоимость с НДС' (WHtoWH)= GROUP SUM sumInvoiceWHtoWHDetail(WHtoWHDetail) BY WHtoWHWHtoWHDetail(WHtoWHDetail) IN documentSumGroup;

netWeightWHtoWHDetail 'Вес (ед.)' (WHtoWHDetail) = netWeightSku(skuWHtoWHDetail(WHtoWHDetail));

sumNetWeightWHtoWHDetail 'Общая масса груза, кг.' (WHtoWHDetail) = netWeightWHtoWHDetail(WHtoWHDetail)*quantityWHtoWHDetail(WHtoWHDetail);

sumNetWeightWHtoWH 'Общая масса груза, кг.' (WHtoWH) = GROUP SUM sumNetWeightWHtoWHDetail(WHtoWHDetail) BY WHtoWHWHtoWHDetail(WHtoWHDetail) IN documentSumGroup;

packQuantityWHtoWHDetail 'Количество грузовых мест' (WHtoWHDetail) = round0(quantityWHtoWHDetail(WHtoWHDetail)/
    UNION OVERRIDE 1 IF WHtoWHDetail IS WHtoWHDetail, quantityPackSku(skuWHtoWHDetail(WHtoWHDetail)));

packQuantityWHtoWHDetailWHtoWH 'Общее количество грузовых мест' (WHtoWH) = GROUP SUM packQuantityWHtoWHDetail(WHtoWHDetail) BY WHtoWHWHtoWHDetail(WHtoWHDetail) IN documentSumGroup;


quantityWHtoWHSku (WHtoWH, sku) = GROUP SUM quantityWHtoWHDetail(WHtoWHDetail)
                                          BY WHtoWHWHtoWHDetail(WHtoWHDetail), skuWHtoWHDetail(WHtoWHDetail);


FORM WHtoWH 'Расход со склада на склад'
    OBJECTS w = WHtoWH FIXED PANEL
    PROPERTIES (w) numberObject, seriesObject, dateWHtoWH, timeWHtoWH, nameWarehouseWHtoWH, nameStoreWHtoWH,
                   quantityWHtoWHDetailWHtoWH, supplierSumWHtoWHDetailWHtoWH, sumSupplierVATWHtoWHDetailWHtoWH,
                   sumInvoiceWHtoWHDetailWHtoWH, sumNetWeightWHtoWH, packQuantityWHtoWHDetailWHtoWH, noteWHtoWH

    OBJECTS d = WHtoWHDetail
    PROPERTIES(d) indexWHtoWHDetail, barcodeWHtoWHDetail, nameSkuWHtoWHDetail, nameBrandWHtoWHDetail, sidArticleWHtoWHDetail,
                  nameCategoryWHtoWHDetail, sidColorWHtoWHDetail, nameColorWHtoWHDetail, shortNameUOMWHtoWHDetail, balanceBSkuWHtoWHDetail,
                  quantityWHtoWHDetail, importerPriceWHtoWHDetail, supplierPriceWHtoWHDetail, supplierSumWHtoWHDetail, numberSupplierVATWHtoWHDetail, valueSupplierVATWHtoWHDetail,
                  sumSupplierVATWHtoWHDetail, sumInvoiceWHtoWHDetail

    PROPERTIES(w) TODRAW d addDetailDialogSkuWHtoWHDetailWHtoWH, addDetailDialogBarcodeWHtoWHDetailWHtoWH,
                  deleteWHtoWHDetailWHtoWH


    PROPERTIES(d) ADDOBJ, delete
//    PROPERTIES(w) TODRAW(d) addDetailDialogTerminalWHtoWHDetailWHtoWH

    FILTERS inWHtoWHWHtoWHDetail(w, d)

    EDIT WHtoWH OBJECT w
;

DESIGN WHtoWH FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        ADD w.box {
            childConstraints = TO THE RIGHT;
            NEW row1 {
                childConstraints = TO THE BOTTOM;
                ADD w.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (nameWarehouseWHtoWH);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateWHtoWH);
                    ADD PROPERTY (timeWHtoWH);
                }
                NEW case {
                childConstraints = TO THE BOTTOM;
                    title = 'Контрагент';
                    ADD PROPERTY (nameStoreWHtoWH);
                }
                ADD w.documentPrmGroup;
            }
            NEW row2 {
                ADD w.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        };
        ADD d.box;
        ADD functions.box;
    }
}

FORM WHtoWHs 'Расходы со склада на склад'
    OBJECTS w = WHtoWH
    PROPERTIES (w) READONLY objectClassName, numberObject, seriesObject, dateWHtoWH, timeWHtoWH, nameWarehouseWHtoWH, nameStoreWHtoWH,
                   countWHtoWHDetailWHtoWH, quantityWHtoWHDetailWHtoWH, supplierSumWHtoWHDetailWHtoWH, sumSupplierVATWHtoWHDetailWHtoWH,
                   sumInvoiceWHtoWHDetailWHtoWH, packQuantityWHtoWHDetailWHtoWH, sumNetWeightWHtoWH

    PROPERTIES (w) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed


    PROPERTIES (w) ADDFORM, EDITFORM SHOWIF isDraftWHtoWH(w), delete FORCE PANEL SHOWIF isDraftWHtoWH(w),
                   postWHtoWH SHOWIF isDraftWHtoWH(w), unpostWHtoWH SHOWIF isPostedWHtoWH(w)

    PROPERTIES (w ) FORCE PANEL printConsignmentVerticalA, printConsignmentHorizontalA,
                   printConsignmentVerticalB, printConsignmentHorizontalB,
                   printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                   printConsignmentSimpleVertical, printConsignmentSimpleAttach

    OBJECTS d = WHtoWHDetail
    PROPERTIES(d) READONLY indexWHtoWHDetail, barcodeWHtoWHDetail, nameSkuWHtoWHDetail, shortNameUOMWHtoWHDetail,
                  quantityWHtoWHDetail, importerPriceWHtoWHDetail, supplierPriceWHtoWHDetail, supplierSumWHtoWHDetail, numberSupplierVATWHtoWHDetail, valueSupplierVATWHtoWHDetail,
                  sumSupplierVATWHtoWHDetail, sumInvoiceWHtoWHDetail

    FILTERS inWHtoWHWHtoWHDetail(w, d)
;

DESIGN WHtoWHs FROM DEFAULT {
    ADD w.box{
        PROPERTY (delete(w)) {
            panelLocation = TOOLBAR;
            askConfirm = TRUE;
        PROPERTY (objectClassName(w)) {
            preferredCharWidth = 15;
        }
        }
    }
    ADD d.box;

    NEW footer.container {
        childConstraints = TO THE BOTTOM;

        NEW cont3 {
            childConstraints = TO THE RIGHT;
            ADD w.historyGroup {
                childConstraints = TO THE BOTTOM;
            }

            ADD w.postedGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        ADD w.printGroup {
            childConstraints = TO THE BOTTOM;
            NEW case55{
                childConstraints = TO THE RIGHT;

                NEW contOne {
                    title = 'Накладная';
                    ADD PROPERTY(editConsignment);
                }
                NEW tn{
                    childConstraints = TO THE RIGHT;
                    title = 'ТН-2';
                    ADD PROPERTY(printConsignmentSimpleVertical);
                    ADD PROPERTY(printConsignmentSimpleHorizontal);
                    ADD PROPERTY(printConsignmentSimpleAttach);
                }
            }
            NEW ttn1{
                childConstraints = TO THE RIGHT;
                title = 'ТТН-1';
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentHorizontalB);
                ADD PROPERTY(printConsignmentAttach);
            }

        }

    }
    ADD functions.box;
}

@defineConsignment(WHtoWH);
@implementConsignmentHeader(WHtoWH, warehouse);

senderConsignment (consignment) += companyWarehouse(warehouseWHtoWH(consignment));
recipientConsignment (consignment) += companyStore(storeWHtoWH(consignment));

consignmentConsignmentDetail (consignmentDetail) += WHtoWHWHtoWHDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuWHtoWHDetail (consignmentDetail);
shortNameUOMConsignmentDetail (consignmentDetail) += shortNameUOMWHtoWHDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantityWHtoWHDetail (consignmentDetail);
priceConsignmentDetail (consignmentDetail) += supplierPriceWHtoWHDetail (consignmentDetail);
vatConsignmentDetail (consignmentDetail) += valueSupplierVATWHtoWHDetail (consignmentDetail);
sumVATConsignmentDetail (consignmentDetail) += sumSupplierVATWHtoWHDetail (consignmentDetail);
sumConsignmentDetail (consignmentDetail) += supplierSumWHtoWHDetail (consignmentDetail);
sumInvoiceConsignmentDetail (consignmentDetail) += sumInvoiceWHtoWHDetail (consignmentDetail);
grossWeightConsignmentDetail (consignmentDetail) += sumNetWeightWHtoWHDetail (consignmentDetail);
packQuantityConsignmentDetail (consignmentDetail) += packQuantityWHtoWHDetail(consignmentDetail);
//currencyConsignment(consignment) += currencyWHtoWH(consignment);
//isCustomsFlowConsignment (consignment) += consignment IS WHtoWH;
noteConsignment(consignment) += noteWHtoWH(consignment);

//noteConsignmentDetail

//todo: Надо наполнить свойствами накладную. (УНП...)



