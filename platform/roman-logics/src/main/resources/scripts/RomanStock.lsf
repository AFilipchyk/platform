MODULE RomanStock;

REQUIRE StockBy, Warehouse, Barcode, RomanLogicsModule, Contract, MasterData, I18n;

PRIORITY Stock, RomanLogicsModule;

EXTEND FORM dialogSku
    PROPERTIES(s) READONLY FORCE GRID nameCategoryArticleSku, nameBrandSupplierArticleSku, sidArticleSku,
                             sidSizeSupplierItem, sidColorSupplierItem, nameColorSupplierItem
;

nameCategoryArticleSkuLanguage 'Наименование (иностр.)' (sku, language) =
    languageName(categoryArticleSku(sku), language);

// implement абстрактных свойств
descriptionItem 'Наименование' (item) =
        [FORMULA STRING[200] ' CAST($1 AS TEXT) || \'/\' || CAST($2 AS TEXT) || \'/\' || CAST($3 AS TEXT) || \' (\' || CAST($4 AS TEXT) || \', \' || CAST($5 AS TEXT) || \')\''](
        sidArticleSku(item), sidColorSupplierItem(item), sidSizeSupplierItem(item), nameCategoryArticleSku(item), nameBrandSupplierArticleSku(item));

descriptionItemLanguage 'Наименование (иностр.)' (item, language) =
        [FORMULA STRING[200] ' CAST($1 AS TEXT) || \'/\' || CAST($2 AS TEXT) || \'/\' || CAST($3 AS TEXT) || \' (\' || CAST($4 AS TEXT) || \', \' || CAST($5 AS TEXT) || \')\''](
        sidArticleSku(item), sidColorSupplierItem(item), sidSizeSupplierItem(item) , nameCategoryArticleSkuLanguage(item, language), nameBrandSupplierArticleSku(item));

nameSku(sku) += descriptionItem (sku);
nameSkuLanguage(sku, language) += descriptionItemLanguage(sku, language);

// Единицы измерения
UOMSku (sku) += unitOfMeasureArticleSku (sku);

EXTEND FORM UOM
    PROPERTIES(u) nameOrigin, sidUnitOfMeasure
;

EXTEND FORM UOMs
    PROPERTIES(u) READONLY FORCE GRID nameOrigin, sidUnitOfMeasure
;

// объявляем группы sku
parentSkuGroup (skuGroup) += parentCategory(skuGroup);
nameSkuGroupLanguage(skuGroup, language) += languageName(skuGroup, language) IF skuGroup IS category;

skuGroupSku (sku) += categoryArticleSku(sku);

// добавляем еще один регистр (бухгалтерский)
@defineSkuLedgerBalanceProperties(A, ' (бухг.)');

EXTEND FORM currentBalanceSkuStock
    PROPERTIES(s, st) READONLY currentBalanceASkuStock AFTER currentBalanceSkuStock, averagePriceASkuStock AFTER averagePriceSkuStock, currentSumASkuStock AFTER currentSumSkuStock
    PROPERTIES(bil) READONLY skipASkuLedger AFTER skipSkuLedger
    PROPERTIES(bt, st) READONLY currentBalanceABatchStock AFTER currentBalanceBatchStock
;

EXTEND FORM currentBalanceBatchStock
    PROPERTIES(bil) READONLY skipASkuLedger AFTER skipSkuLedger
    PROPERTIES(bt, st) READONLY currentBalanceABatchStock AFTER currentBalanceBatchStock
;

TABLE articleStock (article, stock);
TABLE articleStockDate(article, stock, DATE);
TABLE articleDate(article, DATE);

currentBalanceArticleStock 'Тек. остаток' = GROUP SUM currentBalanceSkuStock(sku, stock) BY articleSku(sku), stock PERSISTENT;
currentBalanceArticle 'Тек. остаток' = GROUP SUM currentBalanceArticleStock(article, stock) BY article PERSISTENT;

balanceBArticleDate 'Остаток на начало дня' (article, date) = GROUP SUM balanceBSkuStockDate(sku, stock, date) BY articleSku(sku), date;
balanceAArticleDate 'Остаток на конец дня' (article, date) = GROUP SUM balanceASkuStockDate(sku, stock, date) BY articleSku(sku), date;

// создаем разные типы партий (бухгалтерские, управленческие)
//CLASS STATIC batchType {
//    batchTypeA 'Бухгалтерская',
//    batchTypeB 'Управленческая'
//}

//orderABatchType (type) = IF type == batchType.batchTypeA THEN 0 IF type == batchType.batchTypeA ELSE 1 IF type IS batchType PERSISTENT;
//orderBBatchType (type) = IF type == batchType.batchTypeB THEN 0 IF type == batchType.batchTypeB ELSE 1 IF type IS batchType PERSISTENT;
//
//batchTypeBatch = ABSTRACT batchType (batch) PERSISTENT;
//nameBatchTypeBatch 'Тип партии' (batch) = name(batchTypeBatch(batch)) IN base;

//orderABatch (batch) = orderABatchType(batchTypeBatch(batch)) PERSISTENT;
//orderBBatch (batch) = orderBBatchType(batchTypeBatch(batch)) PERSISTENT;

//META implementABatch (concrete, skuProp)
//    @implementBatch(concrete, skuProp);
//    batchTypeBatch (batch) += batchType.batchTypeA IF batch IS concrete##Detail;
//END
//
//META implementBBatch (concrete, skuProp)
//    @implementBatch(concrete, skuProp);
//    batchTypeBatch (batch) += batchType.batchTypeB IF batch IS concrete##Detail;
//END

CLASS ABSTRACT batchA 'Бухгалтерская партия' : batch;
CLASS ABSTRACT batchB 'Управленческая партия' : batch;

typeBatch 'Тип партии' (batch) = IF batch IS batchA THEN 'А' AND batch IS batchA
                                                    ELSE 'Б' AND batch IS batchB FIXEDCHARWIDTH 2;

orderABatch (batch) = IF batch IS batchA THEN 0 IF batch IS batchA ELSE 1 IF batch IS batchB PERSISTENT;
orderBBatch (batch) = IF batch IS batchB THEN 0 IF batch IS batchB ELSE 1 IF batch IS batchA PERSISTENT;

META implementSkuLedgerOutFIFOBalanceA (object, stockProp)
    @implementSkuLedgerOutFIFOBalance (object, stockProp);
    orderOutFIFOSkuLedgerBatch (ledger, batch) += orderABatch(batch) IF ledger IS object;
END

META implementSkuLedgerOutFIFOBalanceB (object, stockProp)
    @implementSkuLedgerOutFIFOBalance (object, stockProp);
    orderOutFIFOSkuLedgerBatch (ledger, batch) += orderBBatch(batch) IF ledger IS object;
END

// ------------------------------------------------- Внутренние партии ----------------------------------------- //
ownerBatchA = ABSTRACT legalEntity(batchA) PERSISTENT;
nameOwnerBatchA 'Собственник' (batch) = name(ownerBatchA(batch)) IN base;

commissionContractSkuBatchA = ABSTRACT contractSku (batchA) PERSISTENT INDEXED;
numberCommissionContractSkuBatchA 'Номер договора комиссии' (batch) = numberContract(commissionContractSkuBatchA(batch)) IN base;

importerPriceBatchA 'Цена импортера' (batchA) = ABSTRACT NUMERIC[14,2] (batchA) PERSISTENT;

supplierPriceBatchA 'Цена поставщика' (batchA) = ABSTRACT NUMERIC[14,2] (batchA) PERSISTENT;
supplierSumSkuLedger 'Сумма поставщика' (ledger) = GROUP SUM costSkuLedgerBatch(ledger, batch) * supplierPriceBatchA(batch) BY ledger;

rangeVATBatchA (batchA) = ABSTRACT range (batchA) PERSISTENT;
numberSupplierVATBatchA 'НДС, номер' (batchA) = numberRange(rangeVATBatchA(batchA));
valueSupplierVATBatchA 'НДС,%' (batchA) = valueRateRangeDate
    (rangeVATBatchA(batchA), dateBatch(batchA));

CONSTRAINT taxRange(rangeVATBatchA(batchA)) != tax.taxVAT OR
           countryRange(rangeVATBatchA(batchA)) != countryLegalEntity(ownerBatchA(batchA))
           CHECKED BY rangeVATBatchA
           MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС и стране собственника партии';

EXTEND FORM currentBalanceBatchStock
    PROPERTIES(bt) READONLY FORCE GRID typeBatch, importerPriceBatchA, supplierPriceBatchA, numberSupplierVATBatchA, valueSupplierVATBatchA, nameOwnerBatchA
;

EXTEND FORM currentBalanceSkuStock
    PROPERTIES(bt) READONLY FORCE GRID typeBatch, nameOwnerBatchA, importerPriceBatchA, supplierPriceBatchA
;

EXTEND FORM costSkuLedger
    PROPERTIES(bt) READONLY importerPriceBatchA, supplierPriceBatchA, numberSupplierVATBatchA, valueSupplierVATBatchA
;

// ------------------------------------------------- Кол-во в грузовом месте ----------------------------------- //

META defineDocumentDetailQuantityPackCustomPrefix (detail, skuProp, prefix, caption)
    prefix###quantityPack###detail 'Кол-во грузовых мест'###caption (detail) = round0(quantity###detail(detail) / quantityPackSku(skuProp###detail(detail))) PERSISTENT;
END
META defineDocumentInterfaceDetailQuantityPackCustomPrefix (detail, skuProp, prefix, caption)
    @defineDocumentDetailQuantityPackCustomPrefix(detail, skuProp, prefix, caption);
    @defineDocumentDetailQuantityPackCustomPrefix (user###detail, skuProp, prefix, caption);
    prefix###quantityPack###detail (detail) += prefix###quantityPack###user###detail(detail);
END

META defineDocumentDetailQuantityPackCustom (detail, skuProp)
    @defineDocumentDetailQuantityPackCustomPrefix(detail, skuProp, , );
END
META defineDocumentInterfaceDetailQuantityPackCustom (detail, skuProp)
    @defineDocumentInterfaceDetailQuantityPackCustomPrefix(detail, skuProp, , );
END

META defineDocumentDetailQuantityPackPrefix (object, skuProp, prefix, caption)
    @defineDocumentDetailQuantityPackCustomPrefix(object##Detail, skuProp, prefix, caption);
END
META defineDocumentInterfaceDetailQuantityPackPrefix (object, skuProp, prefix, caption)
    @defineDocumentInterfaceDetailQuantityPackCustomPrefix(object##Detail, skuProp, prefix, caption);
END

META defineDocumentDetailQuantityPack (object, skuProp)
    @defineDocumentDetailQuantityPackPrefix(object, skuProp, , );
END
META defineDocumentInterfaceDetailQuantityPack (object, skuProp)
    @defineDocumentInterfaceDetailQuantityPackPrefix(object, skuProp, , );
END

quantityPackSku 'Количество в грузовом месте' = DATA NUMERIC[9,3] (sku);
Stock.netWeightSku(sku) += RomanLogicsModule.netWeightSku(sku);

FORM batchADialogForm 'Партия'
    OBJECTS ba = batchA
    PROPERTIES (ba) READONLY nameSkuBatch, descriptionBatch, nameOwnerBatchA, importerPriceBatchA, supplierPriceBatchA,
                   numberSupplierVATBatchA, valueSupplierVATBatchA, currentBalanceBatch

    DIALOG batchA OBJECT ba
;

priceSkuStockDateTime 'Цена' = ABSTRACT NUMERIC[14,2] (sku, stock, DATETIME);
priceBatchStockDateTime 'Цена' = ABSTRACT NUMERIC[14,2] (batch, stock, DATETIME);

priceBatchStockDateTime(batch, stock, dateTime) += supplierPriceBatchA(batch) IF stock IS warehouse AND dateTime IS DATETIME;
//priceSkuStockDateTime(sku, stock, dateTime) += 155.2 IF sku IS sku IF stock IS warehouse AND dateTime IS DATETIME;