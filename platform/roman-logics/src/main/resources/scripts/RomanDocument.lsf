MODULE RomanDocument;

REQUIRE Document, Stock, Barcode, StorePrice, Supplier;

META defineRBDocument (object, stock, stockCaption, skuProp, caption)

    @defineDocumentBasePosted(object);

    @defineDocumentStock(object, stock, stockCaption);

    @defineDocumentDetailQuantity(object);

    @defineDocumentDetailNumbered(object);

    @defineDocumentHeaderQuantity(object);

    @defineDocumentDescription (object, caption);

    @implementSkuLedger (object, skuProp, stock);

    @defineDocumentDetailProps (object);

    @defineAddDetailDialogSkuStock(object, sku, stock, dialogSku);

    @defineAddDetailDialogBarcode(object, sku);
END

META defineRBCorrespondingDocument (object, stock, stockCaption, skuProp, caption, corresponder, corresponderCaption)

    @defineRBDocument (object, stock, stockCaption, skuProp, caption);

    corresponder###object (object) = DATA corresponder (object) IN idGroup;
    name###corresponder###object corresponderCaption (object) = name (corresponder###object(object)) IN documentHeaderGroup;

END

META defineRBCorrespondingDocumentWithRetailPrices (object, stock, stockCaption, skuProp, caption, corresponder, corresponderCaption)

    @defineRBCorrespondingDocument (object, stock, stockCaption, skuProp, caption, corresponder, corresponderCaption);

    @defineDocumentDetailBasePrice(object);
    @defineDocumentDetailBaseSum(object);

    @defineDocumentDetailInvoicePrice(object);
    @defineDocumentDetailInvoiceSum(object);

    @defineDocumentHeaderBaseSum(object);
    @defineDocumentHeaderInvoiceSum(object);

    @implementBaseSkuDepartmentStoreLedger(object, sku);
END

// ----------------------------- количество упаковок и вес товаров ----------------------- //
META defineDocumentDetailPackWeightSku (object)
    packQuantity###object##Detail 'Количество грузовых мест' (idetail) = round0(quantity###object##Detail(idetail)/
        UNION OVERRIDE 1 IF idetail IS object##Detail, quantityPackSku(sku###object##Detail(idetail)));

    grossWeight###object##Detail 'Масса груза, кг.' (idetail) = round0(
        netWeightSku(sku###object##Detail(idetail))*quantity###object##Detail(idetail));
END

META defineDocumentHeaderSkuQuantityCustom (object, detail)
    quantity###detail##Sku###object 'Кол-во товара в документе' (sku, object) = GROUP SUM quantity###detail(idetail)
                                                                                      BY  sku###detail(idetail),
                                                                                          object###detail(idetail);
END

META defineDocumentHeaderSkuQuantity (object)
    @defineDocumentHeaderSkuQuantityCustom(object, object###detail);
END


// для использования метакода должно быть определено свойство sku###object##Detail
META defineDocumentDetailProps (object)

    barcode###object##Detail 'Штрих-код' (object##Detail) = barcode(sku###object##Detail(object##Detail));

    sidSize###object##Detail 'Размер' (object##Detail) = sidSizeSupplierItem(sku###object##Detail(object##Detail));
    sidColor###object##Detail 'Цвет (код)' (object##Detail) = sidColorSupplierItem(sku###object##Detail(object##Detail));
    nameColor###object##Detail 'Цвет' (object##Detail) = nameColorSupplierItem(sku###object##Detail(object##Detail));

    article###object##Detail 'Артикул (ИД)' (object##Detail) = articleSku(sku###object##Detail(object##Detail)) IN idGroup;
    sidArticle###object##Detail 'Артикул' (object##Detail) = sidArticle(article###object##Detail(object##Detail));
    brand###object##Detail 'Бренд (ИД)' (object##Detail) = brandSupplierArticle(article###object##Detail(object##Detail)) IN idGroup;
    nameBrand###object##Detail 'Бренд' (object##Detail) = name(brand###object##Detail(object##Detail));
    category###object##Detail 'Номенклатурная группа (ИД)' (object##Detail) = categoryArticle(article###object##Detail(object##Detail)) IN idGroup;
    nameCategory###object##Detail 'Номенклатурная группа' (object##Detail) = name(category###object##Detail(object##Detail));

    UOM###object##Detail 'Единица измерения (ИД)' (object##Detail) = DATA UOM (object##Detail) IN idGroup;
    shortNameUOM###object##Detail 'Единица измерения' (object##Detail) = shortName(UOM###object##Detail(object##Detail));
END

//-----------Для подбора товаров------------------//

    @defineDialogSku(dialogSku);
    @defineDialogSkuBarcode(dialogSku);
    @defineDialogSkuSupplier(dialogSku);
  //  @defineDialogSkuStorePrice(dialogSku);

