MODULE Freight;

REQUIRE System,
        Stock,
        Store,
        I18n,
        LegalEntity,
        Barcode,
        RetailCRM,
        AccountDocument,
        Employee,
        UserPriceChange,
        Contract,
        Supplier,
        Warehouse,
        RomanLogicsModule;

PRIORITY Stock, LegalEntity, Store, RomanLogicsModule;

mainCompositionSkuFreight 'Состав (иностр.)' (sku, freight) = mainCompositionSkuLanguage(sku, languageFreight(freight));
additionalCompositionSkuFreight 'Доп. состав (иностр.)' (sku, freight) = additionalCompositionSkuLanguage(sku, languageFreight(freight));

changeMainCompositionSkuLanguage = ACTION (sku, freight) {
    REQUEST STRING[200] INPUT;
    SET mainCompositionSkuLanguage(sku, language) IF language == languageFreight(freight) <- requestedString();
}

changeAdditionalCompositionSkuLanguage = ACTION (sku, freight) {
    REQUEST STRING[200] INPUT;
    SET additionalCompositionSkuLanguage(sku, language) IF language == languageFreight(freight) <- requestedString();
}

mainCompositionLanguageFreightSku (freight, sku) <- mainCompositionSkuFreight(sku, freight) WHEN ASSIGNED (freight IS freightChanged);
additionalCompositionLanguageFreightSku (freight, sku) <- additionalCompositionSkuFreight(sku, freight) WHEN ASSIGNED (freight IS freightChanged);

nameCountryLanguageFreightSku 'Страна (иностр.)' (freight, sku) = nameCountrySkuLanguage(sku, languageFreight(freight));
nameCountryOfOriginLanguageFreightSku 'Страна (иностр.)' (freight, sku) = languageName(countryOfOriginFreightSku(freight, sku), languageFreight(freight));

nameCategoryFreightLanguageSku 'Номенклатурная группа (иностр.)' (freight, sku) = nameCategoryArticleSkuLanguage(sku, languageFreight(freight));

CONSTRAINT customsZoneFreight(freight) != customsZoneCustomCategory10(customCategory10SkuFreight(sku, freight))
    CHECKED BY customCategory10SkuFreight
    MESSAGE 'ТН ВЭД для другой таможенной зоны';

customCategory10LanguageFreightSku = DATA customCategory10(freight, sku);
sidCustomCategory10LanguageFreightSku 'ТН ВЭД (иностр.)' (freight, sku) = sidCustomCategory10(customCategory10LanguageFreightSku(freight, sku));

customCategory10LanguageFreightSku(freight, sku) <- customCategory10SkuFreight(sku, freight) WHEN ASSIGNED (freight IS freightChanged);

CONSTRAINT customsZoneFreight(freight) != customsZoneCustomCategory10(customCategory10LanguageFreightSku(freight, sku))
    CHECKED BY customCategory10LanguageFreightSku
    MESSAGE 'ТН ВЭД для другой таможенной зоны';

CONSTRAINT customsZoneFreight(freight) != customsZoneCustomCategory10(customCategory10CategoryGenderCompositionTypeFabricFreight(category, gender, composition, typeFabric, freight))
    CHECKED BY customCategory10CategoryGenderCompositionTypeFabricFreight
    MESSAGE 'ТН ВЭД для другой таможенной зоны';

META dateFreight (object, caption)

    FORM date###object caption
        OBJECTS d = DATE FIXED PANEL
        PROPERTIES(d) OBJVALUE
    ;

END

@dateFreight(freightShipped, 'Дата отгрузки');
@dateFreight(freightArrived, 'Дата прибытия');

toShipFreight 'Отгрузить' =  [ACTION (freight) {

    FORM dateFreightShipped MODAL;
        IF formResult() == formResult.ok THEN {
            SET dateShipmentFreight(freight) <- chosenDate('d');
            CHANGECLASS freight TO freightShipped;
            EXEC apply();
        }

}](freight) IF freight IS freightPriced AND NOT freight IS freightShipped IMAGE 'sign_tick.png';

toArrivedFreight 'Прибыл' = [ACTION (freight) {

    FORM dateFreightArrived MODAL;
        IF formResult() == formResult.ok THEN {
            SET dateArrivalFreight(freight) <- chosenDate('d');
            CHANGECLASS freight TO freightArrived;
            EXEC apply();
        }

}](freight) IF freight IS freightShipped AND NOT freight IS freightArrived;

FORM freight 'Фрахт'

    OBJECTS f = freight FIXED PANEL
    PROPERTIES(f) objectClassName, nameNumeratorObject, numberObject, seriesObject, date, dateShipmentFreight,
                  dateArrivalFreight, nameCountryFreight, nameLanguageFreight, nameRouteFreight, nameExporterFreight,
                  descriptionFreight, tonnageDataFreight, volumeDataFreight, palletCountDataFreight,
                  nameCurrencyFreight, sumFreightFreight

    EDIT freight OBJECT f
;

DESIGN freight FROM DEFAULT {

    main{

        NEW headerContainer{

            caption = 'Шапка документа';
            childConstraints = TO THE BOTTOM;
            NEW firstContainer{
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(objectClassName);
                ADD PROPERTY(nameNumeratorObject);
                ADD PROPERTY(numberObject);
                ADD PROPERTY(seriesObject);
            }
            NEW dateContainer{
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(date);
                ADD PROPERTY(dateShipmentFreight);
                ADD PROPERTY(dateArrivalFreight);
            }
        }

        NEW paramContainer {

            caption = 'Параметры документа';
            ADD PROPERTY(nameCountryFreight);
            ADD PROPERTY(nameLanguageFreight);
            ADD PROPERTY(nameRouteFreight);
            ADD PROPERTY(nameExporterFreight);
            ADD PROPERTY(descriptionFreight);
        }

        NEW sumContainer{

            caption = 'Суммы документа';
            childConstraints = TO THE BOTTOM;
            ADD PROPERTY(tonnageDataFreight);
            ADD PROPERTY(volumeDataFreight);
            ADD PROPERTY(palletCountDataFreight);
            ADD PROPERTY(nameCurrencyFreight);
            ADD PROPERTY(sumFreightFreight);
        }
        POSITION sumContainer TO THE RIGHT headerContainer;
        POSITION sumContainer TO THE RIGHT paramContainer;
    }

    ADD functions.box;
}

EXTEND FORM freightListForm
    PROPERTIES (freight) READONLY nameCountryFreight BEFORE nameRouteFreight(freight), nameLanguageFreight BEFORE nameRouteFreight(freight)
    PROPERTIES (freight) FORCE GRID toShipFreight, toArrivedFreight

    PROPERTIES (freight) ADDFORM, EDITFORM, delete FORCE PANEL
;

EXTEND DESIGN freightListForm{

    PROPERTY(delete(freight)){
        panelLocation = TOOLBAR;
        askConfirm=TRUE;
    }
}

EXTEND FORM freightChangeForm

    PROPERTIES (freight, sku) nameCategoryFreightLanguageSku AFTER nameCategoryArticleSku(sku) SHOWIF languageFreight(freight),
                              nameCountryLanguageFreightSku  AFTER nameCountrySku(sku) SHOWIF languageFreight(freight)

    PROPERTIES (sku) mainCompositionOriginSku, translationMainCompositionSku, mainCompositionSku
    PROPERTIES (sku, freight) sidCustomCategory10SkuFreight BEFORE nameCountrySku(sku) SHOWIF languageFreight(freight),
                              translationMainCompositionSkuFreight SHOWIF languageFreight(freight)
    PROPERTIES (sku, freight) mainCompositionSkuFreight ON CHANGE EXEC changeMainCompositionSkuLanguage(sku, freight) SHOWIF languageFreight(freight)

    PROPERTIES (sku) additionalCompositionOriginSku, translationAdditionalCompositionSku, additionalCompositionSku
    PROPERTIES (sku, freight) translationAdditionalCompositionSkuFreight SHOWIF languageFreight(freight)
    PROPERTIES (sku, freight) additionalCompositionSkuFreight ON CHANGE EXEC changeAdditionalCompositionSkuLanguage(sku, freight) SHOWIF languageFreight(freight)

    PROPERTIES (freight, skuFreight) sidCustomCategory10LanguageFreightSku BEFORE nameCountryOfOriginFreightSku SHOWIF languageFreight(freight),
                                     mainCompositionOriginFreightSku, translationMainCompositionFreightSku, mainCompositionFreightSku,
                                     translationMainCompositionLanguageFreightSku SHOWIF languageFreight(freight),
                                     mainCompositionLanguageFreightSku SHOWIF languageFreight(freight),

                                     additionalCompositionOriginFreightSku, translationAdditionalCompositionFreightSku, additionalCompositionFreightSku,
                                     translationAdditionalCompositionLanguageFreightSku SHOWIF languageFreight(freight),
                                     additionalCompositionLanguageFreightSku SHOWIF languageFreight(freight),

                                     nameCountryOfOriginLanguageFreightSku AFTER nameCountryOfOriginFreightSku(freight, skuFreight) SHOWIF languageFreight(freight),
                                     sidCustomCategoryOriginFreightSku BEFORE sidCustomCategory10FreightSku
;
