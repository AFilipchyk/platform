//To Store From Store (the same company)
MODULE StoSDirect;

REQUIRE System,

        Stock,
        Numerator,
        Document,
        RomanDocument,
        Consignment,
        Declaration,
        MasterData,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, Stock;

CLASS StoSDir 'Перемещение (расход) на магазин (одно юрлицо)' : historyObject, numberedObject, consignment;
CLASS StoSDirDetail 'Строка перемещения (расход) на магазин (одно юрлицо)' : outAutoSkuLedger, consignmentDetail;
CLASS StoSDirPosted 'Закрытое перемещение (расход) на магазин (одно юрлицо)' : StoSDir, postedObject, consignment;


skuStoSDirDetail 'Товар (ИД)' (StoSDirDetail) = DATA sku (StoSDirDetail)IN idGroup;
nameSkuStoSDirDetail 'Наименование товара' (StoSDirDetail) = nameSku(skuStoSDirDetail(StoSDirDetail));

@defineRBCorrespondingDocumentWithRetailPrices(StoSDir, departmentStore, 'Отдел документа', sku, 'Перемещение (расход) на магазин (одно юрлицо)', store, 'Магазин-получатель');
@defineDocumentDetailPackWeightSku(StoSDir);
@defineDocumentHeaderSkuQuantity(StoSDir);

senderStoreStoSDir 'Магазин-отправитель (ИД)' (StoSDir) = storeDepartmentStore(departmentStoreStoSDir(StoSDir));
nameSenderStoreStoSDirDetail 'Магазин-отправитель' (StoSDirDetail) = name(senderStoreStoSDir(StoSDirDetail)) MINCHARWIDTH 20 PREFCHARWIDTH 20;

CONSTRAINT StoSDir IS StoSDir AND NOT companyStore(senderStoreStoSDir(StoSDir)) == companyStore(storeStoSDir(StoSDir))
    CHECKED BY storeStoSDir  MESSAGE 'Перемещение (расход) не в рамках одного юрлица';


//quantityOutAutoSkuLedger (ledger) += quantityStoSDirDetail (ledger);
//descriptionSkuLedger (ledger) += descriptionStoSDirDetail (ledger);

FORM StoSDir 'Перемещение (расход) на магазин (одно юрлицо)'
    OBJECTS s = StoSDir FIXED PANEL
    PROPERTIES (s) numberObject, seriesObject, dateStoSDir, timeStoSDir, nameDepartmentStoreStoSDir, nameStoreStoSDir,
                   quantityStoSDirDetailStoSDir, retailVATSumStoSDirDetailStoSDir,
                   retailSumStoSDirDetailStoSDir, grossWeightConsignmentDetailConsignment, packQuantityConsignmentDetailConsignment, noteStoSDir

    OBJECTS d = StoSDirDetail
    PROPERTIES(d) indexStoSDirDetail, barcodeStoSDirDetail, nameSkuStoSDirDetail, nameBrandStoSDirDetail, sidArticleStoSDirDetail,
                  nameCategoryStoSDirDetail, sidColorStoSDirDetail, nameColorStoSDirDetail, shortNameUOMStoSDirDetail, balanceBSkuStoSDirDetail,
                  quantityStoSDirDetail, retailPriceStoSDirDetail, numberRetailVATStoSDirDetail, valueRetailVATStoSDirDetail,
                  retailVATSumStoSDirDetail, retailSumStoSDirDetail

    PROPERTIES(s) TODRAW d addDetailDialogSkuStoSDirDetailStoSDir, addDetailDialogBarcodeStoSDirDetailStoSDir,
                  deleteStoSDirDetailStoSDir


    PROPERTIES(d) ADDOBJ, delete

    FILTERS inStoSDirStoSDirDetail(s, d)

    EDIT StoSDir OBJECT s
;

DESIGN StoSDir FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        ADD s.box {
            childConstraints = TO THE RIGHT;
            NEW row1 {
                childConstraints = TO THE BOTTOM;
                ADD s.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (nameDepartmentStoreStoSDir);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateStoSDir);
                    ADD PROPERTY (timeStoSDir);
                }
                NEW case {
                childConstraints = TO THE BOTTOM;
                    title = 'Контрагент';
                    ADD PROPERTY (nameStoreStoSDir);
                }
                ADD s.documentPrmGroup;
            }
            NEW row2 {
                childConstraints = TO THE BOTTOM;
                ADD s.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                    ADD PROPERTY(quantityStoSDirDetailStoSDir);
                    ADD PROPERTY(retailVATSumStoSDirDetailStoSDir);
                    ADD PROPERTY(retailSumStoSDirDetailStoSDir);
                }
                ADD s.sumConsignmentGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        }
        ADD d.box;
        ADD functions.box;
    }
}

FORM StoSDirs 'Перемещения (расход) на магазин (одно юрлицо)'
    OBJECTS s = StoSDir
    PROPERTIES (s) READONLY objectClassName, numberObject, seriesObject, dateStoSDir, timeStoSDir, nameDepartmentStoreStoSDir, nameStoreStoSDir,
                   countStoSDirDetailStoSDir, quantityStoSDirDetailStoSDir, retailVATSumStoSDirDetailStoSDir,
                   retailSumStoSDirDetailStoSDir, grossWeightConsignmentDetailConsignment, packQuantityConsignmentDetailConsignment

    PROPERTIES (s) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed


    PROPERTIES (s) ADDFORM, EDITFORM SHOWIF isDraftStoSDir(s), delete FORCE PANEL SHOWIF isDraftStoSDir(s),
                   postStoSDir SHOWIF isDraftStoSDir(s), unpostStoSDir SHOWIF isPostedStoSDir(s)

    PROPERTIES (s) FORCE PANEL printConsignmentVerticalA, printConsignmentHorizontalA,
                   printConsignmentVerticalB, printConsignmentHorizontalB,
                   printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                   printConsignmentSimpleVertical, printConsignmentSimpleAttach

    OBJECTS d = StoSDirDetail
    PROPERTIES(d) READONLY indexStoSDirDetail, barcodeStoSDirDetail, nameSkuStoSDirDetail, shortNameUOMStoSDirDetail, balanceBSkuStoSDirDetail,
                  quantityStoSDirDetail, retailPriceStoSDirDetail, numberRetailVATStoSDirDetail, valueRetailVATStoSDirDetail,
                  retailVATSumStoSDirDetail, retailSumStoSDirDetail

    FILTERS inStoSDirStoSDirDetail(s, d)
;

DESIGN StoSDirs FROM DEFAULT {
    ADD s.box{
        PROPERTY (delete(s)) {
            panelLocation = TOOLBAR;
            askConfirm = TRUE;
        PROPERTY (objectClassName(s)) {
            preferredCharWidth = 15;
        }
        }
    }
    ADD d.box;

    NEW footer.container {
        childConstraints = TO THE BOTTOM;

        NEW cont3 {
            childConstraints = TO THE RIGHT;
            ADD s.historyGroup {
                childConstraints = TO THE BOTTOM;
            }

            ADD s.postedGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        ADD s.printGroup {
            childConstraints = TO THE BOTTOM;
            NEW case55{
                childConstraints = TO THE RIGHT;

                NEW contOne {
                    title = 'Накладная';
                    ADD PROPERTY(editConsignment);
                }
                NEW tn{
                    childConstraints = TO THE RIGHT;
                    title = 'ТН-2';
                    ADD PROPERTY(printConsignmentSimpleVertical);
                    ADD PROPERTY(printConsignmentSimpleHorizontal);
                    ADD PROPERTY(printConsignmentSimpleAttach);
                }
            }
            NEW ttn1{
                childConstraints = TO THE RIGHT;
                title = 'ТТН-1';
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentHorizontalB);
                ADD PROPERTY(printConsignmentAttach);
            }

        }

    }
    ADD functions.box;
}

FORM StoSDirsDialog 'Перемещения (расход) на магазин (одно юрлицо)'
    OBJECTS s = StoSDirPosted
    PROPERTIES (s) READONLY objectClassName, numberObject, seriesObject, dateStoSDir, nameDepartmentStoreStoSDir,
                   nameStoreStoSDir, countStoSDirDetailStoSDir, quantityStoSDirDetailStoSDir,
                   retailVATSumStoSDirDetailStoSDir, retailSumStoSDirDetailStoSDir
    DIALOG StoSDirPosted OBJECT s
;

@defineConsignment(StoSDir);
@implementConsignmentHeader(StoSDir, departmentStore);

senderConsignment (consignment) += companyStore(senderStoreStoSDir(consignment));
recipientConsignment (consignment) += companyStore(storeStoSDir(consignment));

consignmentConsignmentDetail (consignmentDetail) += StoSDirStoSDirDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuStoSDirDetail (consignmentDetail);
shortNameUOMConsignmentDetail (consignmentDetail) += shortNameUOMStoSDirDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantityStoSDirDetail (consignmentDetail);
priceConsignmentDetail (consignmentDetail) += retailPriceStoSDirDetail (consignmentDetail);
vatConsignmentDetail (consignmentDetail) += valueRetailVATStoSDirDetail (consignmentDetail);
sumVATConsignmentDetail (consignmentDetail) += retailVATSumStoSDirDetail (consignmentDetail);
sumConsignmentDetail (consignmentDetail) += retailSumStoSDirDetail (consignmentDetail) (-) retailVATSumStoSDirDetailStoSDir(consignmentDetail);
sumInvoiceConsignmentDetail (consignmentDetail) += retailSumStoSDirDetail (consignmentDetail);
grossWeightConsignmentDetail (consignmentDetail) += grossWeightStoSDirDetail (consignmentDetail);
packQuantityConsignmentDetail (consignmentDetail) += packQuantityStoSDirDetail(consignmentDetail);
//currencyConsignment(consignment) += currencyStoSDir(consignment);
//isCustomsFlowConsignment (consignment) += consignment IS StoSDir;
noteConsignment(consignment) += noteStoSDir(consignment);

//noteConsignmentDetail

//todo: Надо наполнить свойствами накладную. (УНП...)