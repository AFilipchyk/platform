MODULE RomanPOS;

REQUIRE POS, RomanStock;

NAMESPACE POS;

userSkipAReceiptDetail (receiptDetail) = DATA BOOLEAN (receiptDetail);

diffCompanyReceipt (receipt) = companyCashRegister (cashRegisterZReport(zReportReceipt(receipt)))!= companyDepartmentStore (departmentStoreReceipt (receipt));

skipAReceiptSaleDetail 'Не бухг.' (receiptDetail) = UNION OVERRIDE diffCompanyReceipt (receiptReceiptDetail (receiptDetail)),
                                                            userSkipAReceiptDetail (receiptDetail);

skipASkuLedger (ledger) += skipAReceiptSaleDetail (ledger);

sumSkipAReceiptSaleDetailZReport 'Сумма (не бухг.)' (zReport) = GROUP SUM sumReceiptSaleDetail (receiptSaleDetail)
                                                   IF skipAReceiptSaleDetail (receiptSaleDetail)
                                                   BY zReportReceiptDetail (receiptSaleDetail);

sumAccountReceiptDetailZReport 'Сумма учетная' (zReport) = sumReceiptDetailZReport (zReport) - sumSkipAReceiptSaleDetailZReport (zReport);

@implementAccountZReportLedger (sumAccountReceiptDetailZReport);

selectSkipAReceiptDetailZReport 'Подобрать строки' = ACTION (zReport, sumSelect) {
    LOCAL sumLeft = NUMERIC[16,2] ();
    SET sumLeft() <- sumSelect;

    SET skipAReceiptSaleDetail(receiptDetail) <- NULL WHERE zReportReceiptDetail(receiptDetail) == zReport;
    FOR zReportReceiptDetail(receiptDetail) == zReport ORDER DESC sumReceiptSaleDetail(receiptDetail) DO {
        IF sumReceiptSaleDetail(receiptDetail) <= sumLeft() THEN {
            SET skipAReceiptSaleDetail(receiptDetail) <- TRUE;
            SET sumLeft() <- sumLeft() - sumReceiptSaleDetail(receiptDetail);
        }
    }
}

selectDialogSkipAReceiptDetailZReport 'Подобрать строки' = ACTION (zReport) NEWSESSION {
    FORM dialogNumeric MODAL;
    IF formResult() == formResult.ok THEN {
        EXEC selectSkipAReceiptDetailZReport(zReport, chosenNumeric('n'));
        EXEC apply();
    }
} TOOLBAR;

EXTEND FORM zReport
    PROPERTIES (z) sumSkipAReceiptSaleDetailZReport, sumAccountReceiptDetailZReport
    PROPERTIES (d) skipAReceiptSaleDetail
;

EXTEND FORM zReports
    PROPERTIES (z) READONLY sumSkipAReceiptSaleDetailZReport, sumAccountReceiptDetailZReport
    PROPERTIES (d) READONLY FORCE GRID skipAReceiptSaleDetail
;