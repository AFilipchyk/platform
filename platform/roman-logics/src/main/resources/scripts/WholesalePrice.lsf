MODULE WholesalePrice;

REQUIRE System,
        Declaration,
        MasterData,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, RB;

CLASS wholesalePriceDocument 'Оптовый прайс' : transaction;

dateTimeWholesalePriceDocument 'Дата/время' = DATA DATETIME(wholesalePriceDocument);
dateTimeWholesalePriceDocument(wholesalePriceDocument) <- currentDateTime() WHEN ASSIGNED(wholesalePriceDocument IS wholesalePriceDocument);

declarationWholesalePriceDocument 'Декларация оптового прайса (ИД)' = DATA declaration(wholesalePriceDocument) IN idGroup;

inArticleWholesalePriceDocument 'Товар в документе' = DATA BOOLEAN(wholesalePriceDocument, article);

priceCustomWholesalePriceDocumentArticle 'Себестоимость (БУ)' (wholesalePriceDocument, article) = DATA NUMERIC[14,2](wholesalePriceDocument, article) IN baseGroup;
//priceCustomDeclarationArticle(declarationWholesalePriceDocument(wholesalePriceDocument), article);
priceCustomWholesalePriceDocumentArticle (wholesalePriceDocument, article) <- priceCustomDateTime(article, dateTimeWholesalePriceDocument(wholesalePriceDocument))
    WHEN ASSIGNED (inArticleWholesalePriceDocument(wholesalePriceDocument, article));

priceManagerialWholesalePriceDocumentArticle 'Себестоимость (УУ)' (wholesalePriceDocument, article) = DATA NUMERIC[14,2](wholesalePriceDocument, article) IN baseGroup;
//priceManagerialDeclarationArticle(declarationWholesalePriceDocument(wholesalePriceDocument), article);
priceManagerialWholesalePriceDocumentArticle (wholesalePriceDocument, article) <- priceManagerialDateTime(article, dateTimeWholesalePriceDocument(wholesalePriceDocument))
    WHEN ASSIGNED (inArticleWholesalePriceDocument(wholesalePriceDocument, article));

priceWholesalePriceDocumentArticle 'Оптовая цена' = DATA NUMERIC[14,2](wholesalePriceDocument, article) IN baseGroup;

priceWholesalePriceDocumentArticle(wholesalePriceDocument, article) <- priceCustomWholesalePriceDocumentArticle(wholesalePriceDocument, article)
    WHEN ASSIGNED (inArticleWholesalePriceDocument(wholesalePriceDocument, article));

orderWholesalePriceDocument = LIST(dateTimeWholesalePriceDocument(wholesalePriceDocument), wholesalePriceDocument) PERSISTENT;

concatWholesalePriceDocumentArticleDateTime (article, dateTime) = GROUP MAX orderWholesalePriceDocument(wholesalePriceDocument) AND
                                                                   priceWholesalePriceDocumentArticle(wholesalePriceDocument, article) AND
                                                                   dateTimeWholesalePriceDocument(wholesalePriceDocument) < (dateTime AS DATETIME)
                                                                BY article, dateTime;

documentWholesalePriceArticleDateTime (article, dateTime) = concatWholesalePriceDocumentArticleDateTime(article, dateTime)[2];

priceWholesaleArticleDateTime(article, dateTime) = priceWholesalePriceDocumentArticle(documentWholesalePriceArticleDateTime(article, dateTime), article);



FORM wholesalePriceDocument 'Оптовый прайс'

    OBJECTS w=wholesalePriceDocument FIXED PANEL
    PROPERTIES(w) dateTimeWholesalePriceDocument

    TREE treeSupplierBrand v=STRING[3], s=supplier, b=brandSupplier
    PROPERTIES READONLY OBJVALUE(v), name(s), name(b)

    TREE treeCategory vv=STRING[3], c=category
    PROPERTIES READONLY OBJVALUE(vv), name(c)

    OBJECTS aa=article
    PROPERTIES (aa) READONLY sidArticle, nameCategoryArticle, nameBrandSupplierArticle
    PROPERTIES (w, aa) inArticleWholesalePriceDocument

    FILTERS stringEqualsAll(v), inBrandSupplier(s, b), inSupplierBrandArticle(s, b, aa), stringEqualsAll(vv), inArticleCategory(c, aa)

    OBJECTS a=article
    PROPERTIES(a) READONLY sidArticle, nameCategoryArticle, nameBrandSupplierArticle
    PROPERTIES (w, a) priceCustomWholesalePriceDocumentArticle, priceManagerialWholesalePriceDocumentArticle, priceWholesalePriceDocumentArticle

    FILTERS inArticleWholesalePriceDocument(w, a)

    FILTERGROUP filtersCategory
        FILTER 'Показывать артикулы ном. группы' 'F10' inArticleCategory(c, a)
    FILTERGROUP filtersBrand
        FILTER 'Показывать артикулы бренда' 'F11' inArticleBrand(b, a)

    EDIT wholesalePriceDocument OBJECT w
;

DESIGN wholesalePriceDocument FROM DEFAULT {


    treeSupplierBrand.tree.box {
        title = 'Поставщики-Бренды';
        fillHorizontal = 1.5;
    }
    treeCategory.tree.box {
        title = 'Номенклатурные группы';
    }

    aa.box {
        fillHorizontal = 1.5;
    }

    POSITION treeSupplierBrand.tree.box TO THE LEFT treeCategory.tree.box;
    POSITION treeCategory.tree.box TO THE LEFT aa.box;

}

FORM wholesalePriceDocuments 'Оптовые прайсы'

    OBJECTS w=wholesalePriceDocument
    PROPERTIES(w) READONLY dateTimeWholesalePriceDocument
    PROPERTIES(w) ADDFORM, EDITFORM, delete

    OBJECTS a=article
    PROPERTIES(a) READONLY sidArticle, nameCategoryArticle, nameBrandSupplierArticle
    PROPERTIES (w, a) READONLY priceCustomWholesalePriceDocumentArticle, priceManagerialWholesalePriceDocumentArticle, priceWholesalePriceDocumentArticle

    FILTERS inArticleWholesalePriceDocument(w, a)
;


toCreateWholesalePriceDocument 'Прайс по декларации' =  ACTION (declaration) NEWSESSION {
    ADDOBJ wholesalePriceDocument;
    FOR w == addedObject() DO {
        SET declarationWholesalePriceDocument(w) <- declaration AS declaration;
        SET priceCustomWholesalePriceDocumentArticle(w, article) <- priceCustomDeclarationArticle(declaration, article);
        SET priceManagerialWholesalePriceDocumentArticle(w, article) <- priceManagerialDeclarationArticle(declaration, article);
        SET priceWholesalePriceDocumentArticle(w, article) <- priceCustomDeclarationArticle(declaration, article)*rateExchangeDeclaration(declaration);
        SET inArticleWholesalePriceDocument(w, article) <- inDeclarationArticle(declaration, article);

        FORM wholesalePriceDocument OBJECTS w=addedObject() MODAL;
        IF formResult() == formResult.ok THEN {
            EXEC apply();
        };
    };
} CONFIRM;

EXTEND FORM declarations
     PROPERTIES(d) toCreateWholesalePriceDocument
;