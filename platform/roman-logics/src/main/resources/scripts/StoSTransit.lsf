//To Store From Store (the different company)
MODULE StoSTransit;

REQUIRE System,

        Stock,
        Numerator,
        Document,
        RomanDocument,
        Consignment,
        Declaration,
        MasterData,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, Stock;

CLASS StoSTr 'Перемещение (расход) на магазин (др. юрлицо)' : historyObject, numberedObject, consignment;
CLASS StoSTrDetail 'Строка перемещения (расход) на магазин (др. юрлицо)' : outAutoSkuLedger, consignmentDetail;
CLASS StoSTrPosted 'Закрытое перемещение (расход) на магазин (др.юрлицо)' : StoSTr, postedObject, consignment;


skuStoSTrDetail 'Товар (ИД)' (StoSTrDetail) = DATA sku (StoSTrDetail)IN idGroup;
nameSkuStoSTrDetail 'Наименование товара' (StoSTrDetail) = nameSku(skuStoSTrDetail(StoSTrDetail));

@defineDocumentBasePosted(StoSTr);
@defineDocumentStock(StoSTr, departmentStore, 'Отдел документа');

storeStoSTr 'Магазин-отправитель (ИД)' (StoSTr) = storeDepartmentStore(departmentStoreStoSTr(StoSTr));
nameStoreStoSTrDetail 'Магазин-отправитель' (StoSTrDetail) = name(storeStoSTr(StoSTrDetail)) MINCHARWIDTH 20 PREFCHARWIDTH 20;


recipientStoreStoSTr = DATA store(StoSTr) AUTOSET;
nameRecipientStoreStoSTr 'Магазин-получатель' (StoSTr) = name(recipientStoreStoSTr(StoSTr)) IN documentHeaderGroup
        MINCHARWIDTH 20 PREFCHARWIDTH 20;

recipientStoreStoSTrDetail (StoSTrDetail) = recipientStoreStoSTr(StoSTrStoSTrDetail(StoSTrDetail));
nameRecipientStoreStoSTrDetail 'Магазин-получатель' (StoSTrDetail) = name(recipientStoreStoSTrDetail(StoSTrDetail)) MINCHARWIDTH 20 PREFCHARWIDTH 20;

@defineDocumentDetailQuantity(StoSTr);
@defineDocumentDetailNumbered(StoSTr);
@defineDocumentHeaderQuantity(StoSTr);
@defineDocumentDescription(StoSTr, 'Перемещение (расход) на магазин (др. юрлицо)');
@implementSkuLedger (StoSTr, sku, departmentStore);
@defineDocumentDetailProps (StoSTr);
@defineAddDetailDialogSkuStock(StoSTr, sku, departmentStore, dialogSku);

CONSTRAINT companyStore(recipientStoreStoSTr(StoSTr)) == companyStore(storeStoSTr(StoSTr))
    CHECKED BY storeStoSTr, recipientStoreStoSTr  MESSAGE 'Перемещение (расход) в рамках одного юрлица';

transitWarehouseStoSTr 'Промежуточный склад (ИД)' (WHtoSTr) = DATA warehouse (StoSTr) IN idGroup;
nameTransitWarehouseStoSTr 'Промежуточный склад' (StoSTr) = name(transitWarehouseStoSTr(StoSTr)) IN documentHeaderGroup  MINCHARWIDTH 20 PREFCHARWIDTH 20;

CONSTRAINT companyWarehouse(transitWarehouseStoSTr(StoSTr)) == companyStore(storeStoSTr(StoSTr))
    CHECKED BY storeStoSTr, transitWarehouseStoSTr MESSAGE 'Промежуточный склад должен быть другого юрлица';

CONSTRAINT companyWarehouse(transitWarehouseStoSTr(StoSTr)) != companyStore(recipientStoreStoSTr(StoSTr))
    CHECKED BY recipientStoreStoSTr, transitWarehouseStoSTr MESSAGE 'Промежуточный склад и магазин-получатель не одного юрлица';


quantityOutAutoSkuLedger (ledger) += quantityStoSTrDetail (ledger);
descriptionSkuLedger (ledger) += descriptionStoSTrDetail (ledger);

UOMStoSTrDetail 'Единица измерения (ИД)' (StoSTrDetail) = DATA UOM (StoSTrDetail) IN idGroup;
shortNameUOMStoSTrDetail 'Единица измерения' (StoSTrDetail) = shortName(UOMStoSTrDetail(StoSTrDetail));

retailPriceStoSTrDetail 'Розничная цена (ед.)' (StoSTrDetail) = DATA NUMERIC[14,3] (StoSTrDetail);
retailSumStoSTrDetail 'Розничная сумма' (StoSTrDetail) = retailPriceStoSTrDetail(StoSTrDetail) * quantityStoSTrDetail(StoSTrDetail);
retailSumStoSTrDetailStoSTr 'Розничная сумма' (StoSTr)= GROUP SUM retailSumStoSTrDetail(StoSTrDetail)
    BY StoSTrStoSTrDetail(StoSTrDetail) IN documentSumGroup;

retailVATStoSTrDetail(StoSTrDetail) = DATA range (StoSTrDetail);
numberRetailVATStoSTrDetail 'НДС, номер' (StoSTrDetail) = numberRange(retailVATStoSTrDetail(StoSTrDetail));
valueRetailVATStoSTrDetail 'НДС, %' (StoSTrDetail) = valueRateRangeDate
    (retailVATStoSTrDetail(StoSTrDetail), dateStoSTrDetail(StoSTrDetail));

CONSTRAINT taxRange(retailVATStoSTrDetail(StoSTrDetail)) != tax.taxVAT CHECKED BY retailVATStoSTrDetail MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';

retailPriceStoSTrDetailStoSTrSku 'Цена по расходу' (StoSTr, sku) = GROUP EQUAL retailPriceStoSTrDetail(StoSTrDetail)
    BY StoSTrStoSTrDetail(StoSTrDetail), skuStoSTrDetail(StoSTrDetail);

retailVATStoSTrDetailStoSTrSku 'Шкала по расходу' (StoSTr, sku) = GROUP EQUAL retailVATStoSTrDetail(StoSTrDetail)
    BY StoSTrStoSTrDetail(StoSTrDetail), skuStoSTrDetail(StoSTrDetail);

sumRetailVATStoSTrDetail 'Сумма НДС' (StoSTrDetail) = [X*Y/100](
    retailSumStoSTrDetail(StoSTrDetail), valueRetailVATStoSTrDetail(StoSTrDetail));

sumRetailVATStoSTrDetailStoSTr 'Сумма НДС' (StoSTr) = GROUP SUM sumRetailVATStoSTrDetail(StoSTrDetail)
    BY StoSTrStoSTrDetail(StoSTrDetail) IN documentSumGroup;

importerSumStoSTrDetail 'Стоимость без НДС' (StoSTrDetail) = retailSumStoSTrDetail(StoSTrDetail) (-) sumRetailVATStoSTrDetail(StoSTrDetail);

importerSumStoSTrDetailStoSTr 'Стоимость без НДС' (StoSTr)= GROUP SUM importerSumStoSTrDetail(StoSTrDetail) BY StoSTrStoSTrDetail(StoSTrDetail) IN documentSumGroup;


netWeightStoSTrDetail 'Вес (ед.)' (StoSTrDetail) = netWeightSku(skuStoSTrDetail(StoSTrDetail));

sumNetWeightStoSTrDetail 'Общая масса груза, кг.' (StoSTrDetail) = netWeightStoSTrDetail(StoSTrDetail)*quantityStoSTrDetail(StoSTrDetail);

sumNetWeightStoSTr 'Общая масса груза, кг.' (StoSTr) = GROUP SUM sumNetWeightStoSTrDetail(StoSTrDetail) BY StoSTrStoSTrDetail(StoSTrDetail) IN documentSumGroup;

packQuantityStoSTrDetail 'Количество грузовых мест' (StoSTrDetail) = round0(quantityStoSTrDetail(StoSTrDetail)/
    UNION OVERRIDE 1 IF StoSTrDetail IS StoSTrDetail, quantityPackSku(skuStoSTrDetail(StoSTrDetail)));

packQuantityStoSTrDetailStoSTr 'Общее количество грузовых мест' (StoSTr) = GROUP SUM packQuantityStoSTrDetail(StoSTrDetail) BY StoSTrStoSTrDetail(StoSTrDetail) IN documentSumGroup;

balanceBSkuStoSTrDetail 'Остаток до' (StoSTrDetail) = balanceBSkuStockDateTime(skuStoSTrDetail(StoSTrDetail), departmentStoreStoSTrDetail(StoSTrDetail), dateTimeStoSTrDetail(StoSTrDetail));


quantityStoSTrSku (StoSTr, sku) = GROUP SUM quantityStoSTrDetail(StoSTrDetail)
                                          BY StoSTrStoSTrDetail(StoSTrDetail), skuStoSTrDetail(StoSTrDetail);

@defineAddDetailDialogBarcode(StoSTr, sku);

FORM StoSTr 'Перемещение (расход) на магазин (др. юрлицо)'
    OBJECTS s = StoSTr FIXED PANEL
    PROPERTIES (s) numberObject, seriesObject, dateStoSTr, timeStoSTr, nameDepartmentStoreStoSTr, nameRecipientStoreStoSTr, nameTransitWarehouseStoSTr,
                   quantityStoSTrDetailStoSTr, importerSumStoSTrDetailStoSTr, sumRetailVATStoSTrDetailStoSTr,
                   retailSumStoSTrDetailStoSTr, sumNetWeightStoSTr, packQuantityStoSTrDetailStoSTr, noteStoSTr

    OBJECTS d = StoSTrDetail
    PROPERTIES(d) indexStoSTrDetail, barcodeStoSTrDetail, nameSkuStoSTrDetail, nameBrandStoSTrDetail, sidArticleStoSTrDetail,
                  nameCategoryStoSTrDetail, sidColorStoSTrDetail, nameColorStoSTrDetail, shortNameUOMStoSTrDetail, balanceBSkuStoSTrDetail,
                  quantityStoSTrDetail, retailPriceStoSTrDetail, importerSumStoSTrDetail, numberRetailVATStoSTrDetail, valueRetailVATStoSTrDetail,
                  sumRetailVATStoSTrDetail, retailSumStoSTrDetail

    PROPERTIES(s) TODRAW d addDetailDialogSkuStoSTrDetailStoSTr, addDetailDialogBarcodeStoSTrDetailStoSTr, deleteStoSTrDetailStoSTr


    PROPERTIES(d) ADDOBJ, delete

    FILTERS inStoSTrStoSTrDetail(s, d)

    EDIT StoSTr OBJECT s
;

DESIGN StoSTr FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        ADD s.box {
            childConstraints = TO THE RIGHT;
            NEW row1 {
                childConstraints = TO THE BOTTOM;
                ADD s.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (nameDepartmentStoreStoSTr);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateStoSTr);
                    ADD PROPERTY (timeStoSTr);
                }
                NEW case1 {
                childConstraints = TO THE RIGHT;
                    NEW case11 {
                        title = 'Контрагент';
                        ADD PROPERTY (nameRecipientStoreStoSTr);
                    }
                    NEW case12 {
                        title = 'Транзит';
                        ADD PROPERTY (nameTransitWarehouseStoSTr);
                    }
                }
                ADD s.documentPrmGroup;
            }
            NEW row2 {
                ADD s.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        };
        ADD d.box;
        ADD functions.box;
    }
}

FORM StoSTrs 'Перемещения (расход) на магазин (др. юрлицо)'
    OBJECTS s = StoSTr
    PROPERTIES (s) READONLY objectClassName, numberObject, seriesObject, dateStoSTr, timeStoSTr, nameDepartmentStoreStoSTr, nameRecipientStoreStoSTr,
                   countStoSTrDetailStoSTr, quantityStoSTrDetailStoSTr, importerSumStoSTrDetailStoSTr, sumRetailVATStoSTrDetailStoSTr,
                   retailSumStoSTrDetailStoSTr, sumNetWeightStoSTr, packQuantityStoSTrDetailStoSTr

    PROPERTIES (s) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed


    PROPERTIES (s) ADDFORM, EDITFORM SHOWIF isDraftStoSTr(s), delete FORCE PANEL SHOWIF isDraftStoSTr(s),
                   postStoSTr SHOWIF isDraftStoSTr(s), unpostStoSTr SHOWIF isPostedStoSTr(s)

    PROPERTIES (s) FORCE PANEL printConsignmentVerticalA, printConsignmentHorizontalA,
                   printConsignmentVerticalB, printConsignmentHorizontalB,
                   printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                   printConsignmentSimpleVertical, printConsignmentSimpleAttach

    OBJECTS d = StoSTrDetail
    PROPERTIES(d) READONLY indexStoSTrDetail, barcodeStoSTrDetail, nameSkuStoSTrDetail, shortNameUOMStoSTrDetail,
                  quantityStoSTrDetail, retailPriceStoSTrDetail, importerSumStoSTrDetail, numberRetailVATStoSTrDetail, valueRetailVATStoSTrDetail,
                  sumRetailVATStoSTrDetail, retailSumStoSTrDetail

    FILTERS inStoSTrStoSTrDetail(s, d)
;

DESIGN StoSTrs FROM DEFAULT {
    ADD s.box{
        PROPERTY (delete(s)) {
            panelLocation = TOOLBAR;
            askConfirm = TRUE;
        PROPERTY (objectClassName(s)) {
            preferredCharWidth = 15;
        }
        }
    }
    ADD d.box;

    NEW footer.container {
        childConstraints = TO THE BOTTOM;

        NEW cont3 {
            childConstraints = TO THE RIGHT;
            ADD s.historyGroup {
                childConstraints = TO THE BOTTOM;
            }

            ADD s.postedGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        ADD s.printGroup {
            childConstraints = TO THE BOTTOM;
            NEW case55{
                childConstraints = TO THE RIGHT;

                NEW contOne {
                    title = 'Накладная';
                    ADD PROPERTY(editConsignment);
                }
                NEW tn{
                    childConstraints = TO THE RIGHT;
                    title = 'ТН-2';
                    ADD PROPERTY(printConsignmentSimpleVertical);
                    ADD PROPERTY(printConsignmentSimpleHorizontal);
                    ADD PROPERTY(printConsignmentSimpleAttach);
                }
            }
            NEW ttn1{
                childConstraints = TO THE RIGHT;
                title = 'ТТН-1';
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentHorizontalB);
                ADD PROPERTY(printConsignmentAttach);
            }

        }

    }
    ADD functions.box;
}

FORM StoSTrsDialog 'Перемещения (расход) на магазин (др. юрлицо)'
    OBJECTS s = StoSTrPosted
    PROPERTIES (s) READONLY objectClassName, numberObject, seriesObject, dateStoSTr, nameDepartmentStoreStoSTr,
                   nameRecipientStoreStoSTr, countStoSTrDetailStoSTr, quantityStoSTrDetailStoSTr, importerSumStoSTrDetailStoSTr,
                   sumRetailVATStoSTrDetailStoSTr, retailSumStoSTrDetailStoSTr
    DIALOG StoSTrPosted OBJECT s
;

@defineConsignment(StoSTr);
@implementConsignmentHeader(StoSTr, departmentStore);

senderConsignment (consignment) += companyStore(storeStoSTr(consignment));
recipientConsignment (consignment) += companyStore(recipientStoreStoSTr(consignment));

consignmentConsignmentDetail (consignmentDetail) += StoSTrStoSTrDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuStoSTrDetail (consignmentDetail);
shortNameUOMConsignmentDetail (consignmentDetail) += shortNameUOMStoSTrDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantityStoSTrDetail (consignmentDetail);
priceConsignmentDetail (consignmentDetail) += retailPriceStoSTrDetail (consignmentDetail);
vatConsignmentDetail (consignmentDetail) += valueRetailVATStoSTrDetail (consignmentDetail);
sumVATConsignmentDetail (consignmentDetail) += sumRetailVATStoSTrDetail (consignmentDetail);
sumConsignmentDetail (consignmentDetail) += importerSumStoSTrDetail (consignmentDetail);
sumInvoiceConsignmentDetail (consignmentDetail) += retailSumStoSTrDetail (consignmentDetail);
grossWeightConsignmentDetail (consignmentDetail) += sumNetWeightStoSTrDetail (consignmentDetail);
packQuantityConsignmentDetail (consignmentDetail) += packQuantityStoSTrDetail(consignmentDetail);
//currencyConsignment(consignment) += currencyStoSTr(consignment);
//isCustomsFlowConsignment (consignment) += consignment IS StoSTr;
noteConsignment(consignment) += noteStoSTr(consignment);

//noteConsignmentDetail

//todo: Надо наполнить свойствами накладную. (УНП...)