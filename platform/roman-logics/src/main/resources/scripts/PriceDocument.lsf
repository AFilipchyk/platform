MODULE PriceDocument;

REQUIRE System,
        //Document,
        //RomanDocument,
        //Consignment,
        Declaration,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, RB;

//NAMESPACE RB;

inBrandSupplier (supplier, brandSupplier) = supplierBrandSupplier(brandSupplier) == supplier;
inArticleBrand (brandSupplier, article) = brandSupplierArticle(article) == brandSupplier;

inArticleCategory (category, article) = UNION OVERRIDE categoryArticle(article) == category,
                                                       article == article AND NOT category;

stringEqualsAll(string) = string == 'Все';

inSupplierBrandArticle(supplier, brand, article) = UNION OVERRIDE brandSupplierArticle(article) == brand AND supplier,
                                                                  supplierArticle(article) == supplier AND NOT brand,
                                                                  article == article AND NOT supplier AND NOT brand;

CLASS wholesalePriceDocument 'Оптовый прайс' : transaction;

declarationWholesalePriceDocument 'Декларация оптового прайса (ИД)' = DATA declaration(wholesalePriceDocument) IN idGroup;

inArticleWholesalePriceDocument 'Товар в документе' = DATA BOOLEAN(wholesalePriceDocument, article);

priceWholesalePriceDocumentArticle 'Цена' = DATA NUMERIC[14,2](wholesalePriceDocument, article) IN baseGroup;


FORM wholesalePriceDocument 'Оптовый прайс'

    OBJECTS w=wholesalePriceDocument FIXED PANEL
    PROPERTIES(w) date

    TREE treeSupplierBrand v=STRING[3], s=supplier, b=brandSupplier
    PROPERTIES READONLY OBJVALUE(v), name(s), name(b)

    TREE treeCategory vv=STRING[3], c=category
    PROPERTIES READONLY OBJVALUE(vv), name(c)

    OBJECTS aa=article
    PROPERTIES (aa) READONLY sidArticle, nameCategoryArticle, nameBrandSupplierArticle
    PROPERTIES (w, aa) inArticleWholesalePriceDocument

    FILTERS stringEqualsAll(v), inBrandSupplier(s, b), inSupplierBrandArticle(s, b, aa), stringEqualsAll(vv), inArticleCategory(c, aa)

    OBJECTS a=article
    PROPERTIES(a) READONLY sidArticle, nameCategoryArticle, nameBrandSupplierArticle
    PROPERTIES (w, a) priceWholesalePriceDocumentArticle

    FILTERS inArticleWholesalePriceDocument(w, a)

    FILTERGROUP filtersCategory
        FILTER 'Показывать артикулы ном. группы' 'F10' inArticleCategory(c, a)
    FILTERGROUP filtersBrand
        FILTER 'Показывать артикулы бренда' 'F11' inArticleBrand(b, a)

    EDIT wholesalePriceDocument OBJECT w
;

DESIGN wholesalePriceDocument FROM DEFAULT {
    POSITION treeSupplierBrand.box TO THE LEFT treeCategory.box;
    POSITION treeCategory.box TO THE LEFT aa.box;

    treeSupplierBrand.tree {
        fillHorizontal = 1.5;
    }

    aa.box {
        fillHorizontal = 1.5;
    }

}

FORM wholesalePriceDocuments 'Оптовые прайсы'

    OBJECTS w=wholesalePriceDocument
    PROPERTIES(w) READONLY date
    PROPERTIES(w) ADDFORM, EDITFORM, delete

    OBJECTS a=article
    PROPERTIES(a) READONLY sidArticle, nameCategoryArticle, nameBrandSupplierArticle
    PROPERTIES (w, a) READONLY priceWholesalePriceDocumentArticle

    FILTERS inArticleWholesalePriceDocument(w, a)
;


toCreateWholesalePriceDocument 'Прайс по декларации' =  ACTION (declaration) NEWSESSION {
    ADDOBJ wholesalePriceDocument;
    FOR w == addedObject() DO {
        SET declarationWholesalePriceDocument(w) <- declaration AS declaration;
        SET priceWholesalePriceDocumentArticle(w, article) <- priceDeclarationArticle(declaration, article);
        SET inArticleWholesalePriceDocument(w, article) <- inDeclarationArticle(declaration, article);

        FORM wholesalePriceDocument OBJECTS w=addedObject() MODAL;
        IF formResult() == formResult.ok THEN {
            EXEC apply();
        };
    };
} CONFIRM;

EXTEND FORM declarations
     PROPERTIES(d) toCreateWholesalePriceDocument
;