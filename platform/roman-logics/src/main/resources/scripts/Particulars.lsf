MODULE Particulars;

REQUIRE System,
        MasterData,
        POS,
        RomanStock,
        PriceInterval,
        UserPriceChange,
        RetailPrice,
        Currency,
        RomanSales,
        RomanLogicsModule;

PRIORITY MasterData, Stock, RomanLogicsModule;

GROUP totalGroupSupplier 'Итого (поставщик)' : publicGroup;


quantityReceivedArticleDateFromTo 'Поступило за интервал' (article, dateFrom, dateTo) = (balanceAArticleDate(article, dateTo) AND dateFrom AS DATE) (-)
    (balanceBArticleDate(article, dateFrom) AND dateTo AS DATE) (+) quantitySoldArticleDateFromTo(article, dateFrom, dateTo);

quantityReceivedSupplierDateFromTo 'Поступило' (supplier, dateFrom, dateTo)= GROUP SUM quantityReceivedArticleDateFromTo(article, dateFrom, dateTo)
    BY supplierArticle(article), dateFrom, dateTo;
quantityReceivedBrandSupplierDateFromTo 'Поступило' (brandSupplier, dateFrom, dateTo)= GROUP SUM quantityReceivedArticleDateFromTo(article, dateFrom, dateTo)
    BY brandSupplierArticle(article), dateFrom, dateTo;
quantityReceivedCategorySupplierDateFromTo 'Поступило' (categorySupplier, dateFrom, dateTo)= GROUP SUM quantityReceivedArticleDateFromTo(article, dateFrom, dateTo)
    BY categorySupplierArticle(article), dateFrom, dateTo;
quantityReceivedSubCategorySupplierDateFromTo 'Поступило' (subCategorySupplier, dateFrom, dateTo)= GROUP SUM quantityReceivedArticleDateFromTo(article, dateFrom, dateTo)
    BY subCategorySupplierArticle(article), dateFrom, dateTo;


quantitySoldSupplierDateFromTo 'Продано за интервал (кол-во)' (supplier, dateFrom, dateTo) = GROUP SUM
    quantitySoldArticleDateFromTo(article, dateFrom, dateTo) BY supplierArticle(article), dateFrom, dateTo;
quantitySoldBrandSupplierDateFromTo 'Продано за интервал (кол-во)' (brandSupplier, dateFrom, dateTo) = GROUP SUM
    quantitySoldArticleDateFromTo(article, dateFrom, dateTo) BY brandSupplierArticle(article), dateFrom, dateTo;
quantitySoldCategorySupplierDateFromTo 'Продано за интервал (кол-во)' (categorySupplier, dateFrom, dateTo) = GROUP SUM
    quantitySoldArticleDateFromTo(article, dateFrom, dateTo) BY categorySupplierArticle(article), dateFrom, dateTo;
quantitySoldSubCategorySupplierDateFromTo 'Продано за интервал (кол-во)' (subCategorySupplier, dateFrom, dateTo) = GROUP SUM
    quantitySoldArticleDateFromTo(article, dateFrom, dateTo) BY subCategorySupplierArticle(article), dateFrom, dateTo;


balanceASupplierDateTo 'Остаток на конец' (supplier, dateTo)= GROUP SUM balanceAArticleDate(article, dateTo)
    BY supplierArticle(article), dateTo;
balanceABrandSupplierDateTo 'Остаток на конец' (brandSupplier, dateTo)= GROUP SUM balanceAArticleDate(article, dateTo)
    BY brandSupplierArticle(article), dateTo;
balanceACategorySupplierDateTo 'Остаток на конец' (categorySupplier, dateTo)= GROUP SUM balanceAArticleDate(article, dateTo)
    BY categorySupplierArticle(article), dateTo;
balanceASubCategorySupplierDateTo 'Остаток на конец' (subCategorySupplier, dateTo)= GROUP SUM balanceAArticleDate(article, dateTo)
    BY subCategorySupplierArticle(article), dateTo;


percentBalanceReceivedArticleDateFromTo '%, остатка' (article, dateFrom, dateTo) = round2([X*100/Y](
    balanceAArticleDate(article, dateTo), quantityReceivedArticleDateFromTo(article, dateFrom, dateTo)));

percentBalanceReceivedSupplierDateFromTo '%, остатка' (supplier, dateFrom, dateTo) = round2([X*100/Y](
    balanceASupplierDateTo(supplier, dateTo), quantityReceivedSupplierDateFromTo(supplier, dateFrom, dateTo)));
percentBalanceReceivedBrandSupplierDateFromTo '%, остатка' (brandSupplier, dateFrom, dateTo) = round2([X*100/Y](
    balanceABrandSupplierDateTo(brandSupplier, dateTo), quantityReceivedBrandSupplierDateFromTo(brandSupplier, dateFrom, dateTo)));
percentBalanceReceivedCategorySupplierDateFromTo '%, остатка' (categorySupplier, dateFrom, dateTo) = round2([X*100/Y](
    balanceACategorySupplierDateTo(categorySupplier, dateTo), quantityReceivedCategorySupplierDateFromTo(categorySupplier, dateFrom, dateTo)));
percentBalanceReceivedSubCategorySupplierDateFromTo '%, остатка' (subCategorySupplier, dateFrom, dateTo) = round2([X*100/Y](
    balanceASubCategorySupplierDateTo(subCategorySupplier, dateTo), quantityReceivedSubCategorySupplierDateFromTo(subCategorySupplier, dateFrom, dateTo)));

FORM salesArticle 'Продажи по группам'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES OBJVALUE(dFrom), OBJVALUE(dTo)

    OBJECTS s=supplier FIXED PANEL
    PROPERTIES     SELECTOR name(s)
    PROPERTIES     READONLY quantityReceivedSupplierDateFromTo(s,dFrom,dTo), quantitySoldSupplierDateFromTo(s,dFrom,dTo), balanceASupplierDateTo(s,dTo),
                   percentBalanceReceivedSupplierDateFromTo(s,dFrom,dTo)

    OBJECTS b=brandSupplier
    PROPERTIES     READONLY name(b), quantityReceivedBrandSupplierDateFromTo(b,dFrom,dTo), quantitySoldBrandSupplierDateFromTo(b,dFrom,dTo), balanceABrandSupplierDateTo(b,dTo),
                   percentBalanceReceivedBrandSupplierDateFromTo(b,dFrom,dTo)

    OBJECTS c=categorySupplier
    PROPERTIES     READONLY name(c), quantityReceivedCategorySupplierDateFromTo(c,dFrom,dTo), quantitySoldCategorySupplierDateFromTo(c,dFrom,dTo), balanceACategorySupplierDateTo(c,dTo),
                   percentBalanceReceivedCategorySupplierDateFromTo(c,dFrom,dTo)

    OBJECTS sc=subCategorySupplier
    PROPERTIES     READONLY name(sc), sidSubCategorySupplier(sc), quantityReceivedSubCategorySupplierDateFromTo(sc,dFrom,dTo), quantitySoldSubCategorySupplierDateFromTo(sc,dFrom,dTo), balanceASubCategorySupplierDateTo(sc,dTo),
                   percentBalanceReceivedSubCategorySupplierDateFromTo(sc,dFrom,dTo)

    OBJECTS aa=article
    PROPERTIES (aa)READONLY sidArticle, nameSupplierArticle, nameBrandSupplierArticle, nameCategorySupplierArticle, nameSubCategorySupplierArticle

    PROPERTIES  READONLY balanceBArticleDate(aa,dFrom), quantityReceivedArticleDateFromTo(aa, dFrom, dTo), quantitySoldArticleDateFromTo(aa, dFrom, dTo),
                balanceAArticleDate(aa, dTo), percentBalanceReceivedArticleDateFromTo(aa, dFrom, dTo)

    FILTERS  supplierBrandSupplier(b) == s,
             supplierCategorySupplier(c) == s,
             categorySupplierSubCategorySupplier(sc) == c

    FILTERGROUP filtersSold
        FILTER 'Артикул поставщика' 'F10' supplierArticle(aa) == s
        FILTER 'Артикул бренда' 'F9' brandSupplierArticle(aa) ==b


    FILTERGROUP filtersSold1
        FILTER 'Артикул группы' 'F8' categorySupplierArticle(aa) == c
        FILTER 'Артикул подгруппы' 'F7' subCategorySupplierArticle(aa) == sc

    FILTERGROUP filtersSoldArticle
        FILTER 'Артикул с движением' 'F11' quantityReceivedArticleDateFromTo(aa, dFrom, dTo) OR quantitySoldArticleDateFromTo(aa, dFrom, dTo) DEFAULT
;

DESIGN salesArticle FROM DEFAULT {

    main {
        NEW topContainer {
            childConstraints = TO THE BOTTOM;
            NEW wor {
                childConstraints = TO THE BOTTOM;
                ADD dates.box {
                    childConstraints = TO THE RIGHT;

                }
                ADD s.box {
                    childConstraints = TO THE RIGHT;
                    PROPERTY (name(s)){preferredCharWidth = 25;}
                }
            }
            NEW row {
                childConstraints = TO THE RIGHT;
                type = SPLITH;
                NEW row1 {
                    fillHorizontal = 1;
                    childConstraints = TO THE BOTTOM;

                    ADD b.box;
                    ADD c.box;
                    ADD sc.box;
                }

                ADD aa.box {
                    fillHorizontal = 2.5;
                }
            }
        }
        ADD functions.box;
    }
}

NAVIGATOR {
    salesNavigator 'Продажи' {
        ADD salesArticle;
    }
}