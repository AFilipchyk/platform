MODULE WriteOff;

REQUIRE System,
        Document,
        RomanDocument,
        Stock,
        RomanStock,
        RetailPrice,
        Employee,
        StorePriceTransfer,
        Consignment,
        WholesalePrice,
        AccountDocument,
        MasterData,
        RomanLogicsModule;

PRIORITY Stock, RomanLogicsModule;


//----------------------------------------------- Списание товара ---------------------------------------------------//
CLASS reasonOff 'Причина списания' : named;
TABLE reasonOff (reasonOff);

FORM reasonOff 'Причина списания'
    OBJECTS r=reasonOff  FIXED PANEL
    PROPERTIES(r) name
;

CLASS writeOff 'Списание' : outAccountDocumentLedger, historyObject, numeratedObject;
CLASS writeOffDetail 'Строка списания' : outFIFOSkuLedger;
CLASS writeOffPosted 'Закрытое списание' : writeOff, postedObject;
CLASS writeOffBatchDetail 'Строка списания товара (по партиям)';

@defineNumeratedObject(writeOff, 'Нумератор для актов списания', 'АС');

@defineDocument(writeOff);
@defineDocumentStock(writeOff, stock, 'Склад');
@defineDocumentPosted(writeOff);
@defineDocumentDescription (writeOff, 'Списание товара');

companyWriteOff (writeOff) = companyStock(stockWriteOff(writeOff));
nameCompanyWriteOff 'Компания' (writeOff)= name(companyWriteOff(writeOff));
addressLegalEntityWriteOff 'Адрес' (writeOff) = addressLegalEntityDate(companyWriteOff(writeOff), dateWriteOff(writeOff));
UNPLegalEntityWriteOff 'УНП' (writeOff) = UNPLegalEntity(companyWriteOff(writeOff));

@defineDocumentDetailNumbered(writeOff);
@defineDocumentDetailSku(writeOff, sku);
@defineDocumentDetailQuantity(writeOff);

@defineDocumentDetailPackWeightSku(writeOff);
@defineDocumentDetailSkuBalance(writeOff, sku, stock);
@defineDocumentHeaderQuantity(writeOff);
@defineDocumentHeaderSkuQuantity(writeOff, sku);

@defineAddDetailDialogSkuStock(writeOff, sku, stock, dialogSku);
@defineAddDetailDialogBarcode(writeOff, sku);

reasonOffWriteOff 'Причина списания (ИД)' = DATA reasonOff (writeOff) IN idGroup;
nameReasonOffWriteOff 'Причина списания' (writeOff) = name(reasonOffWriteOff (writeOff)) IN documentPrmGroup;

@defineDocumentDetailSkuArticle(writeOff);

TABLE writeOffBatchDetail(writeOffBatchDetail);

costWriteOffDetailBatch (ledger, batch) = costDataSkuLedgerBatch(ledger, batch) AND ledger IS writeOffDetail;
@defineAggregationDouble(writeOffDetail, batch, writeOffBatchDetail, costWriteOffDetailBatch);

writeOffWriteOffBatchDetail (detail) = writeOffWriteOffDetail(writeOffDetailWriteOffBatchDetail(detail));
@defineDocumentDetailIndexCustom(writeOff, writeOffBatchDetail);

@defineDocumentAggregationDetailSku(writeOff, writeOffBatch, sku);
@defineDocumentDetailSkuArticle(writeOffBatch);


descriptionBatchWriteOffBatchDetail 'Партия' (detail) = descriptionBatch(batchWriteOffBatchDetail(detail));
quantityWriteOffBatchDetail 'Кол-во' (detail) = costDataSkuLedgerBatch(writeOffDetailWriteOffBatchDetail(detail), batchWriteOffBatchDetail(detail));

notChangeBalanceWriteOff 'Управленческое списание' (writeOff) = DATA BOOLEAN (writeOff) IN documentPrmGroup;
notChangeBalanceWriteOffDetail 'Управленческое списание' (writeOffDetail) = notChangeBalanceWriteOff(writeOffWriteOffDetail(writeOffDetail));

FORM writeOff 'Списание товара'
    OBJECTS t = writeOff FIXED PANEL

    PROPERTIES (t) objectClassName, nameNumeratorObject, numberObject, seriesObject, dateWriteOff, timeWriteOff,
                   nameStockWriteOff, noteWriteOff,
                   countWriteOffDetailWriteOff, quantityWriteOffDetailWriteOff, nameReasonOffWriteOff, notChangeBalanceWriteOff

    OBJECTS d = writeOffDetail
    PROPERTIES(d)  indexWriteOffDetail, barcodeWriteOffDetail,
                   nameCategoryWriteOffDetail, nameBrandWriteOffDetail,
                   sidArticleWriteOffDetail, sidSizeWriteOffDetail,
                   sidColorWriteOffDetail, nameColorWriteOffDetail,
                   quantityWriteOffDetail, shortNameUOMWriteOffDetail,
                   ADDOBJ, delete

    PROPERTIES(t) TODRAW d addDetailDialogSkuStockWriteOffDetailWriteOff, addDetailInputBarcodeWriteOffDetailWriteOff,
                           deleteWriteOffDetailWriteOff

    FILTERS writeOffWriteOffDetail(d) == t

    EVENTS
        ON OK EXEC prePostWriteOff(t)

    EDIT writeOff OBJECT t
;

DESIGN writeOff FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        t.box {
            childConstraints = TO THE RIGHT;
            NEW columnHeaderPrm {
                childConstraints = TO THE BOTTOM;
                ADD t.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (objectClassName);
                    ADD PROPERTY (nameStockWriteOff);
                    ADD PROPERTY (nameNumeratorObject);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateWriteOff);
                    ADD PROPERTY (timeWriteOff);
                }
                ADD t.documentPrmGroup;
            }
            NEW columnHeaderSum {
                ADD t.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        }
        PROPERTY(formOkAction) {
            caption = 'Провести';
        }
    }
}

FORM writeOffs 'Списания товара'
    OBJECTS t = writeOff
    PROPERTIES (t) READONLY isPostedWriteOff FORCE GRID, numberObject, seriesObject, dateWriteOff, timeWriteOff,
                            nameStockWriteOff, noteWriteOff, nameReasonOffWriteOff,
                            countWriteOffDetailWriteOff, quantityWriteOffDetailWriteOff

    PROPERTIES (t) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed

    PROPERTIES (t) ADDFORM, EDITFORM, delete FORCE PANEL

    OBJECTS d = writeOffDetail
    PROPERTIES(d) READONLY  indexWriteOffDetail, barcodeWriteOffDetail,
                            nameCategoryWriteOffDetail, nameBrandWriteOffDetail,
                            sidArticleWriteOffDetail, sidSizeWriteOffDetail,
                            sidColorWriteOffDetail, nameColorWriteOffDetail,
                            quantityWriteOffDetail, shortNameUOMWriteOffDetail
    FILTERS writeOffWriteOffDetail(d) == t

    OBJECTS o = writeOffBatchDetail
    FILTERS writeOffWriteOffBatchDetail(o) == t
;


DESIGN writeOffs FROM DEFAULT {
    PROPERTY (delete(t)) {
        panelLocation = TOOLBAR;
        askConfirm = TRUE;
    }

    NEW documentContainer BEFORE functions.box {
        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        ADD t.box;

        NEW documentDetail {
            type = TABBED;

            ADD d.box {
                title = 'Спецификация';
            }
            NEW documentHistory {
                title = 'История';

                ADD t.historyGroup;
                ADD t.postedGroup;
            }
            ADD o.box {
                title = 'Партии';
            }
        }
    }
}


@defineDocumentTransferRetail(writeOff);

useRetailPriceWriteOffDetail(detail) = stockWriteOffDetail(detail) IS departmentStore;
accountPriceWriteOffBatchDetail 'Учетная цена' (detail) = IF useRetailPriceWriteOffDetail(writeOffDetailWriteOffBatchDetail(detail))
        THEN retailPriceWriteOffDetail(writeOffDetailWriteOffBatchDetail(detail))
        ELSE supplierPriceBatchA(batchWriteOffBatchDetail(detail));
accountSumWriteOffBatchDetail 'Учетная сумма' (detail) = accountPriceWriteOffBatchDetail(detail)*quantityWriteOffBatchDetail(detail);

importerPriceWriteOffBatchDetail 'Цена импортера' (detail) = importerPriceBatchA(batchWriteOffBatchDetail(detail));
supplierPriceWriteOffBatchDetail 'Цена поставщика' (detail) = supplierPriceBatchA(batchWriteOffBatchDetail(detail));

@defineDocumentTransferAccount(writeOff, useRetailPriceWriteOffDetail);
@extendDocumentFormDetailSkuArticleReadonly(writeOffs, o, writeOffBatch);

EXTEND FORM writeOffs
    PROPERTIES(o) READONLY descriptionBatchWriteOffBatchDetail, importerPriceWriteOffBatchDetail, supplierPriceWriteOffBatchDetail
;

//--------------------------------Комиссия для списания----------------------------------//
CLASS writeOffCommittee 'Комиссия для списания товаров' : committee;

writeOffCommitteeWriteOff (writeOff) = DATA writeOffCommittee (writeOff) IN idGroup;
nameWriteOffCommitteeWriteOff 'Комиссия для списания' (writeOff) = name(writeOffCommitteeWriteOff(writeOff)) IN documentPrmGroup;


inWriteOffEmployee (writeOff, employee) = inCommitteeEmployee(writeOffCommitteeWriteOff(writeOff), employee);
namePositionEmployeeWriteOff 'Члены комиссии' (writeOff) = namePositionEmployeeCommittee(writeOffCommitteeWriteOff(writeOff)) MINCHARWIDTH 50 PREFCHARWIDTH 50;

writeOffCommitteeStock 'Комиссия для списания товаров (ИД)' = DATA writeOffCommittee (stock);
nameWriteOffCommitteeStock 'Комиссия для реестра' (stock) = name(writeOffCommitteeStock(stock));
isWriteOffCommitteeStock 'По умолчанию' (writeOffCommittee, stock) = writeOffCommitteeStock(stock) == writeOffCommittee;
CONSTRAINT writeOffCommitteeStock(stock) AND NOT inCommitteeEmployeeDivision(writeOffCommitteeStock(stock), stock)
    CHECKED BY writeOffCommitteeStock MESSAGE 'Для отдела выбрана комиссия, которая для него не действует';

writeOffCommitteeWriteOff(writeOff) <- writeOffCommitteeStock(stockWriteOff(writeOff))
    WHEN ASSIGNED(writeOff IS writeOff);

EXTEND FORM warehouse PROPERTIES nameWriteOffCommitteeStock(w);
EXTEND FORM departmentStore PROPERTIES nameWriteOffCommitteeStock(d);

EXTEND FORM writeOff PROPERTIES(t) nameWriteOffCommitteeWriteOff;
EXTEND FORM writeOffs PROPERTIES(t) READONLY nameWriteOffCommitteeWriteOff AFTER nameReasonOffWriteOff;
FORM writeOffCommittee 'Комиссия для списания отходов'
    OBJECTS c=writeOffCommittee FIXED PANEL
    PROPERTIES(c)      name

    TREE stockTree sg = stockGroup PARENT parentStockGroup
    PROPERTIES READONLY sgTreeName = name(sg)

    OBJECTS ts=stock
    PROPERTIES    READONLY tsTreeName = name(ts)
    PROPERTIES(c, ts) inCommitteeEmployeeDivision FORCE GRID, isWriteOffCommitteeStock

    FILTERS isParentStockGroupStock(sg, ts)
    ORDER BY tsTreeName


    OBJECTS e=employee
    PROPERTIES(e)      READONLY name, userFirstName, userLastName, namePositionEmployee
    PROPERTIES(e)      ADDSESSIONFORM, EDITSESSIONFORM, delete

    PROPERTIES(c, e)   inCommitteeEmployee
    FILTERS            countDivisionEmployeeCommittee (e, c)
    FILTERGROUP filters6
        FILTER 'Показывать только членов комиссии' 'F10' inCommitteeEmployee(c, e)

    FILTERGROUP filters5
        FILTER 'Показывать отделы только для данной комиссии' 'F9' inCommitteeEmployeeDivision(c, ts)

    EDIT writeOffCommittee OBJECT c
;

DESIGN writeOffCommittee FROM DEFAULT {
    main {
        preferredSize = (1024, 768);

        NEW caseOne BEFORE e.box {
            childConstraints = TO THE RIGHT;
            title = 'Отделы, для которых действуют комиссии';

            ADD stockTree.tree {
                fillHorizontal = 1;
            }

            ADD ts.box {
                fillHorizontal = 2;
            }
        };
    }
}

FORM writeOffCommittees 'Комиссии для списания товара'
    OBJECTS w=writeOffCommittee
    PROPERTIES(w)      READONLY name, nameEmployeeDivisionCommittee, nameEmployeeCommittee
    PROPERTIES(w)      ADDFORM, EDITFORM

    DIALOG writeOffCommittee OBJECT w
;

FORM writeOffPrint 'Списание' PRINT
    OBJECTS w=writeOff FIXED PANEL
    PROPERTIES (w)  SELECTOR numberObject, seriesObject, nameStockWriteOff,
                   dateWriteOff, timeWriteOff, nameReasonOffWriteOff, noteWriteOff, quantityWriteOffDetailWriteOff, accountSumWriteOff,
                   nameCompanyWriteOff, addressLegalEntityWriteOff, UNPLegalEntityWriteOff, nameWriteOffCommitteeWriteOff,
                   namePositionEmployeeWriteOff, writeOffCommitteeWriteOff

    OBJECTS d = writeOffBatchDetail
    PROPERTIES(d) READONLY indexWriteOffBatchDetail, barcodeWriteOffBatchDetail, nameSkuWriteOffBatchDetail,

                            quantityWriteOffBatchDetail, shortNameUOMWriteOffBatchDetail,
                            accountPriceWriteOffBatchDetail, accountSumWriteOffBatchDetail
    FILTERS writeOffWriteOffBatchDetail(d) == w

    OBJECTS e=employee

    PROPERTIES(e) SELECTOR   commonName, namePositionEmployee

    FILTERS inWriteOffEmployee(w, e)

    EDIT writeOff OBJECT w
;

toPrintWriteOff 'Акт списания' (writeOff) = ACTION FORM writeOffPrint OBJECTS w IMAGE 'print.png' IN printGroup;

isPostedNotChangeBalanceWriteOff (writeOff) = isPostedWriteOff(writeOff) AND NOT notChangeBalanceWriteOff(writeOff);

EXTEND FORM writeOffs
    PROPERTIES     FORCE PANEL toPrintWriteOff(t) SHOWIF isPostedNotChangeBalanceWriteOff(t)
    PROPERTIES(t) READONLY notChangeBalanceWriteOff
;
EXTEND DESIGN writeOffs {
    documentDetail {
        NEW printTab {
            title = 'Печатные формы';
            NEW printContainer {
                childConstraints = TO THE BOTTOM;
                fillVertical = 1.0; // todo : иначе кнопка не всегда показывается, нужно будет пофиксить как-нибудь
                ADD PROPERTY (toPrintWriteOff);
            }
        }
    }
}


// Проводим по регистру
@implementSkuLedger(writeOff, sku, stock);
quantityOutFIFOSkuLedger (ledger) += quantityWriteOffDetail (ledger);
limitOutFIFOSkuLedgerBatch(ledger, batch) += IF notChangeBalanceWriteOffDetail(ledger) THEN
                                                currentBalanceBatchStock(batch, stockWriteOffDetail(ledger)) AND batch IS batchB ELSE
                                                currentBalanceABatchStock(batch, stockWriteOffDetail(ledger)) AND batch IS batchA;
sumOutSkuLedger(ledger) += accountSumWriteOffDetail(ledger);

skipASkuLedger (ledger) += notChangeBalanceWriteOffDetail(ledger);
//---------------------------------------- Товарный отчет ------------------------- //

dateTimeAccountDocumentLedger (ledger) += dateTimeWriteOff(ledger);
isPostedAccountDocumentLedger (ledger) += isPostedWriteOff(ledger);
stockAccountDocumentLedger (ledger) += stockWriteOff(ledger);
descriptionAccountDocumentLedger (ledger) += descriptionWriteOff(ledger);

sumOutAccountDocumentLedger (ledger) += accountSumWriteOff(ledger);
sumItemOutAccountDocumentLedger (ledger) += accountSumWriteOff(ledger);
sumContainerOutAccountDocumentLedger (ledger) += 0.0 IF ledger IS writeOff;