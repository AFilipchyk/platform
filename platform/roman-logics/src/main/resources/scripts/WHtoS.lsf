//From WareHouse to Store
MODULE WHtoS;

REQUIRE System,

        Stock,
        Numerator,
        Document,
        RomanDocument,
        Consignment,
        Declaration,
        MasterData,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, Stock;

CLASS WHtoS 'Расход на магазин' : historyObject, numberedObject, consignment;
CLASS WHtoSDetail 'Строка расхода на магазин' : outAutoSkuLedger, consignmentDetail;
CLASS WHtoSPosted 'Закрытый расход на магазин' : WHtoS, postedObject, consignment;


skuWHtoSDetail 'Товар (ИД)' (WHtoSDetail) = DATA sku (WHtoSDetail)IN idGroup;

@defineRBCorrespondingDocument(WHtoS, warehouse, 'Оптовый склад', sku, 'Расход на магазин', store, 'Магазин');

nameImporterWHtoS 'Компания' (WHtoS) = name(importerWarehouse(warehouseWHtoS(WHtoS)));

//quantityOutAutoBalanceSkuLedger (outAutoBalanceSkuLedger) += quantityWHtoSDetail (outAutoBalanceSkuLedger);

netWeightWHtoSDetail 'Вес нетто (ед.)' (WHtoSDetail) = netWeightSku(skuWHtoSDetail(WHtoSDetail));

sumNetWeightWHtoSDetail 'Вес нетто' (WHtoSDetail) = netWeightWHtoSDetail(WHtoSDetail)*quantityWHtoSDetail(WHtoSDetail);

sumNetWeightWHtoS 'Вес нетто' (WHtoS) = GROUP SUM sumNetWeightWHtoSDetail(WHtoSDetail) BY WHtoSWHtoSDetail(WHtoSDetail);

//priceCStoWHDetail 'Цена' (CStoWHDetail) = supplierPriceDeclarationDetail (declarationDetailCStoWHDetail(CStoWHDetail));
//sumCStoWHDetail 'Стоимость' (CStoWHDetail) = priceCStoWHDetail(CStoWHDetail) * quantityCStoWHDetail(CStoWHDetail);
//totalSumCStoWHDetail 'Общая стоимость' (CStoWH) =
//    GROUP SUM sumCStoWHDetail(CStoWHDetail) BY CStoWHCStoWHDetail(CStoWHDetail) IN documentSumGroup PERSISTENT  MINCHARWIDTH 15 PREFCHARWIDTH 15;

balanceBSkuWHtoSDetail 'Остаток до' (WHtoSDetail) = balanceBSkuStockDateTime(skuWHtoSDetail(WHtoSDetail), warehouseWHtoSDetail(WHtoSDetail), dateTimeWHtoSDetail(WHtoSDetail));

@defineAddDetailDialogBarcode(WHtoS, sku);

FORM WHtoS 'Расход на магазин'
    OBJECTS w = WHtoS FIXED PANEL
    PROPERTIES (w) READONLY objectClassName
    PROPERTIES (w) numberObject, seriesObject, dateWHtoS, timeWHtoS, nameWarehouseWHtoS, nameStoreWHtoS,
                   quantityWHtoSDetail, sumNetWeightWHtoS

    OBJECTS d = WHtoSDetail
    PROPERTIES(d) indexWHtoSDetail, barcodeWHtoSDetail, nameBrandWHtoSDetail, sidArticleWHtoSDetail,
                  nameCategoryWHtoSDetail, sidColorWHtoSDetail, nameColorWHtoSDetail, sumNetWeightWHtoSDetail,
                  balanceBSkuWHtoSDetail, quantityWHtoSDetail
    PROPERTIES(w) TODRAW d addDetailDialogSkuWHtoSDetailWHtoS, addDetailDialogBarcodeWHtoSDetailWHtoS,
                  deleteWHtoSDetailWHtoS


    PROPERTIES(d) ADDOBJ, delete
//    PROPERTIES(w) TODRAW(d) addDetailDialogTerminalWHtoSDetailWHtoS

    FILTERS inWHtoSWHtoSDetail(w, d)

    EDIT WHtoS OBJECT w
;

DESIGN WHtoS FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        PROPERTY (objectClassName(w)) {
            preferredCharWidth = 15;
        }
    }
}

FORM WHtoSs 'Расходы на магазин'
    OBJECTS w = WHtoS
    PROPERTIES (w) READONLY objectClassName, numberObject, seriesObject, dateWHtoS, timeWHtoS, nameWarehouseWHtoS, nameStoreWHtoS,
                            quantityWHtoSDetail, sumNetWeightWHtoS

    PROPERTIES (w) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed


    PROPERTIES (w) ADDFORM, EDITFORM SHOWIF isDraftWHtoS(w), delete FORCE PANEL SHOWIF isDraftWHtoS(w),
                   postWHtoS SHOWIF isDraftWHtoS(w), unpostWHtoS SHOWIF isPostedWHtoS(w)

    PROPERTIES (w ) FORCE PANEL printConsignmentVerticalA, printConsignmentHorizontalA,
                   printConsignmentVerticalB, printConsignmentHorizontalB,
                   printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                   printConsignmentSimpleVertical, printConsignmentSimpleAttach

    OBJECTS d = WHtoSDetail
    PROPERTIES(d) READONLY indexWHtoSDetail, barcodeWHtoSDetail, nameBrandWHtoSDetail, sidArticleWHtoSDetail,
                  nameCategoryWHtoSDetail, sidColorWHtoSDetail, nameColorWHtoSDetail, sumNetWeightWHtoSDetail,
                  balanceBSkuWHtoSDetail, quantityWHtoSDetail

    FILTERS inWHtoSWHtoSDetail(w, d)
;

DESIGN WHtoSs FROM DEFAULT {
    ADD w.box{
        PROPERTY (delete(w)) {
            panelLocation = TOOLBAR;
            askConfirm = TRUE;
        PROPERTY (objectClassName(w)) {
            preferredCharWidth = 15;
        }
        }
    }
    ADD d.box;

    NEW footer.container {
        childConstraints = TO THE BOTTOM;

        NEW cont3 {
            childConstraints = TO THE RIGHT;
            ADD w.historyGroup {
                childConstraints = TO THE BOTTOM;
            }

            ADD w.postedGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        ADD w.printGroup {
            childConstraints = TO THE BOTTOM;
            NEW case55{
                childConstraints = TO THE RIGHT;

                NEW contOne {
                    title = 'Накладная';
                    ADD PROPERTY(editConsignment);
                }
                NEW tn{
                    childConstraints = TO THE RIGHT;
                    title = 'ТН-2';
                    ADD PROPERTY(printConsignmentSimpleVertical);
                    ADD PROPERTY(printConsignmentSimpleHorizontal);
                    ADD PROPERTY(printConsignmentSimpleAttach);
                }
            }
            NEW ttn1{
                childConstraints = TO THE RIGHT;
                title = 'ТТН-1';
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentHorizontalB);
                ADD PROPERTY(printConsignmentAttach);
            }

        }

    }
    ADD functions.box;
}

@defineConsignment(WHtoS);
@implementConsignmentHeader(WHtoS, warehouse);

senderConsignment (consignment) += warehouseWHtoS(consignment);

consignmentConsignmentDetail (consignmentDetail) += WHtoSWHtoSDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuWHtoSDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantityWHtoSDetail (consignmentDetail);
//priceConsignmentDetail (consignmentDetail) += priceWHtoSDetail (consignmentDetail);
//sumInvoiceConsignmentDetail (consignmentDetail) += sumWHtoSDetail (consignmentDetail);
//sumConsignmentDetail (consignmentDetail) += sumWHtoSDetail (consignmentDetail);
grossWeightConsignmentDetail (consignmentDetail) += sumNetWeightWHtoS (consignmentDetail);

//currencyConsignment(consignment) += currencyWHtoS(consignment);
//isCustomsFlowConsignment (consignment) += consignment IS WHtoS;
noteConsignment(consignment) += noteWHtoS(consignment);

//todo: Надо наполнить свойствами накладную.