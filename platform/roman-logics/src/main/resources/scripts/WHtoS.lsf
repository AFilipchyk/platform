//From WareHouse to Store
MODULE WHtoS;

REQUIRE System,

        Stock,
        Numerator,
        Document,
        RomanDocument,
        Consignment,
        Declaration,
        WholesalePrice,
        MasterData,
        RomanLogicsModule;

PRIORITY RomanLogicsModule, Stock;

CLASS WHtoS 'Расход на магазин' : historyObject, numberedObject, consignment;
CLASS WHtoSDetail 'Строка расхода на магазин' : outAutoSkuLedger, consignmentDetail;
CLASS WHtoSPosted 'Закрытый расход на магазин' : WHtoS, postedObject, consignment;


skuWHtoSDetail 'Товар (ИД)' (WHtoSDetail) = DATA sku (WHtoSDetail)IN idGroup;
nameSkuWHtoSDetail 'Наименование товара' (WHtoSDetail) = nameSku(skuWHtoSDetail(WHtoSDetail));

@defineRBCorrespondingDocument(WHtoS, warehouse, 'Оптовый склад', sku, 'Расход на магазин (прямой)', store, 'Магазин');

CONSTRAINT WHtoS IS WHtoS AND NOT companyWarehouse(warehouseWHtoS(WHtoS)) == companyStore(storeWHtoS(WHtoS))
    CHECKED BY storeWHtoS MESSAGE 'Перемещение не в рамках одного юрлица';

//nameImporterWHtoS 'Компания' (WHtoS) = name(importerWarehouse(warehouseWHtoS(WHtoS)));

quantityOutAutoSkuLedger (ledger) += quantityWHtoSDetail (ledger);

importerPriceWHtoSDetail 'Цена изготовителя/импортера' (WHtoSDetail) = DATA NUMERIC[14,3] (WHtoSDetail);
supplierPriceWHtoSDetail 'Цена поставщика без НДС' (WHtoSDetail) = DATA NUMERIC[14,3] (WHtoSDetail);

importerPriceWHtoSSku 'Цена без НДС' (WHtoS, sku) = GROUP MAX importerPriceWHtoSDetail(WHtoSDetail) BY WHtoSWHtoSDetail(WHtoSDetail), skuWHtoSDetail(WHtoSDetail);

wholesalePriceWHtoSDetail 'Оптовая цена' (WHtoSDetail) =  priceWholesaleArticleDateTime(articleWHtoSDetail(WHtoSDetail), dateTimeWHtoSDetail(WHtoSDetail));
supplierPriceWHtoSDetail (WHtoSDetail) <- wholesalePriceWHtoSDetail(WHtoSDetail) WHEN CHANGED(skuWHtoSDetail(WHtoSDetail)) OR CHANGED (dateTimeWHtoSDetail(WHtoSDetail));

supplierSumWHtoSDetail 'Стоимость без НДС' (WHtoSDetail) = supplierPriceWHtoSDetail(WHtoSDetail) * quantityWHtoSDetail(WHtoSDetail);

supplierSumWHtoSDetailWHtoS 'Стоимость без НДС' (WHtoS)= GROUP SUM supplierSumWHtoSDetail(WHtoSDetail) BY WHtoSWHtoSDetail(WHtoSDetail) IN documentSumGroup;


supplierVATWHtoSDetail(WHtoSDetail) = DATA range (WHtoSDetail);
numberSupplierVATWHtoSDetail 'НДС, номер' (WHtoSDetail) = numberRange(supplierVATWHtoSDetail(WHtoSDetail));
valueSupplierVATWHtoSDetail 'НДС, %' (WHtoSDetail) = valueRateRangeDate
    (supplierVATWHtoSDetail(WHtoSDetail), dateWHtoSDetail(WHtoSDetail));

CONSTRAINT taxRange(supplierVATWHtoSDetail(WHtoSDetail)) != tax.taxVAT CHECKED BY supplierVATWHtoSDetail MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';


sumSupplierVATWHtoSDetail 'Сумма НДС' (WHtoSDetail) = [X*Y/100](
    supplierSumWHtoSDetail(WHtoSDetail), valueSupplierVATWHtoSDetail(WHtoSDetail));

sumSupplierVATWHtoSDetailWHtoS 'Сумма НДС' (WHtoS) = GROUP SUM sumSupplierVATWHtoSDetail(WHtoSDetail) BY WHtoSWHtoSDetail(WHtoSDetail) IN documentSumGroup;

sumInvoiceWHtoSDetail 'Стоимость с НДС' (WHtoSDetail) = supplierSumWHtoSDetail(WHtoSDetail) (+) sumSupplierVATWHtoSDetail(WHtoSDetail);

sumInvoiceWHtoSDetailWHtoS 'Стоимость с НДС' (WHtoS)= GROUP SUM sumInvoiceWHtoSDetail(WHtoSDetail) BY WHtoSWHtoSDetail(WHtoSDetail) IN documentSumGroup;

netWeightWHtoSDetail 'Вес (ед.)' (WHtoSDetail) = netWeightSku(skuWHtoSDetail(WHtoSDetail));

sumNetWeightWHtoSDetail 'Общая масса груза, кг.' (WHtoSDetail) = netWeightWHtoSDetail(WHtoSDetail)*quantityWHtoSDetail(WHtoSDetail);

sumNetWeightWHtoS 'Общая масса груза, кг.' (WHtoS) = GROUP SUM sumNetWeightWHtoSDetail(WHtoSDetail) BY WHtoSWHtoSDetail(WHtoSDetail) IN documentSumGroup;

packQuantityWHtoSDetail 'Количество грузовых мест' (WHtoSDetail) = round0(quantityWHtoSDetail(WHtoSDetail)/
    UNION OVERRIDE 1 IF WHtoSDetail IS WHtoSDetail, quantityPackSku(skuWHtoSDetail(WHtoSDetail)));

packQuantityWHtoSDetailWHtoS 'Общее количество грузовых мест' (WHtoS) = GROUP SUM packQuantityWHtoSDetail(WHtoSDetail) BY WHtoSWHtoSDetail(WHtoSDetail) IN documentSumGroup;


quantityWHtoSSku (WHtoS, sku) = GROUP SUM quantityWHtoSDetail(WHtoSDetail)
                                          BY WHtoSWHtoSDetail(WHtoSDetail), skuWHtoSDetail(WHtoSDetail);


FORM WHtoS 'Расход со склада на магазин'
    OBJECTS w = WHtoS FIXED PANEL
    PROPERTIES (w) numberObject, seriesObject, dateWHtoS, timeWHtoS, nameWarehouseWHtoS, nameStoreWHtoS,
                   quantityWHtoSDetailWHtoS, supplierSumWHtoSDetailWHtoS, sumSupplierVATWHtoSDetailWHtoS,
                   sumInvoiceWHtoSDetailWHtoS, sumNetWeightWHtoS, packQuantityWHtoSDetailWHtoS, noteWHtoS

    OBJECTS d = WHtoSDetail
    PROPERTIES(d) indexWHtoSDetail, barcodeWHtoSDetail, nameSkuWHtoSDetail, nameBrandWHtoSDetail, sidArticleWHtoSDetail,
                  nameCategoryWHtoSDetail, sidColorWHtoSDetail, nameColorWHtoSDetail, shortNameUOMWHtoSDetail, balanceBSkuWHtoSDetail,
                  quantityWHtoSDetail, importerPriceWHtoSDetail, supplierPriceWHtoSDetail, supplierSumWHtoSDetail, numberSupplierVATWHtoSDetail, valueSupplierVATWHtoSDetail,
                  sumSupplierVATWHtoSDetail, sumInvoiceWHtoSDetail

    PROPERTIES(w) TODRAW d addDetailDialogSkuWHtoSDetailWHtoS, addDetailDialogBarcodeWHtoSDetailWHtoS,
                  deleteWHtoSDetailWHtoS


    PROPERTIES(d) ADDOBJ, delete
//    PROPERTIES(w) TODRAW(d) addDetailDialogTerminalWHtoSDetailWHtoS

    FILTERS inWHtoSWHtoSDetail(w, d)

    EDIT WHtoS OBJECT w
;

DESIGN WHtoS FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        ADD w.box {
            childConstraints = TO THE RIGHT;
            NEW row1 {
                childConstraints = TO THE BOTTOM;
                ADD w.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY (nameWarehouseWHtoS);
                    ADD PROPERTY (numberObject);
                    ADD PROPERTY (seriesObject);
                    ADD PROPERTY (dateWHtoS);
                    ADD PROPERTY (timeWHtoS);
                }
                NEW case {
                childConstraints = TO THE BOTTOM;
                    title = 'Контрагент';
                    ADD PROPERTY (nameStoreWHtoS);
                }
                ADD w.documentPrmGroup;
            }
            NEW row2 {
                ADD w.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        };
        ADD d.box;
        ADD functions.box;
    }
}

FORM WHtoSs 'Расходы со склада на магазин'
    OBJECTS w = WHtoS
    PROPERTIES (w) READONLY objectClassName, numberObject, seriesObject, dateWHtoS, timeWHtoS, nameWarehouseWHtoS, nameStoreWHtoS,
                   countWHtoSDetailWHtoS, quantityWHtoSDetailWHtoS, supplierSumWHtoSDetailWHtoS, sumSupplierVATWHtoSDetailWHtoS,
                   sumInvoiceWHtoSDetailWHtoS, packQuantityWHtoSDetailWHtoS, sumNetWeightWHtoS

    PROPERTIES (w) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed


    PROPERTIES (w) ADDFORM, EDITFORM SHOWIF isDraftWHtoS(w), delete FORCE PANEL SHOWIF isDraftWHtoS(w),
                   postWHtoS SHOWIF isDraftWHtoS(w), unpostWHtoS SHOWIF isPostedWHtoS(w)

    PROPERTIES (w ) FORCE PANEL printConsignmentVerticalA, printConsignmentHorizontalA,
                   printConsignmentVerticalB, printConsignmentHorizontalB,
                   printConsignmentAttach, printConsignmentSimpleHorizontal, editConsignment,
                   printConsignmentSimpleVertical, printConsignmentSimpleAttach

    OBJECTS d = WHtoSDetail
    PROPERTIES(d) READONLY indexWHtoSDetail, barcodeWHtoSDetail, nameSkuWHtoSDetail, shortNameUOMWHtoSDetail,
                  quantityWHtoSDetail, importerPriceWHtoSDetail, supplierPriceWHtoSDetail, supplierSumWHtoSDetail, numberSupplierVATWHtoSDetail, valueSupplierVATWHtoSDetail,
                  sumSupplierVATWHtoSDetail, sumInvoiceWHtoSDetail

    FILTERS inWHtoSWHtoSDetail(w, d)
;

DESIGN WHtoSs FROM DEFAULT {
    ADD w.box{
        PROPERTY (delete(w)) {
            panelLocation = TOOLBAR;
            askConfirm = TRUE;
        PROPERTY (objectClassName(w)) {
            preferredCharWidth = 15;
        }
        }
    }
    ADD d.box;

    NEW footer.container {
        childConstraints = TO THE BOTTOM;

        NEW cont3 {
            childConstraints = TO THE RIGHT;
            ADD w.historyGroup {
                childConstraints = TO THE BOTTOM;
            }

            ADD w.postedGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        ADD w.printGroup {
            childConstraints = TO THE BOTTOM;
            NEW case55{
                childConstraints = TO THE RIGHT;

                NEW contOne {
                    title = 'Накладная';
                    ADD PROPERTY(editConsignment);
                }
                NEW tn{
                    childConstraints = TO THE RIGHT;
                    title = 'ТН-2';
                    ADD PROPERTY(printConsignmentSimpleVertical);
                    ADD PROPERTY(printConsignmentSimpleHorizontal);
                    ADD PROPERTY(printConsignmentSimpleAttach);
                }
            }
            NEW ttn1{
                childConstraints = TO THE RIGHT;
                title = 'ТТН-1';
                ADD PROPERTY(printConsignmentVerticalA);
                ADD PROPERTY(printConsignmentHorizontalA);
                ADD PROPERTY(printConsignmentVerticalB);
                ADD PROPERTY(printConsignmentHorizontalB);
                ADD PROPERTY(printConsignmentAttach);
            }

        }

    }
    ADD functions.box;
}

@defineConsignment(WHtoS);
@implementConsignmentHeader(WHtoS, warehouse);

senderConsignment (consignment) += companyWarehouse(warehouseWHtoS(consignment));
recipientConsignment (consignment) += companyStore(storeWHtoS(consignment));

consignmentConsignmentDetail (consignmentDetail) += WHtoSWHtoSDetail (consignmentDetail);
skuConsignmentDetail (consignmentDetail) += skuWHtoSDetail (consignmentDetail);
shortNameUOMConsignmentDetail (consignmentDetail) += shortNameUOMWHtoSDetail (consignmentDetail);
quantityConsignmentDetail (consignmentDetail) += quantityWHtoSDetail (consignmentDetail);
priceConsignmentDetail (consignmentDetail) += supplierPriceWHtoSDetail (consignmentDetail);
vatConsignmentDetail (consignmentDetail) += valueSupplierVATWHtoSDetail (consignmentDetail);
sumVATConsignmentDetail (consignmentDetail) += sumSupplierVATWHtoSDetail (consignmentDetail);
sumConsignmentDetail (consignmentDetail) += supplierSumWHtoSDetail (consignmentDetail);
sumInvoiceConsignmentDetail (consignmentDetail) += sumInvoiceWHtoSDetail (consignmentDetail);
grossWeightConsignmentDetail (consignmentDetail) += sumNetWeightWHtoSDetail (consignmentDetail);
packQuantityConsignmentDetail (consignmentDetail) += packQuantityWHtoSDetail(consignmentDetail);
//currencyConsignment(consignment) += currencyWHtoS(consignment);
//isCustomsFlowConsignment (consignment) += consignment IS WHtoS;
noteConsignment(consignment) += noteWHtoS(consignment);

//noteConsignmentDetail

//todo: Надо наполнить свойствами накладную. (УНП...)



