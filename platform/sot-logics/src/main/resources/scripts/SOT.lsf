MODULE SOT;

REQUIRE System,
        Integration,
        Item,
        LegalEntity,
        PurchaseInvoice,
        PurchaseShipment,
        SalePurchaseInvoice,
        Shipment,
        Stock,
        Warehouse,
        MasterData;

CLASS sotUOM 'Единица измерений SOT' : externalizable;
TABLE sotUOM(sotUOM);

nameSotUOM 'Наименование' (sotUOM) = DATA STRING[100](sotUOM) IN base;

netWeightSotUOM 'Вес нетто' (sotUOM) = DATA NUMERIC[9,3](sotUOM) IN base;

UOMSotUOM 'Единица измерений' = DATA UOM (sotUOM);
nameUOMSotUOM 'Единица измерений' (sotUOM) = name(UOMSotUOM(sotUOM)) IN base;

sotUOMWeightUOM (weight, uom) = GROUP MAX sotUOM BY netWeightSotUOM(sotUOM), UOMSotUOM(sotUOM) WHERE sotUOM IS sotUOM;

sotUOMItem = DATA sotUOM (item);
nameSotUOMItem 'Единица измерений SOT' (item) = nameSotUOM(sotUOMItem(item)) IN itemBaseGroup;

sotSIDItem 'SOT SID'= DATA STRING[100] (item);
sotSIDUserInvoiceDetail 'SOT SID' = DATA STRING[100] (Purchase.userInvoiceDetail) IN base;

sotSIDBatch 'SOT SID' (batch) = ABSTRACT STRING[100] (batch) PERSISTENT;
sotSIDBatch (batch) += sotSIDUserInvoiceDetail(invoiceDetailShipmentDetail(shipmentDetailShipmentBatch(batch))) IF batch IS shipmentBatch;

sotSIDOverrideUserInvoiceDetail (userInvoiceDetail) = sotSIDItem (Sale.skuInvoiceDetail(userInvoiceDetail)) OR sotSIDBatch (Sale.batchInvoiceDetail(userInvoiceDetail));

sotUOMItem(i) <- sotUOMWeightUOM(netWeightItem(i), UOMItem(i)) WHEN CHANGED(netWeightItem(i)) OR CHANGED(UOMItem(i));

mag2LegalEntityStock 'Код MAG2'= DATA STRING[100] (legalEntity, stock);
mag2Stock 'Код MAG2' (stock) = mag2LegalEntityStock(legalEntityStock(stock), stock);

EXTEND FORM item PROPERTIES(i) BEFORE nameUOMItem(i) nameSotUOMItem SHOWIF showUOMItem(i);
EXTEND FORM items PROPERTIES(i) dateTimeLastMovedSku, sotSIDItem, sidExternalizable;

EXTEND FORM warehouses
    PROPERTIES (w) READONLY mag2Stock BEFORE delete(w), sidExternalizable BEFORE delete(w)
;

EXTEND FORM batches
    PROPERTIES (bt) READONLY sotSIDBatch BEFORE nameSkuBatch(bt)
;

FORM sotUOMs 'Единицы измерений SOT'
    OBJECTS su=sotUOM
    PROPERTIES(su) sidExternalizable, nameSotUOM, netWeightSotUOM, nameUOMSotUOM, ADDOBJ, delete
;

NAVIGATOR {
    masterData {
            skuNavigator {
                ADD sotUOMs;
            }
    }
}