MODULE ContractPayment;

REQUIRE ContractLedger, Payment;

EXTEND CLASS payment : contractLedger, contractALedger, outContractLedger;

contractPayment 'Контракт' (payment) = DATA contract (payment) AUTOSET;
numberContractPayment 'Номер' (payment) = numberContract(contractPayment(payment));

currencyContractPayment(payment) = currencyContract(contractPayment(payment));
nameCurrencyContractPayment 'Валюта' (payment) = name(currencyContractPayment(payment));

payerPayment(payment) <- partyBContract(contractPayment(payment)) WHEN ASSIGNED (contractPayment(payment));
beneficiaryPayment(payment) <- partyAContract(contractPayment(payment)) WHEN ASSIGNED (contractPayment(payment));

typeExchangeContractPayment (payment) = DATA typeExchange (payment);
nameTypeExchangeContractPayment 'Тип обмена' (payment) = name(typeExchangeContractPayment(payment));
rateExchangeContractPayment 'Курс' (payment) = DATA NUMERIC[15,8] (payment) PREFCHARWIDTH 15;
rateExchangeContractPayment (payment) <- UNION OVERRIDE 1.0 IF payment IS payment AND currencyContractPayment(payment) == currencyAccountToPayment(payment) AND NOT typeExchangeContractPayment(payment),
                                         rateTypeExchangeCurrencyDate(typeExchangeContractPayment(payment), currencyContractPayment(payment), datePayment(payment)) AND currencyContractPayment(payment) != currencyAccountToPayment(payment)
    WHEN CHANGED(currencyContractPayment(payment)) OR
         CHANGED(typeExchangeContractPayment(payment)) OR
         CHANGED(datePayment(payment)) OR
         CHANGED(rateToPayment(payment)) OR
         CHANGED(currencyContractPayment(payment)) OR
         CHANGED(currencyAccountToPayment(payment));

sumContractPayment 'Сумма платежа по контракту' (payment) = DATA NUMERIC[16,2] (payment) PREFCHARWIDTH 16;
sumContractPayment(payment) <- IF currencyContractPayment(payment) == currencyAccountToPayment(payment)
                               THEN sumToPayment(payment)
                               ELSE rateExchangeContractPayment(payment)*sumToPayment(payment)
    WHEN CHANGED(sumToPayment(payment)) OR
         CHANGED(currencyContractPayment(payment)) OR
         CHANGED(currencyAccountToPayment(payment)) OR
         CHANGED(typeExchangeContractPayment(payment)) OR
         CHANGED(rateExchangeContractPayment(payment));

CONSTRAINT currencyTypeExchange(typeExchangeContractPayment(payment)) == currencyContractPayment(payment)
    CHECKED BY typeExchangeContractPayment MESSAGE 'Валюта платежа должна быть отличной от валюты типа обмена';

@implementContractLedger( ,payment, contract);
sumContractLedger(contractLedger) += -sumContractPayment(contractLedger);

@implementContractLedger(a ,payment, contract);
sumContractALedger(contractALedger) += -sumContractPayment(contractALedger);

@implementOutContractLedgerInContractLedgerPrefix(out, payment, contract);
sumOutContractLedger(outContractLedger) += sumContractPayment(outContractLedger);

//------------------------------------------- Actions --------------------------------------------------------------//

setRateExchangeContractPayment = ACTION (payment){
    REQUEST NUMERIC[16,2] INPUT;
    SET rateExchangeContractPayment(payment) <- requestedNumeric()/sumToPayment(payment);
}

//----------------------------------------------- Цвета --------------------------------------------------------------//

backgroundSumContractLedgerDate 'Цвет' (contractLedger, date) = RGB(255,238,165) AND dateContractLedger(contractLedger) <= (date AS DATE) OR
                                                                RGB(255,160,160) AND dateContractLedger(contractLedger) > (date AS DATE);
backgroundContractLedgerDate 'Цвет' (contractLedger, date) = RGB(255,160,160) AND dateContractLedger(contractLedger) > (date AS DATE);
backgroundSumContract 'Цвет' (contract) = RGB(255,238,165) AND contract IS contract;
backgroundSumContractALedgerDate 'Цвет' (contractALedger, date) = RGB(232,184,146) AND dateContractALedger(contractALedger) <= (date AS DATE) OR
                                                              RGB(255,160,160) AND dateContractALedger(contractALedger) > (date AS DATE);
backgroundContractALedgerDate 'Цвет' (contractALedger, date) = RGB(255,160,160) AND dateContractALedger(contractALedger) > (date AS DATE);
backgroundSumContractA 'Цвет' (contract) = RGB(232,184,146) AND contract IS contract;
backgroundBonusSumInContractLedger 'Цвет' (inContractLedger) = RGB(213,249,185) AND inContractLedger IS inContractLedger;
backgroundBonusSumContract 'Цвет' (contract) = RGB(213,249,185) AND contract IS contract;
backgroundPaymentRequestDate 'Цвет' (paymentRequest, date) = RGB(255,160,160) AND datePaymentRequest(paymentRequest) > (date AS DATE);
backgroundOutContractLedgerDate 'Цвет' (outContractLedger, date) = RGB(255,160,160) AND dateOutContractLedger(outContractLedger) > (date AS DATE);
backgroundInContractLedgerDate 'Цвет' (inContractLedger, date) = RGB(255,160,160) AND dateInContractLedger(inContractLedger) > (date AS DATE);

showTypeExchangeContractPayment(payment) = currencyContractPayment(payment) != currencyAccountToPayment(payment);

//-------------------------------------------- Форма платежей -------------------------------------------------------//

EXTEND FORM payment
    OBJECTS i = inContractLedger
    PROPERTIES(p) numberContractPayment,
                  costedInContractLedgerOutContractLedger,
                  writeOutContractLedgerFIFO TODRAW i FORCE PANEL TOOLBAR,
                  writeOutContractLedgerLIFO TODRAW i FORCE PANEL TOOLBAR,
                  nameTypeExchangeContractPayment SHOWIF showTypeExchangeContractPayment(p),
                  rateExchangeContractPayment SHOWIF showTypeExchangeContractPayment(p),
                  sumContractPayment ON CHANGE setRateExchangeContractPayment(p),
                  nameCurrencyContractPayment

    PROPERTIES(i) READONLY descriptionInContractLedger, debtInContractLedger
    PROPERTIES(p, i) costOutContractLedgerInContractLedger

    FILTERS contractPayment(p) == contractInContractLedger(i)
;

EXTEND DESIGN payment{
    main{
        columnContainer{
            NEW thirdColumn{
                childConstraints = TO THE BOTTOM;
                caption = 'Контракт';
                ADD PROPERTY(numberContractPayment);
                ADD PROPERTY(nameCurrencyContractPayment);
                ADD PROPERTY(sumContractPayment);
                ADD PROPERTY(costedInContractLedgerOutContractLedger);
                ADD PROPERTY(nameTypeExchangeContractPayment);
                ADD PROPERTY(rateExchangeContractPayment);
            }
        }
        ADD i.box BEFORE functions.box;
    }

    POSITION PROPERTY(writeOutContractLedgerLIFO) TO THE RIGHT PROPERTY(writeOutContractLedgerFIFO);
}

EXTEND FORM payments
    PROPERTIES(p) BACKGROUND backgroundPaymentDate(p, d) READONLY numberContractPayment AFTER dateTimePayment,
                                                                  nameCurrencyContractPayment BEFORE delete,
                                                                  sumContractPayment BEFORE delete

;

//------------------------------------- Платежи по договору --------------------------------------------------------//

payContractDate 'Оплатить по договору' = ACTION (contract, date) NEWSESSION {

    ADDOBJ payment;
    FOR pm == addedObject() DO {
        SET dateTimePayment(pm) <- toDateTime(date AS DATE);
        SET contractPayment(pm) <- contract AS contract;
        SET currencyContractPayment(pm) <- currencyContract(contract);
        SET sumContractPayment(pm) <- debtSumContractDate(contract, date);
        SET costOutContractLedgerInContractLedger(pm, inContractLedger) <- debtInContractLedgerDate(inContractLedger, date)
            WHERE contractPayment(pm) == contractInContractLedger(inContractLedger);
        FORM payment OBJECTS p = pm MODAL;
        IF formResult() == formResult.ok THEN {
            EXEC apply();
        }
    }

}

//------------------------------------------- Платеж c учетом бонусов договора --------------------------------------------//

payPaymentConditionInContractLedgerDate 'Оплатить c учетом бонусов договора' = [ACTION (inContractLedger, date) NEWSESSION {

    ADDOBJ payment;
    FOR pm == addedObject() DO {
        SET dateTimePayment(pm) <- toDateTime(date AS DATE);
        SET contractPayment(pm) <- contractInContractLedger(inContractLedger);
        SET currencyContractPayment(pm) <- currencyContract(contractInContractLedger(inContractLedger));
        SET sumContractPayment(pm) <- bonusDebtSumInContractLedgerDate(inContractLedger, date);
        SET costOutContractLedgerInContractLedger(pm, inContractLedger) <- bonusDebtSumInContractLedgerDate(inContractLedger, date);
        FORM payment OBJECTS p = pm MODAL;
        IF formResult() == formResult.ok THEN {
            ADDOBJ creditMemoOut;
            FOR cm == addedObject() DO {
                SET dateTimeCreditMemoOut(cm) <- toDateTime(date AS DATE);
                SET contractCreditMemoOut(cm) <- contractInContractLedger(inContractLedger);
                SET sumCreditMemoOut(cm) <- bonusSumInContractLedger(inContractLedger, date);
                SET costOutContractLedgerInContractLedger(cm, inContractLedger) <- bonusSumInContractLedger(inContractLedger, date);
                FORM creditMemo OBJECTS c = cm MODAL;
                IF formResult() == formResult.ok THEN {
                    EXEC apply();
                }
            }
        }
    }
}](inContractLedger, date) AND bonusSumInContractLedger(inContractLedger, date) > 0.0;

payPaymentConditionContractDate 'Оплатить c учетом бонусов договора' = [ACTION (contract, date) NEWSESSION {

    ADDOBJ payment;
    FOR pm == addedObject() DO {
        SET dateTimePayment(pm) <- toDateTime(date AS DATE);
        SET contractPayment(pm) <- contract AS contract;
        SET currencyContractPayment(pm) <- currencyContract(contract);
        SET sumContractPayment(pm) <- bonusDebtSumContractDate(contract, date);
        SET costOutContractLedgerInContractLedger(pm, inContractLedger) <- bonusDebtSumInContractLedgerDate(inContractLedger, date)
            WHERE contractPayment(pm) == contractInContractLedger(inContractLedger);
        FORM payment OBJECTS p = pm MODAL;
        IF formResult() == formResult.ok THEN {
            ADDOBJ creditMemoOut;
            FOR cm == addedObject() DO {
                SET dateTimeCreditMemoOut(cm) <- toDateTime(date AS DATE);
                SET contractCreditMemoOut(cm) <- contract AS contract;
                SET sumCreditMemoOut(cm) <- bonusSumContractDate(contract, date);
                SET costOutContractLedgerInContractLedger(cm, inContractLedger) <- bonusSumInContractLedger(inContractLedger, date)
                    WHERE contractCreditMemo(cm) == contractInContractLedger(inContractLedger);
                FORM creditMemo OBJECTS c = cm MODAL;
                IF formResult() == formResult.ok THEN {
                    EXEC apply();
                }
            }
        }
    }

}](contract, date) AND bonusSumContractDate(contract, date) > 0.0;

EXTEND FORM contractLedger
    PROPERTIES(c, d) FORCE PANEL TOOLBAR payContractDate, payPaymentConditionContractDate

    PROPERTIES(ic, d) FORCE PANEL TOOLBAR payPaymentConditionInContractLedgerDate

    OBJECTS pm = payment
    PROPERTIES(pm) READONLY objectClassName BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            dateTimePayment BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            seriesNumberObject BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            namePayerPayment BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            nameBeneficiaryPayment BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            nameCurrencyContractPayment BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            nameTypeExchangeContractPayment BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            rateExchangeContractPayment BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            sumContractPayment BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            costedInContractLedgerOutContractLedger BACKGROUND backgroundOutContractLedgerDate(pm, d),
                            notePayment BACKGROUND backgroundOutContractLedgerDate(pm, d)
    PROPERTIES(pm) FORCE PANEL TOOLBAR writeOutContractLedgerFIFO, writeOutContractLedgerLIFO
    PROPERTIES(pm) ADDFORM, EDITFORM, delete
    ORDER BY dateTimePayment

    FILTERS contractPayment(pm) == c
;

EXTEND DESIGN contractLedger{
    secondContainer {
        ADD pm.box BEFORE pr.box;
    }
}

