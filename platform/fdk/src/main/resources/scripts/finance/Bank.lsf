MODULE Bank;

REQUIRE System, LegalEntity, Finance, PaymentLedger;

CLASS bank 'Банк' : named;
TABLE bank (bank);
TABLE bankDate (bank, DATE);

GROUP bank 'Информация о банке' : base;

MFOBank 'Код МФО' = DATA STRING[9] (bank) IN bank;
departmentBank 'Отдел банка' = DATA STRING[100] (bank) IN bank;
CBUBank 'ЦБУ' = DATA STRING[3] (bank) IN bank;
infoBank 'Дополнительные сведения' = DATA STRING[100] (bank) IN bank;

countryBank = DATA country (bank);
nameCountryBank 'Страна' (bank) = name(countryBank(bank)) IN bank MINCHARWIDTH 10 PREFCHARWIDTH 15 MAXCHARWIDTH 20;

@defineHistorizable(addressBank, 'Адрес банка', STRING[150], bank, name, bank);

FORM bank 'Банк'
    OBJECTS b=bank FIXED PANEL
    PROPERTIES(b)  name, addressBank, MFOBank, departmentBank, CBUBank, nameCountryBank, infoBank
    EDIT bank OBJECT b
;

FORM banks 'Банки'
    OBJECTS b=bank
    PROPERTIES(b) READONLY name, addressBank, MFOBank, departmentBank, CBUBank, nameCountryBank, infoBank
    PROPERTIES(b) ADDFORM, EDITFORM, delete
;

NAVIGATOR {
    financeMasterData {
        ADD banks;
    }
}

// ----------------------------------- Расчетный счет ------------------------------------------ //

CLASS account 'Расчетный счет';
TABLE account (account);

bankAccount = DATA bank (account);

GROUP accountGroup 'Банковская информация' : base;

currencyAccount = DATA currency (account);
nameCurrencyAccount 'Валюта счета' = name(currencyAccount(account)) IN accountGroup;

numberAccount 'Номер расчетного счета'  = DATA STRING[13] (account) IN accountGroup FIXEDCHARWIDTH 13;
accountNumber 'Расчетный счет по номеру' (string) = GROUP UNIQUE account BY numberAccount (account) WHERE account IS account;

nameBankAccount 'Наименование банка' (account) = name(bankAccount(account)) IN accountGroup;
addressBankAccount 'Адрес банка' (account) = addressBank(bankAccount(account)) IN accountGroup;
MFOBankAccount 'Код МФО банка' (account) = MFOBank(bankAccount(account)) IN accountGroup;
departmentBankAccount 'Отдел банка' (account) = departmentBank(bankAccount(account)) IN accountGroup;
CBUBankAccount 'ЦБУ банка' (account) = CBUBank(bankAccount(account)) IN accountGroup;
noteAccount 'Примечание'  = DATA STRING[50] (account) IN accountGroup;

EXTEND CLASS account : PaymentLedger.account;
nameAccount(account) += numberAccount(account);
PaymentLedger.currencyAccount(account) += currencyAccount(account);

// ------------------------ Привязка банков к юрлицам ------------------------ //

legalEntityAccount = DATA legalEntity (account);
defaultAccountLegalEntity(legalEntity) = GROUP MAX account BY legalEntityAccount(account);
PaymentLedger.legalEntityAccount(account) += legalEntityAccount(account);

userAccountLegalEntity = DATA account (legalEntity);
CONSTRAINT legalEntityAccount(userAccountLegalEntity(legalEntity)) != legalEntity CHECKED MESSAGE 'ошибка: Р/сч. по умолчанию должен соответствовать р/сч. Ю.Л.';

accountLegalEntity (legalEntity) = UNION OVERRIDE defaultAccountLegalEntity(legalEntity), userAccountLegalEntity(legalEntity);
numberAccountLegalEntity 'Основной р/сч.' (legalEntity) = numberAccount(accountLegalEntity(legalEntity)) IN lawGroup;

equalsLegalEntityAccount 'Основной' (legalEntity, account) = userAccountLegalEntity(legalEntity) == account;

EXTEND FORM legalEntity
    PROPERTIES(l)  numberAccountLegalEntity

    OBJECTS a=account
    PROPERTIES(a)  numberAccount, nameCurrencyAccount, nameBankAccount, addressBankAccount, departmentBankAccount,
                   CBUBankAccount, MFOBankAccount, noteAccount, ADDOBJ, delete
    FILTERS legalEntityAccount(a) == l
    PROPERTIES equalsLegalEntityAccount(l, a)
;

EXTEND DESIGN legalEntity {
    accountContainer {
        ADD a.box{
            caption = 'Расчетные счета';
            fillHorizontal = 2.0;
        }
    }
}

// ------------------------ Макросы ------------------------ //

META defineDocumentAccount(object, contact, caption)
    numberAccount###contact###object 'Расчетный счет'###caption (object) = numberAccountLegalEntity(contact###object(object)) IN documentPrmGroup;
    nameBank###contact###object 'Банк'###caption (object) = nameBankAccount(accountLegalEntity(contact###object(object))) IN documentPrmGroup;
    addressBank###contact###object 'Адрес банка'###caption (object) = addressBankAccount(accountLegalEntity(contact###object(object))) IN documentPrmGroup;
    departmentBank###contact###object 'Отдел банка'###caption (object) = departmentBankAccount(accountLegalEntity(contact###object(object))) IN documentPrmGroup;
    CBUBank###contact###object 'ЦБУ банка'###caption (object) = CBUBankAccount(accountLegalEntity(contact###object(object))) IN documentPrmGroup;
    MFOBank###contact###object 'Код МФО банка'###caption (object) = MFOBankAccount(accountLegalEntity(contact###object(object))) IN documentPrmGroup;
END
