MODULE Pricing;

REQUIRE Invoice, Employee, Store, Label, Machinery;


// ----------------------------------- Создавать акт расценки по умолчанию для отдела ------------------------------------------ //

costLedgerDepartmentStore 'Учет по себестоимости' = DATA BOOLEAN (departmentStore) IN bookkeepingGroup;
EXTEND FORM departmentStore PROPERTIES(d) costLedgerDepartmentStore;

createPricingDepartmentStore 'Создавать акт расценки' = DATA BOOLEAN (departmentStore) IN createGroup;
EXTEND FORM departmentStore PROPERTIES createPricingDepartmentStore(d);

EXTEND DESIGN departmentStore{
    row21 { ADD PROPERTY(createPricingDepartmentStore);}
}

// ----------------------------------- Комиссия ------------------------------------------ //

CLASS pricingCommittee 'Комиссия для акта расценки' : committee;

//@defineCommitteeEmployee(committee, chairman, 'Председатель');
@defineCommitteeEmployeePrefix(pricingCommittee, formedMan, 'Цены сформировал');
@defineCommitteeEmployeePrefix(pricingCommittee, checkedMan, 'Реестр проверил');
@defineCommitteeEmployeePrefix(pricingCommittee, labeledMan, 'Товар промаркировал');
@defineCommitteeEmployeePrefix(pricingCommittee, accountantMan, 'Бухгалтер');
@defineCommitteeEmployeePrefix(pricingCommittee, headMan, 'Зав. магазином');

// ----------------------------------- Комиссия по умолчанию для отдела ------------------------------------------ //

pricingCommitteeDepartmentStore 'Комиссия для акта расценки (ИД)' = DATA pricingCommittee (departmentStore);
namePricingCommitteeDepartmentStore 'Комиссия для акта расценки' (departmentStore) = name(pricingCommitteeDepartmentStore(departmentStore)) IN committeeGroup;
isDefaultPricingCommitteeDepartmentStore 'По умолчанию' (pricingCommittee, departmentStore) = pricingCommitteeDepartmentStore(departmentStore) == pricingCommittee;
CONSTRAINT pricingCommitteeDepartmentStore(departmentStore) AND NOT inCommitteeEmployeeDivision(pricingCommitteeDepartmentStore(departmentStore), departmentStore)
    CHECKED BY pricingCommitteeDepartmentStore MESSAGE 'Для отдела выбрана комиссия, которая для него не действует';

EXTEND FORM departmentStore PROPERTIES namePricingCommitteeDepartmentStore(d);

// ----------------------------------- Вид цен по умолчанию для отдела ------------------------------------------ //
priceListTypeDepartmentStore  = DATA priceListType (departmentStore);
namePriceListTypeDepartmentStore 'Вид цен для акта расценки' (departmentStore) = name(priceListTypeDepartmentStore(departmentStore)) IN bookkeepingGroup;

EXTEND FORM departmentStore PROPERTIES namePriceListTypeDepartmentStore(d);
// ----------------------------------- Формы для комиссий для реестра цен ------------------------------------------ //

FORM pricingCommittee 'Комиссия для акта расценки'
    OBJECTS c=pricingCommittee FIXED PANEL
    PROPERTIES(c)      name, nameFormedManPricingCommittee, nameCheckedManPricingCommittee,
                       nameLabeledManPricingCommittee, nameAccountantManPricingCommittee, nameHeadManPricingCommittee

    TREE treeStore a=STRING[3], t=chainStores, st=storeType, s=store
    PROPERTIES         READONLY OBJVALUE(a), name(t), name(st), name(s)
    FILTERS            stringEqualsAll(a), inChainStoresStoreType (t, st), inStoreTypeStore(st, s)

    OBJECTS dep=departmentStore
    PROPERTIES(dep)    READONLY depName = name
    PROPERTIES(c, dep) inCommitteeEmployeeDivision, isDefaultPricingCommitteeDepartmentStore
    FILTERS            inChainStoresStoreTypeStoreDepartmentStore(t, st, s, dep)
    ORDER BY depName

    FILTERGROUP filters5
        FILTER 'Показывать отделы только для данной комиссии' 'F9' inCommitteeEmployeeDivision(c, dep)

    EDIT pricingCommittee OBJECT c
;
@extendFormFilterStockAccess(departmentStore, dep, pricingCommittee);
@extendFormFilterStockGroupAccess(chainStores, t, pricingCommittee, accessEmployeeEmployeeDivisionGroup);
@extendFormFilterStockGroupAccess(storeType, st, pricingCommittee, accessEmployeeEmployeeDivisionGroup);
@extendFormFilterStockGroupAccess(store, s, pricingCommittee, accessEmployeeEmployeeDivisionGroup);

DESIGN pricingCommittee FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        childConstraints = TO THE BOTTOM;

        NEW caseOne AFTER c.box {
            childConstraints = TO THE RIGHT;
            title = 'Отделы, для которых действуют комиссии';

            ADD treeStore.tree {
                fillHorizontal = 1;
            }

            ADD dep.box{
                fillHorizontal = 2;
            }
        }

        NEW oneCase AFTER caseOne{
            title = 'Состав комиссии';
            fillHorizontal = 3;
            childConstraints = TO THE BOTTOM;
            ADD PROPERTY (nameFormedManPricingCommittee);
            ADD PROPERTY (nameCheckedManPricingCommittee);
            ADD PROPERTY (nameLabeledManPricingCommittee);
            ADD PROPERTY (nameAccountantManPricingCommittee);
            ADD PROPERTY (nameHeadManPricingCommittee);
        }

    }
}

FORM pricingCommitteeDialog 'Комиссии для акта расценки'
    OBJECTS r=pricingCommittee
    PROPERTIES(r)      READONLY name, nameEmployeeDivisionCommittee, nameFormedManPricingCommittee, nameCheckedManPricingCommittee, nameLabeledManPricingCommittee,
                       nameAccountantManPricingCommittee, nameHeadManPricingCommittee
    PROPERTIES(r)      ADDFORM, EDITFORM

    DIALOG pricingCommittee OBJECT r
;

// ----------------------------------- Макрос для задания комиссии для документов ------------------------------------------ //

GROUP pricingCommitteeGroup 'Комиссия' : public;

META defineDocumentHeaderPricingCommittee(object, stockClass)
    pricingCommittee###object (object) = DATA pricingCommittee(object);

    namePricingCommittee###object 'Комиссия для акта расценки' (object) = commonName(pricingCommittee###object(object)) IN documentPrmGroup MINCHARWIDTH 20 PREFCHARWIDTH 40;
    pricingCommittee###object (object) <- pricingCommitteeDepartmentStore(stockClass###object(object))
        WHEN CHANGED(stockClass###object(object));

    nameFormedMan###object 'Цены сформировал' (object) = nameFormedManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameCheckedMan###object 'Реестр проверил' (object) = nameCheckedManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameLabeledMan###object 'Товар промаркировал' (object) = nameLabeledManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameAccountantMan###object 'Бухгалтер' (object) = nameAccountantManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameHeadMan###object 'Зав. магазином' (object) = nameHeadManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;

END
META defineDocumentHeaderAbstractPricingCommittee(object, stockClass)
    pricingCommittee###object (object) = ABSTRACT pricingCommittee(object) PERSISTENT;

    namePricingCommittee###object 'Комиссия для акта расценки' (object) = commonName(pricingCommittee###object(object)) IN documentPrmGroup MINCHARWIDTH 20 PREFCHARWIDTH 40;

    nameFormedMan###object 'Цены сформировал' (object) = nameFormedManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameCheckedMan###object 'Реестр проверил' (object) = nameCheckedManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameLabeledMan###object 'Товар промаркировал' (object) = nameLabeledManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameAccountantMan###object 'Бухгалтер' (object) = nameAccountantManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;
    nameHeadMan###object 'Зав. магазином' (object) = nameHeadManPricingCommittee(pricingCommittee###object(object)) IN pricingCommitteeGroup;

END

META defineDocumentInterfaceHeaderPricingCommittee (object, stockClass)
    @defineDocumentHeaderAbstractPricingCommittee (object, stockClass);
    @defineDocumentHeaderPricingCommittee (user###object, stockClass);
    pricingCommittee###object (object) += pricingCommittee###user###object(object);
END

// ---------------------------------------------- Надбавка -------------------------------------------- //

META defineDocumentDetailMarkupCustomPrefix (detail, prefix, caption)
    prefix###markup###detail 'Надбавка,%'###caption = DATA NUMERIC[8,3] (detail);
END
META defineDocumentAbstractDetailMarkupCustomPrefix (detail, prefix, caption)
    prefix###markup###detail 'Надбавка,%'###caption = ABSTRACT NUMERIC[8,3] (detail) PERSISTENT;
END
META defineDocumentInterfaceDetailMarkupCustomPrefix (detail, prefix, caption)
    @defineDocumentAbstractDetailMarkupCustomPrefix(detail, prefix, caption);
    @defineDocumentDetailMarkupCustomPrefix(user###detail, prefix, caption);
    prefix###markup###detail (detail) += prefix###markup###user###detail(detail);
END

META defineDocumentDetailMarkupPrefix (object, prefix, caption)
    @defineDocumentDetailMarkupCustomPrefix(object##Detail, prefix, caption);
END
META defineDocumentAbstractDetailMarkupPrefix (object, prefix, caption)
    @defineDocumentAbstractDetailMarkupCustomPrefix(object##Detail, prefix, caption);
END
META defineDocumentInterfaceDetailMarkupPrefix (object, prefix, caption)
    @defineDocumentInterfaceDetailMarkupCustomPrefix(object##Detail, prefix, caption);
END

META defineDocumentDetailMarkupSumCustomPrefix (detail, prefix, caption)
    prefix###markupSum###detail 'Сумма надбавки'###caption = DATA NUMERIC[14,3] (detail);
END
META defineDocumentAbstractDetailMarkupSumCustomPrefix (detail, prefix, caption)
    prefix###markupSum###detail 'Сумма надбавки'###caption = ABSTRACT NUMERIC[14,3] (detail) PERSISTENT;
END
META defineDocumentInterfaceDetailMarkupSumCustomPrefix (detail, prefix, caption)
    @defineDocumentAbstractDetailMarkupSumCustomPrefix(detail, prefix, caption);
    @defineDocumentDetailMarkupSumCustomPrefix(user###detail, prefix, caption);
    prefix###markupSum###detail (detail) += prefix###markupSum###user###detail(detail);
END
META defineDocumentInterfaceDetailMarkupSumPrefix (object, prefix, caption)
    @defineDocumentInterfaceDetailMarkupSumCustomPrefix(object##Detail, prefix, caption);
END
META defineDocumentInterfaceDetailMarkupSumPrefix (object, prefix)
    @defineDocumentInterfaceDetailMarkupSumPrefix(object, prefix, );
END

META deriveDocumentDetailMarkupSumCustomPrefix(detail, prefixA, prefixB, prefixC)
    prefixA###markupSum###detail(detail) <- prefixB###sum###detail(detail) (-) prefixA###VATSum###detail (detail) (-) prefixC###sum###detail(detail)
                    WHEN CHANGED(prefixB###sum###detail(detail)) OR CHANGED (prefixA###VATSum###detail (detail)) OR CHANGED (prefixC###sum###detail(detail));
END
META deriveDocumentDetailMarkupSumPrefix(object, prefixA, prefixB, prefixC)
    @deriveDocumentDetailMarkupSumCustomPrefix(object##Detail, prefixA, prefixB, prefixC);
END

//------------------------  derive or change ------------------------------------//

META deriveDocumentDetailPriceMarkupCustomPrefix(detail, prefix, currencyProp)
    prefix###price###detail(detail)  <- roundCurrency([X*(Y+100)*(Z+100)/10000](
    0.0 IF detail IS detail OR price###detail(detail),
    0.0 IF detail IS detail OR prefix###markup###detail(detail),
    value###prefix##VAT###detail(detail)), currencyProp###detail(detail))
                    WHEN CHANGED(price###detail(detail)) OR CHANGED (prefix###markup###detail(detail)) OR CHANGED(prefix##VAT###detail(detail)) OR CHANGED(currencyProp###detail(detail));
END

META deriveDocumentDetailPriceMarkupCustomPrefix(detail, prefix)
    @deriveDocumentDetailPriceMarkupCustomPrefix(detail, prefix, currency);
END

META changeDocumentDetailMarkupCustomPrefix(detail, prefixA, prefixB, prefixC, prefixD)
    calc###prefixC###markup###detail(detail)  = [round2(((X/Z*100/(100+Y))-1)*100)](
        0.0 IF detail IS detail OR prefixA###price###detail(detail),
        prefixB###price###detail(detail),
        0.0 IF detail IS detail OR value###prefixD###VAT###detail(detail));

    change###prefixA###price###detail = ACTION (detail) {

        REQUEST NUMERIC[14,2] INPUT;
        SET prefixA###price###detail(detail) <- requestedNumeric();

        SET prefixC###markup###detail(detail) <- calc###prefixC###markup###detail(detail);
    }
END
META changeDocumentDetailPriceCustomPrefix(detail, prefixA, prefixB, prefixC, prefixD, currencyProp)

    calc###prefixA###price###detail(detail)  = roundCurrency([X*(Y+100)*(Z+100)/10000](
    0.0 IF detail IS detail OR prefixB###price###detail(detail),
    0.0 IF detail IS detail OR prefixC###markup###detail(detail),
    value###prefixD###VAT###detail(detail)), currencyProp###detail(detail));

    change###prefixC###markup###detail = ACTION (detail) {

        REQUEST NUMERIC[8,3] INPUT;
        SET prefixC###markup###detail(detail) <- requestedNumeric();

        SET prefixA###price###detail(detail) <- calc###prefixA###price###detail(detail);
    }
END

//----------------------------------------------- Расценка ---------------------------------------------------//

CLASS ABSTRACT pricing 'Акт расценки';
CLASS ABSTRACT pricingDetail 'Строка акта расценки';

CLASS userPricing 'Акт расценки (польз.)' : pricing, historizable, numeratedDocument;
CLASS userPricingDetail 'Строка акта расценки (польз.)' : pricingDetail;
CLASS userPricingPosted 'Проведенный акт расценки (польз.)' : userPricing, postedObject;

@defineDocumentInterface(pricing);

@defineDocumentInterfaceNumber(pricing);

@defineDocumentInterfaceDataStock(pricing, departmentStore, 'Склад');
@defineDocumentInterfacePosted(pricing);

@defineDocumentInterfaceDescription(pricing, 'Акт расценки');

@defineDocumentInterfaceCurrency(pricing);
@deriveDocumentCurrency(userPricing, departmentStore);

@defineDocumentInterfaceLegalEntity (pricing, supplier, 'Поставщик');
CONSTRAINT supplierUserPricing(userPricing) AND NOT isSellerLegalEntity(supplierUserPricing(userPricing))
    CHECKED BY supplierUserPricing MESSAGE 'Для акта расценки выбрано в качестве поставщика организация, не являющаяся поставщиком или компанией';

@defineDocumentInterfaceDetailSku(pricing, sku);

@defineDocumentInterfaceDetailQuantity(pricing);

@defineDocumentInterfaceDetailPricePrefix(pricing, , ' входная');
@defineDocumentInterfaceDetailDataSumPrefix (pricing, , ' входная');
@deriveDocumentDetailSum(userPricing, quantity);

@defineDocumentInterfaceHeaderQuantity(pricing);
@defineDocumentHeaderSkuQuantity(pricing, sku);
@defineDocumentHeaderSkuQuantity(userPricing, sku);
@defineDocumentInterfaceHeaderSumCustom (pricing, pricingDetail, ' входная');

@defineDocumentInterfaceDetailPricePrefix(pricing, retail, ' выходная');
@defineDocumentInterfaceDetailVAT(pricing, countryDepartmentStore, retail, );
@deriveDocumentDetailVAT(userPricing, retail);
@deriveDocumentDetailValueVAT(userPricing, retail);

@defineDocumentInterfaceDetailMarkupPrefix (pricing, retail, );
@changeDocumentDetailMarkupCustomPrefix(userPricingDetail, retail, , retail, retail);
@changeDocumentDetailPriceCustomPrefix(userPricingDetail, retail, , retail, retail, currency);
@defineDocumentInterfaceDetailDataSumPrefix (pricing, retail, ' выходная');

@deriveDocumentDetailPriceMarkupCustomPrefix(userPricingDetail, retail);
@deriveDocumentDetailSumPrefix(userPricing, retail, currency, quantity);

@defineDocumentInterfaceHeaderSumPrefix (pricing, retail, ' выходная');
@defineAddDetailDialogSkuStock(userPricing, sku, departmentStore, dialogSku);
@defineAddDetailDialogBarcode(userPricing, sku);

@defineDocumentInterfaceDetailVATDataSumPrefix (pricing, retail, ' розничная');
@deriveDocumentDetailReverseVATSumPrefix(userPricing, retail, retail);

@defineDocumentInterfaceDetailMarkupSumPrefix (pricing, retail);
@deriveDocumentDetailMarkupSumPrefix(userPricing, retail, retail, );

@defineDocumentInterfaceHeaderSumPrefix (pricing, retailMarkup, ' надбавки');
@defineDocumentInterfaceHeaderSumPrefix (pricing, retailVAT, ' НДС');

@defineDocumentInterfaceHeaderPricingCommittee (pricing, departmentStore);

@defineDocumentInterfaceHeaderSkip (pricing, changeLedger, 'Не изменять текущие цены');
@defineDocumentInterfaceDetailSkip(pricing, changeLedger, 'Не изменять текущие цены');

isReturnPricing 'Обратный' = ABSTRACT BOOLEAN (pricing);

//------------------------------------ Печатные формы акт расценки ------------------------------------------------ //

FORM pricing 'Акт расценки (печать)' PRINT
    OBJECTS p=pricing FIXED PANEL
    PROPERTIES (p)  SELECTOR objectClassName

    PROPERTIES (p) nameDepartmentStorePricing, nameLegalEntityStockPricing, numberPricing, seriesPricing, datePricing, timePricing,
                   nameSupplierPricing, nameCurrencyPricing, notePricing,
                   countPricingDetailPricing, quantityPricingDetailPricing, sumPricingDetailPricing,
                   retailMarkupSumPricingDetailPricing, retailVATSumPricingDetailPricing,
                   retailSumPricingDetailPricing, namePricingCommitteePricing,
                   nameFormedManPricing, nameCheckedManPricing, nameLabeledManPricing, nameAccountantManPricing,
                   nameHeadManPricing    //notPassToBookkeepingListRegister

    OBJECTS d=pricingDetail

    PROPERTIES (d) indexPricingDetail, idBarcodeSkuPricingDetail, nameSkuPricingDetail, shortNameUOMSkuPricingDetail,
                   quantityPricingDetail, pricePricingDetail, sumPricingDetail,
                   retailMarkupPricingDetail, retailMarkupSumPricingDetail, numberRetailVATPricingDetail, valueRetailVATPricingDetail, retailVATSumPricingDetail,
                   retailPricePricingDetail, retailSumPricingDetail

    FILTERS pricingPricingDetail(d) == p
;

printPricing 'Акт расценки' (pricing) = ACTION FORM pricing OBJECTS p IMAGE 'print.png' IN printGroup;

// --------------------------- Формы --------------------------------- //

backgroundRetailPricing 'Цвет' (pricing) = RGB(224, 255, 255) IF pricing IS pricing;
FORM userPricing 'Акт расценки'
    OBJECTS p = userPricing FIXED PANEL
    PROPERTIES (p) objectClassName, nameDepartmentStoreUserPricing, nameNumeratorObject, numberObject, seriesObject, dateUserPricing, timeUserPricing,
                   nameSupplierUserPricing, nameCurrencyUserPricing, noteUserPricing,
                   countUserPricingDetailUserPricing, quantityUserPricingDetailUserPricing, sumUserPricingDetailUserPricing,
                   retailSumUserPricingDetailUserPricing BACKGROUND backgroundRetailPricing(p), namePricingCommitteeUserPricing BACKGROUND backgroundRetailPricing(p), skipChangeLedgerUserPricing
    PROPERTIES (p) SHOWIF pricingCommitteeUserPricing(p)  nameFormedManUserPricing, nameCheckedManUserPricing, nameLabeledManUserPricing,
                   nameAccountantManUserPricing, nameHeadManUserPricing

    OBJECTS d = userPricingDetail
    PROPERTIES (d) indexUserPricingDetail, idBarcodeSkuUserPricingDetail, nameSkuUserPricingDetail, shortNameUOMSkuUserPricingDetail,
                   quantityUserPricingDetail, priceUserPricingDetail, sumUserPricingDetail
    PROPERTIES (d) BACKGROUND backgroundRetailPricing(p)
                   retailMarkupUserPricingDetail ON CHANGE EXEC changeRetailMarkupUserPricingDetail(d), retailMarkupSumUserPricingDetail, numberRetailVATUserPricingDetail, valueRetailVATUserPricingDetail,
                   retailVATSumUserPricingDetail, retailPriceUserPricingDetail ON CHANGE EXEC changeRetailPriceUserPricingDetail(d), retailSumUserPricingDetail
    PROPERTIES (d) nameDepartmentStoreUserPricingDetail, ADDOBJ, delete


    PROPERTIES(p) TODRAW d addDetailDialogSkuStockUserPricingDetailUserPricing,
                           addDetailInputBarcodeUserPricingDetailUserPricing, deleteUserPricingDetailUserPricing
    FILTERS userPricingUserPricingDetail(d) == p

    EVENTS
        ON OK EXEC prePostUserPricing(p)

    EDIT userPricing OBJECT p
;

DESIGN userPricing FROM DEFAULT{
    main {
        preferredSize = (1024, 768);

        NEW header.box BEFORE d.box {
            childConstraints = TO THE RIGHT;

            NEW headerRow1 {
                childConstraints = TO THE BOTTOM;

                ADD p.documentHeaderGroup {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY(objectClassName) { preferredCharWidth = 10; }
                    ADD PROPERTY(nameDepartmentStoreUserPricing);
                    ADD PROPERTY(nameNumeratorObject);
                    ADD PROPERTY(numberObject);
                    ADD PROPERTY(seriesObject);
                    ADD PROPERTY(dateUserPricing);
                    ADD PROPERTY(timeUserPricing);
                }

                NEW headerColumn2 {
                    childConstraints = TO THE RIGHT;
                    ADD p.documentPrmGroup;
                }
                ADD p.pricingCommitteeGroup;
            }

            ADD p.documentSumGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        d.panel{
            childConstraints = TO THE BOTTOM;
        }
        PROPERTY(formOk) {
            caption = 'Провести';
        }
    }
}

addUserPricing 'Добавить' = ACTION ADDFORM userPricing;
editUserPricing 'Редактировать' (userPricing) = ACTION EDITFORM userPricing;

FORM pricings 'Акты расценки' TITLE 'Акты расценки'
    OBJECTS p = pricing
    PROPERTIES (p) READONLY isPostedPricing FORCE GRID, isReturnPricing, objectClassName, numberPricing, seriesPricing, datePricing, timePricing,
                            nameDepartmentStorePricing, nameSupplierPricing, nameCurrencyPricing, notePricing,
                            countPricingDetailPricing, quantityPricingDetailPricing, sumPricingDetailPricing,
                            retailSumPricingDetailPricing BACKGROUND backgroundRetailPricing(p), skipChangeLedgerPricing

    PROPERTIES (p) READONLY FORCE PANEL nameUserCreatedHistorizable, timeCreatedHistorizable, hostnameComputerCreatedHistorizable, nameUserClosed, timeClosed, hostnameComputerClosed

    PROPERTIES ()  addUserPricing TODRAW p
    PROPERTIES (p) editUserPricing
    PROPERTIES (p) delete FORCE PANEL DRAWTOTOOLBAR SHOWIF isUserPricing(p)
    PROPERTIES (p) printPricing FORCE PANEL

    OBJECTS d = pricingDetail
    PROPERTIES (d) READONLY indexPricingDetail, idBarcodeSkuPricingDetail, nameSkuPricingDetail, shortNameUOMSkuPricingDetail,
                   quantityPricingDetail, pricePricingDetail, sumPricingDetail
    PROPERTIES (d) BACKGROUND backgroundRetailPricing(p) retailMarkupPricingDetail, retailMarkupSumPricingDetail, numberRetailVATPricingDetail, valueRetailVATPricingDetail, retailVATSumPricingDetail,
                   retailPricePricingDetail, retailSumPricingDetail
    PROPERTIES (d) nameDepartmentStorePricingDetail

    FILTERS pricingPricingDetail(d) == p

    DIALOG pricing OBJECT p
;
@extendFormFilterAccess(pricing, p, pricings, departmentStore);

DESIGN pricings FROM DEFAULT {
    PROPERTY (delete(p)) {
        askConfirm = TRUE;
    }

    NEW documentContainer BEFORE functions.box {
        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        ADD p.box;

        NEW documentDetail {
            type = TABBED;

            ADD d.box {
                title = 'Спецификация';
            }
            NEW documentHistory {
                title = 'История';

                ADD p.historyGroup;
                ADD p.postedGroup;
            }
            NEW printTab {
                title = 'Печатные формы';
                NEW printContainer {
                    title = 'Печать';
                    childConstraints = TO THE BOTTOM;
                    fillVertical = 1.0; // todo : иначе кнопка не всегда показывается, нужно будет пофиксить как-нибудь
                    ADD p.printGroup;
                }
            }
        }
    }
}

NAVIGATOR {
    retailNavigator {
        NEW pricingNavigator 'Учет в розничных ценах' {
            ADD pricings;
            ADD pricingCommitteeDialog;
        }
    }
}

// ---------------------------- Создание PriceList ----------------------------------- //

EXTEND CLASS systemLedgerPriceListType {
    supplierPricingPriceListType 'Поставщика (расценка)',
    retailPricingPriceListType 'Розничная (расценка)'
}
includeVATSystemLedgerPriceListType (priceListType) += TRUE WHEN priceListType == systemLedgerPriceListType.retailPricingPriceListType;

EXTEND CLASS pricingDetail : priceListLedger;

fromDateTimePriceListLedger (ledger) += dateTimePricingDetail(ledger);

isPostedPriceListLedger(ledger) += isPostedPricingDetail(ledger);
skipPriceListLedger(ledger) += skipChangeLedgerPricingDetail(ledger);

skuPriceListLedger (ledger) += skuPricingDetail(ledger);

descriptionPriceListLedger (ledger) += descriptionPricingDetail(ledger);

pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += pricePricingDetail(ledger) WHEN CLASS(pricePricingDetail(ledger)) AND type == systemLedgerPriceListType.supplierPricingPriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += retailPricePricingDetail(ledger) WHEN CLASS(retailPricePricingDetail(ledger)) AND type == systemLedgerPriceListType.retailPricingPriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += retailPricePricingDetail(ledger) WHEN CLASS(retailPricePricingDetail(ledger)) AND type == systemLedgerPriceListType.accountPriceListType;
inPriceListLedgerStock (ledger, stock) += departmentStorePricingDetail(ledger) == stock;

// ------------------------------------- Печать ценников ------------------------------ //

@Label.implementPriceTransactionDocument(pricing);
@defineDocumentLabelTransaction(pricing, sku, departmentStore);
@extendFormDocumentLabelTransaction(pricings, p, pricing);

// ------------------------------------- Загрузка в оборудование ------------------------------ //

@Machinery.implementPriceTransactionDocument(pricing);
@defineDocumentMachineryPriceTransaction(pricing, sku, departmentStore);
@extendFormDocumentMachineryPriceTransaction(pricings, p, pricing);

// ------------------------------------- Автоматическое создание расценки для документа -------------------- //

//-- аггр.объект
META definePricingAggregation(concrete, sign, caption, captionPost, stockProp, prefix)

    @defineDocumentInterfaceHeaderPricingCommittee(invoice, stockProp);
    @defineDocumentInterfaceDetailPricePrefix(invoice, retail, ' розничная');
    @defineDocumentInterfaceDetailDataSumPrefix (invoice, retail, ' розничная');

    @defineDocumentInterfaceDetailVAT(invoice, country###stockProp, retail, ' розничный');
    @defineDocumentInterfaceDetailVATDataSumPrefix (invoice, retail, ' розничная');

    @defineDocumentInterfaceDetailMarkupPrefix (invoice, retail, );
    @defineDocumentInterfaceDetailMarkupSumPrefix (invoice, retail);
    @defineDocumentInterfaceDetailQuantityPrefix (invoice, pricing, ' (расценено)');
    extraPricingQuantityUserInvoiceDetail = ABSTRACT NUMERIC[14,3] (userInvoiceDetail) PERSISTENT;
    pricingQuantityUserInvoiceDetail(detail) <- quantityUserInvoiceDetail(detail) (+) extraPricingQuantityUserInvoiceDetail(detail)
        WHEN CHANGED(quantityUserInvoiceDetail(detail)) OR CHANGED(extraPricingQuantityUserInvoiceDetail(detail));

    @deriveDocumentDetailValueVAT(userInvoice, retail);
    @deriveDocumentDetailSumPrefix(userInvoice, retail, currency, pricingQuantity);
    @deriveDocumentDetailReverseVATSumPrefix(userInvoice, retail, retail);
    @deriveDocumentDetailMarkupSumPrefix(userInvoice, retail, retail, );

    @changeDocumentDetailMarkupCustomPrefix(userInvoiceDetail, retail, , retail, retail);
    @changeDocumentDetailPriceCustomPrefix(userInvoiceDetail, retail, , retail, retail, currency);

    @defineDocumentInterfaceHeaderSumPrefix (invoice, retail, ' розничная');

    @deriveDocumentDetailVAT(userInvoice, retail, date, sku, stockProp);

    @defineDocumentInterfaceCreate (invoice, createPricing, 'Создать акт расценки');
    @deriveDocumentHeaderCreate(userInvoice, createPricing, departmentStore, stockProp);

    showCreatePricingInvoice (invoice) = stockProp###invoice(invoice) IS departmentStore AND NOT costLedgerDepartmentStore(stockProp###invoice(invoice));
    showPricingInvoice (invoice) = showCreatePricingInvoice (invoice) AND NOT createPricingInvoice(invoice);
    toShowPricingInvoice (invoice) = showCreatePricingInvoice (invoice) AND createPricingInvoice(invoice);

    backgroundRetailInvoice 'Цвет' (invoice) = RGB(224, 255, 255) IF invoice IS invoice;
    EXTEND FORM userInvoice
        PROPERTIES(i) SHOWIF showCreatePricingInvoice(i) BACKGROUND backgroundRetailInvoice(i) createPricingUserInvoice
        PROPERTIES(i) SHOWIF createPricingUserInvoice(i) BACKGROUND backgroundRetailInvoice(i) namePricingCommitteeUserInvoice,
                      retailSumUserInvoiceDetailUserInvoice

        PROPERTIES(d) BEFORE name###stockProp###userInvoiceDetail(d) SHOWIF createPricingInvoice(i) BACKGROUND backgroundRetailInvoice(i)
                      retailMarkupUserInvoiceDetail ON CHANGE EXEC changeRetailMarkupUserInvoiceDetail(d), retailMarkupSumUserInvoiceDetail, numberRetailVATUserInvoiceDetail,
                      valueRetailVATUserInvoiceDetail, retailVATSumUserInvoiceDetail,
                      retailPriceUserInvoiceDetail ON CHANGE EXEC changeRetailPriceUserInvoiceDetail(d),
                      retailSumUserInvoiceDetail
        PROPERTIES(d) pricingQuantityUserInvoiceDetail AFTER quantityUserInvoiceDetail SHOWIF toShowPricingInvoice(i) BACKGROUND backgroundRetailInvoice(i)

        HINTTABLE LIST retailMarkupUserInvoiceDetail, valueRetailVATUserInvoiceDetail, retailPriceUserInvoiceDetail
    ;
    EXTEND DESIGN userInvoice {
        headerRow14 {
            NEW headerRow141 {
                title = 'Расценка';
                childConstraints = TO THE RIGHTBOTTOM;
                ADD PROPERTY(createPricingUserInvoice);
                ADD PROPERTY(namePricingCommitteeUserInvoice);
            }
        }
    }
    EXTEND FORM invoices
        PROPERTIES(i) READONLY BACKGROUND backgroundRetailInvoice(i) createPricingInvoice, retailSumInvoiceDetailInvoice BEFORE ordersInvoice(i)
        PROPERTIES(d) READONLY BEFORE name###stockProp###invoiceDetail(d) SHOWIF createPricingInvoice(i) BACKGROUND backgroundRetailInvoice(i)
                      retailMarkupInvoiceDetail, retailMarkupSumInvoiceDetail, numberRetailVATInvoiceDetail,
                      valueRetailVATInvoiceDetail, retailVATSumInvoiceDetail,
                      retailPriceInvoiceDetail, retailSumInvoiceDetail
        PROPERTIES(d) READONLY BACKGROUND backgroundRetailInvoice(i) pricingQuantityInvoiceDetail AFTER quantityInvoiceDetail  SHOWIF toShowPricingInvoice(i)
    ;


    CLASS concrete###pricing caption###sign : pricing;
    CLASS concrete###pricingPosted captionPost###sign : concrete###pricing, postedObject;
    CLASS concrete###pricingDetail caption###sign : pricingDetail;

    @defineDocumentTables(concrete###pricing);

    needToPricingInvoiceDetail (invoiceDetail) = createPricingInvoiceDetail(invoiceDetail) AND isStockSkuInvoiceDetail(invoiceDetail) PERSISTENT;

    needToPricingInvoice (invoice)= GROUP SUM 1 IF needToPricingInvoiceDetail(invoiceDetail)
        BY invoiceInvoiceDetail(invoiceDetail) PERSISTENT;

    @defineDocumentAggregation(invoice, concrete###pricing, needToPricingInvoice);


    printPricingInvoice 'Акт расценки' (invoice) = printPricing(concrete###pricingInvoice(invoice)) IMAGE 'print.png' IN printGroup;
    EXTEND FORM invoices
        PROPERTIES(i) FORCE PANEL printPricingInvoice SHOWIF createPricingInvoice(i)
    ;
    EXTEND DESIGN invoices {printContainer { ADD i.printGroup;}}

    pricingPricingDetail(detail) += concrete###pricing###concrete###pricingDetail(detail);
    @defineDocumentDetailIndex(concrete###pricing);

    datePricing(pricing) += date###concrete###pricing(pricing);
    timePricing(pricing) += time###concrete###pricing(pricing);

    @defineDocumentAggregationStockPrefix(invoice, concrete###pricing, stockProp, 'Склад', , );
    departmentStorePricing(pricing) += stockProp###concrete###pricing(pricing) IF stockProp###concrete###pricing(pricing) IS departmentStore;
    dataDepartmentStorePricingDetail(pricingDetail) += data###stockProp###invoiceDetail(invoiceDetail###concrete###pricingDetail(pricingDetail))
                                                       IF data###stockProp###invoiceDetail(invoiceDetail###concrete###pricingDetail(pricingDetail)) IS departmentStore;

    @defineDocumentAggregationStockPrefix(invoice, concrete###pricing, supplier, 'Поставщик', , );
    supplierPricing(pricing) += supplier###concrete###pricing(pricing);

    @defineDocumentAggregationPosted(invoice, concrete###pricing);
    isPostedPricing(pricing) += isPosted###concrete###pricing(pricing);

    number###concrete###pricing 'Номер документа' (concrete###pricing) = numberInvoice(invoice###concrete###pricing(concrete###pricing));
    numberPricing(pricing) += number###concrete###pricing(pricing);

    series###concrete###pricing 'Серия документа' (concrete###pricing) = seriesInvoice(invoice###concrete###pricing(concrete###pricing));
    seriesPricing(pricing) += series###concrete###pricing(pricing);

    seriesNumber###concrete###pricing 'Серия/номер документа' (concrete###pricing) = seriesNumberInvoice(invoice###concrete###pricing(concrete###pricing));

    noteInvoice###concrete###pricing 'Примечание' (concrete###pricing) = noteInvoice(invoice###concrete###pricing(concrete###pricing));
    notePricing(pricing) += noteInvoice###concrete###pricing(pricing);

    currency###concrete###pricing  (concrete###pricing) = currencyInvoice(invoice###concrete###pricing(concrete###pricing));
    currencyPricing (pricing) += currency###concrete###pricing(pricing);
    pricingCommitteePricing(pricing) += pricingCommitteeInvoice(invoice###concrete###pricing(pricing));

    @defineDocumentDescription(concrete###pricing, concrete###pricingDetail, seriesNumber###concrete###pricing, caption###sign);
    descriptionPricing (pricing) += description###concrete###pricing(pricing);

    @defineDocumentAggregationDetailSku(invoice, concrete###pricing, sku);
    skuPricingDetail(pricingDetail) +=  sku###concrete###pricingDetail(pricingDetail);

    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, pricingQuantity, 'Кол-во (расценено)');
    quantityPricingDetail(pricingDetail) += pricingQuantity###concrete###pricingDetail(pricingDetail);

//----------------------------------------------------------------------------------------------------------
    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, prefix###price, 'Цена поставщика');
    pricePricingDetail(pricingDetail) += prefix###price###concrete###pricingDetail(pricingDetail);
    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, prefix###sum, 'Сумма поставщика');
    sumPricingDetail(pricingDetail) += prefix###sum###concrete###pricingDetail(pricingDetail);

    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, retailPrice, 'Розничная цена');
    retailPricePricingDetail(pricingDetail) += retailPrice###concrete###pricingDetail(pricingDetail);
    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, retailSum, 'Сумма розничная');
    retailSumPricingDetail(pricingDetail) += retailSum###concrete###pricingDetail(pricingDetail);

    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, retailMarkup, 'Надбавка');
    retailMarkupPricingDetail(pricingDetail) += retailMarkup###concrete###pricingDetail(pricingDetail);
    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, retailMarkupSum, 'Сумма надбавки');
    retailMarkupSumPricingDetail(pricingDetail) += retailMarkupSum###concrete###pricingDetail(pricingDetail);

    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, retailVAT, 'НДС');
    retailVATPricingDetail(pricingDetail) += retailVAT###concrete###pricingDetail(pricingDetail);
    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, valueRetailVAT, 'НДС,%');
    valueRetailVATPricingDetail(pricingDetail) += valueRetailVAT###concrete###pricingDetail(pricingDetail);
    @defineDocumentAggregationDetailProperty (invoice, concrete###pricing, retailVATSum, 'Сумма НДС');
    retailVATSumPricingDetail(pricingDetail) += retailVATSum###concrete###pricingDetail(pricingDetail);

END
//--  Связь накладной и расценки
META definePricingRelation(sign, stockProp)

    invoiceDetailPricingDetail = ABSTRACT invoiceDetail (pricingDetail) PERSISTENT;
    invoiceDetailUserPricingDetail = DATA invoiceDetail (userPricingDetail);
    invoiceDetailPricingDetail(pricingDetail) += invoiceDetailUserPricingDetail(pricingDetail);

    CONSTRAINT supplierPricingDetail(detail) != supplierInvoiceDetail(invoiceDetailUserPricingDetail(detail)) OR
               departmentStorePricingDetail(detail) != stockProp###invoice(invoiceDetailUserPricingDetail(detail)) OR
               skuUserPricingDetail(detail) != skuInvoiceDetail(invoiceDetailUserPricingDetail(detail))
        CHECKED BY invoiceDetailUserPricingDetail
            MESSAGE 'Поставщик, покупатель и товар в накладной и акте расценки должны соответствовать друг другу';

    descriptionIndexInvoiceDetailPricingDetail 'Строка накладной' (detail) = descriptionIndexInvoiceDetail(invoiceDetailPricingDetail(detail));
    descriptionIndexInvoiceDetailUserPricingDetail 'Строка накладной' (detail) = descriptionIndexInvoiceDetail(invoiceDetailUserPricingDetail(detail));

    quantityPricingDetailInvoicePricing (invoice, pricing) = GROUP SUM quantityPricingDetail(pricingDetail) BY invoiceInvoiceDetail(invoiceDetailPricingDetail(pricingDetail)), pricingPricingDetail(pricingDetail);

    invoicesPricing 'Накладные' (pricing) = GROUP CONCAT toString255(descriptionInvoice(invoice)) IF quantityPricingDetailInvoicePricing(invoice, pricing) , ', '
                                                    BY pricing
                                                    ORDER invoice IN invoiceGroup MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;

    pricingInvoiceDetail 'Кол-во (расценено)' (invoiceDetail) = GROUP SUM quantityPricingDetail(pricingDetail) IF isPostedPricingDetail(pricingDetail)
                                                                       BY invoiceDetailPricingDetail(pricingDetail) PERSISTENT;

    toPricingInvoiceDetail 'Не расценено' (invoiceDetail) = quantityInvoiceDetail (invoiceDetail) (-) pricingInvoiceDetail(invoiceDetail);

    toPricingInvoiceDetailStockInvoice 'Не расценено по отделу' (stock, invoice) =
        GROUP SUM toPricingInvoiceDetail(invoiceDetail) IF toPricingInvoiceDetail(invoiceDetail) > 0
              BY stockProp###invoiceDetail(invoiceDetail), invoiceInvoiceDetail(invoiceDetail);

    // Создание расценки на основе инвойса //

    FORM pricingInvoices 'Накладные'###sign
        OBJECTS s = legalEntity FIXED PANEL
        PROPERTIES (s) READONLY name
        OBJECTS c = stock FIXED PANEL
        PROPERTIES (c) READONLY name

        OBJECTS i = invoice
        PROPERTIES (i) READONLY isPostedInvoice FORCE GRID, objectClassName, numberInvoice, seriesInvoice, dateInvoice, timeInvoice,
                                nameSupplierInvoice, nameSupplierStockInvoice, nameCustomerInvoice, nameCustomerStockInvoice, nameCurrencyInvoice, noteInvoice,
                                numberContractSkuInvoice, isCommissionInvoice,
                                countInvoiceDetailInvoice, quantityInvoiceDetailInvoice, sumInvoiceDetailInvoice,
                                VATSumInvoiceDetailInvoice, invoiceSumInvoiceDetailInvoice, ordersInvoice
        FILTERS supplierInvoice(i) == s,
                stockProp###invoice(i) == c,
                isPostedInvoice(i)
        FILTERGROUP invoice
            FILTER 'Накладные с нерасцененными товарами' 'F10' toPricingInvoiceDetailStockInvoice(c,i) DEFAULT
            FILTER 'Накладные со склада' 'F9' countInvoiceDetailStockInvoice(c,i)

        OBJECTS d = invoiceDetail
        PROPERTIES (d) READONLY indexInvoiceDetail, idBarcodeSkuInvoiceDetail, nameSkuInvoiceDetail, shortNameUOMSkuInvoiceDetail,
                       quantityInvoiceDetail, pricingInvoiceDetail, priceInvoiceDetail, sumInvoiceDetail,
                       name###stockProp###invoiceDetail, descriptionOrderInvoiceDetail, toPricingInvoiceDetail
        FILTERS invoiceInvoiceDetail(d) == i
        FILTERGROUP invoiceDetail
            FILTER 'Строки с нерасцененными товарами' 'F10' stockProp###invoiceDetail(d) == c AND toPricingInvoiceDetail(d) > 0 DEFAULT
            FILTER 'Строки со склада' 'F9' stockProp###invoiceDetail(d) == c

    ;

    DESIGN pricingInvoices FROM DEFAULT {
        main {
            preferredSize = (1024, 768);
            NEW documentContainer BEFORE functions.box {

                childConstraints = TO THE BOTTOM;

                NEW headerBox {
                    childConstraints = TO THE RIGHT;
                    ADD s.box { title = 'Поставщик';}
                    ADD c.box { title = 'Склад';}
                }
                ADD i.box;
                NEW documentDetail {
                    type = TABBED;

                    ADD d.box {
                        title = 'Спецификация';
                    }
                    NEW printTab {
                        title = 'Печатные формы';
                        NEW printContainer {
                            title = 'Печать';
                            childConstraints = TO THE BOTTOM;
                            fillVertical = 1.0;
                        }
                    }
                }
            }
            PROPERTY(toPricingInvoiceDetail) { background = #FFFFCC; }
        }
    }

    fillInvoiceUserPricing 'Заполнить на основе накладной' =  ACTION (userPricing) {
        FORM pricingInvoices OBJECTS s = supplierUserPricing(userPricing), c = departmentStoreUserPricing(userPricing) MODAL;
        IF formResult() == formResult.ok THEN {
            LOCAL saleInvoice = invoice();
            SET saleInvoice() <- chosenObject('i');

            FOR invoiceInvoiceDetail(invoiceDetail) == saleInvoice() AND
                stockProp###invoiceDetail(invoiceDetail) == departmentStoreUserPricing(userPricing) AND
                toPricingInvoiceDetail(invoiceDetail) > 0
                ADDOBJ d = userPricingDetail DO {
                SET userPricingUserPricingDetail(d) <- userPricing;
                SET invoiceDetailUserPricingDetail(d) <- invoiceDetail;

                SET skuUserPricingDetail(d) <- skuInvoiceDetail(invoiceDetail);
                SET quantityUserPricingDetail (d) <- toPricingInvoiceDetail(invoiceDetail);
                SET priceUserPricingDetail (d) <- priceInvoiceDetail(invoiceDetail);

                SET retailVATUserPricingDetail(d) <- VATInvoiceDetail(invoiceDetail);
                SET valueRetailVATUserPricingDetail(d) <- valueVATInvoiceDetail(invoiceDetail);
            }
        }
    } IN invoiceGroup;

    EXTEND FORM userPricing
        PROPERTIES(p) fillInvoiceUserPricing, invoicesPricing READONLY
        PROPERTIES(d) descriptionIndexInvoiceDetailUserPricingDetail BEFORE delete(d)
    ;
    EXTEND DESIGN userPricing { headerColumn2 { ADD p.invoiceGroup; } }

    EXTEND FORM pricings
        PROPERTIES(p) READONLY invoicesPricing
        PROPERTIES(d) READONLY descriptionIndexInvoiceDetailPricingDetail
    ;

    EXTEND FORM userInvoice
            PROPERTIES(d) READONLY pricingInvoiceDetail AFTER quantityUserInvoiceDetail SHOWIF showPricingInvoice(i) BACKGROUND backgroundRetailInvoice(i)
    ;
    EXTEND FORM invoices
            PROPERTIES(d) READONLY BACKGROUND backgroundRetailInvoice(i) pricingInvoiceDetail AFTER quantityInvoiceDetail  SHOWIF showPricingInvoice(i)
    ;
    EXTEND FORM pricingInvoices
        PROPERTIES(i) READONLY createPricingInvoice, retailSumInvoiceDetailInvoice BEFORE ordersInvoice(i)
        PROPERTIES(d) READONLY BEFORE name###stockProp###invoiceDetail(d) SHOWIF createPricingInvoice(i)
                      retailMarkupInvoiceDetail, retailMarkupSumInvoiceDetail, numberRetailVATInvoiceDetail,
                      valueRetailVATInvoiceDetail, retailVATSumInvoiceDetail,
                      retailPriceInvoiceDetail, retailSumInvoiceDetail
    ;

    invoiceDetailPricingDetail(pricingDetail) += invoiceDetailInvoicePricingDetail(pricingDetail);
END
