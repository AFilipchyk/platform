MODULE Customer;

REQUIRE System,
        Historizable,
        Utils,
        LegalEntity,
        Stock,
        Contract,
        Agreement;


// ---------------------------------------- Договоры ------------------------------------ //

CLASS customer 'Оптовый покупатель' : legalEntity;
TABLE customer (customer);

CLASS contractSkuCustomer 'Договор с опт. покупателем' : contractSku;

companyContractSkuCustomer = DATA company (contractSkuCustomer) AUTOSET;
nameCompanyContractSkuCustomer 'Продавец' (contractSkuCustomer) = name(companyContractSkuCustomer(contractSkuCustomer)) IN recognizeGroup MAXCHARWIDTH 30 PREFCHARWIDTH 30;
partyAContract (contract) += companyContractSkuCustomer(contract);

customerContractSkuCustomer = DATA customer (contractSkuCustomer) AUTOSET;
nameCustomerContractSkuCustomer 'Покупатель' (contractSkuCustomer) = name(customerContractSkuCustomer(contractSkuCustomer)) IN recognizeGroup MAXCHARWIDTH 30 PREFCHARWIDTH 30;
partyBContract (contract) += customerContractSkuCustomer(contract);

TABLE companyCustomer(company, customer);
@defineContractDefault(contractSkuCustomer, company, customer);

// ---------------------------------------- Группы покупателей -------------------------------- //

CLASS customerGroup 'Группа покупателей' : named, legalEntityGroup;

customerGroupCustomer (customer) = DATA customerGroup (customer) AUTOSET NOT NULL;
nameCustomerGroupCustomer 'Группа покупателей' (customer) = name(customerGroupCustomer(customer));

@defineHierarchy(customerGroup);
parentLegalEntityGroup(customerGroup) += parentCustomerGroup(customerGroup);
legalEntityGroupLegalEntity(customer) += customerGroupCustomer(customer);

inCustomerGroupCustomer (customerGroup, customer) =
    UNION OVERRIDE customer IS customer AND NOT customerGroup IS customerGroup, isParentCustomerGroupCustomerGroup(customerGroupCustomer(customer), customerGroup);

FORM customerGroup 'Группа покупателей'
    OBJECTS s = customerGroup FIXED PANEL
    PROPERTIES(s) name, nameParentLegalEntityGroup

    EDIT customerGroup OBJECT s
;

FORM customerGroups 'Группы покупателей'
    TREE customerGroupTree sg = customerGroup PARENT parentCustomerGroup
    PROPERTIES READONLY sgTreeName = name(sg)
    ORDER BY name(sg)

    DIALOG customerGroup OBJECT sg
;

// Формы
FORM contractSkuCustomer 'Договор с опт. покупателем'
    OBJECTS c=contractSkuCustomer FIXED PANEL
    PROPERTIES(c) nameCompanyContractSkuCustomer, nameCustomerContractSkuCustomer,
                  numberContract, dateFromContract, dateToContract,
                  nameCurrencyContract, noteContract

    OBJECTS pc = paymentCondition
    PROPERTIES(pc) READONLY datePaymentCondition, numberContractPaymentCondition, nameTypePaymentCondition,
                            nameFormPaymentCondition
    PROPERTIES(pc) ADDSESSIONFORM, EDITSESSIONFORM, delete
    ORDER BY datePaymentCondition

    FILTERS contractPaymentCondition(pc) == c

    EDIT contractSkuCustomer OBJECT c
;

FORM contractsSkuCustomer 'Договора'
    OBJECTS c=contractSkuCustomer
    PROPERTIES(c) READONLY numberContract, dateFromContract, dateToContract,
                           nameCompanyContractSkuCustomer, nameCustomerContractSkuCustomer

    PROPERTIES(c) ADDFORM, EDITFORM, delete
    ORDER BY dateFromContract

    DIALOG contractSkuCustomer OBJECT c
;

FORM customer 'Оптовый покупатель'
    OBJECTS cu=customer FIXED PANEL
    PROPERTIES(cu) name, nameCustomerGroupCustomer, nameOwnershipLegalEntity,
                   shortNameOwnershipLegalEntity, fullNameLegalEntity,
                   addressLegalEntity ON CHANGE EXEC dialogAddressLegalEntity(cu),
                   postAddressLegalEntity ON CHANGE EXEC dialogPostAddressLegalEntity(cu),
                   managerLegalEntity ON CHANGE EXEC dialogManagerLegalEntity(cu),
                   accountantLegalEntity ON CHANGE EXEC dialogAccountantLegalEntity(cu),
                   phoneLegalEntity ON CHANGE EXEC dialogPhoneLegalEntity(cu),
                   emailLegalEntity, siteLegalEntity

    OBJECTS a=account
    PROPERTIES(a)  numberAccount, nameBankAccount, addressBankAccount, departmentBankAccount, CBUBankAccount, MFOBankAccount, noteAccount, ADDOBJ, delete
    FILTERS legalEntityAccount(a) == cu

    OBJECTS co=company
    PROPERTIES(co)      name
    PROPERTIES(co, cu)   numberContractSkuCustomerCompanyCustomer

    OBJECTS c=contractSkuCustomer
    PROPERTIES(c)  READONLY numberContract, nameCompanyContractSkuCustomer, nameTypeContractSku, nameFormContractSku, dateFromContract,
                   dateToContract, nameCurrencyContract, noteContract
    PROPERTIES(c)  ADDFORM, EDITFORM, delete
    FILTERS companyContractSkuCustomer(c) == co,
            customerContractSkuCustomer(c) == cu

    OBJECTS ag=agreement
    PROPERTIES(ag, cu) isDefaultAgreementLegalEntity, inAgreementLegalEntityOver
    PROPERTIES(ag) READONLY name, numberObject, seriesObject, dateAgreement, timeAgreement,
                            fromDateAgreement, fromTimeAgreement, toDateAgreement, toTimeAgreement,
                            nameCurrencyAgreement, namePriceListTypeAgreement, noteAgreement
    PROPERTIES(cu) addAgreementsLegalEntity TODRAW ag FORCE PANEL DRAWTOTOOLBAR
    FILTERS inAgreementLegalEntityOver(ag, cu)

    FILTERGROUP filters
        FILTER 'Есть договор' 'F10' countContractPartyAPartyB(co, cu) DEFAULT

    EDIT customer OBJECT cu
;

DESIGN customer FROM DEFAULT {
    main{
       preferredSize = (1024, 768);
       cu.box {
            childConstraints = TO THE RIGHT;

            NEW column1 {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(name(cu));
                ADD PROPERTY(nameCustomerGroupCustomer);
                ADD cu.lawGroup;
            }

            NEW column2 {
                childConstraints = TO THE BOTTOM;
                ADD cu.contactGroup;
                ADD cu.managementGroup;
            }
       }

       NEW extendContainer BEFORE functions.box {
            type = TABBED;
            ADD a.box;
            NEW customerContainer {
                title = 'Договоры с поставщиками';
                type = SPLITH;
                childConstraints = TO THE RIGHT;

                ADD co.box;
                ADD c.box {
                    fillHorizontal = 2;
                    c.grid { defaultComponent = TRUE; }
                }
            }
            ADD ag.box{
                caption = 'Соглашения';
            }
       }
   }
}

FORM customers 'Оптовые покупатели'
    TREE customerGroupTree cg = customerGroup PARENT parentCustomerGroup
    PROPERTIES READONLY cgTreeName = name(cg)
    PROPERTIES(cg) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    OBJECTS cu=customer
    PROPERTIES(cu) READONLY name, fullNameLegalEntity, nameCustomerGroupCustomer, shortNameOwnershipLegalEntity, numberAccountLegalEntity, addressLegalEntity, phoneLegalEntity
    PROPERTIES(cu) ADDFORM, EDITFORM, delete

    FILTERS inCustomerGroupCustomer(cg, cu)
;

DESIGN customers FROM DEFAULT{
    NEW topContainer{
        childConstraints = TO THE RIGHT;
        type = SPLITH;
        ADD customerGroupTree.tree.box{
            PROPERTY(delete(cg)){
                askConfirm = TRUE;
            }
        }
        ADD cu.box{
            fillHorizontal = 2.0;
        }
    }
    ADD functions.box;
}

// --------------------------------------------------- Макросы по добавлению покупателя в документы ------------------------------------ //
META defineDocumentHeaderCustomer (object)
    customer###object (object) = DATA customer (object);
    nameCustomer###object 'Покупатель' (object)= name(customer###object(object)) IN documentPrmGroup MINCHARWIDTH 10 PREFCHARWIDTH 20;
END
META defineDocumentAbstractHeaderCustomer (object)
    customer###object (object) = ABSTRACT customer (object);
    nameCustomer###object 'Покупатель' (object)= name(customer###object(object)) IN documentPrmGroup MINCHARWIDTH 10 PREFCHARWIDTH 20;
END
META defineDocumentInterfaceHeaderCustomer (object)
    @defineDocumentAbstractHeaderCustomer(object);
    @defineDocumentHeaderCustomer(user###object);
    customer###object (object) += customer###user###object(object);
END

META defineDocumentDetailCustomer (object, detail)
    customer###detail (idetail) = customer###object(object###detail(idetail));
    nameCustomer###detail 'Покупатель' (idetail) = nameCustomer###object(object###detail(idetail));
END

META defineDocumentCustomer (object, detail)
    @defineDocumentHeaderCustomer(object);
    @defineDocumentDetailCustomer(object, detail);
END
META defineDocumentDetailDataCustomer (object, detail)
    dataCustomer###detail (detail) = DATA customer (detail);
    customer###detail (idetail) = UNION OVERRIDE customer###object(object###detail(idetail)), dataCustomer###detail(idetail) PERSISTENT;
    nameCustomer###detail 'Покупатель' (idetail) = name(customer###detail(idetail));
END
META defineDocumentDataCustomer (object, detail)
    @defineDocumentHeaderCustomer(object);
    @defineDocumentDetailDataCustomer(object, detail);
END
META defineDocumentDataCustomer (object)
    @defineDocumentDataCustomer(object, object##Detail);
END
META defineDocumentDetailCustomer(detail)
    customer###detail (detail) = DATA customer (detail);
    nameCustomer###detail 'Покупатель' (detail) = name(customer###detail (detail));
END

META defineDocumentAbstractCustomer (object, detail)
    @defineDocumentAbstractHeaderCustomer(object);
    @defineDocumentDetailCustomer(object, detail);
END
META defineDocumentInterfaceCustomer (object, detail)
    @defineDocumentInterfaceHeaderCustomer(object);
    @defineDocumentDetailCustomer(object, detail);
    @defineDocumentDetailCustomer(user###object, user###detail);
END

META defineDocumentCustomer (object)
    @defineDocumentCustomer(object, object##Detail);
END
META defineDocumentAbstractCustomer (object)
    @defineDocumentAbstractCustomer(object, object##Detail);
END
META defineDocumentInterfaceCustomer (object)
    @defineDocumentInterfaceCustomer(object, object##Detail);
END