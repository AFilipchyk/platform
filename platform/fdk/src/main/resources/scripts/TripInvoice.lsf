MODULE TripInvoice;

REQUIRE Order, Shipment, Invoice, Trip;

quantityTripInvoice (trip, invoice) = GROUP SUM quantityOrderInvoice(order, invoice) IF inTripOrder(trip, order) BY trip, invoice;

tripInvoice = DATA trip(invoice);

inTripInvoice 'Включен' (trip, invoice) = tripInvoice(invoice)==trip;
filterTripInvoice(trip, invoice) = tripInvoice(invoice)==trip OR (trip IS trip AND invoice IS invoice AND NOT tripInvoice(invoice));

quantityInvoicedTripSku 'Кол-во в накладных' (trip, sku) = GROUP SUM quantityInvoiceDetail(invoiceDetail) BY tripInvoice(invoiceInvoiceDetail(invoiceDetail)), skuInvoiceDetail(invoiceDetail);

grossWeightInvoicedTrip 'Суммарный вес накладных' (trip) = GROUP SUM grossWeightInvoiceDetailInvoice(invoice) BY tripInvoice(invoice);

createConsignmentTrip 'Создать накладные по заказам' (trip) = ACTION (trip) {
    FOR tripOrder(order) == trip DO {
        EXEC createUserInvoicePostedOrder(order);
        SET tripInvoice(invoice) <- trip IF quantityTripInvoice (trip, invoice);

        EXEC apply();
    };
    EXEC createShipmentsTrip(trip);
    EXEC apply();
} TOOLBAR CONFIRM;


EXTEND FORM trip

    PROPERTIES (t) grossWeightInvoicedTrip BEFORE grossWeightShipmentedTrip(t)

    OBJECTS i=invoice
    PROPERTIES (t, i) inTripInvoice
    PROPERTIES (i) READONLY numberInvoice, seriesInvoice, dateInvoice, nameFromLegalEntityInvoice, nameSupplierStockInvoice, nameToLegalEntityInvoice, nameCustomerStockInvoice

    PROPERTIES quantityInvoicedTripSku(t, sk) AFTER quantityOrderedTripSku

    FILTERGROUP filterDateInvoice
        FILTER 'Текущие' 'F9' dateInvoice(i) <= dateTrip(t) DEFAULT
    FILTERGROUP filterInvoice
        FILTER 'Не расписанные или в текущем рейсе' 'F11' filterTripInvoice(t, i) DEFAULT
        FILTER 'В текущем рейсе' 'F10' inTripInvoice(t, i)

    FILTERS customerInvoice(i) == partyB() OR (i IS invoice AND NOT partyB()),
            supplierInvoice(i) == partyA() OR (i IS invoice AND NOT partyA())

    OBJECTS id=invoiceDetail
    PROPERTIES (id) READONLY idBarcodeSkuInvoiceDetail, nameSkuInvoiceDetail, shortNameUOMSkuInvoiceDetail, quantityInvoiceDetail, grossWeightInvoiceDetail
    FILTERS invoiceInvoiceDetail(id)==i

    OBJECTS i2=invoice
    PROPERTIES(i2) READONLY numberInvoice, seriesInvoice, dateInvoice, nameSupplierStockInvoice, nameCustomerStockInvoice
    PROPERTIES(i2) editInvoice
    PROPERTIES(sk, i2) READONLY quantityInvoiceDetailSkuInvoice
    FILTERS inTripInvoice(t, i2), quantityInvoiceDetailSkuInvoice(sk, i2)>0

    FILTERGROUP filterSku2
        FILTER 'Товары в заказах или накладных' 'F7' quantityOrderedTripSku(t, sk)>0 OR quantityInvoicedTripSku(t, sk)>0 DEFAULT
        FILTER 'Товары в накладных' 'F6' quantityInvoicedTripSku(t, sk)>0
        FILTER 'Товары в заказах и накладных' 'F5' quantityOrderedTripSku(t, sk)>0 AND quantityInvoicedTripSku(t, sk)>0
;

EXTEND DESIGN trip {

    sumContainer {
        ADD PROPERTY(grossWeightInvoicedTrip) AFTER PROPERTY(grossWeightOrderedTrip);
    };

    bottomContainer {
        NEW invoiceContainer AFTER orderContainer{
            caption = 'Накладные';
            ADD i.box;
            ADD id.box;
        };
    }

    documentContainer {
        ADD i2.box AFTER o2.box;
    }
}

EXTEND FORM trips
    PROPERTIES (t) grossWeightInvoicedTrip AFTER grossWeightOrderedTrip(t)
    OBJECTS i=invoice
    PROPERTIES (i) READONLY numberInvoice, seriesInvoice, dateInvoice, nameFromLegalEntityInvoice, nameSupplierStockInvoice, nameToLegalEntityInvoice, nameCustomerStockInvoice
    FILTERS inTripInvoice(t, i)
;

EXTEND DESIGN trips {
    bottomContainer{
        ADD i.box AFTER o.box {
            caption = 'Накладные';
        };
    }
}

EXTEND FORM waybill
    OBJECTS i=invoice
    PROPERTIES (i) seriesNumberObject, dateInvoice, addressSupplierStockInvoice, addressCustomerStockInvoice
    FILTERS inTripInvoice(t, i)
;