MODULE Historizable;

REQUIRE System;

META defineHistorizable(property, caption, type, object, objectIdentity, group)
    data###property##Date caption = DATA type (object, DATE) IN group;

    date###property##Date (object, date) = GROUP MAX dateIn AND data###property##Date(object, dateIn) AND dateIn <= (date AS DATE) BY object, date;

    property##Date caption (object, date) = data###property##Date(object, date###property##Date(object, date)) IN group;
    over###property##Date caption = UNION OVERRIDE property##Date(object, date), data###property##Date(object, date) IN group;

    property caption (object) = property##Date(object, currentDate()) IN group;

    FORM add###property caption
        OBJECTS a=object FIXED PANEL, d=DATE FIXED PANEL
        PROPERTIES objectIdentity(a) READONLY, OBJVALUE(d), data###property##Date(a, d)
    ;
    DESIGN add###property FROM DEFAULT {
        PROPERTY(objectIdentity(a)) { focusable = FALSE; }
    }

    add###property 'Добавить' (object) = ACTION FORM add###property OBJECTS a MODAL TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

    FORM dialog###property caption
        OBJECTS a=object FIXED PANEL, d=DATE
        PROPERTIES objectIdentity(a) READONLY, add###property(a) TODRAW d FORCE PANEL, OBJVALUE(d) READONLY, data###property##Date(a, d)
        FILTERS data###property##Date(a, d)
    ;
    DESIGN dialog###property FROM DEFAULT {
        PROPERTY(objectIdentity(a)) { focusable = FALSE; }
    }

    dialog###property caption (object) = ACTION FORM dialog###property OBJECTS a MODAL SHORTCUT property DEFAULT;
END

// ---------------------------------- Свойство объект-дата для (пример, стат.класса)------------------------------------------ //

META defineHistorizableCustom(property, caption, type, typeIdentity, object, objectIdentity, group)
    data###property##Date caption = DATA type (object, DATE);
    data###typeIdentity###property##Date caption (object, date) = typeIdentity(data###property##Date(object, date)) IN group;

    date###property##Date (object, date) = GROUP MAX dateIn AND data###property##Date(object, dateIn) AND dateIn <= (date AS DATE) BY object, date;

    property##Date caption (object, date) = data###property##Date(object, date###property##Date(object, date)) IN group;

    property caption (object) = property##Date(object, currentDate());
    typeIdentity###property caption (object) = typeIdentity(property(object)) IN group;

    FORM add###property caption
        OBJECTS a=object FIXED PANEL, d=DATE FIXED PANEL
        PROPERTIES objectIdentity(a) READONLY, OBJVALUE(d), data###typeIdentity###property##Date(a, d)
    ;
    DESIGN add###property FROM DEFAULT {
        PROPERTY(objectIdentity(a)) { focusable = FALSE; }
    }

    add###property 'Добавить' (object) = ACTION FORM add###property OBJECTS a MODAL TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

    FORM dialog###property caption
        OBJECTS a=object FIXED PANEL, d=DATE
        PROPERTIES objectIdentity(a) READONLY, add###property(a) TODRAW d FORCE PANEL, OBJVALUE(d) READONLY, data###typeIdentity###property##Date(a, d)
        FILTERS data###property##Date(a, d)
    ;
    DESIGN dialog###property FROM DEFAULT {
        PROPERTY(objectIdentity(a)) { focusable = FALSE; }
    }

    dialog###property caption (object) = ACTION FORM dialog###property OBJECTS a MODAL SHORTCUT typeIdentity###property DEFAULT;
END

// ----------------------------------- Свойство объект1-объект2-дата------------------------------------------ //
META defineHistorizableDouble(property, caption, type, object1, object1Identity, object2, object2Identity, group)
    data###property##Date caption = DATA type (object1, object2, DATE) IN group;

    date###property##Date (object1, object2, date) = GROUP MAX dateIn AND data###property##Date(object1, object2, dateIn) AND dateIn <= (date AS DATE) BY object1, object2, date;

    property##Date caption (object1, object2, date) = data###property##Date(object1, object2, date###property##Date(object1, object2, date));
    over###property##Date caption = UNION OVERRIDE property##Date(object1, object2, date), data###property##Date(object1, object2, date);

    property caption (object1, object2) = property##Date(object1, object2, currentDate()) IN group;

    FORM add###property caption
        OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, d=DATE FIXED PANEL
        PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, OBJVALUE(d), data###property##Date(a, b, d)
    ;
    DESIGN add###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
    }

    add###property 'Добавить' (object1, object2) = ACTION FORM add###property OBJECTS a, b MODAL TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

    FORM dialog###property caption
        OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, d=DATE
        PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, add###property(a, b) TODRAW d FORCE PANEL, OBJVALUE(d) READONLY, data###property##Date(a, b, d)
        FILTERS data###property##Date(a, b, d)
    ;
    DESIGN dialog###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
    }

    dialog###property caption (object1, object2) = ACTION FORM dialog###property OBJECTS a, b MODAL SHORTCUT property DEFAULT;
    overDialog###property##Date caption (object1, object2) = ACTION FORM dialog###property OBJECTS a, b MODAL SHORTCUT over###property##Date;
END
// ----------------------------------- Свойство объект1-объект2-дата ------------------------------------------ //

META defineHistorizableDoubleHeader(property, caption, type, typeIdentity, object1, object2, fixedCharWidth, group)
    data###property##Date caption = DATA type (object1, object2, DATE);
    data###typeIdentity###property##Date caption (object1, object2, date) = typeIdentity(data###property##Date(object1, object2, date)) FIXEDCHARWIDTH fixedCharWidth IN group;

    date###property##Date (object1, object2, date) = GROUP MAX dateIn AND data###property##Date(object1, object2, dateIn) AND dateIn <= (date AS DATE) BY object1, object2, date;
END

META defineHistorizableDoubleFooter(property, caption, typeIdentity, object1, object1Identity, object2, object2Identity, fixedCharWidth, group)
    typeIdentity###property##Date caption (object1, object2, date) = typeIdentity(property##Date(object1, object2, date)) FIXEDCHARWIDTH fixedCharWidth IN group;

    over###property##Date caption = UNION OVERRIDE property##Date(object1, object2, date), data###property##Date(object1, object2, date);

    over###typeIdentity###property##Date caption (object1, object2, date) = typeIdentity(over###property##Date(object1, object2, date)) FIXEDCHARWIDTH fixedCharWidth IN group;

    property caption (object1, object2) = property##Date(object1, object2, date###property##Date(object1, object2, currentDate()));
    typeIdentity###property caption (object1, object2) = typeIdentity(property(object1, object2)) FIXEDCHARWIDTH fixedCharWidth IN group;

    FORM add###property caption
        OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, d=DATE FIXED PANEL
        PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, OBJVALUE(d), data###typeIdentity###property##Date(a, b, d)
    ;
    DESIGN add###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
    }

    add###property 'Добавить' (object1, object2) = ACTION FORM add###property OBJECTS a, b MODAL TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

    FORM dialog###property caption
        OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, d=DATE
        PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, add###property(a, b) TODRAW d FORCE PANEL, OBJVALUE(d) READONLY, data###typeIdentity###property##Date(a, b, d)
        FILTERS data###property##Date(a, b, d)
    ;
    DESIGN dialog###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
    }

    dialog###property caption (object1, object2) = ACTION FORM dialog###property OBJECTS a, b MODAL SHORTCUT typeIdentity###property DEFAULT;
    overDialog###property##Date caption (object1, object2) = ACTION FORM dialog###property OBJECTS a, b MODAL SHORTCUT over###typeIdentity###property##Date;
END

META defineHistorizableDoubleCustom(property, caption, type, typeIdentity, object1, object1Identity, object2, object2Identity, fixedCharWidth, group)
    @defineHistorizableDoubleHeader(property, caption, type, typeIdentity, object1, object2, fixedCharWidth, group);
    property##Date caption (object1, object2, date) = data###property##Date(object1, object2, date###property##Date(object1, object2, date));
    @defineHistorizableDoubleFooter(property, caption, typeIdentity, object1, object1Identity, object2, object2Identity, fixedCharWidth, group);
END

META defineHistorizableDoubleDefaultCustom(property, defaultProperty, caption, type, typeIdentity, object1, object1Identity, object2, object2Identity, fixedCharWidth, group)
    @defineHistorizableDoubleHeader(property, caption, type, typeIdentity, object1, object2, fixedCharWidth, group);
    property##Date caption (object1, object2, date) = (defaultProperty(object1, object2) AND date IS DATE) OR data###property##Date(object1, object2, date###property##Date(object1, object2, date));
    @defineHistorizableDoubleFooter(property, caption, typeIdentity, object1, object1Identity, object2, object2Identity, fixedCharWidth, group);
END

// ----------------------------------- Свойство объект1-объект2-объект3-дата------------------------------------------ //
META defineHistorizableTriple(property, caption, type, object1, object1Identity, object2, object2Identity, object3, object3Identity, group)
    data###property##Date caption = DATA type (object1, object2, object3, DATE) IN group;

    date###property##Date (object1, object2, object3, date) = GROUP MAX dateIn AND data###property##Date(object1, object2, object3, dateIn) AND dateIn <= (date AS DATE) BY object1, object2, object3, date;

    property##Date caption (object1, object2, object3, date) = data###property##Date(object1, object2, object3, date###property##Date(object1, object2, object3, date));
    over###property##Date caption = UNION OVERRIDE property##Date(object1, object2, object3, date), data###property##Date(object1, object2, object3, date);

    property caption (object1, object2, object3) = property##Date(object1, object2, object3, currentDate()) IN group;

    FORM add###property caption
        OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, c=object3  FIXED PANEL, d=DATE FIXED PANEL
        PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, object3Identity(c) READONLY, OBJVALUE(d), data###property##Date(a, b, c, d)
    ;
    DESIGN add###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
        PROPERTY(object3Identity(c)) { focusable = FALSE; }
    }

    add###property 'Добавить' (object1, object2, object3) = ACTION FORM add###property OBJECTS a, b, c MODAL TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

    FORM dialog###property caption
        OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, c=object3 FIXED PANEL, d=DATE
        PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, object3Identity(c) READONLY, add###property(a, b, c) TODRAW d FORCE PANEL, OBJVALUE(d) READONLY, data###property##Date(a, b, c, d)
        FILTERS data###property##Date(a, b, c, d)
    ;
    DESIGN dialog###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
        PROPERTY(object3Identity(c)) { focusable = FALSE; }
    }

    dialog###property caption (object1, object2, object3) = ACTION FORM dialog###property OBJECTS a, b, c MODAL SHORTCUT property DEFAULT;
    overDialog###property##Date caption (object1, object2, object3) = ACTION FORM dialog###property OBJECTS a, b, c MODAL SHORTCUT over###property##Date;
END
// -------------------- Свойство объект1-объект2-объект3-дата ---------------------------- //

META defineHistorizableTripleDefaultCustom(property, defaultProperty, caption, type, typeIdentity, object1, object1Identity, object2, object2Identity, object3, object3Identity, fixedCharWidth, group)
    data###property##Date caption = DATA type (object1, object2, object3, DATE);
    data###typeIdentity###property##Date caption (object1, object2, object3, date) = typeIdentity(data###property##Date(object1, object2, object3, date)) IN group;

    date###property##Date (object1, object2, object3, date) = GROUP MAX dateIn AND data###property##Date(object1, object2, object3, dateIn) AND dateIn <= (date AS DATE) BY object1, object2, object3, date;

    property##Date caption (object1, object2, object3, date) = UNION OVERRIDE defaultProperty(object1, object2, object3) AND date IS DATE,
                                                                             data###property##Date(object1, object2, object3, date###property##Date(object1, object2, object3, date));
    over###property##Date caption = UNION OVERRIDE property##Date(object1, object2, object3, date), data###property##Date(object1, object2, object3, date);
    over###typeIdentity###property##Date caption (object1, object2, object3, date) = typeIdentity(over###property##Date(object1, object2, object3, date)) FIXEDCHARWIDTH fixedCharWidth IN group;

    property caption (object1, object2, object3) = property##Date(object1, object2, object3, currentDate());
    typeIdentity###property caption (object1, object2, object3) = typeIdentity(property(object1, object2, object3)) FIXEDCHARWIDTH fixedCharWidth IN group;

    FORM add###property caption
    OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, c=object3 FIXED PANEL, d=DATE FIXED PANEL
    PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, object3Identity(c) READONLY, OBJVALUE(d), data###typeIdentity###property##Date(a, b, c, d);
    DESIGN add###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
        PROPERTY(object3Identity(c)) { focusable = FALSE; }
    }

    add###property 'Добавить' (object1, object2, object3) = ACTION FORM add###property OBJECTS a, b, c MODAL TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

    FORM dialog###property caption
    OBJECTS a=object1 FIXED PANEL, b=object2 FIXED PANEL, c=object3 FIXED PANEL, d=DATE
    PROPERTIES object1Identity(a) READONLY, object2Identity(b) READONLY, object3Identity(c) READONLY, add###property(a, b, c) TODRAW d FORCE PANEL, OBJVALUE(d) READONLY, data###typeIdentity###property##Date(a, b, c, d)
    FILTERS data###property##Date(a, b, c, d);
    DESIGN dialog###property FROM DEFAULT {
        PROPERTY(object1Identity(a)) { focusable = FALSE; }
        PROPERTY(object2Identity(b)) { focusable = FALSE; }
        PROPERTY(object3Identity(c)) { focusable = FALSE; }
    }

    dialog###property caption (object1, object2, object3) = ACTION FORM dialog###property OBJECTS a, b, c MODAL SHORTCUT typeIdentity###property DEFAULT;
    over###dialog###property##Date caption (object1, object2, object3) = ACTION FORM dialog###property OBJECTS a, b, c MODAL SHORTCUT over###typeIdentity###property##Date;
END

// ----------------------------------- Historizable Ledger -------------------------------------- //

META defineHistorizableLedgerDouble(property, caption, type, key0, key1)
    dateTime###property##Ledger 'Дата/время' (ledger) = ABSTRACT DATETIME (property##Ledger) PERSISTENT INDEXED;
    date###property##Ledger 'Дата' (ledger) = dateInTime(dateTime###property##Ledger(ledger)) PERSISTENT;

    isPosted###property##Ledger 'Закрыт' (ledger) = ABSTRACT BOOLEAN (property##Ledger) PERSISTENT;

    key0###property##Ledger (ledger) = ABSTRACT key0 (property##Ledger) PERSISTENT INDEXED;

    key1###property##Ledger (ledger) = ABSTRACT key1 (property##Ledger) PERSISTENT INDEXED;

    description###property##Ledger 'Описание' (ledger) = ABSTRACT STRING[200] (property##Ledger) PERSISTENT;

    property###property##Ledger caption (ledger) = ABSTRACT type (property##Ledger) PERSISTENT;

    orderT###property##Ledger = LIST(dateTime###property##Ledger(ledger), ledger) PERSISTENT;

    concatLedgerB###property##DateTime (key0, key1, dateTime) = GROUP MAX orderT###property##Ledger(ledger) AND
        isPosted###property##Ledger(ledger) AND dateTime###property##Ledger(ledger) < (dateTime AS DATETIME)
        BY key0###property##Ledger(ledger), key1###property##Ledger(ledger), dateTime;

    ledgerB###property##DateTime (key0, key1, dateTime) = concatLedgerB###property##DateTime(key0, key1, dateTime)[2];

    concatLedgerA###property##DateTime (key0, key1, dateTime) = GROUP MAX orderT###property##Ledger(ledger) AND
        isPosted###property##Ledger(ledger) AND dateTime###property##Ledger(ledger) <= (dateTime AS DATETIME)
        BY key0###property##Ledger(ledger), key1###property##Ledger(ledger), dateTime;

    ledgerA###property##DateTime (key0, key1, dateTime) = concatLedgerA###property##DateTime(key0, key1, dateTime)[2];

    concatLedgerCurrent###property (key0, key1) = GROUP MAX orderT###property##Ledger(ledger) AND
        isPosted###property##Ledger(ledger)
        BY key0###property##Ledger(ledger), key1###property##Ledger(ledger);

    property##BLedgerDateTime caption##' (до)' (key0, key1, dateTime) = property###property##Ledger(ledgerB###property##DateTime(key0, key1, dateTime));
    property##ALedgerDateTime caption##' (после)' (key0, key1, dateTime) = property###property##Ledger(ledgerA###property##DateTime(key0, key1, dateTime));

    orderD###property##Ledger = LIST(date###property##Ledger(ledger), ledger) PERSISTENT;

    concatLedgerB###property##Date (key0, key1, date) = GROUP MAX orderD###property##Ledger(ledger) AND
        isPosted###property##Ledger(ledger) AND date###property##Ledger(ledger) < (date AS DATE)
        BY key0###property##Ledger(ledger), key1###property##Ledger(ledger), date;

    ledgerB###property##Date (key0, key1, date) = concatLedgerB###property##Date(key0, key1, date)[2];

    concatLedgerA###property##Date (key0, key1, date) = GROUP MAX orderD###property##Ledger(ledger) AND
        isPosted###property##Ledger(ledger) AND date###property##Ledger(ledger) <= (date AS DATE)
        BY key0###property##Ledger(ledger), key1###property##Ledger(ledger), date;

    ledgerA###property##Date (key0, key1, date) = concatLedgerA###property##Date(key0, key1, date)[2];

    concatLedgerCurrentDate###property (key0, key1) = GROUP MAX orderD###property##Ledger(ledger) AND
        isPosted###property##Ledger(ledger)
        BY key0###property##Ledger(ledger), key1###property##Ledger(ledger);

    property##BLedgerDate caption##' (до)' (key0, key1, date) = property###property##Ledger(ledgerB###property##Date(key0, key1, date));
    property##ALedgerDate caption##' (после)' (key0, key1, date) = property###property##Ledger(ledgerA###property##Date(key0, key1, date));

    currentLedger###property (key0, key1) = concatLedgerCurrent###property(key0, key1)[2];
    current###property##Ledger caption (key0, key1) = property###property##Ledger(currentLedger###property(key0, key1));
END

META implementHistorizableLedgerDouble(property, key0, key1, concrete, key0Prop, key1Prop, propertyProp)
    dateTime###property##Ledger (ledger) += dateTime###concrete##Detail(ledger);
    isPosted###property##Ledger (ledger) += isPosted###concrete##Detail(ledger);
    key0###property##Ledger (ledger) += key0Prop###concrete##Detail(ledger);
    key1###property##Ledger (ledger) += key1Prop###concrete##Detail(ledger);
    description###property##Ledger (ledger) += description###concrete##Detail(ledger);
    property###property##Ledger (ledger) += propertyProp###concrete##Detail(ledger);
END
