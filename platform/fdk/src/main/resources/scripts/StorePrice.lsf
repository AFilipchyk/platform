MODULE StorePrice;

REQUIRE System, Historizable, Tax, Stock, Barcode, Store, Ware;

GROUP priceDetailGroup 'Цены' : publicGroup;

META defineDocumentDetailInPricePrefix (object, prefix, caption)
    prefix###importerPrice###object##Detail 'Цена изготовителя/импортера'###caption = DATA NUMERIC[14,2] (object##Detail) IN priceDetailGroup;
    prefix###supplierPrice###object##Detail 'Цена поставщика без НДС'###caption = DATA NUMERIC[14,2] (object##Detail) IN priceDetailGroup;
END
META defineDocumentDetailInPrice (object)
    @defineDocumentDetailInPricePrefix(object,,);
END

META defineDocumentDetailInvoicePrice (object)
    supplierMarkup###object##Detail 'Оптовая надбавка/торговая скидка, %' (detail) = DATA NUMERIC[6,2] (object##Detail) IN priceDetailGroup;
    overSupplierMarkup###object##Detail 'Оптовая надбавка/торговая скидка, %' (detail) =
            UNION OVERRIDE 0 IF detail IS object##Detail, supplierMarkup###object##Detail(detail);

    supplierRange###object##Detail(detail) = DATA range (object##Detail);
    numberSupplierRange###object##Detail 'НДС поставщика, номер' (detail) = numberRange(supplierRange###object##Detail(detail)) IN priceDetailGroup;
    valueSupplierRange###object##Detail 'НДС поставщика,%' (detail) = valueRateRangeDate
        (supplierRange###object##Detail(detail), date###object##Detail(detail)) IN priceDetailGroup;

    CONSTRAINT taxRange(supplierRange###object##Detail(detail)) != tax.taxVAT CHECKED BY supplierRange###object##Detail MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';
END

META defineDocumentDetailWareRangePrefix (object, prefix, caption)
    prefix###wareRange###object##Detail (detail) = rangeWareDate(prefix###ware###object##Detail(detail), date###object##Detail(detail));
    prefix###numberWareRange###object##Detail 'НДС посуды, номер'###caption (detail) = numberRange(prefix###wareRange###object##Detail(detail)) IN priceDetailGroup;
    prefix###valueWareRange###object##Detail 'НДС посуды, %'###caption (detail) = valueRateRangeDate(prefix###wareRange###object##Detail(detail), date###object##Detail(detail)) IN priceDetailGroup;
END
META defineDocumentDetailWareRange (object)
    @defineDocumentDetailWareRangePrefix(object,,);
END

META defineDocumentDetailWarePrefix (object, prefix, caption)
    prefix###ware###object##Detail = DATA ware(object##Detail);
    prefix###nameWare###object##Detail 'Посуда'###caption (detail) = name(prefix###ware###object##Detail(detail)) IN priceDetailGroup;

    prefix###warePrice###object##Detail 'Цена посуды с НДС'###caption = DATA NUMERIC[14,2] (object##Detail) IN priceDetailGroup;

    @defineDocumentDetailWareRangePrefix(object, prefix, caption);
END
META defineDocumentDetailWare (object)
    @defineDocumentDetailWarePrefix(object,,);
END

META defineDocumentDetailRetailPricePrefix (object, prefix, caption)
    prefix###retailPrice###object##Detail 'Розничная цена'###caption = DATA NUMERIC[14,2] (object##Detail) IN priceDetailGroup;
END
META defineDocumentDetailRetailPrice (object)
    @defineDocumentDetailRetailPricePrefix(object,,);
END

META defineDocumentDetailOutPricePrefix (object, prefix, caption)
    prefix###retailRange###object##Detail = DATA range (object##Detail);
    prefix###numberRetailRange###object##Detail 'НДС розничный, номер'###caption (detail) = numberRange(prefix###retailRange###object##Detail(detail)) IN priceDetailGroup;
    prefix###valueRetailRange###object##Detail 'НДС розничный, %'###caption (detail) =
       valueRateRangeDate(prefix###retailRange###object##Detail(detail), date###object##Detail(detail)) IN priceDetailGroup;

    CONSTRAINT taxRange(prefix###retailRange###object##Detail(detail)) != tax.taxVAT CHECKED BY prefix###retailRange###object##Detail MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';

    @defineDocumentDetailRetailPricePrefix(object, prefix, caption);
    @defineDocumentDetailOutPriceAggregationPrefix (object, prefix, caption);
END
META defineDocumentDetailOutPrice (object)
    @defineDocumentDetailOutPricePrefix(object, ,);
END

META defineDocumentDetailOutPriceAggregationPrefix (object, prefix, caption)
    prefix###retailIPrice###object##Detail 'Цена розничная (без посуды)'###caption (detail) =
        prefix###retailPrice###object##Detail(detail) (-) prefix###warePrice###object##Detail(detail);

    prefix###retailVATIPrice###object##Detail 'Цена НДС (без посуды)'###caption (detail) = [round0(X*Y/(100+Y))](
                prefix###retailIPrice###object##Detail(detail), prefix###valueRetailRange###object##Detail(detail));

    prefix###markupPrice###object##Detail 'Цена надбавки'###caption (detail) =
        prefix###retailIPrice###object##Detail(detail) (-) prefix###retailVATIPrice###object##Detail(detail) (-) prefix###supplierPrice###object##Detail(detail);

    prefix###calcRetailMarkup###object##Detail 'Наценка, %'###caption (detail) = [round2(X*100/Y)](
        prefix###markupPrice###object##Detail(detail), prefix###supplierPrice###object##Detail(detail));
END
META defineDocumentDetailOutPriceAggregation (object)
    @defineDocumentDetailOutPriceAggregationPrefix(object, ,);
END

META defineDocumentDetailBasePricePrefix (object, prefix, caption)
    @defineDocumentDetailWarePrefix(object, prefix, caption);
    @defineDocumentDetailInPricePrefix(object, prefix, caption);
    @defineDocumentDetailOutPricePrefix(object, prefix, caption);
END
META defineDocumentDetailBasePrice (object)
    @defineDocumentDetailBasePricePrefix(object,,);
END

META defineDocumentDetailCalcRange (object)
    calcRange###object##Detail = DATA range (object##Detail);
    calcRange###object##Detail (detail) <- rangeVATTaxTaxUnit(tax.taxVAT,
                                storeDepartmentStore(departmentStore###object##Detail(detail))) WHEN detail IS object##Detail;

    numberCalcRange###object##Detail 'НДС расчетный, номер' (detail) = numberRange(calcRange###object##Detail(detail));
    dataCalcRange###object##Detail 'НДС расчетный, %' (detail) =
       valueRateRangeDate(calcRange###object##Detail(detail), date###object##Detail(detail));

    CONSTRAINT taxRange(calcRange###object##Detail(detail)) != tax.taxVAT CHECKED BY calcRange###object##Detail MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';
END

META defineDocumentDetailAccountPrice (object)
    accountPrice###object##Detail 'Учетная цена' = DATA NUMERIC[14,2] (object##Detail) IN baseGroup;
END

// ----------------------------------- Объявление сумм для detail -------------------------- //

META defineDocumentDetailInSumPrefix (object, prefix, caption)
    prefix###supplierISum###object##Detail 'Сумма поставщика без НДС'###caption (detail) = round0(quantity###object##Detail(detail) * prefix###supplierPrice###object##Detail(detail));
END
META defineDocumentDetailInSum (object)
    @defineDocumentDetailInSumPrefix(object,,);
END

META defineDocumentDetailInvoiceSum (object)
    supplierVATIPrice###object##Detail 'Цена НДС поставщика (без посуды)' (detail) = [X*Y/100](
        supplierPrice###object##Detail(detail), valueSupplierRange###object##Detail(detail));

    calcSupplierVATISum###object##Detail 'Сумма НДС (расч.)' (detail) = round0(
        quantity###object##Detail(detail) * supplierVATIPrice###object##Detail(detail));

    supplierVATISum###object##Detail 'Сумма НДС поставщика (без посуды)' (detail) = DATA NUMERIC[16,2] (object##Detail) IN baseGroup;
    supplierVATISum###object##Detail(detail) <- calcSupplierVATISum###object##Detail(detail)
                                                       WHEN CHANGED(calcSupplierVATISum###object##Detail(detail));

    calcInvoiceISum###object##Detail 'Сумма поставщика с НДС (расч.)' (detail) =
        supplierISum###object##Detail(detail) (+) supplierVATISum###object##Detail(detail);

    invoiceISum###object##Detail 'Сумма поставщика с НДС (без посуды)' (detail) = DATA NUMERIC[16,2] (object##Detail) IN baseGroup;
    invoiceISum###object##Detail(detail) <- calcInvoiceISum###object##Detail(detail)
                                          WHEN CHANGED(calcInvoiceISum###object##Detail(detail));

    invoiceSum###object##Detail 'Сумма поставщика с НДС' (detail) =
        invoiceISum###object##Detail(detail) (+) wareSum###object##Detail(detail);

    supplierSum###object##Detail 'Сумма поставщика без НДС' (detail) =
        invoiceISum###object##Detail (detail) (-) supplierVATISum###object##Detail (detail) (+) wareSupplierSum###object##Detail(detail);
END

META defineDocumentDetailWareSumPrefix (object, prefix, caption)
    prefix###wareSum###object##Detail 'Сумма посуды с НДС'###caption (detail) = quantity###object##Detail(detail) * prefix###warePrice###object##Detail(detail);

    prefix###wareVATPrice###object##Detail 'Цена НДС посуды'###caption (object##Detail) = [round1(X*Y/(100+Y))](
        prefix###warePrice###object##Detail (object##Detail), prefix###valueWareRange###object##Detail(object##Detail));

    prefix###wareVATSum###object##Detail 'Сумма НДС по посуде'###caption (object##Detail) = round0(
        quantity###object##Detail(object##Detail) * prefix###wareVATPrice###object##Detail(object##Detail));

    prefix###wareSupplierPrice###object##Detail 'Цена посуды без НДС'###caption (object##Detail) =
        prefix###warePrice###object##Detail (object##Detail) (-) prefix###wareVATPrice###object##Detail(object##Detail);

    prefix###wareSupplierSum###object##Detail 'Сумма посуды без НДС'###caption (object##Detail) =
        prefix###wareSum###object##Detail(object##Detail) (-) prefix###wareVATSum###object##Detail(object##Detail);
END
META defineDocumentDetailWareSum (object)
    @defineDocumentDetailWareSumPrefix(object,,);
END

META defineDocumentDetailRetailSumPrefix (object, prefix, caption)
    prefix###retailSum###object##Detail 'Розничная сумма'###caption (detail) = round0(quantity###object##Detail(detail) * prefix###retailPrice###object##Detail(detail));
END
META defineDocumentDetailRetailSum (object)
    @defineDocumentDetailRetailSumPrefix(object,,);
END

META defineDocumentDetailOutSumPrefix (object, prefix, caption)
    @defineDocumentDetailRetailSumPrefix(object, prefix, caption);

    prefix###retailISum###object##Detail 'Розничная сумма (без посуды)'###caption (detail) = prefix###retailSum###object##Detail(detail) (-) prefix###wareSum###object##Detail(detail);

    prefix###retailVATISum###object##Detail 'Сумма НДС (без посуды)'###caption (detail) = [round0(X*Y/(100+Y))](
                prefix###retailISum###object##Detail(detail), prefix###valueRetailRange###object##Detail(detail));

    prefix###retailVATSum###object##Detail 'Сумма НДС'###caption (detail) = prefix###retailVATISum###object##Detail(detail) (+) prefix###wareVATSum###object##Detail(detail);

    prefix###markupSum###object##Detail 'Сумма надбавки'###caption (detail) =
        prefix###retailISum###object##Detail(detail) (-) prefix###retailVATISum###object##Detail(detail) (-) prefix###supplierISum###object##Detail(detail);
END
META defineDocumentDetailOutSum (object)
    @defineDocumentDetailOutSumPrefix(object,,);
END

META defineDocumentDetailBaseSumPrefix (object, prefix, caption)
    @defineDocumentDetailWareSumPrefix(object, prefix, caption);
    @defineDocumentDetailInSumPrefix(object, prefix, caption);
    @defineDocumentDetailOutSumPrefix(object, prefix, caption);
END
META defineDocumentDetailBaseSum (object)
    @defineDocumentDetailBaseSumPrefix(object,,);
END

META defineDocumentDetailCalcVATSum (object)
    calcVATSum###object##Detail 'Сумма НДС (расч.)' (detail) = round0(retailSum###object##Detail(detail) * dataCalcRange###object##Detail(detail) / 100);

    calcSaleSum###object##Detail 'Сумма без НДС (расч.)' (detail) = retailSum###object##Detail(detail) (-) calcVATSum###object##Detail(detail);
END

META defineDocumentDetailAccountSumCustom (object, detail)
    accountSum###detail 'Учетная сумма' (detail) = quantity###detail(detail) * accountPrice###detail(detail);
END
META defineDocumentDetailAccountSum (object)
    @defineDocumentDetailAccountSumCustom(object, object##Detail);
END

// ----------------------------------- Объявление сумм для header -------------------------- //

META defineDocumentHeaderInSumCustomPrefix (object, detail, prefix, caption)
    prefix###supplierISum###detail###object 'Сумма поставщика без НДС (без посуды)'###caption (object) =
        GROUP SUM prefix###supplierISum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;
END
META defineDocumentHeaderInSumCustom (object, detail)
    @defineDocumentHeaderInSumCustomPrefix(object, detail, , )
END
META defineDocumentHeaderInSumPrefix (object, prefix, caption)
    @defineDocumentHeaderInSumCustomPrefix(object, object##Detail, prefix, caption);
END
META defineDocumentHeaderInSum (object)
    @defineDocumentHeaderInSumCustomPrefix(object, object##Detail, , );
END

META defineDocumentHeaderInvoiceSum (object)
    supplierVATISum###object##Detail###object 'Сумма НДС поставщика (без посуды)' (object) =
        GROUP SUM supplierVATISum###object##Detail(detail) BY object###object##Detail(detail);

    invoiceISum###object##Detail###object 'Сумма поставщика с НДС (без посуды)' (object) =
        GROUP SUM invoiceISum###object##Detail(object##Detail) BY object###object##Detail(object##Detail);

    invoiceVATSum###object##Detail###object 'Сумма НДС поставщика' (object) =
        supplierVATISum###object##Detail###object(object) (+) wareVATSum###object##Detail###object(object) IN documentSumGroup PERSISTENT;

    invoiceSum###object##Detail###object 'Сумма поставщика с НДС' (object) =
        invoiceISum###object##Detail###object(object) (+) wareSum###object##Detail###object(object) IN documentSumGroup PERSISTENT;

    supplierSum###object##Detail###object 'Сумма поставщика без НДС' (object) =
        invoiceSum###object##Detail###object (object) (-) invoiceVATSum###object##Detail###object (object);
END

META defineDocumentHeaderWareSumCustomPrefix (object, detail, prefix, caption)
    prefix###wareSum###detail###object 'Сумма посуды с НДС'###caption (object) =
        GROUP SUM prefix###wareSum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;

    prefix###wareVATSum###detail###object 'Сумма НДС по посуде'###caption (object) =
        GROUP SUM prefix###wareVATSum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;
END
META defineDocumentHeaderWareSumCustom (object, detail)
    @defineDocumentHeaderWareSumCustomPrefix(object, detail, , );
END
META defineDocumentHeaderWareSumPrefix (object, prefix, caption)
    @defineDocumentHeaderWareSumCustomPrefix(object, object##Detail, prefix, caption);
END
META defineDocumentHeaderWareSum (object)
    @defineDocumentHeaderWareSumCustomPrefix(object, object##Detail, , );
END

META defineDocumentHeaderRetailSumCustomPrefix (object, detail, prefix, caption)
    prefix###retailSum###detail###object 'Розничная сумма'###caption (object) =
        GROUP SUM prefix###retailSum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;
END
META defineDocumentHeaderRetailSumCustom (object, detail)
    @defineDocumentHeaderRetailSumCustomPrefix(object, detail, , );
END
META defineDocumentHeaderRetailSumPrefix (object, prefix, caption)
    @defineDocumentHeaderRetailSumCustomPrefix(object, object##Detail, prefix, caption);
END
META defineDocumentHeaderRetailSum (object)
    @defineDocumentHeaderRetailSumCustomPrefix(object, object##Detail, , );
END

META defineDocumentHeaderOutSumCustomPrefix (object, detail, prefix, caption)
    @defineDocumentHeaderRetailSumCustomPrefix(object, detail, prefix, caption);

    prefix###retailVATISum###detail###object 'Сумма НДС (без посуды)'###caption (object) =
        GROUP SUM prefix###retailVATISum###detail(idetail) BY object###detail(idetail);

    prefix###retailVATSum###detail###object 'Сумма НДС'###caption (detail) = prefix###retailVATISum###detail###object(detail) (+) prefix###wareVATSum###detail###object(detail);

    prefix###markupSum###detail###object 'Сумма надбавки'###caption (object) =
        GROUP SUM prefix###markupSum###detail(idetail) BY object###detail(idetail);
END
META defineDocumentHeaderOutSumCustom (object, detail)
    @defineDocumentHeaderOutSumCustomPrefix(object, detail, , );
END
META defineDocumentHeaderOutSumPrefix (object)
    @defineDocumentHeaderOutSumCustomPrefix(object, object##Detail, prefix, caption);
END
META defineDocumentHeaderOutSum (object)
    @defineDocumentHeaderOutSumCustomPrefix(object, object##Detail, , );
END

META defineDocumentHeaderBaseSumCustomPrefix (object, detail, prefix, caption)
    @defineDocumentHeaderWareSumCustomPrefix(object, detail, prefix, caption);
    @defineDocumentHeaderInSumCustomPrefix(object, detail, prefix, caption);
    @defineDocumentHeaderOutSumCustomPrefix(object, detail, prefix, caption);
END
META defineDocumentHeaderBaseSumCustom (object, detail)
    @defineDocumentHeaderBaseSumCustomPrefix(object, detail, , );
END
META defineDocumentHeaderBaseSumPrefix (object, prefix, caption)
    @defineDocumentHeaderBaseSumCustomPrefix(object, object##Detail, prefix, caption);
END
META defineDocumentHeaderBaseSum (object)
    @defineDocumentHeaderBaseSumCustomPrefix(object, object##Detail, , );
END

META defineDocumentHeaderCalcVATSumCustom (object, detail)
    calcVATSum###detail###object 'Сумма НДС (расч., окр.)' (iobject) = GROUP SUM calcVATSum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;

    calcSaleSum###detail###object 'Сумма без НДС (расч., окр.)' (iobject) = retailSum###detail###object(iobject) (-) calcVATSum###detail###object(iobject) IN documentSumGroup;
END

META defineDocumentHeaderCalcVATSum (object)
    @defineDocumentHeaderCalcVATSumCustom(object, object##Detail);
END

META defineDocumentHeaderAccountSumCustom (object, detail)
    accountSum###detail###object 'Учетная сумма' (object) = GROUP SUM accountSum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;
END

META defineDocumentHeaderAccountSum (object)
    @defineDocumentHeaderAccountSumCustom(object, object##Detail);
END

// ------------------------- Агрегации ---------------------------------- //

META defineDocumentDetailAggregationPricePrefix (primObject, aggrObject, prefixP, prefixA, caption)
    prefixA###importerPrice###aggrObject##Detail 'Цена изготовителя'###caption (detail) = prefixP###importerPrice###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
    prefixA###supplierPrice###aggrObject##Detail 'Цена поставщика'###caption (detail) = prefixP###supplierPrice###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));

    prefixA###retailRange###aggrObject##Detail (detail) = prefixP###retailRange###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
    prefixA###valueRetailRange###aggrObject##Detail 'НДС, %'###caption (detail) = prefixP###valueRetailRange###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));

    prefixA###ware###aggrObject##Detail (detail) = prefixP###ware###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
    prefixA###valueWareRange###aggrObject##Detail 'НДС посуды, %'###caption (detail) =
          valueRateRangeDate(rangeWareDate(prefixA###ware###aggrObject##Detail(detail), date###aggrObject##Detail(detail)),
                                                                       date###aggrObject##Detail(detail));

    prefixA###warePrice###aggrObject##Detail 'Цена посуды'###caption (detail) = prefixP###warePrice###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));

    prefixA###retailPrice###aggrObject##Detail 'Розничная цена'###caption (detail) = prefixP###retailPrice###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
END
META defineDocumentDetailAggregationPrice (primObject, aggrObject)
    @defineDocumentDetailAggregationPricePrefix(primObject, aggrObject, , , );
END

//---------------------------- Ценовые регистры ----------------------------------------//

META defineSkuDepartmentStoreLedger(property, caption, type)
    @defineHistorizableLedgerDouble(property, caption, type, sku, departmentStore);

    nameSku###property##Ledger 'SKU' (ledger) = nameSku(sku###property##Ledger(ledger));
    nameDepartmentStore###property##Ledger 'Отдел магазина' (ledger) = name(departmentStore###property##Ledger(ledger));
END

META implementSkuDepartmentStoreLedgerCustom(property, concrete, skuProp, propertyProp)
    @implementHistorizableLedgerDouble(property, sku, departmentStore, concrete, skuProp, departmentStore, propertyProp);
END

META implementSkuDepartmentStoreLedger(property, concrete, skuProp)
    @implementSkuDepartmentStoreLedgerCustom(property, concrete, skuProp, property);
END

META implementBaseISkuDepartmentStoreLedger(concrete, skuProp)
    @implementSkuDepartmentStoreLedger(importerPrice, concrete, skuProp);
    @implementSkuDepartmentStoreLedger(supplierPrice, concrete, skuProp);
    @implementSkuDepartmentStoreLedger(retailRange, concrete, skuProp);
    @implementSkuDepartmentStoreLedger(retailPrice, concrete, skuProp);
    @implementSkuDepartmentStoreLedgerCustom(accountPrice, concrete, skuProp, retailPrice);
END

META implementBaseSkuDepartmentStoreLedger(concrete, skuProp)
    @implementSkuDepartmentStoreLedger(warePrice, concrete, skuProp);
    wareWarePriceLedger(ledger) += ware###concrete##Detail(ledger);
    @implementBaseISkuDepartmentStoreLedger(concrete, skuProp);
END

CLASS ABSTRACT warePriceLedger 'Изменение цены посуды';
TABLE warePriceLedger (warePriceLedger);

@defineSkuDepartmentStoreLedger(warePrice, 'Цена посуды', NUMERIC[14,2]);
wareWarePriceLedger(ledger) = ABSTRACT ware (warePriceLedger) PERSISTENT;
nameWareWarePriceLedger 'Посуда' (ledger) = name(wareWarePriceLedger(ledger));
currentWareLedger 'Посуда' (sku, departmentStore) = wareWarePriceLedger(currentLedgerWarePrice(sku, departmentStore));
wareBLedgerDateTime (sku, departmentStore, dateTime) = wareWarePriceLedger(ledgerBWarePriceDateTime(sku, departmentStore, dateTime));

CLASS ABSTRACT importerPriceLedger 'Изменение цены изготовителя/импортера';
TABLE importerPriceLedger (importerPriceLedger);
@defineSkuDepartmentStoreLedger(importerPrice, 'Цена изготовителя', NUMERIC[14,2]);

CLASS ABSTRACT supplierPriceLedger 'Изменение цены поставщика';
TABLE supplierPriceLedger (supplierPriceLedger);
@defineSkuDepartmentStoreLedger(supplierPrice, 'Цена поставщика', NUMERIC[14,2]);

CLASS ABSTRACT retailRangeLedger 'Изменение НДС';
TABLE retailRangeLedger (retailRangeLedger);
@defineSkuDepartmentStoreLedger(retailRange, 'НДС', range);
valueRetailRangeRetailRangeLedger 'НДС, %' (ledger) = valueRateRangeDate(retailRangeRetailRangeLedger(ledger), dateInTime(dateTimeRetailRangeLedger(ledger))) PERSISTENT;
valueRetailRangeBLedgerDateTime 'НДС, % (до)' (sku, departmentStore, dateTime) =
            valueRateRangeDate(retailRangeBLedgerDateTime(sku, departmentStore, dateTime), dateInTime(dateTime));
valueRetailRangeALedgerDateTime 'НДС, % (после)' (sku, departmentStore, dateTime) =
            valueRateRangeDate(retailRangeALedgerDateTime(sku, departmentStore, dateTime), dateInTime(dateTime));
currentValueRetailRangeLedger 'НДС, %' (sku, departmentStore) =
            valueCurrentRateRange(currentRetailRangeLedger(sku, departmentStore));

CLASS ABSTRACT retailPriceLedger 'Изменение розничной цены';
TABLE retailPriceLedger (retailPriceLedger);
@defineSkuDepartmentStoreLedger(retailPrice, 'Розничная цена', NUMERIC[14,2]);
currentRetailPriceBarcodeDepartmentStore 'Розничная цена' (barcode, departmentStore) = currentRetailPriceLedger(skuBarcode(barcode), departmentStore);

CLASS ABSTRACT accountPriceLedger 'Изменение учетной цены';
TABLE accountPriceLedger (accountPriceLedger);
@defineSkuDepartmentStoreLedger(accountPrice, 'Учетная цена', NUMERIC[14,2]);

// ------------------------------------- Заполнение текущих параметров --------------------------------- //

META derivePriceLedgerOnSkuChangePrefix (ledger, concrete, prefixP, prefixS)
    prefixP###ledger###concrete##Detail (detail)  <- ledger##BLedgerDateTime(
                    prefixS###concrete##Detail(detail), departmentStore###concrete##Detail(detail), dateTime###concrete##Detail(detail))
                    WHEN CHANGED(prefixS###concrete##Detail(detail));
END

META derivePriceLedgerOnSkuChange (ledger, concrete, prefixS)
    @derivePriceLedgerOnSkuChangePrefix(ledger, concrete, , prefixS);
END

META deriveBasePriceLedgerOnSkuChangePrefix (concrete, prefixP, prefixS)
    @derivePriceLedgerOnSkuChangePrefix(ware, concrete, prefixP, prefixS);
    @derivePriceLedgerOnSkuChangePrefix(warePrice, concrete, prefixP, prefixS);
    @derivePriceLedgerOnSkuChangePrefix(importerPrice, concrete, prefixP, prefixS);
    @derivePriceLedgerOnSkuChangePrefix(supplierPrice, concrete, prefixP, prefixS);
    @derivePriceLedgerOnSkuChangePrefix(retailRange, concrete, prefixP, prefixS);
    @derivePriceLedgerOnSkuChangePrefix(retailPrice, concrete, prefixP, prefixS);
END

META deriveBasePriceLedgerOnSkuChange (concrete, prefixS)
    @deriveBasePriceLedgerOnSkuChangePrefix(concrete, , prefixS);
END

META derivePrevPriceLedgerOnSkuChangePrefix (ledger, concrete, prefixP, prefixS)
    prefixP###ledger###concrete##Detail (detail)  <- [PREV(ledger##BLedgerDateTime(sku, departmentStore, dateTime))](
                    prefixS###concrete##Detail(detail), departmentStore###concrete##Detail(detail), dateTime###concrete##Detail(detail))
                    WHEN CHANGED(prefixS###concrete##Detail(detail));
END

META derivePrevBasePriceLedgerOnSkuChangePrefix (concrete, prefixP, prefixS)
    @derivePrevPriceLedgerOnSkuChangePrefix(ware, concrete, prefixP, prefixS);
    @derivePrevPriceLedgerOnSkuChangePrefix(warePrice, concrete, prefixP, prefixS);
    @derivePrevPriceLedgerOnSkuChangePrefix(importerPrice, concrete, prefixP, prefixS);
    @derivePrevPriceLedgerOnSkuChangePrefix(supplierPrice, concrete, prefixP, prefixS);
    @derivePrevPriceLedgerOnSkuChangePrefix(retailRange, concrete, prefixP, prefixS);
    @derivePrevPriceLedgerOnSkuChangePrefix(retailPrice, concrete, prefixP, prefixS);
END

META derivePrevBasePriceLedgerOnSkuChange (concrete, prefixS)
    @derivePrevBasePriceLedgerOnSkuChangePrefix (concrete, , prefixS);
END

META implementSalesLedgerStore(concrete)
    @implementSalesLedger(concrete, item, departmentStore);
    quantitySalesLedger (ledger) += quantity###concrete##Detail(ledger);
    VATSalesLedger (ledger) += retailRange###concrete##Detail(ledger);
    sumSalesLedger (ledger) += retailSum###concrete##Detail(ledger);
END

// ----------------------------------------- Расширяем формы остатков ценами ----------------------------------- //

EXTEND FORM currentBalanceSkuStock
    PROPERTIES(s, st)   currentImporterPriceLedger, currentSupplierPriceLedger,
                        currentAccountPriceLedger, currentValueRetailRangeLedger, currentRetailPriceLedger,
                        currentWarePriceLedger

    OBJECTS apl=accountPriceLedger
    PROPERTIES (apl) descriptionAccountPriceLedger, dateTimeAccountPriceLedger, accountPriceAccountPriceLedger
    FILTERS isPostedAccountPriceLedger(apl) AND skuAccountPriceLedger(apl)==s AND departmentStoreAccountPriceLedger(apl)==st
    ORDER BY dateTimeAccountPriceLedger DESC

    OBJECTS rpl=retailPriceLedger
    PROPERTIES (rpl) descriptionRetailPriceLedger, dateTimeRetailPriceLedger, retailPriceRetailPriceLedger
    FILTERS isPostedRetailPriceLedger(rpl) AND skuRetailPriceLedger(rpl)==s AND departmentStoreRetailPriceLedger(rpl)==st
    ORDER BY dateTimeRetailPriceLedger DESC


    OBJECTS ipl=importerPriceLedger
    PROPERTIES(ipl) descriptionImporterPriceLedger, dateTimeImporterPriceLedger, importerPriceImporterPriceLedger
    FILTERS isPostedImporterPriceLedger(ipl) AND skuImporterPriceLedger(ipl)==s AND departmentStoreImporterPriceLedger(ipl)==st
    ORDER BY dateTimeImporterPriceLedger DESC

    OBJECTS spl=supplierPriceLedger
    PROPERTIES(spl) descriptionSupplierPriceLedger, dateTimeSupplierPriceLedger, supplierPriceSupplierPriceLedger
    FILTERS isPostedSupplierPriceLedger(spl) AND skuSupplierPriceLedger(spl)==s AND departmentStoreSupplierPriceLedger(spl)==st
    ORDER BY dateTimeSupplierPriceLedger DESC

    OBJECTS rvl=retailRangeLedger
    PROPERTIES(rvl) descriptionRetailRangeLedger, dateTimeRetailRangeLedger, valueRetailRangeRetailRangeLedger
    FILTERS isPostedRetailRangeLedger(rvl) AND skuRetailRangeLedger(rvl)==s AND departmentStoreRetailRangeLedger(rvl)==st
    ORDER BY dateTimeRetailRangeLedger DESC

    OBJECTS wpl=warePriceLedger
    PROPERTIES(wpl) descriptionWarePriceLedger, dateTimeWarePriceLedger, nameWareWarePriceLedger, warePriceWarePriceLedger
    FILTERS isPostedWarePriceLedger(wpl) AND skuWarePriceLedger(wpl)==s AND departmentStoreWarePriceLedger(wpl)==st
    ORDER BY dateTimeWarePriceLedger DESC
;

EXTEND DESIGN currentBalanceSkuStock {
    ledger.box {
        ADD apl.box;
        ADD rpl.box;
        ADD ipl.box;
        ADD spl.box;
        ADD rvl.box;
        ADD wpl.box;
    }
}

EXTEND FORM balanceSkuStock
    PROPERTIES (s, st, t) READONLY importerPriceALedgerDateTime, supplierPriceALedgerDateTime,
                                   accountPriceBLedgerDateTime, valueRetailRangeALedgerDateTime,
                                   retailPriceALedgerDateTime, warePriceALedgerDateTime

    OBJECTS apl=accountPriceLedger
    PROPERTIES (apl) descriptionAccountPriceLedger, dateTimeAccountPriceLedger, accountPriceAccountPriceLedger
    FILTERS isPostedAccountPriceLedger(apl) AND skuAccountPriceLedger(apl)==s AND departmentStoreAccountPriceLedger(apl)==st
    ORDER BY dateTimeAccountPriceLedger DESC

    OBJECTS rpl=retailPriceLedger
    PROPERTIES (rpl) descriptionRetailPriceLedger, dateTimeRetailPriceLedger, retailPriceRetailPriceLedger
    FILTERS isPostedRetailPriceLedger(rpl) AND skuRetailPriceLedger(rpl)==s AND departmentStoreRetailPriceLedger(rpl)==st
    ORDER BY dateTimeRetailPriceLedger DESC

    OBJECTS ipl=importerPriceLedger
    PROPERTIES(ipl) descriptionImporterPriceLedger, dateTimeImporterPriceLedger, importerPriceImporterPriceLedger
    FILTERS isPostedImporterPriceLedger(ipl) AND skuImporterPriceLedger(ipl)==s AND departmentStoreImporterPriceLedger(ipl)==st
    ORDER BY dateTimeImporterPriceLedger DESC

    OBJECTS spl=supplierPriceLedger
    PROPERTIES(spl) descriptionSupplierPriceLedger, dateTimeSupplierPriceLedger, supplierPriceSupplierPriceLedger
    FILTERS isPostedSupplierPriceLedger(spl) AND skuSupplierPriceLedger(spl)==s AND departmentStoreSupplierPriceLedger(spl)==st
    ORDER BY dateTimeSupplierPriceLedger DESC

    OBJECTS rvl=retailRangeLedger
    PROPERTIES(rvl) descriptionRetailRangeLedger, dateTimeRetailRangeLedger, valueRetailRangeRetailRangeLedger
    FILTERS isPostedRetailRangeLedger(rvl) AND skuRetailRangeLedger(rvl)==s AND departmentStoreRetailRangeLedger(rvl)==st
    ORDER BY dateTimeRetailRangeLedger DESC

    OBJECTS wpl= warePriceLedger
    PROPERTIES(wpl) descriptionWarePriceLedger, dateTimeWarePriceLedger, nameWareWarePriceLedger, warePriceWarePriceLedger
    FILTERS isPostedWarePriceLedger(wpl) AND skuWarePriceLedger(wpl)==s AND departmentStoreWarePriceLedger(wpl)==st
    ORDER BY dateTimeWarePriceLedger DESC
;

EXTEND DESIGN balanceSkuStock {
    ledger.box {
        ADD apl.box;
        ADD rpl.box;
        ADD ipl.box;
        ADD spl.box;
        ADD rvl.box;
        ADD wpl.box;
    }
}

// ----------------------------------------- Расширяем формы подбора товаров ----------------------------------- //

META defineDialogSkuStorePrice (form)

    form###supplierPrice 'Цена поставщика' (sku) = currentSupplierPriceLedger(sku, form###stock());
    form###valueRetailRange 'НДС, %' (sku) = currentValueRetailRangeLedger(sku, form###stock());
    form###retailPrice 'Розничная цена' (sku) = currentRetailPriceLedger(sku, form###stock());

    EXTEND FORM form
        PROPERTIES(s) READONLY SHOWIF form###stock() form###supplierPrice, form###valueRetailRange, form###retailPrice
        PROPERTIES(si) READONLY SHOWIF form###stock() form###supplierPrice, form###valueRetailRange, form###retailPrice
    ;
END
