MODULE Order;

REQUIRE System,
        Utils,
        Historizable,
        Stock,
        Numerator,
        Document,
        Currency,
        Barcode,
        PriceList,
        Agreement;


PRIORITY Utils, Stock;


//----------------------------------------------- Заказ ---------------------------------------------------//

META defineOrder(sign, stockProp)

    CLASS ABSTRACT order 'Заказ'###sign;
    CLASS ABSTRACT orderDetail 'Строка заказа'###sign : named;

    CLASS userOrder 'Заказ (польз.)'###sign : order, historyObject, numeratedObject;
    CLASS userOrderDetail 'Строка заказа (польз.)'###sign : orderDetail;
    CLASS userOrderPosted 'Проведенный заказ (польз.)'###sign : userOrder, postedObject;

    @defineDocumentInterface(order);

    @defineDocumentInterfaceNumber(order);

    @defineDocumentInterfaceLegalEntity (order, supplier, 'Поставщик');
    @defineDocumentInterfaceLegalEntity (order, customer, 'Покупатель');

    @defineDocumentInterfaceDataStock(order, stock, 'Склад поставщика', supplier);
    @defineDocumentInterfaceDataStock(order, stock, 'Склад покупателя', customer);

    @defineDocumentInterfacePosted(order);

    @defineDocumentInterfaceTimeShipment(order);

    @defineDocumentClosed (order);

    @defineDocumentInterfaceDescription(order, 'Заказ'###sign);

    @defineDocumentInterfaceCurrency(order);
    @deriveDocumentCurrency(userOrder, stockProp);

    @defineDocumentInterfaceDataPriceListType(order);

    @defineDocumentInterfaceDetailSku(order, sku);

    @defineDocumentInterfaceDetailQuantity(order);

    @defineDocumentInterfaceDetailPrice(order);
    @deriveDocumentDetailPricePriceListType(userOrder, stockProp);

    @defineDocumentInterfaceDetailDataSum(order);
    @deriveDocumentDetailSum(userOrder, quantity);

    @defineDocumentInterfaceHeaderQuantity(order);
    @defineDocumentHeaderSkuQuantity(order, sku);
    @defineDocumentHeaderSkuQuantity(userOrder, sku);
    @defineDocumentInterfaceHeaderSum(order);

    @defineAddDetailDialogSkuStock(userOrder, sku, stockProp, dialogSku);
    @defineAddDetailDialogBarcode(userOrder, sku);

    @defineDocumentInterfaceHeaderAgreement(order, supplierUserOrder, customerUserOrder);
    @deriveDocumentDetailPriceListTypeAgreement(order);

    @defineDocumentDetailPackWeightSku(order);

    grossWeightOrder 'Вес брутто' (order) = GROUP SUM grossWeightOrderDetail(orderDetail) BY orderOrderDetail(orderDetail);

    countOrderDetailStockOrder 'Кол-во строк по складу '(stock, order) = GROUP SUM 1 BY stockProp###orderDetail(orderDetail), orderOrderDetail(orderDetail);


// --------------------------- Формы Заказа ---------------------------------

    FORM userOrder 'Заказ'###sign
        OBJECTS o = userOrder FIXED PANEL
        PROPERTIES (o) objectClassName, nameSupplierStockUserOrder, nameCustomerStockUserOrder, nameSupplierStockUserOrder, nameCustomerStockUserOrder,
                       nameNumeratorObject, numberObject, seriesObject, dateUserOrder, timeUserOrder,
                       nameCurrencyUserOrder, nameAgreementUserOrder, namePriceListTypeUserOrder, noteUserOrder,
                       countUserOrderDetailUserOrder, quantityUserOrderDetailUserOrder, sumUserOrderDetailUserOrder, shipmentDateUserOrder,
                       shipmentTimeUserOrder

        OBJECTS d = userOrderDetail
        PROPERTIES (d) indexUserOrderDetail, idBarcodeSkuUserOrderDetail, nameSkuUserOrderDetail, shortNameUOMSkuUserOrderDetail,
                       quantityUserOrderDetail, namePriceListTypeUserOrderDetail, priceUserOrderDetail, sumUserOrderDetail, name###stockProp###userOrderDetail, shipmentDateUserOrderDetail,
                       shipmentTimeUserOrderDetail, ADDOBJ, delete

        PROPERTIES(o) TODRAW d addDetailDialogSkuStockUserOrderDetailUserOrder,
                               addDetailInputBarcodeUserOrderDetailUserOrder, deleteUserOrderDetailUserOrder
        FILTERS userOrderUserOrderDetail(d) == o

        EVENTS
            ON OK EXEC prePostUserOrder(o)

        EDIT userOrder OBJECT o
    ;

    DESIGN userOrder FROM DEFAULT{

        main {
            preferredSize = (1024, 768);
            NEW specification.box BEFORE functions.box{
                ADD d.box {
                    title = 'Спецификация';
                    d.panel {
                        childConstraints = TO THE BOTTOM;
                    }
                }
            }

            NEW header.box BEFORE specification.box {
                childConstraints = TO THE RIGHT;

                NEW headerRow1 {
                    childConstraints = TO THE BOTTOM;

                    ADD o.documentHeaderGroup {
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(objectClassName) { preferredCharWidth = 10; }
                        ADD PROPERTY(nameSupplierStockUserOrder);
                        ADD PROPERTY(nameCustomerStockUserOrder);
                        ADD PROPERTY(nameNumeratorObject);
                        ADD PROPERTY(numberObject);
                        ADD PROPERTY(seriesObject);
                        ADD PROPERTY(dateUserOrder);
                        ADD PROPERTY(timeUserOrder);
                    }
                    NEW headerRow2 {
                        title = 'Склады';
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(nameSupplierStockUserOrder);
                        ADD PROPERTY(nameCustomerStockUserOrder);
                    }
                    NEW wor {
                        childConstraints = TO THE RIGHTBOTTOM;
                        ADD o.documentPrmGroup {
                            childConstraints = TO THE RIGHT;
                        }
                        ADD o.documentShipmentGroup {
                            childConstraints = TO THE RIGHT;
                        }
                    }
                }

                ADD o.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }


            PROPERTY(formOkAction) {
                caption = 'Провести';
            }
        }
    }

//-- SKU
    @defineDocumentSkuStock(userOrder, sku, stockProp);
    @extendFormDocumentSkuStockCustom(userOrder, userOrder, o, stockProp);

    addUserOrder 'Добавить' = ACTION ADDFORM userOrder;
    editUserOrder 'Редактировать' (userOrder) = ACTION EDITFORM userOrder;

    FORM orders 'Заказы' TITLE 'Заказы'###sign
        OBJECTS o = order
        PROPERTIES (o) READONLY isPostedOrder FORCE GRID, isClosedOrder, objectClassName, numberOrder, seriesOrder, dateOrder, timeOrder,
                                nameSupplierStockOrder, nameCustomerStockOrder, nameSupplierStockOrder, nameCustomerStockOrder, nameCurrencyOrder,
                                nameAgreementOrder, namePriceListTypeOrder, noteOrder,
                                countOrderDetailOrder, quantityOrderDetailOrder, sumOrderDetailOrder, shipmentDateOrder, shipmentTimeOrder

        PROPERTIES (o) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed

        PROPERTIES ()  addUserOrder TODRAW o
        PROPERTIES (o) editUserOrder
        PROPERTIES (o) closeOrder SHOWIF isOpendOrder(o), uncloseOrder SHOWIF isClosedOrder(o)
        PROPERTIES     delete(o) FORCE PANEL DRAWTOTOOLBAR  SHOWIF isUserOrder(o)

        OBJECTS d=orderDetail
        PROPERTIES (d) READONLY indexOrderDetail, idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail,
                       quantityOrderDetail, namePriceListTypeOrderDetail, priceOrderDetail, sumOrderDetail, name###stockProp###orderDetail, shipmentDateOrderDetail,
                       shipmentTimeOrderDetail


        FILTERS orderOrderDetail(d) == o
        DIALOG order OBJECT o
    ;

    DESIGN orders FROM DEFAULT {
        PROPERTY (delete(o)) {
            askConfirm = TRUE;
        }

        NEW documentContainer BEFORE functions.box {
            type = SPLITV;
            childConstraints = TO THE BOTTOM;

            ADD o.box;

            NEW documentDetail {
                type = TABBED;

                ADD d.box {
                    title = 'Спецификация';
                }
                NEW documentHistory {
                    title = 'История';

                    ADD o.historyGroup;
                    ADD o.postedGroup;
                }
                NEW printTab {
                    title = 'Печатные формы';
                    NEW printContainer {
                        title = 'Печать';
                        childConstraints = TO THE BOTTOM;
                        fillVertical = 1.0;
                    }
                }
            }
        }
    }
END

