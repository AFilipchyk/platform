MODULE Order;

REQUIRE System,
        Utils,
        Historizable,
        Stock,
        Numerator,
        Document,
        Currency,
        Customer,
        Supplier,
        Barcode,
        PriceList,
        Historizable;


PRIORITY Utils, Stock;


//----------------------------------------------- Заказ ---------------------------------------------------//

META defineOrder(sign, contact)

    CLASS ABSTRACT order 'Заказ'###sign;
    CLASS ABSTRACT orderDetail 'Строка заказа'###sign : named;

    CLASS userOrder 'Заказ (польз.)'###sign : order, historyObject, numeratedDocument;
    CLASS userOrderDetail 'Строка заказа (польз.)'###sign : orderDetail;
    CLASS userOrderPosted 'Проведенный заказ (польз.)'###sign : userOrder, postedObject;
    CLASS userOrderClosed 'Закрытый заказ (польз.)'###sign : userOrderPosted;

    @defineDocumentInterface(order);

    @defineDocumentInterfaceNumber(order);

    @defineDocumentInterfaceDataStock(order, stock, 'Склад');
    @defineDocumentInterfacePosted(order);

    @defineDocumentInterfaceTimeShipment(order);

    @defineDocumentInterfaceClosed(order);

    @defineDocumentInterfaceDescription(order, 'Заказ'###sign);

    @defineDocumentInterfaceCurrency(order);
    @deriveDocumentCurrency(userOrder, stock);

    @defineDocumentInterface###contact(order);
    @defineDocumentInterfaceDataPriceListType(order);

    @defineDocumentInterfaceDetailSku(order, sku);

    @defineDocumentInterfaceDetailQuantity(order);

    @defineDocumentInterfaceDetailPrice(order);
    @deriveDocumentDetailPricePriceListType(userOrder);

    @defineDocumentInterfaceDetailDataSum(order);
    @deriveDocumentDetailSum(userOrder);

    @defineDocumentInterfaceHeaderQuantity(order);
    @defineDocumentHeaderSkuQuantity(order, sku);
    @defineDocumentHeaderSkuQuantity(userOrder, sku);
    @defineDocumentInterfaceHeaderSum(order);

    @defineAddDetailDialogSkuStock(userOrder, sku, stock, dialogSku);
    @defineAddDetailDialogBarcode(userOrder, sku);

    countOrderDetailStockOrder 'Кол-во строк по складу '(stock, order) = GROUP SUM 1 BY stockOrderDetail(orderDetail), orderOrderDetail(orderDetail);

// --------------------------- Формы Заказа ---------------------------------

    FORM userOrder 'Заказ'###sign
        OBJECTS o = userOrder FIXED PANEL
        PROPERTIES (o) objectClassName, nameStockUserOrder, nameNumeratorObject, numberObject, seriesObject, dateUserOrder, timeUserOrder,
                       name###contact###UserOrder, nameCurrencyUserOrder, namePriceListTypeUserOrder, noteUserOrder,
                       countUserOrderDetailUserOrder, quantityUserOrderDetailUserOrder, sumUserOrderDetailUserOrder, shipmentDateUserOrder,
                       shipmentTimeUserOrder

        OBJECTS d = userOrderDetail
        PROPERTIES (d) indexUserOrderDetail, idBarcodeSkuUserOrderDetail, nameSkuUserOrderDetail, shortNameUOMSkuUserOrderDetail,
                       quantityUserOrderDetail, priceUserOrderDetail, sumUserOrderDetail, nameStockUserOrderDetail, namePriceListTypeUserOrderDetail, shipmentDateUserOrderDetail,
                       shipmentTimeUserOrderDetail, ADDOBJ, delete

        PROPERTIES(o) TODRAW d addDetailDialogSkuStockUserOrderDetailUserOrder,
                               addDetailInputBarcodeUserOrderDetailUserOrder, deleteUserOrderDetailUserOrder
        FILTERS userOrderUserOrderDetail(d) == o

        EVENTS
            ON OK EXEC prePostUserOrder(o)

        EDIT userOrder OBJECT o
    ;

    DESIGN userOrder FROM DEFAULT{

        main {
            preferredSize = (1024, 768);
            NEW specification.box BEFORE functions.box{
                ADD d.box {
                    title = 'Спецификация';
                    d.panel {
                        childConstraints = TO THE BOTTOM;
                    }
                }
            }

            NEW header.box BEFORE specification.box {
                childConstraints = TO THE RIGHT;

                NEW headerRow1 {
                    childConstraints = TO THE BOTTOM;

                    ADD o.documentHeaderGroup {
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(objectClassName) { preferredCharWidth = 10; }
                        ADD PROPERTY(nameStockUserOrder);
                        ADD PROPERTY(nameNumeratorObject);
                        ADD PROPERTY(numberObject);
                        ADD PROPERTY(seriesObject);
                        ADD PROPERTY(dateUserOrder);
                        ADD PROPERTY(timeUserOrder);
                    }
                    ADD o.documentShipmentGroup {
                        childConstraints = TO THE RIGHT;
                    }
                    ADD o.documentPrmGroup {
                        childConstraints = TO THE RIGHT;
                    }
                }

                ADD o.documentSumGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }


            PROPERTY(formOkAction) {
                caption = 'Провести';
            }
        }
    }

//-- SKU
    @defineDocumentSkuStock(userOrder);
    @extendDocumentFormSkuStock(userOrder, userOrder, o);


    addUserOrder 'Добавить' = ACTION ADDFORM userOrder;
    editUserOrder 'Редактировать' (userOrder) = ACTION EDITFORM userOrder;

    FORM orders 'Заказы'###sign
        OBJECTS o = order
        PROPERTIES (o) READONLY isPostedOrder FORCE GRID, objectClassName, numberObject, seriesObject, dateOrder, timeOrder,
                                nameStockOrder, name###contact##Order, nameCurrencyOrder, namePriceListTypeOrder, noteOrder,
                                countOrderDetailOrder, quantityOrderDetailOrder, sumOrderDetailOrder, shipmentDateUserOrder, shipmentTimeUserOrder

        PROPERTIES (o) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated, nameUserClosed, timeClosed, hostnameComputerClosed

        PROPERTIES ()  addUserOrder TODRAW o
        PROPERTIES (o) editUserOrder
        PROPERTIES (o) closeUserOrder SHOWIF isOpendOrder(o)
        PROPERTIES     delete(o) FORCE PANEL DRAWTOTOOLBAR  SHOWIF isUserOrder(o)

        OBJECTS d=orderDetail
        PROPERTIES (d) READONLY indexOrderDetail, idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail,
                       quantityOrderDetail, priceOrderDetail, sumOrderDetail, nameStockOrderDetail, namePriceListTypeOrderDetail, shipmentDateUserOrderDetail,
                       shipmentTimeUserOrderDetail


        FILTERS orderOrderDetail(d) == o
        DIALOG order OBJECT o
    ;

    DESIGN orders FROM DEFAULT {
        PROPERTY (delete(o)) {
            askConfirm = TRUE;
        }

        NEW documentContainer BEFORE functions.box {
            type = SPLITV;
            childConstraints = TO THE BOTTOM;

            ADD o.box;

            NEW documentDetail {
                type = TABBED;

                ADD d.box {
                    title = 'Спецификация';
                }
                NEW documentHistory {
                    title = 'История';

                    ADD o.historyGroup;
                    ADD o.postedGroup;
                }
                NEW printTab {
                    title = 'Печатные формы';
                    NEW printContainer {
                        title = 'Печать';
                        childConstraints = TO THE BOTTOM;
                        fillVertical = 1.0;
                    }
                }
            }
        }
    }
END

