MODULE Certificate;

REQUIRE Document;

CLASS ABSTRACT certificate 'Документ качества';

@defineDocumentAbstractHeaderTimePrefix(certificate, ,' создания');
@defineDocumentAbstractHeaderTimePrefix(certificate, to, ' действия');
numberCertificate 'Номер' = ABSTRACT STRING[18] (certificate) IN numberedGroup MINCHARWIDTH 7;
seriesCertificate 'Серия' = ABSTRACT STRING[2] (certificate) IN numberedGroup FIXEDCHARWIDTH 3;
seriesNumberCertificate 'Серия/Номер' (certificate) = ustring2(seriesCertificate(certificate), numberCertificate(certificate)) MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;


descriptionCertificate 'Документ качества' (certificate) =
    [FORMULA STRING[100] ' \'№\' || CAST($1 AS TEXT) ||  \' от \' || CAST($2 AS TEXT)||  \' действует по \' || CAST($3 AS TEXT)']
    (seriesNumberCertificate(certificate), dateCertificate(certificate), toDateCertificate(certificate));

NAVIGATOR {
    masterData {
        NEW certificateNavigator 'Сертификаты' BEFORE regionalData;
    }
}
// -------------------------- Метакод по расширению форм и созданию свойств -------------------- //
META extendCertificateForm(prop, char)

    EXTEND FORM user###prop
        OBJECTS d2 = user###prop##Detail
        PROPERTIES (d2) indexUser###prop##Detail, idBarcodeSkuUser###prop##Detail, nameSkuUser###prop##Detail
        FILTERS user###prop##User###prop##Detail(d2) == char
    ;

    EXTEND DESIGN user###prop{
        specification.box{
            ADD d2.box AFTER d.box {caption = 'Сертификаты';}
        }
    }

    EXTEND FORM prop##s
        OBJECTS d2 = prop##Detail
        PROPERTIES(d2) READONLY index###prop##Detail, idBarcodeSku###prop##Detail, nameSku###prop##Detail
        FILTERS prop###prop##Detail(d2) == char
    ;

    EXTEND DESIGN prop##s{
        documentDetail{
            ADD d2.box AFTER d.box {caption = 'Сертификаты';}
        }
    }

END

META defineCertificateInvoice(prop, caption)
    prop###invoiceDetail = ABSTRACT prop (invoiceDetail);
    description###prop###invoiceDetail caption (invoiceDetail) = description###prop(prop###invoiceDetail(invoiceDetail));
    prop###userInvoice = DATA prop (userInvoice);
    description###prop###userInvoice caption (userInvoice) = description###prop(prop###userInvoice(userInvoice));
    data###prop###userInvoiceDetail = DATA prop (userInvoiceDetail);
    prop###userInvoiceDetail (userInvoiceDetail) =
        UNION OVERRIDE prop###userInvoice(userInvoiceUserInvoiceDetail(userInvoiceDetail)), data###prop###userInvoiceDetail(userInvoiceDetail);
    description###prop###userInvoiceDetail caption (userInvoiceDetail) = description###prop(prop###userInvoiceDetail(userInvoiceDetail));
    prop###invoiceDetail(detail) += prop###userInvoiceDetail(detail);

    EXTEND FORM userInvoice
        PROPERTIES(i) description###prop###userInvoice TODRAW d2 FORCE PANEL
        PROPERTIES(d2) description###prop###userInvoiceDetail
    ;

    EXTEND FORM invoices
        PROPERTIES(d2) READONLY description###prop###invoiceDetail
    ;
END

META defineCertificateShipment(prop, caption)
    prop###shipmentDetail = ABSTRACT prop (shipmentDetail);
    description###prop###shipmentDetail caption (shipmentDetail) = description###prop(prop###shipmentDetail(shipmentDetail));
    prop###userShipment = DATA prop (userShipment);
    description###prop###userShipment caption (userShipment) = description###prop(prop###userShipment(userShipment));
    data###prop###userShipmentDetail = DATA prop (userShipmentDetail);
    prop###userShipmentDetail (userShipmentDetail) =
        UNION OVERRIDE prop###userShipment(userShipmentUserShipmentDetail(userShipmentDetail)), data###prop###userShipmentDetail(userShipmentDetail);
    description###prop###userShipmentDetail caption (userShipmentDetail) = description###prop(prop###userShipmentDetail(userShipmentDetail));
    prop###shipmentDetail(detail) += prop###userShipmentDetail(detail);
    prop###shipmentDetail(detail) += prop###invoiceDetail(invoiceDetailInvoiceShipmentDetail(detail));

    EXTEND FORM userShipment
        PROPERTIES(s) description###prop###userShipment TODRAW d2 FORCE PANEL
        PROPERTIES(d2) description###prop###userShipmentDetail
    ;

    EXTEND FORM shipments
        PROPERTIES(d2) READONLY description###prop###shipmentDetail
    ;
END

META defineCertificateBatch(prop, caption)
    prop###batch = ABSTRACT prop (batch);
    description###prop###batch caption (batch) = description###prop(prop###batch(batch));
    EXTEND FORM currentBalanceSkuStock
        PROPERTIES description###prop###batch(bt)
    ;
    EXTEND FORM balanceSkuStock
        PROPERTIES description###prop###batch(bt)
    ;
    EXTEND FORM currentBalanceBatchStock
        PROPERTIES description###prop###batch(bt)
    ;
    EXTEND FORM balanceBatchStock
        PROPERTIES description###prop###batch(bt)
    ;
END

META implementCertificateBatch(prop)
    prop###batch (batch) += prop###shipmentDetail(shipmentDetailShipmentBatch(batch));
END