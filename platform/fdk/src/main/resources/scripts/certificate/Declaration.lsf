MODULE Declaration;

REQUIRE Certificate, Shipment, CustomsGroup;

CLASS declaration 'Таможенная декларация' : certificate, numberedObject;

@defineDocumentHeaderTimePrefix(declaration, ,' создания');
@implementDocumentHeaderTimePrefix(certificate, , declaration, );
seriesCertificate(declaration) += seriesObject(declaration) AND declaration IS declaration;
numberCertificate(declaration) += numberObject(declaration) AND declaration IS declaration;

descriptionDeclaration 'Таможенная декларация' (declaration) =
    [FORMULA STRING[100] ' \'№\' || CAST($1 AS TEXT) ||  \' от \' || CAST($2 AS TEXT)']
    (seriesNumberObject(declaration), dateDeclaration(declaration));

@defineCertificateBatch(declaration, 'Таможенная декларация');

legalEntityDeclaration = DATA legalEntity(declaration);
namelegalEntityDeclaration 'Импортёр' (declaration) = name(legalEntityDeclaration(declaration));

currencyDeclaration = DATA currency(declaration);
nameCurrencyDeclaration 'Валюта' (declaration) = name(currencyDeclaration(declaration));

homeCurrencyDeclaration = DATA currency(declaration);
nameHomeCurrencyDeclaration 'Валюта (расч.)' (declaration) = name(homeCurrencyDeclaration(declaration));

typeExchangeDeclaration 'Тип обмена (ИД)' = DATA typeExchange(declaration);
nameTypeExchangeDeclaration 'Тип обмена' (declaration) = name(typeExchangeDeclaration(declaration));

rateExchangeDeclaration 'Курс' (declaration) = rateTypeExchangeCurrencyDate(typeExchangeDeclaration(declaration), currencyDeclaration(declaration), dateDeclaration(declaration));
roundCurrencyDeclaration(number, declaration) = roundCurrency(number, homeCurrencyDeclaration(declaration));


FORM declaration 'Таможенная декларация'
    OBJECTS d = declaration FIXED PANEL
    PROPERTIES(d) seriesObject, numberObject, dateDeclaration, timeDeclaration, namelegalEntityDeclaration, nameCurrencyDeclaration,
                  nameHomeCurrencyDeclaration, nameTypeExchangeDeclaration, rateExchangeDeclaration
    EDIT declaration OBJECT d
;

DESIGN declaration FROM DEFAULT{
    main{
        NEW headContainer {
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY(seriesObject);
            ADD PROPERTY(numberObject);
            ADD PROPERTY(dateDeclaration);
            ADD PROPERTY(timeDeclaration);
            ADD PROPERTY(namelegalEntityDeclaration);
            ADD PROPERTY(nameCurrencyDeclaration);
            ADD PROPERTY(nameHomeCurrencyDeclaration);
            ADD PROPERTY(nameTypeExchangeDeclaration);
            ADD PROPERTY(rateExchangeDeclaration);
        }
    }
    ADD functions.box;
}

FORM declarations 'Таможенные декларации'
    OBJECTS d = declaration
    PROPERTIES(d) READONLY seriesNumberObject, dateTimeDeclaration, namelegalEntityDeclaration, nameCurrencyDeclaration
    PROPERTIES(d) ADDFORM, EDITFORM, delete
;

FORM declarationsDialog 'Таможенные декларация'
    OBJECTS d = declaration
    PROPERTIES(d) READONLY seriesNumberObject, dateTimeDeclaration, namelegalEntityDeclaration, nameCurrencyDeclaration,
                  nameHomeCurrencyDeclaration, nameTypeExchangeDeclaration, rateExchangeDeclaration

    DIALOG declaration OBJECT d
;

NAVIGATOR {
    certificateNavigator {
        ADD declarations;
    }
}