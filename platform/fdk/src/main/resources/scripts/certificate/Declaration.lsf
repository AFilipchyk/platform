MODULE Declaration;

REQUIRE Certificate, Shipment, CustomsGroup;

CLASS declaration 'Таможенная декларация' : certificate, numeratedObject;

TABLE declaration(declaration);

CLASS declarationPosted 'Проведенная таможенная декларация' : declaration, postedObject;

@defineNumeratedObjectDefault(declaration, 'Нумератор для деклараций', 'ДД');

@defineDocumentHeaderTimePrefix(declaration, ,' создания');
@implementDocumentHeaderTimePrefix(certificate, , declaration, );

seriesCertificate(declaration) += seriesObject(declaration) AND declaration IS declaration;
numberCertificate(declaration) += numberObject(declaration) AND declaration IS declaration;

descriptionDeclaration 'Таможенная декларация' (declaration) =
    [FORMULA STRING[100] ' \'№\' || CAST($1 AS TEXT) ||  \' от \' || CAST($2 AS TEXT)']
    (seriesNumberObject(declaration), dateDeclaration(declaration));

@defineCertificateObjectBatch(declaration, 'Таможенная декларация');

customsZoneDeclaration = DATA customsZone(declaration);
nameCustomsZoneDeclaration 'Таможенная зона' (declaration) = name(customsZoneDeclaration(declaration));

currencyCustomsZoneDeclaration (declaration) = currencyCustomsZone(customsZoneDeclaration(declaration));
nameCurrencyCustomsZoneDeclaration 'Валюта платежей' (declaration) = name(currencyCustomsZoneDeclaration(declaration));

legalEntityDeclaration = DATA legalEntity(declaration);
nameLegalEntityDeclaration 'Импортёр' (declaration) = name(legalEntityDeclaration(declaration));

currencyDeclaration = DATA currency(declaration);
nameCurrencyDeclaration 'Валюта накладных' (declaration) = name(currencyDeclaration(declaration));

homeCurrencyDeclaration = DATA currency(declaration);
nameHomeCurrencyDeclaration 'Валюта (расч.)' (declaration) = name(homeCurrencyDeclaration(declaration));

typeExchangeDeclaration = DATA typeExchange(declaration);
nameTypeExchangeDeclaration 'Тип обмена' (declaration) = name(typeExchangeDeclaration(declaration));

CONSTRAINT currencyTypeExchange(typeExchangeDeclaration(declaration)) != homeCurrencyDeclaration(declaration)
    CHECKED BY homeCurrencyDeclaration MESSAGE 'Тип обмена должен соответствовать расчётной валюте';

rateExchangeDeclaration 'Курс для накладных' (declaration) = rateTypeExchangeCurrencyDate(typeExchangeDeclaration(declaration), currencyDeclaration(declaration), dateDeclaration(declaration));
roundCurrencyDeclaration(number, declaration) = roundPriceCurrency(number, currencyTypeExchange(typeExchangeDeclaration(declaration)));

rateExchangeCustomsZoneDeclaration 'Курс платежей' = rateTypeExchangeCurrencyDate(typeExchangeDeclaration(declaration), currencyCustomsZoneDeclaration(declaration), dateDeclaration(declaration));


FORM declaration 'Таможенная декларация'
    OBJECTS d = declaration FIXED PANEL
    PROPERTIES(d) objectClassName, nameNumeratorObject, numberObject, seriesObject, dateDeclaration, timeDeclaration,
                  nameLegalEntityDeclaration, nameCurrencyDeclaration, nameCustomsZoneDeclaration, nameHomeCurrencyDeclaration,
                  nameTypeExchangeDeclaration, rateExchangeDeclaration, nameCurrencyCustomsZoneDeclaration, rateExchangeCustomsZoneDeclaration
    EDIT declaration OBJECT d
;

DESIGN declaration FROM DEFAULT{
    main{
        NEW topContainer {
            childConstraints = TO THE BOTTOM;
            NEW headContainer {
                childConstraints = TO THE RIGHTBOTTOM;
                caption = 'Шапка документа';
                ADD PROPERTY(objectClassName);
                ADD PROPERTY(nameNumeratorObject);
                ADD PROPERTY(seriesObject);
                ADD PROPERTY(numberObject);
                ADD PROPERTY(dateDeclaration);
                ADD PROPERTY(timeDeclaration);

            }

            NEW rightContainer {
                childConstraints = TO THE RIGHT;
                NEW parameterContainer {
                    caption = 'Параметры документа';
                    childConstraints = TO THE BOTTOM;
                    ADD PROPERTY(nameLegalEntityDeclaration);
                    ADD PROPERTY(nameCustomsZoneDeclaration);
                }

                NEW currencyContainer {
                    caption = 'Валюты и курсы';
                    childConstraints = TO THE BOTTOM;
                    ADD PROPERTY(nameHomeCurrencyDeclaration);
                    ADD PROPERTY(nameTypeExchangeDeclaration);
                    ADD PROPERTY(nameCurrencyDeclaration);
                    ADD PROPERTY(rateExchangeDeclaration);
                    ADD PROPERTY(nameCurrencyCustomsZoneDeclaration);
                    ADD PROPERTY(rateExchangeCustomsZoneDeclaration);
                }
            }
        }
    }
    ADD functions.box;
}

FORM declarations 'Таможенные декларации'
    OBJECTS d = declaration
    PROPERTIES(d) READONLY objectClassName, numberObject, seriesObject, dateDeclaration, timeDeclaration, nameLegalEntityDeclaration,
                  nameHomeCurrencyDeclaration, nameCurrencyDeclaration, rateExchangeDeclaration, nameCurrencyCustomsZoneDeclaration, rateExchangeCustomsZoneDeclaration
    PROPERTIES(d) ADDFORM, EDITFORM, delete
;

FORM declarationsDialog 'Таможенные декларация'
    OBJECTS d = declaration
    PROPERTIES(d) READONLY seriesNumberObject, dateTimeDeclaration, nameLegalEntityDeclaration, nameCurrencyDeclaration,
                  nameHomeCurrencyDeclaration, nameTypeExchangeDeclaration, rateExchangeDeclaration

    DIALOG declaration OBJECT d
;

NAVIGATOR {
    customsDocuments {
        ADD declarations;
    }
}