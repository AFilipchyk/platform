MODULE CustomsGroup;

REQUIRE Certificate, Hierarchy, Utils, Historizable, Tax;

CLASS customsZone 'Таможенная зона' : named;
TABLE customsZone(customsZone);

currencyCustomsZone = DATA currency(customsZone);
nameCurrencyCustomsZone 'Валюта' (customsZone) = name(currencyCustomsZone(customsZone)) IN base;

CLASS customsGroup 'Позиция ТН ВЭД' : named;
TABLE customsGroup(customsGroup);

codeCustomsGroup 'Код' = DATA STRING[10](customsGroup);

@defineHierarchy(customsGroup);

customsZoneCustomsGroup = DATA customsZone(customsGroup);
nameCustomsZoneCustomsGroup 'Таможенная зона' (customsGroup) = name(customsZoneCustomsGroup(customsGroup)) IN base;

TABLE customsGroupData(customsGroup, DATE);

@defineHistorizableCustom(supplierVATCustomsGroup, 'НДС таможни', range, numberRange, customsGroup, codeCustomsGroup, base);

dataValueSupplierVATCustomsGroupDate 'НДС поставщика, %' (customsGroup, date) = valueRateRangeDate(dataSupplierVATCustomsGroupDate(customsGroup, date), date);
valueCurrentRateSupplierVATCustomsGroup 'НДС поставщика, %' (customsGroup) = valueCurrentRateRange(supplierVATCustomsGroup(customsGroup));

@defineHistorizable(percentDutyCustomsGroup, 'Пошлина (%)', NUMERIC[14,3], customsGroup, codeCustomsGroup, base);
@defineHistorizable(weightDutyCustomsGroup, 'Пошлина (евро)', NUMERIC[14,3], customsGroup, codeCustomsGroup, base);

@defineHistorizable(registrationCustomsGroup, 'Таможенный сбор', NUMERIC[14,3], customsGroup, codeCustomsGroup, base);


FORM customsZone 'Таможенная зона'
    OBJECTS cz = customsZone FIXED PANEL
    PROPERTIES(cz) name, nameCurrencyCustomsZone
    EDIT customsGroup OBJECT cz
;

FORM customsZones 'Таможенная зона'
    OBJECTS cz = customsZone
    PROPERTIES(cz) READONLY name, nameCurrencyCustomsZone
    PROPERTIES(cz) ADDFORM, EDITFORM, delete
;

FORM customsGroup 'Позиция ТН ВЭД'
    OBJECTS cg = customsGroup FIXED PANEL
    PROPERTIES(cg) codeCustomsGroup, name, nameParentCustomsGroup, nameCustomsZoneCustomsGroup,
                   registrationCustomsGroup ON CHANGE EXEC dialogRegistrationCustomsGroup(cg),
                   valueCurrentRateSupplierVATCustomsGroup ON CHANGE EXEC dialogSupplierVATCustomsGroup(cg),
                   percentDutyCustomsGroup ON CHANGE EXEC dialogPercentDutyCustomsGroup(cg),
                   weightDutyCustomsGroup ON CHANGE EXEC dialogWeightDutyCustomsGroup(cg)

    EDIT customsGroup OBJECT cg
;

addCustomsGroup 'Добавить' = ACTION (customsGroup) NEWSESSION {
    ADDOBJ customsGroup;
    FOR cg == addedObject() DO {
        SET parentCustomsGroup(cg) <- customsGroup AS customsGroup;
        FORM customsGroup OBJECTS cg=addedObject() MODAL;
        IF formResult() == formResult.ok THEN
            EXEC apply();
    };
} TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

FORM customsGroups 'Позиции ТН ВЭД'
    OBJECTS cz = customsZone FIXED PANEL
    PROPERTIES(cz) SELECTOR name, nameCurrencyCustomsZone

    TREE treeGroups gcg=customsGroup PARENT parentCustomsGroup
    PROPERTIES READONLY name(gcg)
    PROPERTIES(gcg) addCustomsGroup, EDITFORM
    ORDER BY name

    OBJECTS cg = customsGroup
    PROPERTIES(cg) READONLY codeCustomsGroup, name, canonicalNameCustomsGroup, nameParentCustomsGroup, registrationCustomsGroup,
                   valueCurrentRateSupplierVATCustomsGroup, percentDutyCustomsGroup, weightDutyCustomsGroup
    PROPERTIES(cg) ADDFORM, EDITFORM, delete

    FILTERS customsZoneCustomsGroup(cg) == cz

    FILTERGROUP filters
        FILTER 'Все листья' 'F10' isParentLeafCustomsGroupCustomsGroup(cg, gcg) DEFAULT
        FILTER 'Всех потомков' 'F9' isParentCustomsGroupCustomsGroup(cg, gcg)
        FILTER 'Только непосредственных потомков' 'F8' parentCustomsGroup(cg) == gcg

    DIALOG customsGroup OBJECT cg
;


NAVIGATOR {
    certificateNavigator {
        ADD customsZones;
        ADD customsGroups;
    }
}





