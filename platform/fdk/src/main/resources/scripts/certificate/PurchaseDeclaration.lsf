MODULE PurchaseDeclaration;

REQUIRE Declaration, PurchaseCertificate, PurchaseInvoice;

NAMESPACE Purchase;

@defineDocumentCertificateObject(declaration, invoice, i, 'Таможенная декларация');
@defineDocumentCertificateObject(declaration, shipment, s, 'Таможенная декларация');

declarationShipmentDetail(detail) += declarationInvoiceDetail(invoiceDetailInvoiceShipmentDetail(detail));

declarationBatch (batch) += declarationShipmentDetail(shipmentDetailShipmentBatch(batch));

//---------------------------------- накладные для декларации --------------------------------------------------------//
inDeclarationInvoice 'Вкл.' (declaration, invoice) = declarationUserInvoice(invoice)==declaration;

inDeclarationInvoiceDetail 'Вкл.' (declaration, invoiceDetail) = dataDeclarationUserInvoiceDetail(invoiceDetail)==declaration;

invoicesDeclaration 'Накладные' (declaration) =
    GROUP CONCAT shortDescriptionInvoice(invoice) IF inDeclarationInvoice (declaration, invoice), ','
              BY declaration ORDER invoice PERSISTENT MINCHARWIDTH 40 PREFCHARWIDTH 80 MAXCHARWIDTH 120;

backgroundCustomedInvoice 'Цвет' (invoice) = RGB(255, 224, 224) IF invoice IS invoice;

// свойства товаров
nameCustomsSku 'Наименование (декл.)' = ABSTRACT STRING[200](sku);
nameCustomsSku(sku) += nameSku(sku);

nameCustomsUserInvoiceDetail 'Наименование (декл.)' (userInvoiceDetail) = nameCustomsSku(skuUserInvoiceDetail(userInvoiceDetail));

countryUserInvoiceDetail (userInvoiceDetail) = countrySku(skuUserInvoiceDetail(userInvoiceDetail));
nameCountryUserInvoiceDetail 'Страна' (userInvoiceDetail) = name(countryUserInvoiceDetail(userInvoiceDetail));

UOMUserInvoiceDetail(userInvoiceDetail) = UOMSku(skuUserInvoiceDetail(userInvoiceDetail));

customsGroupInvoiceDetail = ABSTRACT customsGroup(invoiceDetail);
customsGroupUserInvoiceDetail = DATA customsGroup(userInvoiceDetail);
customsGroupInvoiceDetail(detail) += customsGroupUserInvoiceDetail(detail);

codeCustomsGroupInvoiceDetail 'ТН ВЭД' (invoiceDetail) = codeCustomsGroup(customsGroupInvoiceDetail(invoiceDetail)) MINCHARWIDTH 10;

sumNetWeightInvoiceDetail 'Вес нетто' (invoiceDetail) = netWeightSku(skuUserInvoiceDetail(invoiceDetail)) * quantityUserInvoiceDetail(invoiceDetail);
sumGrossWeightInvoiceDetail 'Вес брутто' (invoiceDetail) = grossWeightSku(skuUserInvoiceDetail(invoiceDetail)) * quantityUserInvoiceDetail(invoiceDetail);

FORM declarationInvoices 'Накладные декларации'
    OBJECTS d=declaration FIXED PANEL
    PROPERTIES(d) seriesNumberObject, nameLegalEntityDeclaration, nameCurrencyDeclaration, dateTimeDeclaration

    OBJECTS i=userInvoice
    PROPERTIES (d, i) inDeclarationInvoice
    PROPERTIES (i) READONLY numberObject, seriesObject, dateUserInvoice, timeUserInvoice, nameSupplierUserInvoice, nameCustomerUserInvoice
    FILTERS inDeclarationInvoice(d, i) OR d AS declaration AND NOT declarationUserInvoice(i)

    OBJECTS id=userInvoiceDetail
    PROPERTIES(d, id) inDeclarationInvoiceDetail
    PROPERTIES(id) READONLY indexUserInvoiceDetail, idBarcodeSkuUserInvoiceDetail, nameSkuUserInvoiceDetail, shortNameUOMSkuUserInvoiceDetail,
                   quantityUserInvoiceDetail, priceUserInvoiceDetail
    FILTERS invoiceInvoiceDetail(id)==i

    FILTERGROUP filtersCurrency
        FILTER 'Только с валютой декларации' 'F10' currencyDeclaration(d)==currencyUserInvoice(i) DEFAULT

    FILTERGROUP filtersImporter
        FILTER 'Только импортёра декларации' 'F11' legalEntityDeclaration(d)==customerUserInvoice(i) DEFAULT
;

showFormDeclarationInvoices 'Накладные декларации' = ACTION (declaration) {
    FORM declarationInvoices OBJECTS d=declaration MODAL;
    EXEC apply();
} TOOLBAR;

EXTEND FORM declaration
    PROPERTIES(d) invoicesDeclaration

    OBJECTS id=userInvoiceDetail
    PROPERTIES(id) READONLY seriesNumberInvoiceDetail, idBarcodeSkuUserInvoiceDetail, nameSkuUserInvoiceDetail, shortNameUOMSkuUserInvoiceDetail,
                   nameCustomsUserInvoiceDetail, nameCountryUserInvoiceDetail, sumNetWeightInvoiceDetail, quantityUserInvoiceDetail, priceUserInvoiceDetail
    PROPERTIES(id) codeCustomsGroupInvoiceDetail
    PROPERTIES(id) READONLY homePriceInvoiceDetail, sumHomeInvoiceDetail

    PROPERTIES (d) showFormDeclarationInvoices TODRAW id FORCE PANEL TOOLBAR
    FILTERS declarationUserInvoiceDetail(id) == d
;

EXTEND DESIGN declaration {
    main {
        parameterContainer {
            ADD PROPERTY(invoicesDeclaration);
        }

        ADD id.box {
            fillVertical = 2;
        }
    }
    ADD functions.box;
}

isCustomInvoice 'Таможенные платежи' = ABSTRACT BOOLEAN(invoice);
isCustomUserInvoice 'Таможенные платежи' = DATA BOOLEAN(userInvoice) IN documentPrmGroup;
isCustomInvoice(invoice) += isCustomUserInvoice(invoice);

//------------------------------------------ позиции декларации -------------------------------------------------------//
TABLE declarationNameUOMCustomsGroupCountryPrice(declaration, STRING[200], UOM, customsGroup, country, NUMERIC[14,2]);

quantityGroupDeclaration 'Кол-во' = GROUP SUM quantityUserInvoiceDetail(userInvoiceDetail)
                                           BY declarationUserInvoiceDetail(userInvoiceDetail),
                                              nameCustomsUserInvoiceDetail(userInvoiceDetail),
                                              UOMUserInvoiceDetail(userInvoiceDetail),
                                              customsGroupInvoiceDetail(userInvoiceDetail),
                                              countryUserInvoiceDetail(userInvoiceDetail),
                                              priceUserInvoiceDetail(userInvoiceDetail) PERSISTENT;

CLASS declarationDetail 'Позиция декларации';
TABLE declarationDetail(declarationDetail);

declarationDeclarationDetail = DATA declaration(declarationDetail);

dateDeclarationDetail 'Дата' (declarationDetail) = dateDeclaration(declarationDeclarationDetail(declarationDetail));

@defineDocumentPosted(declaration);

rateExchangeDeclarationDetail 'Курс' = rateExchangeDeclaration(declarationDeclarationDetail(declarationDetail));

roundCurrencyDeclarationDetail '' (number, declarationDetail) = roundCurrencyDeclaration(number, declarationDeclarationDetail(declarationDetail));

nameCustomsDeclarationDetail 'Наименование' = DATA STRING[200](declarationDetail);

UOMDeclarationDetail = DATA UOM(declarationDetail);
shortNameUOMDeclarationDetail 'Ед. изм.' (declarationDetail) = shortName(UOMDeclarationDetail(declarationDetail));

customsGroupDeclarationDetail = DATA customsGroup(declarationDetail);
codeCustomsGroupDeclarationDetail 'ТН ВЭД' (declarationDetail) = codeCustomsGroup(customsGroupDeclarationDetail(declarationDetail)) MINCHARWIDTH 10 MAXCHARWIDTH 10 PREFCHARWIDTH 10;

countryDeclarationDetail = DATA country(declarationDetail);
nameCountryDeclarationDetail 'Страна' (declarationDetail) = name(countryDeclarationDetail(declarationDetail));
sidCountryDeclarationDetail 'Код страны' (declarationDetail) = sidCountry(countryDeclarationDetail(declarationDetail));
sidOrigin2CountryDeclarationDetail 'Код страны' (declarationDetail) = sidOrigin2Country(countryDeclarationDetail(declarationDetail));

priceDeclarationDetail 'Цена' = DATA NUMERIC[14,3](declarationDetail);

numberDefaultDeclarationDetail 'Номер' (declarationDetail) =
        PARTITION SUM 1 IF declarationDetail IS declarationDetail BY declarationDeclarationDetail(declarationDetail)
        ORDER declarationDetail;

userNumberDeclarationDetail 'Пользовательский номер' (declarationDetail) = DATA INTEGER (declarationDetail);
numberDeclarationDetail 'Номер' (declarationDetail) = UNION OVERRIDE numberDefaultDeclarationDetail(declarationDetail), userNumberDeclarationDetail(declarationDetail);

declarationDetailUserNumberNameCustoms (number, name) = GROUP UNIQUE declarationDetail BY
        userNumberDeclarationDetail(declarationDetail), nameCustomsDeclarationDetail(declarationDetail)
        WHERE declarationDetail IS declarationDetail;

declarationDetailInvoiceDetail(invoiceDetail) = DATA declarationDetail(invoiceDetail);
inInvoiceDetailDeclarationDetail 'Вкл.' (invoiceDetail, declarationDetail) = declarationDetailInvoiceDetail(invoiceDetail)==declarationDetail;

sumDataNetWeightDeclarationDetail 'Вес нетто' = DATA NUMERIC[14,3](declarationDetail);
sumDataGrossWeightDeclarationDetail 'Вес брутто' = DATA NUMERIC[14,3](declarationDetail);

// агрегированные свойства
sumNetWeightDeclarationDetail 'Вес нетто' (declarationDetail) = GROUP SUM sumNetWeightInvoiceDetail(invoiceDetail) BY declarationDetailInvoiceDetail(invoiceDetail) PERSISTENT;
overSumNetWeightDeclarationDetail 'Вес нетто' (declarationDetail) = UNION OVERRIDE sumNetWeightDeclarationDetail(declarationDetail), sumDataNetWeightDeclarationDetail(declarationDetail);

sumGrossWeightDeclarationDetail 'Вес брутто' (declarationDetail) = GROUP SUM sumGrossWeightInvoiceDetail(invoiceDetail) BY declarationDetailInvoiceDetail(invoiceDetail) PERSISTENT;
overSumGrossWeightDeclarationDetail 'Вес брутто' (declarationDetail) =  UNION OVERRIDE sumGrossWeightDeclarationDetail(declarationDetail), sumDataGrossWeightDeclarationDetail(declarationDetail);

quantityDeclarationDetail 'Кол-во' (declarationDetail) = GROUP SUM quantityUserInvoiceDetail(invoiceDetail) BY declarationDetailInvoiceDetail(invoiceDetail) PERSISTENT;

sumDeclarationDetail 'Стоимость (валют.)' (declarationDetail) = priceDeclarationDetail(declarationDetail) * quantityDeclarationDetail(declarationDetail);

sumRateDeclarationDetail 'Стоимость (расч.)' (declarationDetail) = sumDeclarationDetail(declarationDetail) * rateExchangeDeclarationDetail(declarationDetail) MINCHARWIDTH 12 PERSISTENT;

sumRateDeclaration 'Сумма' (declaration) = GROUP SUM sumRateDeclarationDetail(declarationDetail) BY declarationDeclarationDetail(declarationDetail) PERSISTENT;

sumDataDeclarationDetail 'Стоимость' (declarationDetail) = DATA NUMERIC[14,3](declarationDetail) MINCHARWIDTH 12;

inDeclarationDetailInvoice = GROUP SUM 1 BY declarationDetailInvoiceDetail(invoiceDetail), invoiceInvoiceDetail(invoiceDetail);

invoicesDeclarationDetail 'Накладные' (declarationDetail) =
    GROUP CONCAT seriesNumberObject(invoice) IF inDeclarationDetailInvoice(declarationDetail, invoice) , ','
              BY declarationDetail ORDER invoice PERSISTENT MINCHARWIDTH 20 PREFCHARWIDTH 40 MAXCHARWIDTH 60;

// Таможенные платежи
// пошлины
sumWeightDutyDeclarationDetail 'Пошлина по весу (валют.)' (declarationDetail) = overSumNetWeightDeclarationDetail(declarationDetail)*
                                                                                weightDutyCustomsGroupDate(customsGroupDeclarationDetail(declarationDetail), dateDeclarationDetail(declarationDetail));

sumRateWeightDutyDeclarationDetail 'Пошлина по весу' (declarationDetail) = sumWeightDutyDeclarationDetail(declarationDetail) *
                                                                           rateExchangeCustomsZoneDeclaration(declarationDeclarationDetail(declarationDetail)) PERSISTENT;

sumPriceDutyDeclarationDetail 'Пошлина по стоимости (валют.)' (declarationDetail) = sumDeclarationDetail(declarationDetail) *
                                                                                    (percentDutyCustomsGroupDate(customsGroupDeclarationDetail(declarationDetail), dateDeclarationDetail(declarationDetail))/100);

sumRatePriceDutyDeclarationDetail 'Пошлина по стоимости' (declarationDetail) = sumPriceDutyDeclarationDetail(declarationDetail) *
                                                                               rateExchangeDeclarationDetail(declarationDetail) PERSISTENT;

sumRateDutyDeclarationDetail 'Пошлина (расч.)' (declarationDetail) = roundCurrencyDeclarationDetail((UNION MAX sumRateWeightDutyDeclarationDetail(declarationDetail),
                                                                                                               sumRatePriceDutyDeclarationDetail(declarationDetail)), declarationDetail) PERSISTENT MINCHARWIDTH 12;

sumDutyDataDeclarationDetail 'Пошлина' (declarationDetail) = DATA NUMERIC[14,3](declarationDetail) MINCHARWIDTH 12;

sumRateDutyDeclaration 'Пошлина' (declaration) = GROUP SUM sumDutyDataDeclarationDetail(declarationDetail) BY declarationDeclarationDetail(declarationDetail) PERSISTENT MINCHARWIDTH 12;

//НДС
VATRangeDataDeclarationDetail 'Шкала НДС (ИД)' = DATA range(declarationDetail);
numberVATRangeDataDeclarationDetail 'Шкала НДС' (declarationDetail) = numberRange(VATRangeDataDeclarationDetail(declarationDetail));
percentVATRangeDeclarationDetail 'НДС, %' (declarationDetail) = valueRateRangeDate(VATRangeDataDeclarationDetail(declarationDetail), dateDeclarationDetail(declarationDetail));

numberVATRangeCustomGroupDeclarationDetail 'Шкала НДС (расч.)' (declarationDetail) = numberRange(supplierVATCustomsGroupDate(customsGroupDeclarationDetail(declarationDetail), dateDeclarationDetail(declarationDetail)));
percentVATCustomsGroupDeclarationDetail 'НДС, %' (declarationDetail) = dataValueSupplierVATCustomsGroupDate(customsGroupDeclarationDetail(declarationDetail), dateDeclarationDetail(declarationDetail));

percentVATDeclarationDetail 'НДС, %' (declarationDetail) = UNION OVERRIDE percentVATCustomsGroupDeclarationDetail(declarationDetail), percentVATRangeDeclarationDetail(declarationDetail);

sumRateVATDeclarationDetail 'НДС (расч.)' (declarationDetail) = roundCurrencyDeclarationDetail((sumRateDeclarationDetail(declarationDetail) + sumRateDutyDeclarationDetail(declarationDetail)) *
                                                                 (percentVATDeclarationDetail(declarationDetail)/100), declarationDetail) MINCHARWIDTH 12;

sumVATDataDeclarationDetail 'НДС' (declarationDetail) = DATA NUMERIC[14,3](declarationDetail) MINCHARWIDTH 12;

sumRateVATDeclaration 'НДС' (declaration) = GROUP SUM sumVATDataDeclarationDetail(declarationDetail) BY declarationDeclarationDetail(declarationDetail) PERSISTENT MINCHARWIDTH 12;

// оформление
sumRegistrationDeclarationDetail 'Таможенный сбор (валют.)' (declarationDetail) = registrationCustomsGroupDate(customsGroupDeclarationDetail(declarationDetail), dateDeclarationDetail(declarationDetail));

sumRegistrationMaxDeclaration 'Таможенный сбор (валют.)' (declaration) = GROUP MAX sumRegistrationDeclarationDetail(declarationDetail) BY declarationDeclarationDetail(declarationDetail) PERSISTENT;

sumRegistrationRateDeclaration 'Таможенный сбор' (declaration) = roundCurrencyDeclaration(sumRegistrationMaxDeclaration(declaration) *
                                                                                          rateExchangeCustomsZoneDeclaration(declaration), declaration) MINCHARWIDTH 12;

sumRegistrationDataDeclaration 'Таможенный сбор' = DATA NUMERIC[14,3](declaration);

sumRegistrationDeclaration 'Таможенный сбор' (declaration) = UNION OVERRIDE sumRegistrationRateDeclaration(declaration), sumRegistrationDataDeclaration(declaration);

setSumDeclarationDetail 'Рассчитать суммы' = ACTION (declaration) {
   FOR declarationDeclarationDetail(declarationDetail) == declaration DO {
      SET sumDataDeclarationDetail(declarationDetail) <- sumRateDeclarationDetail(declarationDetail);
      SET sumDutyDataDeclarationDetail(declarationDetail) <- sumRateDutyDeclarationDetail(declarationDetail);
      SET sumVATDataDeclarationDetail(declarationDetail) <- sumRateVATDeclarationDetail(declarationDetail);
   };
};

toCreateDeclarationDetails 'Создать позиции' = ACTION (declaration) {
    FOR quantityGroupDeclaration(declaration, customsName, UOM, customsGroup, country, price)>0 ADDOBJ dd=declarationDetail DO {
        SET declarationDeclarationDetail(dd) <- declaration;
        SET nameCustomsDeclarationDetail(dd) <- customsName;
        SET UOMDeclarationDetail(dd) <- UOM;
        SET customsGroupDeclarationDetail(dd) <- customsGroup;
        SET countryDeclarationDetail(dd) <- country;
        SET priceDeclarationDetail(dd) <- price;

        SET declarationDetailInvoiceDetail(invoiceDetail) <- dd WHERE declarationUserInvoiceDetail(invoiceDetail) == declaration AND
            nameCustomsUserInvoiceDetail(invoiceDetail) == customsName AND UOMUserInvoiceDetail(invoiceDetail) == UOM AND
            customsGroupInvoiceDetail(invoiceDetail) == customsGroup AND countryUserInvoiceDetail(invoiceDetail) == country AND
            priceUserInvoiceDetail(invoiceDetail) == price;
    };

    EXEC setSumDeclarationDetail(declaration);

} TOOLBAR;

EXTEND FORM declaration
    PROPERTIES(d) sumRateDeclaration, sumRegistrationDeclaration, sumRateDutyDeclaration, sumRateVATDeclaration

    OBJECTS dd=declarationDetail
    PROPERTIES(id, dd) inInvoiceDetailDeclarationDetail
    PROPERTIES(dd) numberDeclarationDetail, invoicesDeclarationDetail READONLY, nameCustomsDeclarationDetail, shortNameUOMDeclarationDetail, nameCountryDeclarationDetail
    PROPERTIES(dd) overSumNetWeightDeclarationDetail, overSumGrossWeightDeclarationDetail
    PROPERTIES(dd) codeCustomsGroupDeclarationDetail, quantityDeclarationDetail READONLY, priceDeclarationDetail
    PROPERTIES(dd) READONLY sumDeclarationDetail, sumRateDeclarationDetail, sumRateDutyDeclarationDetail, numberVATRangeCustomGroupDeclarationDetail
    PROPERTIES(dd) numberVATRangeDataDeclarationDetail
    PROPERTIES(dd) READONLY percentVATDeclarationDetail, sumRateVATDeclarationDetail
    PROPERTIES(dd) sumDataDeclarationDetail, sumDutyDataDeclarationDetail, sumVATDataDeclarationDetail, delete

    PROPERTIES (d) toCreateDeclarationDetails TODRAW dd FORCE PANEL TOOLBAR
    PROPERTIES (d) setSumDeclarationDetail TODRAW dd FORCE PANEL TOOLBAR
    FILTERS declarationDeclarationDetail(dd) == d

    EVENTS
        ON OK EXEC prePostDeclaration(d)
;

EXTEND DESIGN declaration {
    main {
        rightContainer {
            NEW totalContainer {
                caption = 'Суммы документа';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(sumRateDeclaration);
                ADD PROPERTY(sumRegistrationDeclaration);
                ADD PROPERTY(sumRateDutyDeclaration);
                ADD PROPERTY(sumRateVATDeclaration);
            }
        }

        ADD dd.box {
            fillVertical = 2;
        }
    }

    PROPERTY(formOk) {
        caption = 'Провести';
    }

    ADD functions.box;
}

EXTEND FORM declarations
    PROPERTIES(d) FORCE GRID isPostedDeclaration BEFORE objectClassName(d), sumRateDeclaration, sumRegistrationDeclaration, sumRateDutyDeclaration, sumRateVATDeclaration

    OBJECTS dd=declarationDetail
    PROPERTIES(dd) READONLY numberDeclarationDetail, nameCustomsDeclarationDetail, nameCountryDeclarationDetail, sumNetWeightDeclarationDetail,
                   codeCustomsGroupDeclarationDetail, quantityDeclarationDetail, priceDeclarationDetail, sumDeclarationDetail,
                   sumRateDeclarationDetail, sumRateDutyDeclarationDetail, numberVATRangeCustomGroupDeclarationDetail,
                   numberVATRangeDataDeclarationDetail, percentVATDeclarationDetail, sumRateVATDeclarationDetail,
                   sumDataDeclarationDetail, sumDutyDataDeclarationDetail, sumVATDataDeclarationDetail
    FILTERS declarationDeclarationDetail(dd)==d
;

//------------------------------------ определение себестоимости -----------------------------------------------------//
// пошлина
coeffInvoiceDetailDeclarationDetail 'Удельный вес' (invoiceDetail) = sumHomeInvoiceDetail(invoiceDetail)/sumRateDeclarationDetail(declarationDetailInvoiceDetail(invoiceDetail))
                                                                            IF declarationDetailInvoiceDetail(invoiceDetail);

calcDutyInvoiceDetail 'Пошлины' (invoiceDetail) = PARTITION UNGROUP sumDutyDataDeclarationDetail
                                                 PROPORTION STRICT ROUND(0) coeffInvoiceDetailDeclarationDetail(invoiceDetail)
                                                                BY declarationDetailInvoiceDetail(invoiceDetail)
                                                             ORDER invoiceDetail PERSISTENT;

priceDutyInvoiceDetail 'Пошлина (ед.)' = ABSTRACT NUMERIC[14,2](invoiceDetail);
priceDutyUserInvoiceDetail 'Пошлина (ед.)' = DATA NUMERIC[14,2](userInvoiceDetail);
priceDutyInvoiceDetail(detail) += priceDutyUserInvoiceDetail(detail);

calcPriceDutyInvoiceDetail 'Пошлина (ед.)' (invoiceDetail) = roundCurrencyDeclaration(calcDutyInvoiceDetail(invoiceDetail)/quantityUserInvoiceDetail(invoiceDetail),
                                                                                      declarationUserInvoiceDetail(invoiceDetail));

sumDutyCostDeclaration 'Пошлина (факт)' (declaration) = GROUP SUM priceDutyUserInvoiceDetail(invoiceDetail) * quantityUserInvoiceDetail(invoiceDetail) BY declarationUserInvoiceDetail(invoiceDetail);

// таможенный сбор
coeffInvoiceDetailDeclaration 'Удельный вес' (invoiceDetail) = sumHomeInvoiceDetail(invoiceDetail)/sumRateDeclaration(declarationInvoiceDetail(invoiceDetail))
                                                               IF declarationInvoiceDetail(invoiceDetail);

calcRegistrationInvoiceDetail 'Таможенный сбор' (invoiceDetail) = PARTITION UNGROUP sumRegistrationDeclaration
                                                                  PROPORTION STRICT ROUND(0) coeffInvoiceDetailDeclaration(invoiceDetail)
                                                                                 BY declarationInvoiceDetail(invoiceDetail)
                                                                              ORDER invoiceDetail PERSISTENT;

priceRegistrationInvoiceDetail 'Таможенный сбор (ед.)' = ABSTRACT NUMERIC[14,2](invoiceDetail);
priceRegistrationUserInvoiceDetail 'Таможенный сбор (ед.)' = DATA NUMERIC[14,2](userInvoiceDetail);
priceRegistrationInvoiceDetail(detail) += priceRegistrationUserInvoiceDetail(detail);

calcPriceRegistrationInvoiceDetail 'Таможенный сбор (ед.)' (invoiceDetail) = roundCurrencyDeclaration(calcRegistrationInvoiceDetail(invoiceDetail)/quantityUserInvoiceDetail(invoiceDetail),
                                                                                                      declarationUserInvoiceDetail(invoiceDetail));

customCostPriceUserInvoiceDetail(invoiceDetail) += priceRegistrationUserInvoiceDetail(invoiceDetail) (+) priceDutyUserInvoiceDetail(invoiceDetail);

sumRegistrationCostDeclaration 'Таможенный сбор (факт)' (declaration) = GROUP SUM priceRegistrationUserInvoiceDetail(invoiceDetail) * quantityUserInvoiceDetail(invoiceDetail) BY declarationUserInvoiceDetail(invoiceDetail);

setPriceInvoiceDetailes 'Рассчитать расходы (за ед.)' = ACTION (declaration) {
   FOR declarationInvoiceDetail(invoiceDetail) == declaration DO {
      SET priceDutyInvoiceDetail(invoiceDetail) <- calcPriceDutyInvoiceDetail(invoiceDetail);
      SET priceRegistrationInvoiceDetail(invoiceDetail) <- calcPriceRegistrationInvoiceDetail(invoiceDetail);
   };
} TOOLBAR;

EXTEND FORM userInvoice
    PROPERTIES(i) BACKGROUND backgroundCustomedInvoice(i) isCustomUserInvoice
    PROPERTIES(d) BACKGROUND backgroundCustomedInvoice(i) codeCustomsGroupInvoiceDetail BEFORE chargePriceUserInvoiceDetail SHOWIF isCustomUserInvoice(i),
                  priceDutyUserInvoiceDetail BEFORE chargePriceUserInvoiceDetail SHOWIF isCustomUserInvoice(i),
                  priceRegistrationUserInvoiceDetail BEFORE chargePriceUserInvoiceDetail SHOWIF isCustomUserInvoice(i)
;

EXTEND FORM invoices
    PROPERTIES(i) READONLY BACKGROUND backgroundCustomedInvoice(i) isCustomInvoice FORCE GRID
    PROPERTIES(d) READONLY BACKGROUND backgroundCustomedInvoice(i) codeCustomsGroupInvoiceDetail BEFORE chargePriceInvoiceDetail SHOWIF isCustomInvoice(i),
                  priceDutyInvoiceDetail BEFORE chargePriceInvoiceDetail SHOWIF isCustomUserInvoice(i),
                  priceRegistrationInvoiceDetail BEFORE chargePriceInvoiceDetail SHOWIF isCustomUserInvoice(i)
;

EXTEND FORM declaration
    PROPERTIES(d) sumRegistrationCostDeclaration, sumDutyCostDeclaration, setPriceInvoiceDetailes TODRAW id
    PROPERTIES(id) priceDutyInvoiceDetail, priceRegistrationInvoiceDetail
;

EXTEND DESIGN declaration {
    main {
        totalContainer {
            ADD PROPERTY(sumRegistrationCostDeclaration);
            ADD PROPERTY(sumDutyCostDeclaration);
        }
    }
    ADD functions.box;
}




