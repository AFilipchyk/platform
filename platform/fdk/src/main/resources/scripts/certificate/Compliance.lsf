MODULE Compliance;

REQUIRE Certificate, Shipment;

CLASS compliance 'Сертификат соответствия' : certificate, numeratedObject;
TABLE compliance(compliance);

CLASS compliancePosted 'Проведенный сертификат' : compliance, postedObject;

@defineDocumentHeaderTimePrefix(compliance, ,' создания');
@defineDocumentHeaderTimePrefix(compliance, to, ' действия');
@implementDocumentHeaderTimePrefix(certificate, , compliance, );
@implementDocumentHeaderTimePrefix(certificate, to, compliance, to);

seriesCertificate(compliance) += seriesObject(compliance) AND compliance IS compliance;
numberCertificate(compliance) += numberObject(compliance) AND compliance IS compliance;
numberToCompliance (number) = GROUP UNIQUE compliance BY numberCertificate(compliance) WHERE compliance IS compliance;

textCompliance 'Текст сертификата' = DATA TEXT (compliance);

sumCompliance 'Стоимость услуг' = DATA NUMERIC[14,3](compliance);

descriptionCompliance 'Сертификат соответствия' (compliance) =
    [FORMULA STRING[100] ' \'Сертификат № \' || CAST($1 AS TEXT) ||  \' от \' || CAST($2 AS TEXT)||  \' действует по \' || CAST($3 AS TEXT)']
    (seriesNumberObject(compliance), dateCompliance(compliance), toDateCompliance(compliance));

@defineCertificateObjectBatch(compliance, 'Сертификат соответствия');

legalEntityCompliance = DATA legalEntity(compliance);
nameLegalEntityCompliance 'Владелец' (compliance) = name(legalEntityCompliance(compliance));

@defineDocumentHeaderStock(compliance, stock, 'Склад');

CONSTRAINT legalEntityCompliance(compliance) AND stockCompliance(compliance) AND NOT
           inLegalEntityStock(legalEntityCompliance(compliance), stockCompliance (compliance))
    CHECKED BY stockCompliance MESSAGE 'Владелец и склад для сертификата не имеют связи';

FORM compliance 'Сертификат соответствия'
    OBJECTS c = compliance FIXED PANEL
    PROPERTIES(c) objectClassName, nameNumeratorObject, numberObject, seriesObject, dateCompliance, timeCompliance, toDateCompliance, toTimeCompliance,
                  nameLegalEntityCompliance, nameStockCompliance, sumCompliance
    EDIT compliance OBJECT c
;

DESIGN compliance FROM DEFAULT{
    main{
        NEW headContainer {
            childConstraints = TO THE RIGHTBOTTOM;
            caption = 'Шапка документа';
            ADD PROPERTY(objectClassName);
            ADD PROPERTY(nameNumeratorObject);
            ADD PROPERTY(seriesObject);
            ADD PROPERTY(numberObject);
            ADD PROPERTY(dateCompliance);
            ADD PROPERTY(timeCompliance);
            ADD PROPERTY(toDateCompliance);
            ADD PROPERTY(toTimeCompliance);
            ADD PROPERTY(nameLegalEntityCompliance);
            ADD PROPERTY(nameStockCompliance);
            ADD PROPERTY(sumCompliance);
        }
    }
    ADD functions.box;
}

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = compliance
    PROPERTIES(c) READONLY objectClassName, numberObject, seriesObject, dateCompliance, timeCompliance, toDateCompliance,
                  toTimeCompliance, nameLegalEntityCompliance, nameStockCompliance, sumCompliance //, textCompliance
    PROPERTIES(c) ADDFORM, EDITFORM, delete
;

FORM compliancesDialog 'Сертификаты соответствия'
    OBJECTS c = compliance
    PROPERTIES(c) READONLY seriesNumberObject, dateTimeCompliance, toDateTimeCompliance, nameLegalEntityCompliance, nameStockCompliance, sumCompliance//, textCompliance

    DIALOG compliance OBJECT c
;

NAVIGATOR {
    customsDocuments {
        ADD compliances;
    }
}