MODULE Compliance;

REQUIRE Certificate, Shipment;

CLASS compliance 'Сертификат соответствия' : certificate, numberedObject;
TABLE compliance(compliance);

@defineDocumentHeaderTimePrefix(compliance, ,' создания');
@defineDocumentHeaderTimePrefix(compliance, to, ' действия');
@implementDocumentHeaderTimePrefix(certificate, , compliance, );
@implementDocumentHeaderTimePrefix(certificate, to, compliance, to);

seriesCertificate(compliance) += seriesObject(compliance) AND compliance IS compliance;
numberCertificate(compliance) += numberObject(compliance) AND compliance IS compliance;
numberToCompliance (number) = GROUP UNIQUE compliance BY numberCertificate(compliance) WHERE compliance IS compliance;

textCompliance 'Текст сертификата' = DATA TEXT (compliance);

descriptionCompliance 'Сертификат соответствия' (compliance) =
    [FORMULA STRING[100] ' \'№\' || CAST($1 AS TEXT) ||  \' от \' || CAST($2 AS TEXT)||  \' действует по \' || CAST($3 AS TEXT)']
    (seriesNumberObject(compliance), dateCompliance(compliance), toDateCompliance(compliance));

@defineCertificateObjectBatch(compliance, 'Сертификат соответствия');

FORM compliance 'Сертификат соответствия'
    OBJECTS c = compliance FIXED PANEL
    PROPERTIES(c) seriesObject, numberObject, dateCompliance, timeCompliance, toDateCompliance, toTimeCompliance
    EDIT compliance OBJECT c
;

DESIGN compliance FROM DEFAULT{
    main{
        ADD PROPERTY(seriesObject);
        ADD PROPERTY(numberObject);
        ADD PROPERTY(dateCompliance);
        ADD PROPERTY(timeCompliance);
        ADD PROPERTY(toDateCompliance);
        ADD PROPERTY(toTimeCompliance);
    }
    ADD functions.box;
}

FORM compliances 'Сертификаты соответствия'
    OBJECTS c = compliance
    PROPERTIES(c) READONLY seriesNumberObject, dateTimeCompliance, toDateTimeCompliance, textCompliance
    PROPERTIES(c) ADDFORM, EDITFORM, delete
;

FORM compliancesDialog 'Сертификаты соответствия'
    OBJECTS c = compliance
    PROPERTIES(c) READONLY seriesNumberObject, dateTimeCompliance, toDateTimeCompliance, textCompliance

    DIALOG compliance OBJECT c
;

NAVIGATOR {
    certificateNavigator {
        ADD compliances;
    }
}