MODULE PriceInterval;

REQUIRE System, Utils;

CLASS priceInterval 'Диапазон цен';
TABLE priceInterval (priceInterval);

currencyPriceInterval = DATA currency(priceInterval);

fromPriceInterval 'от' = DATA NUMERIC[14,3](priceInterval) IN baseGroup;
toPriceInterval 'до' = DATA NUMERIC[14,3](priceInterval) IN baseGroup;
modifierPriceInterval 'Округлённая цена интервала' = DATA NUMERIC[14,3](priceInterval) IN baseGroup;

denominatorPriceCurrency 'Делитель (округление цен)' =  DATA NUMERIC[14,3](currency);
minPriceCurrency 'Нижний порог (округление цен)' =  DATA NUMERIC[14,3](currency);
priceRoundCurrency 'До скольки знаков округлять розничную цену' =  DATA INTEGER (currency);

integerPriceCurrency 'Целое' (price, currency) = divideInteger((price AS NUMERIC[14,3]), denominatorPriceCurrency(currency));
fractionPriceCurrency 'Дробное' (price, currency) = (price AS NUMERIC[14,3]) - integerPriceCurrency(price, currency)* denominatorPriceCurrency(currency);

inIntervalPriceCurrency(priceInterval, price, currency) = fractionPriceCurrency(price, currency) >= fromPriceInterval(priceInterval)
    AND fractionPriceCurrency(price, currency) <= toPriceInterval(priceInterval);

priceIntervalPriceCurrency (price, currency) = GROUP MAX priceInterval IF inIntervalPriceCurrency(priceInterval, price, currency) BY price AS NUMERIC[14,3], currency;

roundPriceCurrency 'Розничная цена с округлением' (price, currency) = IF (price AS NUMERIC[14,3]) > minPriceCurrency(currency) AND priceIntervalPriceCurrency (price, currency)
    THEN integerPriceCurrency(price, currency)*denominatorPriceCurrency(currency) + modifierPriceInterval(priceIntervalPriceCurrency(price, currency))
    ELSE round(price AS NUMERIC[14,3], priceRoundCurrency(currency));

FORM priceIntervals 'Округления розничных цен'

    OBJECTS c=currency FIXED PANEL
    PROPERTIES (c)  SELECTOR name
    PROPERTIES (c)  denominatorPriceCurrency, minPriceCurrency, priceRoundCurrency

    OBJECTS p=priceInterval
    PROPERTIES(p) fromPriceInterval, toPriceInterval, modifierPriceInterval

    PROPERTIES(p) ADDOBJ, delete

    OBJECTS nu=NUMERIC[14,3] FIXED PANEL
    PROPERTIES val = OBJVALUE(nu)

    PROPERTIES(nu, c) READONLY roundPriceCurrency

    FILTERS currencyPriceInterval(p)==c

;

DESIGN priceIntervals FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        ADD c.box {
            childConstraints = TO THE BOTTOM;
            NEW row1 {
                ADD PROPERTY (name(c)) {
                    preferredCharWidth = 30;
                    panelLabelAbove = TRUE;
                    font = 'Tahoma bold 24';
                }

            }
            NEW row2 {
                childConstraints = TO THE RIGHT;
                ADD PROPERTY (denominatorPriceCurrency(c)) { panelLabelAbove = TRUE; font = 'Tahoma bold 24'; }
                ADD PROPERTY (minPriceCurrency(c))  { panelLabelAbove = TRUE; font = 'Tahoma bold 24'; }
                ADD PROPERTY (priceRoundCurrency(c))  { panelLabelAbove = TRUE; font = 'Tahoma bold 24'; }
            }
        }
        ADD p.box;

        ADD nu.box {
            title = 'Тестовая форма';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY (val) {
                caption = 'Введите число для примера';
                panelLabelAbove = TRUE;
                font = 'Tahoma bold 36';
            }
            ADD PROPERTY (roundPriceCurrency(nu, c)) {
                caption = 'Результат';
                panelLabelAbove = TRUE;
                font = 'Tahoma bold 36';
            }
        }
        ADD functions.box;
    }
}