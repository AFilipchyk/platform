MODULE RetailCRM;

REQUIRE System,

        Stock,
        Store,
        Numerator,
        Document;

PRIORITY Stock;


// ------------------------------------- Физические лица -------------------------------------- //

CLASS client 'Покупатель (Ф.Л.)' : contact;


FORM client 'Покупатель (Ф.Л.)'
    OBJECTS c=client FIXED PANEL
    PROPERTIES(c)      userFirstName, userLastName, userPostAddress, userPhone,
                       email, userBirthday

    EDIT client OBJECT c
;

FORM clients 'Покупатели (Ф.Л.)'
    OBJECTS c=client
    PROPERTIES(c)      READONLY userFirstName, userLastName, userPostAddress, userPhone,
                       email, userBirthday
    PROPERTIES(c)      ADDFORM, EDITFORM, delete
;

//--------------------------------------Дисконтные карточки------------------------------------------------------------------//
CLASS discountCard 'Дисконтная карта' : numeratedObject;
TABLE discountCard (discountCard);

CLASS ABSTRACT discountSkuGroup 'Дисконтная группа товаров' : named;

discountCardSeriesNumber (discountCard) = GROUP UNIQUE discountCard BY seriesNumberObject (discountCard) WHERE discountCard IS discountCard;

clientDiscountCard (discountCard) = DATA client(discountCard) IN idGroup;
nameClientDiscountCard 'Держатель дисконтной карты' (discountCard) = commonName(clientDiscountCard(discountCard)) IN recognizeGroup;

GROUP discountCardDateGroup 'Срок действия' : baseGroup;
dateDiscountCard 'Дата выдачи' (discountCard) = DATA DATE (discountCard) IN discountCardDateGroup;
dateDiscountCard (discountCard) <- currentDate() WHEN ASSIGNED(discountCard IS discountCard);

dateToDiscountCard 'Дата окончания действия' (discountCard) = DATA DATE (discountCard) IN discountCardDateGroup;

inDiscountCardDiscountSkuGroup 'Вкл.' (discountCard, discountSkuGroup) = ABSTRACT BOOLEAN (discountCard, discountSkuGroup);
inDiscountSkuGroupSku 'Вкл' = ABSTRACT BOOLEAN (discountSkuGroup, sku);

percentDiscountCard 'Процент скидки' (discountCard) = DATA NUMERIC[8,3] (discountCard);

// накопительная сумма  : потом посчитать ее по карте

// ---------------------------- Нумераторы по умолчанию --------------------------------- //

defaultNumeratorDiscountCards = DATA numerator ();
nameDefaultNumeratorDiscountCard 'Нумератор для дисконтных карт' = name(defaultNumeratorDiscountCards());

initValueNumeratorObject(object) += defaultNumeratorDiscountCards() IF object IS discountCard;
initWhenNumeratorObject(object) += ASSIGNED(object IS discountCard);

FORM generationDiscountCards 'Генерация дисконтных карт'
    OBJECTS n=numerator  FIXED PANEL
    PROPERTIES(n) SELECTOR name

    OBJECTS quantityCards=INTEGER FIXED PANEL
    PROPERTIES(quantityCards) intValueQuantityCards = OBJVALUE;

DESIGN generationDiscountCards FROM DEFAULT {
    main{
        PROPERTY(intValueQuantityCards) {
            caption = 'Количество дисконтных карт';
        }
    }
}

generateDiscountCards 'Сгенерировать дисконтные карты' = ACTION() {
    FORM generationDiscountCards OBJECTS n=defaultNumeratorDiscountCards() MODAL CHECK;
    IF formResult() == formResult.ok THEN {
        LOCAL num = INTEGER();
        SET num() <- 0;
        WHILE num() < (chosenInt('quantityCards') AS INTEGER) DO {
            ADDOBJ discountCard;
            FOR d == addedObject() DO {
                SET numeratorObject(d) <-chosenObject('n') AS numerator;
                SET num() <- num() + 1;
            }
        }
    }
} TOOLBAR;

FORM discountCard 'Дисконтная карта'
    OBJECTS d=discountCard FIXED PANEL
    PROPERTIES(d) nameClientDiscountCard, percentDiscountCard,
                  nameNumeratorObject, numberObject, seriesObject,
                  dateDiscountCard, dateToDiscountCard

    OBJECTS dg=discountSkuGroup
    PROPERTIES(dg) READONLY name
    PROPERTIES(d, dg) inDiscountCardDiscountSkuGroup

    FILTERGROUP filterDiscountSkuGroup
        FILTER 'Только отмеченные' 'F11' inDiscountCardDiscountSkuGroup(d, dg)  DEFAULT

    EDIT discountCard OBJECT d
;

DESIGN discountCard FROM DEFAULT {
    main{
        preferredSize = (1024, 768);
        d.box {
            d.panel {
                childConstraints = TO THE BOTTOM;
                NEW row1 {
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY(nameClientDiscountCard) {
                        font = 'Tahoma 24';
                    }
                    ADD PROPERTY(percentDiscountCard) {
                        font = 'Tahoma 24';
                    }
                }
                NEW row2 {
                    childConstraints = TO THE RIGHT;
                    ADD d.numberedGroup;
                    ADD d.discountCardDateGroup;
                }
            }
        }
    }
}

FORM discountCards 'Дисконтные карты'
    OBJECTS d=discountCard
    PROPERTIES(d) READONLY numberObject, seriesObject, nameClientDiscountCard, dateDiscountCard, dateToDiscountCard,
                  percentDiscountCard
    PROPERTIES(d) ADDFORM, EDITFORM, delete

    ORDER BY seriesObject, numberObject

    OBJECTS dg=discountSkuGroup
    PROPERTIES(dg) READONLY name
    PROPERTIES(d, dg) READONLY inDiscountCardDiscountSkuGroup

    FILTERGROUP filterDiscountSkuGroup
        FILTER 'Только отмеченные' 'F11' inDiscountCardDiscountSkuGroup(d, dg)  DEFAULT

    PROPERTIES() generateDiscountCards TODRAW d
;

// todo: сделать генерацию штрих-кода для дисконтных карт
//--------------------------Акции----------------------------------------//

CLASS promotion 'Акция' : historyObject, numeratedObject, named;
TABLE promotion (promotion);

CLASS promotionGroup 'Группа акций' : named;
TABLE promotionGroup (promotionGroup);

// ----------------- Нумератор -------------------- //
defaultNumeratorPromotion = DATA numerator ();
nameDefaultNumeratorPromotion 'Нумератор для акций' = name(defaultNumeratorPromotion());

initValueNumeratorObject(object) += defaultNumeratorPromotion() IF object IS promotion;
initWhenNumeratorObject(object) += ASSIGNED(object IS promotion);

promotionGroupPromotion 'Группы акций ИД' (promotion) = DATA promotionGroup(promotion) IN idGroup AUTOSET;
namePromotionGroupPromotion 'Группа акций' (promotion) = name(promotionGroupPromotion(promotion)) IN baseGroup;

CLASS promotionCondition 'Условие акции' : named;
TABLE promotionCondition (promotionCondition);

GROUP retailCRMGroup 'Реквизиты' : recognizeGroup;

dateFromPromotion 'ОТ:' = DATA DATE (promotion) IN retailCRMGroup;
dateToPromotion 'ДО:' = DATA DATE (promotion) IN retailCRMGroup;

timeOfFromPromotion 'ОТ:' =  DATA TIME (promotion) IN retailCRMGroup;
timeOfToPromotion 'ДО:' =  DATA TIME (promotion) IN retailCRMGroup;

includePromotionDay 'В акции' = DATA BOOLEAN (promotion, DOW);
includePromotionStore 'В акции' = DATA BOOLEAN (promotion, store);
includePromotionDiscountCard 'В акции' = DATA BOOLEAN (promotion, discountCard);

cumulativeSumPromotion 'Накоп. сумма' (promotion) = DATA NUMERIC[14,2] (promotion);
minSumBillPromotion 'Сумма чека' (promotion) = DATA NUMERIC[14,2] (promotion);

promotionPromotionCondition = DATA promotion (promotionCondition);

minQuantityPromotionCondition 'Количество С' = DATA NUMERIC[14,3] (promotionCondition);
maxQuantityPromotionCondition 'Количество ПО' = DATA NUMERIC[14,3] (promotionCondition);

minSumPromotionCondition 'Диапазон сумм С' = DATA NUMERIC[16,2] (promotionCondition);
maxSumPromotionCondition 'Диапазон сумм ПО' = DATA NUMERIC[16,2] (promotionCondition);

percentPromotionCondition 'Процент скидки'  = DATA NUMERIC[8,3] (promotionCondition);
resultPricePromotionCondition 'Цена со скидкой' = DATA NUMERIC[14,2] (promotionCondition);

includePromotionConditionSku 'В акции' = DATA BOOLEAN (promotionCondition, sku);
includePromotionConditionSkuGroup 'В акции' = DATA BOOLEAN (promotionCondition, skuGroup);

includePromotionConditionSkuGroupSku 'В акции' (promotionCondition, skuGroup, sku) = UNION OVERRIDE includePromotionConditionSkuGroup(promotionCondition, skuGroup) AND sku AS sku,
                                                                                    includePromotionConditionSku(promotionCondition, sku) AND skuGroup AS skuGroup;



FORM promotion 'Акция'
    OBJECTS sh=promotion   FIXED PANEL
    PROPERTIES(sh)     name, namePromotionGroupPromotion,
                       nameNumeratorObject, numberObject, seriesObject,
                       dateFromPromotion, dateToPromotion, timeOfFromPromotion, timeOfToPromotion,
                       cumulativeSumPromotion, minSumBillPromotion
    OBJECTS d=DOW
    PROPERTIES(d)     READONLY name
    PROPERTIES(sh, d)  includePromotionDay
    FILTERGROUP filterDOW
        FILTER 'Только отмеченные' 'F11' includePromotionDay(sh, d) DEFAULT

    OBJECTS sg=promotionCondition
    PROPERTIES(sg)     name, minQuantityPromotionCondition, maxQuantityPromotionCondition, minSumPromotionCondition, maxSumPromotionCondition, percentPromotionCondition,
                       resultPricePromotionCondition, ADDOBJ, delete

    FILTERS            promotionPromotionCondition(sg)==sh

    TREE skuTree skg = skuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = name(skg)
    ORDER BY skuTreeName
    PROPERTIES(sg, skg) includePromotionConditionSkuGroup

    OBJECTS sk= sku
    PROPERTIES(sk)    READONLY nameSku
    FILTERS           isParentSkuGroupSku(skg, sk)
    PROPERTIES(sg, skg, sk)   includePromotionConditionSkuGroupSku


    FILTERGROUP filterSku
        FILTER 'Только отмеченные' 'F10' includePromotionConditionSkuGroupSku(sg, skg, sk)  DEFAULT

    TREE treeStore a=STRING[3], t=chainStores, st=storeType
    PROPERTIES READONLY OBJVALUE(a), name(t), name(st)

    FILTERS stringEqualsAll(a)
    FILTERS inChainStoresStoreType (t, st)

    OBJECTS s=store
    PROPERTIES(s) READONLY name, addressStore, nameCompanyStore

    FILTERS inChainStoresStoreTypeStore(t, st, s)

    PROPERTIES(sh, s) includePromotionStore
    FILTERGROUP filterStore
        FILTER 'Только отмеченные' 'F9' includePromotionStore(sh, s) DEFAULT

    OBJECTS dis= discountCard
    PROPERTIES(dis)    READONLY numberObject, seriesObject, nameClientDiscountCard
    PROPERTIES(sh, dis) includePromotionDiscountCard
    FILTERGROUP filterDiscountCard
        FILTER 'Только отмеченные' 'F8' includePromotionDiscountCard(sh, dis) DEFAULT

    EDIT promotion OBJECT sh
;

DESIGN promotion FROM DEFAULT {
    main{
        preferredSize = (1024, 768);

        ADD sh.box {

            childConstraints = TO THE RIGHT;
            NEW row01 {
                title = 'Название';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(name(sh)) {
                    preferredCharWidth = 30;

                }
                ADD PROPERTY(namePromotionGroupPromotion(sh));
            }
            ADD sh.numberedGroup {
                childConstraints = TO THE BOTTOM;
            }
            NEW row11 {
                title = 'Диапозон дат';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(dateFromPromotion);
                ADD PROPERTY(dateToPromotion);
            }
            NEW row12 {
                title = 'Часы';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(timeOfFromPromotion);
                ADD PROPERTY(timeOfToPromotion);
            }
            NEW row13 {
                title = 'Суммы';
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(cumulativeSumPromotion);
                ADD PROPERTY(minSumBillPromotion);
            }
        }

        NEW row2 {
            fillVertical = 2;
            fillHorizontal = 3;
            type = TABBED;
            NEW row21 {
                title = 'Условия';
                type = SPLITV;
                childConstraints = TO THE BOTTOM;
                ADD sg.box {
                    fillVertical = 1;
                }
                NEW row212 {
                    type = SPLITH;
                    title = 'Товары';
                    childConstraints = TO THE RIGHT;
                    ADD skuTree.tree.box {
                        fillHorizontal = 1;
                    }
                    ADD sk.box {
                        fillHorizontal = 2;
                    }
                }
            }

            NEW row22 {
                type = SPLITH;
                title = 'Магазины';
                childConstraints = TO THE RIGHT;
                ADD treeStore.tree.box {
                    fillHorizontal = 1;
                }
                ADD s.box {
                    fillHorizontal = 2;
                }
            }
            ADD dis.box;
            ADD d.box;
        }

        ADD functions.box;
    }
}



FORM promotions 'Акции'
    OBJECTS sh=promotion
    PROPERTIES(sh)     READONLY name, numberObject, seriesObject, dateFromPromotion, dateToPromotion, timeOfFromPromotion, timeOfToPromotion,
                       cumulativeSumPromotion, minSumBillPromotion
    PROPERTIES(sh)     ADDFORM, EDITFORM, delete

    PROPERTIES (sh) READONLY FORCE PANEL nameUserCreated, timeCreated, hostnameComputerCreated

;

FORM promotionGroup 'Группа акций'
    OBJECTS st=promotionGroup FIXED PANEL
    PROPERTIES(st)     name
    EDIT promotionGroup OBJECT st

;

FORM promotionGroups 'Группы акций'
    OBJECTS st=promotionGroup
    PROPERTIES(st)     READONLY name
    PROPERTIES(st)     ADDFORM, EDITFORM, delete

    OBJECTS sh=promotion
    PROPERTIES(sh)     READONLY name, seriesObject, numberObject, dateFromPromotion, dateToPromotion, timeOfFromPromotion, timeOfToPromotion,
                       cumulativeSumPromotion, minSumBillPromotion
    PROPERTIES(sh)     ADDFORM, EDITFORM, delete

    FILTERS promotionGroupPromotion(sh)==st

;