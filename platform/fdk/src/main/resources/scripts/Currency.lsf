MODULE Currency;

REQUIRE System, DefaultData;

CLASS currency 'Валюта' : named;
TABLE currency(currency);

symbolCurrency 'Символ' = DATA STRING[5] (currency);
shortNameCurrency 'Валюта сокр.' = DATA STRING[3] (currency);
documentNameCurrency 'Валюта в накладной сокр.' = DATA STRING[10] (currency);
currencyShortName (string) = GROUP UNIQUE currency BY shortNameCurrency(currency) WHERE currency IS currency;

precisionCurrency 'Округление (число знаков после запятой)' = DATA INTEGER(currency);
roundCurrency (number, currency) = round(number, precisionCurrency(currency));

// Формы

FORM currency 'Валюта'
    OBJECTS c = currency FIXED PANEL
    PROPERTIES (c)  name, shortNameCurrency, symbolCurrency, documentNameCurrency, precisionCurrency

    EDIT currency OBJECT c
;

FORM currencies 'Валюты'
    OBJECTS c = currency
    PROPERTIES(c) READONLY name, shortNameCurrency, symbolCurrency, documentNameCurrency, precisionCurrency

    PROPERTIES(c) ADDFORM, EDITFORM, delete

    DIALOG currency OBJECT c
;

// ---------------------------------- Типы обмена ---------------------------- //

CLASS typeExchange 'Тип обмена' : named;
TABLE typeExchange(typeExchange);
TABLE rateExchange (typeExchange, currency, DATE);

currencyTypeExchange 'Валюта типа обмена (ИД)'= DATA currency(typeExchange);
nameCurrencyTypeExchange 'Валюта типа обмена (наим.)' (typeExchange) = name(currencyTypeExchange(typeExchange));
rateExchange 'Курс обмена' = DATA NUMERIC[15,8] (typeExchange, currency, DATE);

dateTypeExchangeCurrencyDate (typeExchange, currency, date)  =
    GROUP MAX dateIn IF rateExchange(typeExchange, currency, dateIn) AND dateIn <= date AND date IS DATE
    BY typeExchange, currency, date;

rateTypeExchangeCurrencyDate 'Курс обмена' (typeExchange, currency, date) =
    rateExchange(typeExchange, currency, dateTypeExchangeCurrencyDate(typeExchange, currency, date));

curRateTypeExchangeCurrency 'Текущий курс обмена' (typeExchange, currency) =
    rateExchange(typeExchange, currency, dateTypeExchangeCurrencyDate(typeExchange, currency, currentDate()));

// -------------------------------------------- Курсы валют --------------------------------- //

FORM dialogTypeExchangeCurrency 'Курс обмена на дату'
    OBJECTS t=typeExchange FIXED PANEL
    OBJECTS c=currency FIXED PANEL
    OBJECTS d=DATE FIXED PANEL
    PROPERTIES READONLY name(t),  nameCurrencyTypeExchange(t), name(c)
    PROPERTIES val=OBJVALUE(d), rateExchange(t,c,d)

;
DESIGN dialogTypeExchangeCurrency FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        ADD t.box {
            ADD PROPERTY(name(t)) { focusable = FALSE; preferredCharWidth = 40;}
        }
        ADD c.box{
            childConstraints = TO THE BOTTOM;
            ADD d.box {
                childConstraints = TO THE RIGHT;

                ADD PROPERTY(rateExchange(t,c,d));
            };
            NEW row {
                title = 'Валюты обмена';
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(nameCurrencyTypeExchange(t)) { caption = 'Валюта (из)'; preferredCharWidth = 20;}
                ADD PROPERTY(name(c)) { caption = 'Валюта (в)'; preferredCharWidth = 20;}
            }
        }
    }
    ADD functions.box;
}

dialogTypeExchangeCurrency 'Добавить' (typeExchange, currency) = ACTION FORM dialogTypeExchangeCurrency OBJECTS t, c MODAL TOOLBAR;
deleteTypeExchangeCurrencyDate 'Удалить' (typeExchange, currency, date) = ACTION SET rateExchange(typeExchange, currency, date) <- NULL IMAGE 'delete.png';

FORM typeExchangeCurrencyDate 'Тип обмена / Курсы валют'

    OBJECTS t = typeExchange
    PROPERTIES(t) READONLY name, nameCurrencyTypeExchange
    PROPERTIES(t) ADDFORM, EDITFORM, delete

    OBJECTS c = currency
    PROPERTIES(c) READONLY name
    PROPERTIES(c) ADDFORM, EDITFORM, delete
    PROPERTIES(t, c) READONLY curRateTypeExchangeCurrency

    OBJECTS d = DATE
    PROPERTIES(d) READONLY OBJVALUE

    PROPERTIES(t, c, d) rateExchange
    PROPERTIES(t, c) dialogTypeExchangeCurrency TODRAW d FORCE PANEL
    PROPERTIES(t, c, d) deleteTypeExchangeCurrencyDate

    FILTERS rateExchange(t, c, d)

;

DESIGN typeExchangeCurrencyDate FROM DEFAULT{
    main {
        childConstraints = TO THE BOTTOM;
        ADD t.box;

        NEW topContainer{
            childConstraints = TO THE RIGHT;
            ADD c.box;
            ADD d.box;

        }
        ADD functions.box;
    }
}

NAVIGATOR {
    catalogElement {
        NEW currencyElement 'Валюты и курсы' {
            ADD currencies;
            ADD typeExchangeCurrencyDate;
        }
    }
}

// --------------------------------------------------- Макросы по добавлению валюты в документы ------------------------------------ //
META defineDocumentHeaderCurrency (object)
    currency###object (object) = DATA currency (object);
    nameCurrency###object 'Валюта' (object)= name(currency###object(object)) IN documentPrmGroup MINCHARWIDTH 10 PREFCHARWIDTH 20;
END
META defineDocumentAbstractHeaderCurrency (object)
    currency###object (object) = ABSTRACT currency (object) PERSISTENT;
    nameCurrency###object 'Валюта' (object)= name(currency###object(object)) IN documentPrmGroup MINCHARWIDTH 10 PREFCHARWIDTH 20;
END
META defineDocumentInterfaceHeaderCurrency (object)
    @defineDocumentAbstractHeaderCurrency(object);
    @defineDocumentHeaderCurrency(user###object);
    currency###object (object) += currency###user###object(object);
END

META defineDocumentDetailCurrency (object, detail)
    currency###detail (idetail) = currency###object(object###detail(idetail)) PERSISTENT;
    nameCurrency###detail 'Валюта' (idetail) = nameCurrency###object(object###detail(idetail));
END
META defineDocumentInterfaceDetailCurrency (object, detail)
    @defineDocumentDetailCurrency(object, detail);
    @defineDocumentDetailCurrency(user###object, user###detail);
END

META defineDocumentCurrency (object, detail)
    @defineDocumentHeaderCurrency(object);
    @defineDocumentDetailCurrency(object, detail);
END
META defineDocumentAbstractCurrency (object, detail)
    @defineDocumentAbstractHeaderCurrency(object);
    @defineDocumentDetailCurrency(object, detail);
END
META defineDocumentInterfaceCurrency (object, detail)
    @defineDocumentInterfaceHeaderCurrency(object);
    @defineDocumentInterfaceDetailCurrency(object, detail);
END

META defineDocumentCurrency (object)
    @defineDocumentCurrency(object, object##Detail);
END
META defineDocumentAbstractCurrency (object)
    @defineDocumentAbstractCurrency(object, object##Detail);
END
META defineDocumentInterfaceCurrency (object)
    @defineDocumentInterfaceCurrency(object, object##Detail);
END

META deriveDocumentCurrency (object, stockProp)
    @deriveDocumentCurrency(object, stockProp, currencyStock);
END

META deriveDocumentCurrency (object, objectProp, currencyProp)
    currency###object (object) <- currencyProp(objectProp###object(object)) WHEN CHANGED(objectProp###object(object));
END

// --------------------------------------------------- Стандартные значения ------------------------------------ //

loadDefaultCurrency 'Добавить валюты' = ACTION (ishortName, iname, isymbol, iprecision) {
    ADDOBJ currency;
    FOR c == addedObject() DO {
       SET shortNameCurrency(c) <- ishortName;
       SET name(c) <- iname;
       SET symbolCurrency(c) <- isymbol;
       SET precisionCurrency(c) <- iprecision;
    }
}

loadDefaultCurrencies 'Загрузить стандартные валюты' = ACTION () {
    EXEC loadDefaultCurrency('AUD', 'Австралийский доллар', ' ', 2);
    EXEC loadDefaultCurrency('BGN', 'Болгарский лев', ' ', 2);
    EXEC loadDefaultCurrency('UAH', 'Гривна', ' ', 2);
    EXEC loadDefaultCurrency('DKK', 'Датская крона', ' ', 2);
    EXEC loadDefaultCurrency('USD', 'Доллар США', '$', 2);
    EXEC loadDefaultCurrency('EUR', 'Евро', '€', 2);
    EXEC loadDefaultCurrency('PLN', 'Злотый', ' ', 2);
    EXEC loadDefaultCurrency('ISK', 'Исландская крона', ' ', 2);
    EXEC loadDefaultCurrency('CAD', 'Канадский доллар', ' ', 2);
    EXEC loadDefaultCurrency('CNY', 'Китайский юань', ' ', 2);
    EXEC loadDefaultCurrency('KWD', 'Кувейтский динар', ' ', 2);
    EXEC loadDefaultCurrency('LVL', 'Латвийский лат', ' ', 2);
    EXEC loadDefaultCurrency('LTL', 'Литовский лит', ' ', 2);
    EXEC loadDefaultCurrency('MDL', 'Молдавский лей', ' ', 2);
    EXEC loadDefaultCurrency('NOK', 'Норвежская крона', ' ', 2);
    EXEC loadDefaultCurrency('RUB', 'Российский рубль', ' ', 2);
    EXEC loadDefaultCurrency('XDR', 'СДР', ' ', 2);
    EXEC loadDefaultCurrency('SGD', 'Сингапурский доллар', ' ', 2);
    EXEC loadDefaultCurrency('KGS', 'Сом', ' ', 2);
    EXEC loadDefaultCurrency('KZT', 'Тенге', ' ', 2);
    EXEC loadDefaultCurrency('TRY', 'Турецкая лира', ' ', 2);
    EXEC loadDefaultCurrency('GBP', 'Фунт стерлингов', ' ', 2);
    EXEC loadDefaultCurrency('CZK', 'Чешская крона', ' ', 2);
    EXEC loadDefaultCurrency('SEK', 'Шведская крона', ' ', 2);
    EXEC loadDefaultCurrency('CHF', 'Швейцарский франк', ' ', 2);
    EXEC loadDefaultCurrency('JPY', 'Иена', ' ', 2);
    EXEC loadDefaultCurrency('IRR', 'Иранский риал', ' ', 2);
    EXEC loadDefaultCurrency('BLR', 'Белорусский рубль', ' ', 0);
} IN loadDefaultGroup;

@implementLoadDefaultData(loadDefaultCurrencies);