MODULE Currency;

REQUIRE System, DefaultData;

dateTypeExchangeCurrencyDate (typeExchange, currency, date)  =
    GROUP MAX dateIn IF rateExchange(typeExchange, currency, dateIn) AND dateIn <= date AND date IS DATE
    BY typeExchange, currency, date;

rateTypeExchangeCurrencyDate 'Курс обмена' (typeExchange, currency, date) =
    rateExchange(typeExchange, currency, dateTypeExchangeCurrencyDate(typeExchange, currency, date));

dateTypeExchangeCurrencyDateEx (typeExchange, currency, date)  =
    GROUP MAX dateIn IF rateExchange(typeExchange, currency, dateIn) AND dateIn <= date AND date IS DATE
    BY typeExchange, currency, date;

curRateTypeExchangeCurrency 'Текущий курс обмена' (typeExchange, currency) =
    rateExchange(typeExchange, currency, dateTypeExchangeCurrencyDateEx(typeExchange, currency, currentDate()));

typeExchangeRetailCurrency 'Тип обмена для розницы ИД' =  DATA typeExchange(currency);
nameTypeExchangeRetailCurrency 'Тип обмена для розницы' (currency) = name(typeExchangeRetailCurrency(currency));

accuracyCurrency 'Число знаков' = DATA INTEGER(currency);
roundCurrency (number, currency) = round(number, accuracyCurrency(currency));

// --------------------------------------------- Формы для ввода курсов валют --------------------------------------- //

FORM currency 'Валюта'
    OBJECTS c = currency FIXED PANEL
    PROPERTIES (c)  name, shortNameCurrency, symbolCurrency, documentNameCurrency, nameTypeExchangeRetailCurrency, accuracyCurrency

    EDIT currency OBJECT c
;

FORM currencies 'Валюты'
    OBJECTS c = currency
    PROPERTIES(c) READONLY name, shortNameCurrency, symbolCurrency, documentNameCurrency, nameTypeExchangeRetailCurrency, accuracyCurrency

    PROPERTIES(c) ADDFORM, EDITFORM, delete

    DIALOG currency OBJECT c
;

// -------------------------------------------- Курсы валют --------------------------------- //

FORM dialogTypeExchangeCurrency 'Курс обмена на дату'
    OBJECTS t=typeExchange FIXED PANEL
    OBJECTS c=currency FIXED PANEL
    OBJECTS d=DATE FIXED PANEL
    PROPERTIES READONLY name(t),  nameCurrencyTypeExchange(t), name(c)
    PROPERTIES val=OBJVALUE(d), rateExchange(t,c,d)

;
DESIGN dialogTypeExchangeCurrency FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        ADD t.box {
            ADD PROPERTY(name(t)) { focusable = FALSE; preferredCharWidth = 40;}
        }
        ADD c.box{
            childConstraints = TO THE BOTTOM;
            ADD d.box {
                childConstraints = TO THE RIGHT;

                ADD PROPERTY(rateExchange(t,c,d));
            };
            NEW row {
                title = 'Валюты обмена';
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(nameCurrencyTypeExchange(t)) { caption = 'Валюта (из)'; preferredCharWidth = 20;}
                ADD PROPERTY(name(c)) { caption = 'Валюта (в)'; preferredCharWidth = 20;}
            }
        }
    }
    ADD functions.box;
}

dialogTypeExchangeCurrency 'Добавить' (typeExchange, currency) = ACTION FORM dialogTypeExchangeCurrency OBJECTS t, c MODAL TOOLBAR;
deleteTypeExchangeCurrencyDate 'Удалить' (typeExchange, currency, date) = ACTION SET rateExchange(typeExchange, currency, date) <- NULL IMAGE 'delete.png';

FORM typeExchangeCurrencyDate 'Тип обмена / Курсы валют'

    OBJECTS t = typeExchange
    PROPERTIES(t) READONLY name, nameCurrencyTypeExchange
    PROPERTIES(t) ADDFORM, EDITFORM, delete

    OBJECTS c = currency
    PROPERTIES(c) READONLY name
    PROPERTIES(c) ADDFORM, EDITFORM, delete
    PROPERTIES(t, c) READONLY curRateTypeExchangeCurrency

    OBJECTS d = DATE
    PROPERTIES(d) READONLY OBJVALUE

    PROPERTIES(t, c, d) rateExchange
    PROPERTIES(t, c) dialogTypeExchangeCurrency TODRAW d FORCE PANEL
    PROPERTIES(t, c, d) deleteTypeExchangeCurrencyDate

    FILTERS rateExchange(t, c, d)

;

DESIGN typeExchangeCurrencyDate FROM DEFAULT{
    main {
        childConstraints = TO THE BOTTOM;
        ADD t.box;

        NEW topContainer{
            childConstraints = TO THE RIGHT;
            ADD c.box;
            ADD d.box;

        }
        ADD functions.box;
    }
}

NAVIGATOR {
    catalogElement {
        NEW currencyElement 'Валюты и курсы' {
            ADD currencies;
            ADD typeExchangeCurrencyDate;
        }
    }
}

// --------------------------------------------------- Макросы по добавлению валюты в документы ------------------------------------ //
META defineDocumentHeaderCurrency (object)
    currency###object 'Валюта ИД' (object) = DATA currency (object);
    nameCurrency###object 'Валюта' (object)= name(currency###object(object)) IN documentPrmGroup MINCHARWIDTH 10 PREFCHARWIDTH 20;
END

META defineDocumentDetailCurrencyCustom (object, detail)
    currency###detail 'Валюта ИД' (idetail) = currency###object(object###detail(idetail));
    nameCurrency###detail 'Валюта' (idetail) = nameCurrency###object(object###detail(idetail));
END

META defineDocumentCurrencyCustom (object, detail)
    @defineDocumentHeaderCurrency(object);
    @defineDocumentDetailCurrencyCustom(object, detail);
END

META defineDocumentCurrency (object)
    @defineDocumentHeaderCurrency(object);
    @defineDocumentDetailCurrencyCustom(object, object##Detail);
END

META deriveDocumentCurrency (object, stockProp)
    currency###object (object) <- currencyStock(stockProp###object(object)) WHEN ASSIGNED(object AS object);
END

// --------------------------------------------------- Стандартные значения ------------------------------------ //

loadDefaultCurrency 'Добавить валюты' = ACTION (stringOne, stringTwo, stringThree) {
    ADDOBJ currency;
    FOR c == addedObject() DO {
       SET shortNameCurrency(c) <-  stringOne AS STRING[3];
       SET name(c) <-  stringTwo AS STRING[110];
       SET symbolCurrency(c) <- stringThree AS STRING[5];
    }
}

loadDefaultCurrencies 'Загрузить стандартные валюты' = ACTION () {
    EXEC loadDefaultCurrency('AUD', 'Австралийский доллар', ' ');
    EXEC loadDefaultCurrency('BGN', 'Болгарский лев', ' ');
    EXEC loadDefaultCurrency('UAH', 'Гривна', ' ');
    EXEC loadDefaultCurrency('DKK', 'Датская крона', ' ');
    EXEC loadDefaultCurrency('USD', 'Доллар США', '$');
    EXEC loadDefaultCurrency('EUR', 'Евро', '€');
    EXEC loadDefaultCurrency('PLN', 'Злотый', ' ');
    EXEC loadDefaultCurrency('ISK', 'Исландская крона', ' ');
    EXEC loadDefaultCurrency('CAD', 'Канадский доллар', ' ');
    EXEC loadDefaultCurrency('CNY', 'Китайский юань', ' ');
    EXEC loadDefaultCurrency('KWD', 'Кувейтский динар', ' ');
    EXEC loadDefaultCurrency('LVL', 'Латвийский лат', ' ');
    EXEC loadDefaultCurrency('LTL', 'Литовский лит', ' ');
    EXEC loadDefaultCurrency('MDL', 'Молдавский лей', ' ');
    EXEC loadDefaultCurrency('NOK', 'Норвежская крона', ' ');
    EXEC loadDefaultCurrency('RUB', 'Российский рубль', ' ');
    EXEC loadDefaultCurrency('XDR', 'СДР', ' ');
    EXEC loadDefaultCurrency('SGD', 'Сингапурский доллар', ' ');
    EXEC loadDefaultCurrency('KGS', 'Сом', ' ');
    EXEC loadDefaultCurrency('KZT', 'Тенге', ' ');
    EXEC loadDefaultCurrency('TRY', 'Турецкая лира', ' ');
    EXEC loadDefaultCurrency('GBP', 'Фунт стерлингов', ' ');
    EXEC loadDefaultCurrency('CZK', 'Чешская крона', ' ');
    EXEC loadDefaultCurrency('SEK', 'Шведская крона', ' ');
    EXEC loadDefaultCurrency('CHF', 'Швейцарский франк', ' ');
    EXEC loadDefaultCurrency('JPY', 'Иена', ' ');
    EXEC loadDefaultCurrency('IRR', 'Иранский риал', ' ');
    EXEC loadDefaultCurrency('BLR', 'Белорусский рубль', ' ');
} IN loadDefaultGroup;

@implementLoadDefaultData(loadDefaultCurrencies);