MODULE Store;

IMPORT BaseLogicsModule;
IMPORT Utils;
IMPORT Historizable;
IMPORT Stock;
IMPORT Employee;
IMPORT Tax;
IMPORT LegalEntity;

// -------------------------------------- Торговая сеть ------------------------------- //

CLASS chainStores 'Торговая сеть' : stockGroup, externalObject, employeeDivisionGroup;
TABLE chainStores (chainStores);

// -------------------------------------- Формат магазина ------------------------------- //

CLASS storeType 'Формат магазина' : stockGroup, employeeDivisionGroup;
TABLE storeType (storeType);

chainStoresStoreType = DATA chainStores (storeType);
nameChainStoresStoreType 'Торговая сеть' (storeType) = name(chainStoresStoreType(storeType)) IN baseGroup;

inChainStoresStoreType(chainStores, storeType) = chainStoresStoreType(storeType) == chainStores;

storeTypeNameChainStores 'Формат магазина по имени и торговой сети' (name, chainStores) = GROUP UNIQUE storeType BY name (storeType), chainStoresStoreType(storeType);

parentStockGroup (storeType) += chainStoresStoreType(storeType);
parentEmployeeDivisionGroup (storeType) += chainStoresStoreType(storeType);

FORM chainStores 'Торговая сеть'
    OBJECTS n=chainStores FIXED PANEL
    PROPERTIES(n) name

    OBJECTS s=storeType
    PROPERTIES(s) name
    PROPERTIES(s) ADDOBJ, delete
    FILTERS inChainStoresStoreType(n, s)

    EDIT chainStores OBJECT n
;

// -------------------------------------- Регион ------------------------------- //

CLASS region 'Регион' : named;
TABLE region (region);

// -------------------------------------- Магазин ------------------------------- //

CLASS store 'Магазин' : stockGroup, externalObject, taxUnit, employeeDivisionGroup;
TABLE store (store);

companyStore = DATA company (store);
nameCompanyStore 'Компания' (store) = name(companyStore(store)) IN baseGroup;

regionStore = DATA region (store);
nameRegionStore 'Регион' (store) = name(regionStore(store));

addressStore 'Адрес' = DATA STRING[100] (store);

storeTypeStore = DATA storeType (store) AUTOSET;
nameStoreTypeStore 'Формат' (store) = name(storeTypeStore(store)) IN baseGroup;

inStoreTypeStore (storeType, store) = storeTypeStore (store) == storeType;

chainStoresStore (store) = chainStoresStoreType(storeTypeStore(store));
nameChainStoresStore 'Торговая сеть' (store) = name(chainStoresStore(store)) IN baseGroup;

inChainStoresStore (chainStores, store) = chainStoresStore(store) == chainStores;

parentStockGroup (store) += storeTypeStore(store);
parentEmployeeDivisionGroup (store) += storeTypeStore(store);

taxUnitGroupTaxUnit(store) += companyStore(store);
descriptionTaxUnit(store) += string2(name(store), addressStore(store));

inChainStoresStoreTypeStore (chainStores, storeType, store) =
    (storeTypeStore(store) == storeType AND chainStores) OR
    (chainStoresStore(store) == chainStores AND NOT storeType) OR
    (store IS store AND NOT storeType AND NOT chainStores);

// -------------------------------------- Отдел магазина ------------------------------- //

CLASS departmentStore 'Отдел магазина' : stock, externalObject, employeeDivision;
TABLE departmentStore (departmentStore);

storeDepartmentStore = DATA store (departmentStore) AUTOSET;
nameStoreDepartmentStore 'Магазин' (departmentStore) = name(storeDepartmentStore(departmentStore)) IN baseGroup;

inStoreDepartment(store, departmentStore) = storeDepartmentStore(departmentStore) == store;

storeTypeDepartmentStore(departmentStore) = storeTypeStore(storeDepartmentStore(departmentStore)) PERSISTENT;
chainStoresDepartmentStore(departmentStore) = chainStoresStoreType(storeTypeDepartmentStore(departmentStore)) PERSISTENT;

@defineHistorizable(tradingSquareDepartmentStore, 'Торговая площадь', NUMERIC[10,2], departmentStore, name, publicGroup);

tradingSquareStoreDate (store, date) = GROUP SUM tradingSquareDepartmentStoreDate(departmentStore, date) BY storeDepartmentStore(departmentStore), date;
tradingSquareStore 'Торговая площадь' (store) = tradingSquareStoreDate(store, currentDate());

stockGroupStock (departmentStore) += storeDepartmentStore(departmentStore);
employeeDivisionGroupEmployeeDivision (departmentStore) += storeDepartmentStore(departmentStore);

inChainStoresStoreTypeStoreDepartment (chainStores, storeType, store, department) =
    (storeDepartmentStore(department) == store AND storeType AND chainStores) OR
    (storeTypeDepartmentStore(department) == storeType AND chainStores AND NOT store) OR
    (chainStoresDepartmentStore(department) == chainStores AND NOT store AND NOT storeType) OR
    (department IS departmentStore AND NOT store AND NOT storeType AND NOT chainStores);

// -------------------------------------- Формы отдела магазинов ------------------------------------------ //

FORM departmentStore 'Отдел магазина'
    OBJECTS d=departmentStore FIXED PANEL
    PROPERTIES(d)   name, nameStoreDepartmentStore, tradingSquareDepartmentStore, dialogTradingSquareDepartmentStore

    OBJECTS e=employee
    PROPERTIES(e) READONLY userFirstName, userLastName
    PROPERTIES(e) ADDSESSIONFORM, EDITSESSIONFORM, delete
    FILTERS inEmployeeDivisionEmployee(d, e)

    EDIT departmentStore OBJECT d
;

DESIGN departmentStore FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
        d.panel {
            childConstraints = TO THE BOTTOM;
        }
    }
}

FORM departmentStores 'Отделы магазинов'
    TREE treeStore a=STRING[3], t=chainStores, st=storeType, s=store
    PROPERTIES READONLY OBJVALUE(a), name(t), name(st), name(s)
    FILTERS stringEqualsAll(a), inChainStoresStoreType (t, st), inStoreTypeStore(st, s)

    OBJECTS d=departmentStore
    PROPERTIES(d) READONLY depName = name
    FILTERS inChainStoresStoreTypeStoreDepartment(t, st, s, d)
    ORDER BY depName

    DIALOG departmentStore OBJECT d
;

DESIGN departmentStores FROM DEFAULT {
    POSITION treeStore.box TO THE LEFT d.box;
    treeStore.tree {
        fillHorizontal = 0.3;
    }
    d.grid {
        defaultComponent = TRUE;
    }
}

FORM store 'Магазин'
    OBJECTS s=store FIXED PANEL
    PROPERTIES(s) name, addressStore, nameStoreTypeStore, tradingSquareStore, nameCompanyStore, nameRegionStore

    OBJECTS d=departmentStore
    PROPERTIES(d) READONLY objectClassName, name, tradingSquareDepartmentStore
    PROPERTIES(d)          ADDSESSIONFORM, EDITSESSIONFORM, delete
    FILTERS inStoreDepartment(s, d)

    EDIT store OBJECT s
;

// -------------------------------------- Формы магазинов ------------------------------------------ //

FORM stores 'Магазины'
    TREE treeStore a=STRING[3], t=chainStores, st=storeType
    PROPERTIES READONLY OBJVALUE(a), name(t), name(st)

    FILTERS stringEqualsAll(a)
    FILTERS inChainStoresStoreType (t, st)

    PROPERTIES(t)          addT=ADDFORM FORCE PANEL, editT=EDITFORM FORCE PANEL, delete FORCE PANEL

    OBJECTS s=store
    PROPERTIES(s) READONLY name, addressStore, nameStoreTypeStore, nameCompanyStore, nameRegionStore
    PROPERTIES(s)          ADDFORM, EDITFORM, delete
    FILTERS inChainStoresStoreTypeStore(t, st, s)

    OBJECTS d=departmentStore
    PROPERTIES(d) READONLY objectClassName, name
    FILTERS inStoreDepartment(s, d)
;

DESIGN stores FROM DEFAULT {
    POSITION treeStore.box TO THE LEFT s.box;
    POSITION treeStore.box TO THE LEFT d.box;

        PROPERTY (delete(t)) {
            caption = 'Удалить';
            panelLocation = TOOLBAR;
            askConfirm = TRUE;
        }

        PROPERTY (addT) {
            caption = 'Добавить';
        }

        PROPERTY (editT) {
            caption = 'Ред-ть';
            maximumCharWidth = 5;
        }
        s.box {
            fillHorizontal = 2.3;
        }

        d.box {
            fillHorizontal = 2.3;
        }
}

// -------------------------------------- Конкретные отделы магазина ------------------------------- //

CLASS cafeStore 'Кафетерий' : departmentStore;
CLASS salesStore 'Торговый зал' : departmentStore;
CLASS warehouseStore 'Склад' : departmentStore;
CLASS restaurantStore 'Ресторан' : departmentStore;
CLASS manufactoryStore 'Цех' : departmentStore;

// -------------------------------------- Макросы ----------------------------------------------- //

META defineDocumentHeaderDepartmentStore (object)
    @defineDocumentHeaderStock(object, departmentStore, 'Отдел документа');
END

META defineDocumentDetailDepartmentStoreCustom (object, detail)
   @defineDocumentDetailStockCustom (object, detail, departmentStore, 'Отдел документа');
END
META defineDocumentDetailDepartmentStore (object)
    @defineDocumentDetailDepartmentStoreCustom (object, object##Detail);
END

META defineDocumentDepartmentStore (object)
    @defineDocumentHeaderDepartmentStore(object);
    @defineDocumentDetailDepartmentStore(object);
END
