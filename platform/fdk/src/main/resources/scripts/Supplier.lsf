MODULE Supplier;

REQUIRE System,
        Historizable,
        Utils,
        LegalEntity,
        Stock,
        Contract;

CLASS supplier 'Поставщик' : legalEntity;
TABLE supplier (supplier);

GROUP orderGroup 'Заказ' : publicGroup;

dataSurePercentSupplier 'Обязательный % заказанных товаров в прих. накладной' = DATA INTEGER (supplier) IN orderGroup;
surePercentSupplier 'Обязательный % заказанных товаров в прих. накладной' = (0 IF supplier IS supplier) OR dataSurePercentSupplier(supplier) IN orderGroup;

allowablePercentSupplier 'Допустимый % изменения цен 1го поставщика' = DATA INTEGER (supplier) IN orderGroup;
afterDaysSupplier 'Максимальное число дней до поставки после заказа' = DATA INTEGER (supplier) IN orderGroup;

periodicitySupply 'Переодичность поставок, дней (при поставках не по графику)' = DATA INTEGER (supplier) IN orderGroup;

returnLossSupplier '% потерь при возврате' (supplier) = DATA NUMERIC[8,3] (supplier) IN orderGroup;

TABLE supplierStock(supplier, stock);
emailOrderSupplierStock 'e-mail для передачи заказа' = DATA STRING[100] (supplier, stock) IN orderGroup MINCHARWIDTH 50 PREFCHARWIDTH 50;
contactSupplierStock 'Контактная информация' = DATA STRING[500] (supplier, stock) IN orderGroup MINCHARWIDTH 50 PREFCHARWIDTH 50;

@defineHistorizableYesNo(controlSupplierStock, 'Контролировать ассортимент поставщика', supplier, name, stock, name, orderGroup);
@defineHistorizableYesNo(sureSupplierStock, 'Обязателен заказ поставщику', supplier, name, stock, name, orderGroup);

// ---------------------------------------- Группы поставщиков -------------------------------- //

CLASS supplierGroup 'Группа поставщиков' : named, legalEntityGroup;

supplierGroupSupplier (supplier) = DATA supplierGroup (supplier) AUTOSET NOT NULL;
nameSupplierGroupSupplier 'Группа поставщиков' (supplier) = name(supplierGroupSupplier(supplier));

@defineHierarchy(supplierGroup);
parentLegalEntityGroup(supplierGroup) += parentSupplierGroup(supplierGroup);
legalEntityGroupLegalEntity(supplier) += supplierGroupSupplier(supplier);

inSupplierGroupSupplier (supplierGroup, supplier) =
    UNION OVERRIDE supplier IS supplier AND NOT supplierGroup IS supplierGroup, isParentSupplierGroupSupplierGroup(supplierGroupSupplier(supplier), supplierGroup);

FORM supplierGroup 'Группа поставщиков'
    OBJECTS s = supplierGroup FIXED PANEL
    PROPERTIES(s) name, nameParentLegalEntityGroup

    EDIT supplierGroup OBJECT s
;

FORM supplierGroups 'Группы поставщиков'
    TREE supplierGroupTree sg = supplierGroup PARENT parentSupplierGroup
    PROPERTIES READONLY sgTreeName = name(sg)
    ORDER BY name(sg)

    DIALOG supplierGroup OBJECT sg
;

// ---------------------------------------- Договора ------------------------------------ //

CLASS contractSkuSupplier 'Договор с поставщиком' : contractSku;

supplierContractSkuSupplier = DATA supplier (contractSkuSupplier) AUTOSET;
nameSupplierContractSkuSupplier 'Поставщик' (contract) = name(supplierContractSkuSupplier(contract)) IN recognizeGroup MAXCHARWIDTH 30 PREFCHARWIDTH 30;
partyAContract (contract) += supplierContractSkuSupplier(contract);

companyContractSkuSupplier = DATA company (contractSkuSupplier) AUTOSET;
nameCompanyContractSkuSupplier 'Компания' (contract) = name(companyContractSkuSupplier(contract)) IN recognizeGroup MAXCHARWIDTH 30 PREFCHARWIDTH 30;
partyBContract (contract) += companyContractSkuSupplier(contract);

TABLE supplierCompany(supplier, company);
@defineContractDefault(contractSkuSupplier, supplier, company);

// Формы
FORM contractSkuSupplier 'Договор'
    OBJECTS c=contractSkuSupplier FIXED PANEL
    PROPERTIES(c) nameCompanyContractSkuSupplier, nameSupplierContractSkuSupplier,
                  numberContract, dateFromContract, dateToContract,
                  nameCurrencyContract, noteContract

    OBJECTS pc = paymentCondition
    PROPERTIES(pc) READONLY datePaymentCondition, numberContractPaymentCondition, nameTypePaymentCondition,
                            nameFormPaymentCondition
    PROPERTIES(pc) ADDSESSIONFORM, EDITSESSIONFORM, delete
    ORDER BY datePaymentCondition

    FILTERS contractPaymentCondition(pc) == c

    EDIT contractSkuSupplier OBJECT c
;

FORM contractsSkuSupplier 'Договора'
    OBJECTS c=contractSkuSupplier
    PROPERTIES(c) READONLY numberContract, dateFromContract, dateToContract,
                           nameCompanyContractSkuSupplier, nameSupplierContractSkuSupplier

    PROPERTIES(c) ADDFORM, EDITFORM, delete
    ORDER BY dateFromContract

    DIALOG contractSkuSupplier OBJECT c
;

// ------------------------------------------ Формы для поставщика --------------------------------------- //

FORM supplier 'Поставщик'
    OBJECTS s=supplier FIXED PANEL
    PROPERTIES(s) name, fullNameLegalEntity, nameSupplierGroupSupplier, nameOwnershipLegalEntity, shortNameOwnershipLegalEntity,
                  emailLegalEntity, siteLegalEntity,
                  addressLegalEntity ON CHANGE EXEC dialogAddressLegalEntity(s),
                  postAddressLegalEntity ON CHANGE EXEC dialogPostAddressLegalEntity(s),
                  managerLegalEntity ON CHANGE EXEC dialogManagerLegalEntity(s),
                  accountantLegalEntity ON CHANGE EXEC dialogAccountantLegalEntity(s),
                  phoneLegalEntity ON CHANGE EXEC dialogPhoneLegalEntity(s),
                  surePercentSupplier, allowablePercentSupplier,
                  afterDaysSupplier, periodicitySupply,
                  returnLossSupplier

    OBJECTS a=account
    PROPERTIES(a) numberAccount, nameBankAccount, addressBankAccount, departmentBankAccount, CBUBankAccount, MFOBankAccount, noteAccount,
                  ADDOBJ, delete
    FILTERS legalEntityAccount(a) == s

    OBJECTS co=company
    PROPERTIES(co) READONLY name
    PROPERTIES(s, co)    numberContractSkuSupplierSupplierCompany
    FILTERGROUP filtersCompany
        FILTER 'Есть договор' 'F5' countContractPartyAPartyB(s, co)

    OBJECTS c=contractSkuSupplier
    PROPERTIES(c)  READONLY numberContract, dateFromContract, dateToContract,
                            nameTypeContractSku, nameFormContractSku,
                            nameCurrencyContract, noteContract
    PROPERTIES(s, co, c) isSupplierCompanyContractSkuSupplier
    PROPERTIES(c)  ADDFORM, EDITFORM, delete
    FILTERS supplierContractSkuSupplier(c) == s,
            companyContractSkuSupplier(c) == co

    OBJECTS l=license
    PROPERTIES(l) numberLicense, dateFromLicense, dateToLicense, ADDOBJ, delete
    FILTERS legalEntityLicense(l) == s

    OBJECTS dt=DATE FIXED PANEL
    PROPERTIES (dt) OBJVALUE BACKGROUND dateDiffersCurrent(dt)

    TREE stockTree sg = stockGroup PARENT parentStockGroup
    PROPERTIES READONLY name(sg)

    OBJECTS st=stock
    PROPERTIES(st) READONLY name, addressStock
    FILTERS stockGroupStock(st) == sg

    PROPERTIES(s, st) emailOrderSupplierStock, contactSupplierStock
    PROPERTIES       overNameControlSupplierStockDate(s, st, dt) BACKGROUND dataControlSupplierStockDate(s, st, dt) ON CHANGE EXEC overDialogControlSupplierStockDate(s, st),
                     overNameSureSupplierStockDate(s, st, dt) BACKGROUND dataSureSupplierStockDate(s, st, dt) ON CHANGE EXEC overDialogSureSupplierStockDate(s, st)

    EDIT supplier OBJECT s
;

DESIGN supplier FROM DEFAULT {
    main{
        preferredSize = (1024, 768);
        s.box {
            childConstraints = TO THE BOTTOM;

            NEW row1 {
                childConstraints = TO THE RIGHT;
                ADD s.lawGroup{
                    childConstraints = TO THE BOTTOM;
                }
                ADD s.orderGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
            NEW row2{
                childConstraints = TO THE RIGHT;
                ADD s.contactGroup {
                    childConstraints = TO THE BOTTOM;
                }
                ADD s.managementGroup {
                    childConstraints = TO THE BOTTOM;
                }
            }
        }

        NEW prop.box BEFORE functions.box{
            type = TABBED;
            ADD a.box;
            NEW firstCase {
                title = 'Договора на поставку';
                type = SPLITH;
                childConstraints = TO THE RIGHT;

                ADD co.box;
                ADD c.box {
                    fillHorizontal = 3;
                }
            }

            ADD l.box;

            ADD st.box {

                ADD dt.box BEFORE st.grid.box;
                ADD stockTree.tree.box BEFORE st.grid.box;
                POSITION stockTree.tree.box TO THE LEFT st.grid.box;
                st.grid.box {
                    fillHorizontal = 3;
                };
            }

        }
    }
}

FORM suppliers 'Поставщики'
    TREE supplierGroupTree sg = supplierGroup PARENT parentSupplierGroup
    PROPERTIES READONLY sgTreeName = name(sg)
    PROPERTIES(sg) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    OBJECTS s=supplier
    PROPERTIES(s) READONLY name, fullNameLegalEntity, nameSupplierGroupSupplier, shortNameOwnershipLegalEntity,
                           numberAccountLegalEntity, addressLegalEntity, phoneLegalEntity
    PROPERTIES(s) ADDFORM, EDITFORM, delete

    FILTERS inSupplierGroupSupplier(sg, s)

    DIALOG supplier OBJECT s
;

DESIGN suppliers FROM DEFAULT{
    NEW topContainer{
        childConstraints = TO THE RIGHT;
        type = SPLITH;
        ADD supplierGroupTree.tree.box{
            PROPERTY(delete(sg)){
                askConfirm = TRUE;
            }
        }
        ADD s.box{
            fillHorizontal = 2.0;
        }
    }
    ADD functions.box;
}

// ----------------------------- Расширяем форму компаний ------------------------------------------- //

EXTEND FORM company
    OBJECTS s=supplier
    PROPERTIES(s)       name
    PROPERTIES(s, co)   numberContractSkuSupplierSupplierCompany

    OBJECTS c=contractSkuSupplier
    PROPERTIES(c)  READONLY numberContract, nameSupplierContractSkuSupplier, nameTypeContractSku, nameFormContractSku, dateFromContract,
                   dateToContract, nameCurrencyContract, noteContract
    PROPERTIES(c)  ADDFORM, EDITFORM, delete
    FILTERS companyContractSkuSupplier(c) == co,
            supplierContractSkuSupplier(c) == s

    FILTERGROUP filters
        FILTER 'Есть договор' 'F10' countContractPartyAPartyB(s, co) DEFAULT
;

EXTEND DESIGN company {
    extendContainer {
        NEW supplierContainer {
            title = 'Договора с поставщиками';
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD s.box;
            ADD c.box {
                fillHorizontal = 2;
            }
        }
    }
}

// -------------------------------------------- Макросы --------------------------------------------- //
META defineDocumentHeaderSupplier (object)
    supplier###object (object) = DATA supplier (object);
    nameSupplier###object 'Поставщик' (object)= name(supplier###object(object)) IN documentPrmGroup MINCHARWIDTH 10 PREFCHARWIDTH 20;
END
META defineDocumentAbstractHeaderSupplier (object)
    supplier###object (object) = ABSTRACT supplier (object);
    nameSupplier###object 'Поставщик' (object)= name(supplier###object(object)) IN documentPrmGroup MINCHARWIDTH 10 PREFCHARWIDTH 20;
END
META defineDocumentInterfaceHeaderSupplier (object)
    @defineDocumentAbstractHeaderSupplier(object);
    @defineDocumentHeaderSupplier(user###object);
    supplier###object (object) += supplier###user###object(object);
END

META defineDocumentDetailSupplier (object, detail)
    supplier###detail (idetail) = supplier###object(object###detail(idetail));
    nameSupplier###detail 'Поставщик' (idetail) = nameSupplier###object(object###detail(idetail));
END

META defineDocumentSupplier (object, detail)
    @defineDocumentHeaderSupplier(object);
    @defineDocumentDetailSupplier(object, detail);
END
META defineDocumentDetailDataSupplier (object, detail)
    dataSupplier###detail (detail) = DATA supplier (detail);
    supplier###detail (idetail) = UNION OVERRIDE supplier###object(object###detail(idetail)), dataSupplier###detail(idetail) PERSISTENT;
    nameSupplier###detail 'Поставщик' (idetail) = name(supplier###detail(idetail));
END
META defineDocumentDataSupplier (object, detail)
    @defineDocumentHeaderSupplier(object);
    @defineDocumentDetailDataSupplier(object, detail);
END
META defineDocumentDataSupplier (object)
    @defineDocumentDataSupplier(object, object##Detail);
END
META defineDocumentDetailSupplier(detail)
    supplier###detail (detail) = DATA supplier (detail);
    nameSupplier###detail 'Поставщик' (detail) = name(supplier###detail (detail));
END

META defineDocumentAbstractSupplier (object, detail)
    @defineDocumentAbstractHeaderSupplier(object);
    @defineDocumentDetailSupplier(object, detail);
END
META defineDocumentInterfaceSupplier (object, detail)
    @defineDocumentInterfaceHeaderSupplier(object);
    @defineDocumentDetailSupplier(object, detail);
    @defineDocumentDetailSupplier(user###object, user###detail);
END

META defineDocumentSupplier (object)
    @defineDocumentSupplier(object, object##Detail);
END
META defineDocumentAbstractSupplier (object)
    @defineDocumentAbstractSupplier(object, object##Detail);
END
META defineDocumentInterfaceSupplier (object)
    @defineDocumentInterfaceSupplier(object, object##Detail);
END

META defineDocumentHeaderSupplierContract(object)
    supplier###object (object) = DATA supplier (object);
    nameSupplier###object 'Поставщик' (object) = name(supplier###object(object)) IN documentPrmGroup MINCHARWIDTH 30 PREFCHARWIDTH 40;

    supplier###object##Detail (detail) = supplier###object(object###object##Detail(detail));

    contract###object (object) = DATA contractSkuSupplier (object);
    numberContract###object 'Договор' (object) = numberContract(contract###object(object)) IN documentPrmGroup;

    dateFromContract###object 'Договор от' (object) = dateFromContract(contract###object(object)) IN documentPrmGroup;

    CONSTRAINT contract###object(object) AND NOT supplier###object(object) == supplierContractSkuSupplier(contract###object(object))
        CHECKED BY contract###object MESSAGE 'Выберите договор данного поставщика';
    CONSTRAINT contract###object(object) AND NOT activeContract(contract###object(object), date###object(object))
        CHECKED BY contract###object MESSAGE 'Выберите действующий договор';
END


// -------------------------------------------- Sku ------------------------------------------------- //

supplierBatch(batch) = ABSTRACT supplier (batch) PERSISTENT INDEXED;
nameSupplierBatch 'Поставщик' (batch) = name(supplierBatch(batch));

countSupplierSku 'Кол-во партий' (supplier, sku) = GROUP SUM 1 BY supplierBatch(batch), skuBatch(batch);

// --------------------------------- Добавляем фильтр в подбор товаров -------------------------------------------- //

META defineDialogSkuSupplier (form)
    form###supplier = SESSION DATA supplier ();
    form###nameSupplier 'Поставщик' () = name(form###supplier()) PREFCHARWIDTH 30;
    form###supplierFilter = (TRUE AND countSupplierSku(form###supplier(), sku)) OR (sku IS sku AND NOT form###supplier());

    EXTEND FORM form
        PROPERTIES form###nameSupplier()
        FILTERS form###supplierFilter(s)
    ;

    EXTEND DESIGN form {
        filterContainer { ADD PROPERTY(form###nameSupplier()); }
    }
END

META defineAddDetailDialogSkuStockSupplier (object, skuProp, stockProp, form)
    @defineAddDetailDialogSkuStockSupplierCustom (object, object##Detail, , skuProp, stockProp, form);
END

META defineAddDetailDialogSkuStockSupplierCustom (object, detail, caption, skuProp, stockProp, form)
    @defineAddDetailDialogSkuStockCustom(object, detail, caption, skuProp, stockProp, form);
    addDetailDialogSkuStockSupplier###detail###object 'Подбор товаров'###caption = ACTION (object) {
        SET form###supplier() <- supplier###object(object);
        EXEC addDetailDialogSkuStock###detail###object(object AS object);
    } TOOLBAR;
END

@defineDialogSkuSupplier(dialogSku);

EXTEND FORM batchDialog
    PROPERTIES(bt) READONLY nameSupplierBatch AFTER nameSkuBatch
;
