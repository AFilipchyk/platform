MODULE Sales;

REQUIRE System, Stock, Tax;

CLASS ABSTRACT salesLedger 'Продажа товара' : skuLedger;
TABLE salesLedger(salesLedger);

dateTimeSalesLedger 'Дата/время' (ledger) = ABSTRACT DATETIME (salesLedger) PERSISTENT INDEXED;
dateSalesLedger 'Дата' (ledger) = dateInTime(dateTimeSalesLedger(ledger)) PERSISTENT INDEXED;

isPostedSalesLedger 'Проведен' (ledger) = ABSTRACT BOOLEAN (salesLedger) PERSISTENT;

skuSalesLedger (ledger) = ABSTRACT sku (salesLedger) PERSISTENT INDEXED;
nameSkuSalesLedger 'SKU' (ledger) = nameSku(skuSalesLedger(ledger));

stockSalesLedger (ledger) = ABSTRACT stock (salesLedger) PERSISTENT INDEXED;
nameStockSalesLedger 'Склад' (ledger) = name(stockSalesLedger(ledger));

descriptionSalesLedger 'Название документа' (ledger) = ABSTRACT STRING[200] (salesLedger);

quantitySalesLedger 'Кол-во' (ledger) = ABSTRACT NUMERIC[14,3] (salesLedger) PERSISTENT;

sumSalesLedger 'Сумма продажи' (ledger) = ABSTRACT NUMERIC[16,2] (salesLedger) PERSISTENT;

sumSoldTypeExchangeSalesLedger 'Сумма продажи валюта' (typeExchange, salesLedger)= roundCurrency((sumSalesLedger (salesLedger)/
    rateTypeExchangeCurrencyDate(typeExchange, currencyStock(stockSalesLedger(salesLedger)), dateSalesLedger(salesLedger))), currencyTypeExchange(typeExchange));


CONSTRAINT quantitySalesLedger(salesLedger) == 0 MESSAGE 'ошибка: Количество продажи не должно быть равно нулю';

averagePriceSalesLedger 'Цена продажи (средняя)' (salesLedger) = sumSalesLedger(salesLedger)/quantitySalesLedger(salesLedger);

VATSalesLedger (ledger) = ABSTRACT range (salesLedger) PERSISTENT;
valueVATSalesLedger 'НДС, %' (ledger) = valueRateRangeDate(VATSalesLedger(ledger), dateSalesLedger(ledger));

sumVATSalesLedger 'Сумма НДС' (ledger) = [X*Y/(100+Y)](
        sumSalesLedger(ledger), valueVATSalesLedger(ledger));


markupSumSalesLedger 'Надбавка' (ledger) = sumSalesLedger(ledger) - sumVATSalesLedger(ledger) - costSumSkuLedger(ledger);

quantitySoldSkuStockDate (sku, stock, date) = GROUP SUM quantitySalesLedger(ledger) IF isPostedSalesLedger(ledger)
    BY skuSalesLedger(ledger), stockSalesLedger(ledger), dateSalesLedger(ledger) PERSISTENT;

sumSoldSkuStockDate (sku, stock, date) = GROUP SUM sumSalesLedger(ledger) IF isPostedSalesLedger(ledger)
    BY skuSalesLedger(ledger), stockSalesLedger(ledger), dateSalesLedger(ledger) PERSISTENT;


quantitySoldSkuStockWeekDateFromTo 'Продано за неделю (кол-во)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockDate(sku, stock, date) AND date >= (dateFrom AS DATE) AND date <= (dateTo AS DATE)
        BY sku, stock, weekInDate(date), dateFrom, dateTo;

sumSoldSkuStockWeekDateFromTo 'Продано за неделю (сумма)' (sku, stock, week, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockDate(sku, stock, date) AND date >= (dateFrom AS DATE) AND date <= (dateTo AS DATE)
        BY sku, stock, weekInDate(date), dateFrom, dateTo;

averagePriceSoldSkuStockWeekDateFromTo 'Средняя цена за неделю' (sku, stock, week, dateFrom, dateTo)=  round2(sumSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) /
        quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo));

averageSoldSkuStockWeekDateFromTo 'Продано за неделю кол-во/цена' (sku, stock, week, dateFrom, dateTo)= [FORMULA STRING[30] 'CAST($1 AS TEXT) || \'(\' || CAST($2 AS TEXT) || \')\'']
        (quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo), averagePriceSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo)) MINCHARWIDTH 10 PREFCHARWIDTH 15;

quantitySkuSoldWeekDateFromTo 'Продано за неделю (кол-во)' (week, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) BY  week, dateFrom, dateTo;

sumSoldWeekDateFromTo 'Продано за неделю (сумма)' (week, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockWeekDateFromTo(sku, stock, week, dateFrom, dateTo) BY  week, dateFrom, dateTo;

quantitySoldSkuStockDateFromTo 'Продано за интервал (кол-во)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockDate(sku, stock, date) AND date >= (dateFrom AS DATE) AND date <= (dateTo AS DATE)
        BY sku, stock, dateFrom, dateTo;

sumSoldSkuStockDateFromTo 'Продано за интервал (сумма)' (sku, stock, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockDate(sku, stock, date) AND date >= (dateFrom AS DATE) AND date <= (dateTo AS DATE)
        BY sku, stock, dateFrom, dateTo;

quantitySoldSkuDateFromTo 'Продано за интервал (кол-во)' (sku, dateFrom, dateTo) = GROUP SUM
        quantitySoldSkuStockDate(sku, stock, date) AND date >= (dateFrom AS DATE) AND date <= (dateTo AS DATE)
        BY sku, dateFrom, dateTo;

sumSoldSkuDateFromTo 'Продано за интервал (сумма)' (sku, dateFrom, dateTo) = GROUP SUM
        sumSoldSkuStockDate(sku, stock, date) AND date >= (dateFrom AS DATE) AND date <= (dateTo AS DATE)
        BY sku, dateFrom, dateTo;

//---------------------------------Тип обмена---------------------------------------//

sumSoldTypeExchangeSkuStockDate (typeExchange, sku, stock, date)= roundCurrency((sumSoldSkuStockDate (sku, stock, date)/ rateTypeExchangeCurrencyDate(typeExchange, currencyStock(stock), date)), currencyTypeExchange(typeExchange));

sumSoldTypeExchangeSkuStockDateFromTo 'Продано за интервал (сумма-валюта)' (typeExchange, sku, stock, dateFrom, dateTo) = GROUP SUM
        sumSoldTypeExchangeSkuStockDate (typeExchange, sku, stock, date) AND date >= (dateFrom AS DATE) AND date <= (dateTo AS DATE)
        BY typeExchange, sku, stock, dateFrom, dateTo;

sumSoldTypeExchangeSkuDateFromTo 'Продано за интервал (сумма-валюта)' (typeExchange, sku, dateFrom, dateTo) = GROUP SUM
        sumSoldTypeExchangeSkuStockDateFromTo(typeExchange, sku, stock, dateFrom, dateTo) BY typeExchange, sku, dateFrom, dateTo;


skuStockAverageInterval 'Интервал расчета продаж в день' = DATA INTEGER ();

averageSoldSkuStock 'Продаж в день' = DATA NUMERIC[14,3] (sku, stock);

calcQuantitySoldInterval = ACTION (dateFrom, dateTo) {
    LOCAL balance = NUMERIC[14,3] (sku, stock);
    LOCAL days = INTEGER (sku, stock);
    LOCAL dateCur = DATE();

    SET dateCur() <- (dateFrom AS DATE);
    SET balance(sku, stock) <- balanceBSkuStockDate(sku, stock, dateFrom);

    WHILE dateCur() <= (dateTo AS DATE) DO {
        SET days(sku, stock) <- days(sku, stock) (+)
                   (1 IF ((balance(sku, stock) > 0) OR (quantitySkuStockDate(sku, stock, dateCur()) > 0)));
        SET balance(sku, stock) <- balance(sku, stock) (+)
                   signedQuantitySkuStockDate(sku, stock, dateCur());
        SET dateCur() <- sumDate(dateCur(), 1);
    }

    SET averageSoldSkuStock(sku, stock) <- quantitySoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) / days(sku, stock);
}
calcCurrentQuantitySoldInterval 'Пересчитать однодневные продажи' = ACTION () NEWSESSION AUTOAPPLY {
    EXEC calcQuantitySoldInterval(subtractDate(currentDate(), skuStockAverageInterval()), subtractDate(currentDate(), 1));
}

EXTEND FORM options
    PROPERTIES() skuStockAverageInterval, calcCurrentQuantitySoldInterval
;
EXTEND DESIGN options {
    commons {
        ADD PROPERTY(skuStockAverageInterval);
        ADD PROPERTY(calcCurrentQuantitySoldInterval);
    }
}

//---------------------------------------------- Формы продаж -------------------------------------//

FORM salesLedger 'Продажи по позициям'

    OBJECTS dates = (dFrom = DATE, dTo = DATE, te=typeExchange) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo), nameType = name(te) SELECTOR, nameCurrencyTypeExchange(te) READONLY

    OBJECTS s = salesLedger
    PROPERTIES(s) READONLY dateSalesLedger, dateTimeSalesLedger, nameStockSalesLedger, nameSkuSalesLedger, descriptionSalesLedger,
                           quantitySalesLedger, costSumSkuLedger, markupSumSalesLedger, sumVATSalesLedger, sumSalesLedger, averagePriceSalesLedger
    PROPERTIES    READONLY sumSoldTypeExchangeSalesLedger (te, s)
    FILTERS isPostedSalesLedger(s)
    FILTERS dateSalesLedger(s) >= dFrom, dateSalesLedger(s) <= dTo
;
DESIGN salesLedger FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        ADD dates.box {
            childConstraints = TO THE RIGHT;
            PROPERTY(nameCurrencyTypeExchange(te)) { caption = 'Валюта'; preferredCharWidth = 20;}
            PROPERTY(nameType) { caption = 'Тип обмена'; preferredCharWidth = 20;}
        }
        ADD s.box{
            childConstraints = TO THE BOTTOM;
        }
    }
    ADD functions.box;
}

FORM salesSkuStock 'Продажи по SKU'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES valFrom = OBJVALUE(dFrom), valTo = OBJVALUE(dTo)

    OBJECTS te = typeExchange FIXED PANEL
    PROPERTIES nameType = name(te) SELECTOR, nameCurrencyTypeExchange(te) READONLY

    OBJECTS w = INTEGER
    FILTERS quantitySkuSoldWeekDateFromTo(w, dFrom, dTo)

    TREE stockTree sg = stockGroup PARENT parentStockGroup, ts = stock
    PROPERTIES READONLY sgTreeName = name(sg), tsTreeName = name(ts)
    FILTERS stockGroupStock(ts) == sg

    TREE skuTree sk = skuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = name(sk)
    ORDER BY skuTreeName

    OBJECTS           sts=(st=stock, s=sku)
    PROPERTIES        READONLY nameSku(s), stockName = name(st)

    FILTERS           isParentSkuGroupSku(sk, s),
                      (st == ts AND sg IS stockGroup) OR (isParentStockGroupStock(sg, st) AND NOT ts)

    ORDER BY          nameSku

    PROPERTIES        balanceBSkuStockDate(s, st, dFrom), quantitySoldSkuStockWeekDateFromTo(s, st, w, dFrom, dTo)  COLUMNS (w) HEADER castToString4 (w),
                      quantitySoldSkuStockDateFromTo(s, st, dFrom, dTo),
                      balanceASkuStockDate(s, st, dTo), sumSoldSkuStockDateFromTo (s, st, dFrom, dTo),  sumSoldTypeExchangeSkuStockDateFromTo(te,s, st, dFrom, dTo)

    FILTERGROUP filtersSold
        FILTER 'Показывать проданные за интервал' 'F11' quantitySoldSkuStockDateFromTo(s, st, dFrom, dTo)

    FILTERGROUP filtersSold
        FILTER 'Показывать с остатками' 'F10' balanceASkuStockDate(s, st, dTo)

;

DESIGN salesSkuStock FROM DEFAULT {

    main {
        NEW topContainer {
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            NEW firstCase {
                type = SPLITV;
                childConstraints = TO THE BOTTOM;

                ADD stockTree.tree.box {title = 'Магазины'; }
                ADD skuTree.tree.box { title = 'Товарные группы'; }
            }

            NEW secondCase {
                type = SPLITV;
                fillHorizontal = 2;
                childConstraints = TO THE BOTTOM;

                ADD dates.box { childConstraints = TO THE RIGHT; }
                ADD te.box {
                    childConstraints = TO THE RIGHT;
                    PROPERTY(nameCurrencyTypeExchange(te)) { caption = 'Валюта'; preferredCharWidth = 20;}
                    PROPERTY(nameType) { caption = 'Тип обмена'; preferredCharWidth = 20;}
                }

                REMOVE w.box;
                ADD sts.box { fillVertical = 2; }

            }
        }
        ADD functions.box;
    }
}

NAVIGATOR {
    NEW salesNavigator 'Продажи' {
        ADD salesLedger;
        ADD salesSkuStock;
    }
}

//---------------------------------------------- Макросы для имплементаций -------------------------------------//

META implementSalesLedgerCustom(concrete, skuProp, stockProp)
    dateTimeSalesLedger (ledger) += dateTime###concrete##Detail(ledger);
    isPostedSalesLedger (ledger) += isPosted###concrete##Detail(ledger);
    skuSalesLedger (ledger) += skuProp###concrete##Detail(ledger);
    stockSalesLedger (ledger) += stockProp###concrete##Detail(ledger);
    descriptionSalesLedger (ledger) += description###concrete##Detail(ledger);
END
META implementSalesLedger(concrete, skuProp, stockProp)
    EXTEND CLASS concrete##Detail : salesLedger;
    @implementSalesLedgerCustom(concrete, skuProp, stockProp);
END

EXTEND FORM currentBalanceSkuStock PROPERTIES(s, st) averageSoldSkuStock READONLY;