MODULE Geo;

REQUIRE Country;

CLASS ABSTRACT POI 'POI';
TABLE POI(POI);

autoSynchronizeCoordinates 'Синхронизировать координаты' = DATA BOOLEAN ();

namePOI 'Название'= ABSTRACT STRING[200] (POI) PERSISTENT;

countryPOI = ABSTRACT Country (POI) PERSISTENT;
nameCountryPOI 'Страна' (poi) = name(countryPOI(poi));

addressPOI 'Адрес' = ABSTRACT STRING[100] (POI) PERSISTENT;

latitudePOI 'Координата X' = ABSTRACT NUMERIC[10,5] (POI);
longitudePOI 'Координата Y' = ABSTRACT NUMERIC[10,5] (POI);

// Получение координат
readLatitude 'Координата X' = DATA SESSION NUMERIC[10,5] ();
readLongitude 'Координата Y' = DATA SESSION NUMERIC[10,5] ();

getCoordinatesAddress = ACTION CUSTOM 'fdk.utils.geo.GetCoordinatesAddressActionProperty';

changeCoordinatesPOI 'Изменить координаты'= ACTION (POI) {

    EXEC getCoordinatesAddress (ustring2CM(nameCountryPOI (POI), addressPOI (POI)));
    SET latitudePOI(POI) <- readLatitude();
    SET longitudePOI(POI) <- readLongitude();

}
WHEN SESSION (CHANGED(countryPOI(POI)) OR CHANGED(addressPOI(POI))) AND autoSynchronizeCoordinates() DO EXEC changeCoordinatesPOI(POI);

// Показать на карте одну точку
showOnMap 'Показать на карте'= ACTION CUSTOM 'fdk.utils.geo.ShowOnMapActionProperty';

showOnMapPOI 'Показать на карте'= ACTION (POI) {
    EXEC showOnMap (latitudePOI (POI), longitudePOI (POI));
}

// Показать на карте путь
numberPathPOI 'Номер' = DATA SESSION INTEGER (POI);
descriptionPathPOI 'Описание' = DATA SESSION STRING[200] (POI);

showOnMapPath 'Показать точки на карте'= ACTION CUSTOM 'fdk.utils.geo.ShowOnMapPathActionProperty';

inPathPOI 'Вкл' = DATA SESSION BOOLEAN (POI);
startPathPOI 'Место старта'= DATA SESSION POI();
calculatePath 'Рассчитать путь'= ACTION CUSTOM 'fdk.utils.geo.CalculatePathActionProperty';

EXTEND FORM options
PROPERTIES() autoSynchronizeCoordinates
;

EXTEND DESIGN options {
    commons {
        ADD PROPERTY(autoSynchronizeCoordinates);
    }
}

