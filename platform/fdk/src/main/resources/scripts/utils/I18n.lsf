MODULE I18n;

REQUIRE System, MasterData, Utils;

// ------------------------------------------------ Язык ---------------------------------------------- //

CLASS dictionary 'Словарь' : named;
TABLE dictionary (dictionary);

insensitiveDictionary 'Нечувствительный к регистру' = DATA BOOLEAN (dictionary);

languageFromDictionary = DATA language (dictionary);
nameLanguageFromDictionary 'С языка'(dictionary) = name(languageFromDictionary(dictionary));
languageToDictionary = DATA language (dictionary);
nameLanguageToDictionary 'На язык'(dictionary) = name(languageToDictionary(dictionary));

CLASS dictionaryEntry 'Слова';
TABLE dictionaryEntry (dictionaryEntry);

dictionaryDictionaryEntry 'Словарь' = DATA dictionary (dictionaryEntry);
termDictionaryEntry 'Термин' = DATA STRING[50] (dictionaryEntry);
insensitiveTermDictionaryEntry(dictionaryEntry) = upper (termDictionaryEntry(dictionaryEntry));
translationDictionaryEntry 'Перевод' = DATA STRING[50] (dictionaryEntry);

dictionaryEntryDictionaryTerm = GROUP UNIQUE dictionaryEntry BY dictionaryDictionaryEntry(dictionaryEntry), termDictionaryEntry(dictionaryEntry) WHERE dictionaryEntry IS dictionaryEntry;
translationDictionaryEntryDictionaryTerm 'Перевод' (dictionary, term) = translationDictionaryEntry(dictionaryEntryDictionaryTerm(dictionary, term));
nameDictionaryDictionaryEntry 'Словарь' (dictionaryEntry) = name (dictionaryDictionaryEntry(dictionaryEntry));
insensitiveDictionaryEntryDictionaryTerm = GROUP MAX dictionaryEntry BY dictionaryDictionaryEntry(dictionaryEntry), insensitiveTermDictionaryEntry(dictionaryEntry) WHERE dictionaryEntry IS dictionaryEntry;
insensitiveTranslationDictionaryEntryDictionaryTerm 'Нечувствительный к регистру перевод' = translationDictionaryEntry (dictionaryEntryDictionaryTerm(dictionary, term));

CLASS language 'Язык' : named;
TABLE language(language);

localeLanguage 'Locale' = DATA STRING[5] (language);

FORM language 'Язык'
    OBJECTS l = language FIXED PANEL
    PROPERTIES(l) name, localeLanguage

    EDIT language OBJECT l
;

FORM languages 'Языки'
    OBJECTS l = language
    PROPERTIES(l) READONLY name, localeLanguage
    PROPERTIES(l)          ADDFORM, EDITFORM, delete

    DIALOG language OBJECT l
;

NAVIGATOR {
    regionalData {
        ADD languages;
    }
}

// ------------------------------------ Перевод через Google Translate -------------------------------- //

translationOriginal 'Текст для перевода' = DATA SESSION TEXT ();
translationResult 'Переведённый текст' = DATA SESSION TEXT ();      //STRING[50]

languageFromTranslation = DATA language ();
nameLanguageFromTranslation 'С языка'() = name(languageFromTranslation());
languageToTranslation = DATA language ();
nameLanguageToTranslation 'На язык'() = name(languageToTranslation());

translate  = ACTION CUSTOM 'fdk.utils.i18n.TranslateActionProperty';

translateTermDictionaryEntry 'Перевести' = ACTION (dictionaryEntry) {

    EXEC translate (toText(termDictionaryEntry(dictionaryEntry)),
            languageFromDictionary(dictionaryDictionaryEntry(dictionaryEntry)),
            languageToDictionary(dictionaryDictionaryEntry(dictionaryEntry)));
    SET translationDictionaryEntry(dictionaryEntry) <- toString50(translationResult());
}

translateText 'Перевести' = ACTION () {
    EXEC translate (translationOriginal(), languageFromTranslation(), languageToTranslation());
}

// ------------------------------------ Перевод через словарь -------------------------------- //

translationOriginalString 'Фраза' = DATA SESSION STRING[50] ();
translationResultString 'Перевод' = DATA SESSION STRING[50] ();

translateDictionary  = ACTION CUSTOM 'fdk.utils.i18n.TranslateDictionaryActionProperty';

WHEN CHANGED (translationOriginalString()) AND dictionary IS dictionary DO EXEC translateDictionary(dictionary, translationOriginalString()) SESSION;

FORM translation 'Перевод'
    PROPERTIES() translationOriginal, translationResult, nameLanguageFromTranslation, nameLanguageToTranslation,
                 translateText
;

EXTEND DESIGN translation {
    main {
        NEW specContainer {
            NEW textContainer {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(nameLanguageFromTranslation());
                ADD PROPERTY(nameLanguageToTranslation());
                ADD PROPERTY(translationOriginal());
                ADD PROPERTY(translationResult());
                ADD PROPERTY(translateText());
                fillHorizontal = 1;
                fillVertical = 1;
                PROPERTY (translationOriginal()) {
                    fillHorizontal = 1;
                    fillVertical = 0.5;
                    preferredSize = ( -1, 200);
                    panelLabelAbove = TRUE;
                }
                PROPERTY (translationResult()) {
                    fillHorizontal = 1;
                    fillVertical = 0.5;
                    preferredSize = ( -1, 200);
                    panelLabelAbove = TRUE;
                }
            }
            ADD functions.box;
        }
    }
}

NAVIGATOR {
    regionalData {
        ADD translation;
    }
}

// ---------------------------------------------- Мультиязычный объект -------------------------------- //

CLASS multiLanguageNamed 'Мультиязычный объект';

TABLE multiLanguageNamedLanguage(multiLanguageNamed, language);
languageName 'Название (иностр.)' = DATA ISTRING[110] (multiLanguageNamed, language);

// ---------------------------------------------- Словари --------------------------------------------- //

FORM dictionary 'Словарь'
    OBJECTS d = dictionary FIXED PANEL
    PROPERTIES(d) name, insensitiveDictionary, nameLanguageFromDictionary, nameLanguageToDictionary

    OBJECTS e = dictionaryEntry
    PROPERTIES(e) termDictionaryEntry, translationDictionaryEntry, translateTermDictionaryEntry, ADDOBJ, delete
    FILTERS       dictionaryDictionaryEntry(e) == d

    EDIT dictionary OBJECT d

    PROPERTIES() translationOriginalString, translationResultString
;

DESIGN dictionary FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        ADD d.box;
        ADD e.box;
        NEW test {
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(translationOriginalString) {
                caption = 'Фраза для перевода';
                panelLabelAbove = TRUE;
                font = 'Tahoma bold 24';
            };
            ADD PROPERTY(translationResultString){
                caption = 'Перевод';
                panelLabelAbove = TRUE;
                font = 'Tahoma bold 24';
            };
        }
        ADD functions.box;
    }
}

FORM dictionaries 'Словари'
    OBJECTS d = dictionary
    PROPERTIES(d) READONLY name, insensitiveDictionary, nameLanguageFromDictionary, nameLanguageToDictionary

    PROPERTIES(d) ADDFORM, EDITFORM, delete

    DIALOG dictionary OBJECT d
;

NAVIGATOR {
    regionalData {
        ADD dictionaries;
    }
}
