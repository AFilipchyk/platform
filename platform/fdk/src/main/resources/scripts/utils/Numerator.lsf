MODULE Numerator;

REQUIRE System, MasterData, DefaultData;

// ------------------------------------------- Нумераторы ----------------------------------------------- //

CLASS numerator 'Нумератор' : named;
TABLE numerator (numerator);

seriesNumerator 'Серия' = DATA STRING[2] (numerator) IN recognize FIXEDCHARWIDTH 3;

minValueNumerator 'Мин. значение' = DATA LONG (numerator) IN recognize MINCHARWIDTH 15;
maxValueNumerator 'Макс. значение' = DATA LONG (numerator) IN recognize MINCHARWIDTH 15;

stringLengthNumerator 'Длина' = DATA INTEGER (numerator) IN base;

curValueNumerator 'Тек. значение' = DATA LONG (numerator) IN recognize MINCHARWIDTH 15;
curValueNumerator(numerator) <- minValueNumerator(numerator) WHEN ASSIGNED(numerator IS numerator);

lpadStringWithZero = FORMULA STRING[18] 'lpad(CAST($1 AS text),$2,\'0\')';

curStringValueNumerator 'Тек. значение (строка)' (numerator) = lpadStringWithZero(curValueNumerator(numerator), stringLengthNumerator(numerator)) IN base;

incrementValueNumerator 'Увеличить значение' = ACTION (numerator) NEWSESSION {
    IF curValueNumerator(numerator) >= maxValueNumerator(numerator) THEN
        MESSAGE 'Счетчик нумератора достиг максимального значения. Обратитесь к администратору.'
    ELSE {
        SET curValueNumerator(numerator) <- curValueNumerator(numerator) + 1;
        EXEC apply();
    };
};

incrementValueSessionNumerator 'Увеличить значение' = ACTION (numerator) {
    IF curValueNumerator(numerator) >= maxValueNumerator(numerator) THEN
        MESSAGE 'Счетчик нумератора достиг максимального значения. Обратитесь к администратору.'
    ELSE
        SET curValueNumerator(numerator) <- curValueNumerator(numerator) + 1;
}

FORM numerator 'Нумератор'
    OBJECTS n = numerator FIXED PANEL
    PROPERTIES(n) name, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator

    EDIT numerator OBJECT n
;

FORM numerators 'Нумераторы'
    OBJECTS n = numerator
    PROPERTIES(n) READONLY name, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator
    PROPERTIES(n)          ADDFORM, EDITFORM, delete

    DIALOG numerator OBJECT n
;

FORM defaultNumerators 'Нумераторы'
    OBJECTS n = numerator
    PROPERTIES(n) READONLY name, seriesNumerator, minValueNumerator, maxValueNumerator, stringLengthNumerator, curValueNumerator, curStringValueNumerator
    PROPERTIES(n)          ADDFORM, EDITFORM, delete
;

DESIGN defaultNumerators FROM DEFAULT {
    main {
        NEW defaultContainer AFTER n.box {
            title = 'Нумераторы по умолчанию';
        }
    }
}

NAVIGATOR {
    masterData {
        ADD defaultNumerators;
    }
}

// ---------------------------------------------- Нумерованные объекты ----------------------------------------------- //

GROUP numberedGroup 'Нумератор' : base;

CLASS ABSTRACT numberedObject 'Нумерованный объект';
TABLE numberedObject (numberedObject);

numberObject 'Номер' = DATA STRING[18] (numberedObject) IN numberedGroup MINCHARWIDTH 7;
seriesObject 'Серия' = DATA STRING[2] (numberedObject) IN numberedGroup FIXEDCHARWIDTH 3;

seriesNumberObject 'Серия/Номер' (numberedObject) = ustring2(seriesObject(numberedObject), numberObject(numberedObject)) MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

initValueNumberObject = ABSTRACT STRING[18] (numberedObject);
initValueSeriesObject = ABSTRACT STRING[2] (numberedObject);

initWhenNumberedObject = ABSTRACT BOOLEAN (numberedObject);

numberObject (object) <- initValueNumberObject(object) WHEN initWhenNumberedObject(object);
seriesObject (object) <- initValueSeriesObject(object) WHEN initWhenNumberedObject(object);

CLASS ABSTRACT numeratedObject 'Нумерируемый объект' : numberedObject;

numeratorObject 'Нумератор (ИД)' = DATA numerator (numeratedObject);
nameNumeratorObject 'Нумератор' (numeratedObject) = name(numeratorObject(numeratedObject)) IN numberedGroup MINCHARWIDTH 7 PREFCHARWIDTH 15;

initValueNumeratorObject = ABSTRACT numerator (numeratedObject);
initWhenNumeratorObject = ABSTRACT BOOLEAN (numeratedObject);

numeratorObject (object) <- initValueNumeratorObject (object) WHEN initWhenNumeratorObject(object);

setValueNumeratedObject 'Сгенерировать номер' = ACTION (numeratedObject) {
    SET numberObject(numeratedObject) <- curStringValueNumerator(numeratorObject(numeratedObject));
    SET seriesObject(numeratedObject) <- seriesNumerator(numeratorObject(numeratedObject));
    EXEC incrementValueNumerator(numeratorObject(numeratedObject));
} IN numberedGroup SHORTCUT numberObject;

setValueSessionNumeratedObject 'Сгенерировать номер' = ACTION (numeratedObject) {
    SET numberObject(numeratedObject) <- curStringValueNumerator(numeratorObject(numeratedObject));
    SET seriesObject(numeratedObject) <- seriesNumerator(numeratorObject(numeratedObject));
    EXEC incrementValueSessionNumerator(numeratorObject(numeratedObject));
} IN numberedGroup SHORTCUT numberObject;

WHEN CHANGED(numeratorObject(numeratedObject)) AND
     NOT CHANGED(numberObject(numeratedObject)) AND
     NOT CHANGED(seriesObject(numeratedObject))
     DO EXEC setValueSessionNumeratedObject(numeratedObject) ;

// --------------------------------------------- Стандартные значения ---------------------------- //

loadDefaultNumerators 'Загрузить стандартные нумераторы' = ABSTRACT ACTION();
@implementLoadDefaultData(loadDefaultNumerators);

// --------------------------------------------- Макрос по объявлению нумерируемого объекта ---------------------------- //

META defineNumeratedObjectDefault(object, caption, series)
    @defineNumeratedObjectDefaultPrefix(object, caption, series, );
END

META defineNumeratedObjectDefaultPrefix(object, caption, series, prefix)

    prefix###defaultNumerator###object = DATA numerator ();
    name###prefix##DefaultNumerator###object caption = name(prefix###defaultNumerator###object());

    EXTEND FORM defaultNumerators
        PROPERTIES() name###prefix##DefaultNumerator###object
    ;
    EXTEND DESIGN defaultNumerators {
        defaultContainer {
            ADD PROPERTY(name###prefix##DefaultNumerator###object);
        }
    }

    loadDefaultNumerators() += ACTION () {
        ADDOBJ numerator;
        FOR n == addedObject() DO {
            SET name(n) <- caption;
            SET seriesNumerator(n) <- series;
            SET minValueNumerator(n) <- 1L;
            SET maxValueNumerator(n) <- 99999L;
            SET stringLengthNumerator(n) <- 5;

            SET prefix###defaultNumerator###object() <- n;
        }
    }

    initValueNumeratorObject(object) += prefix###defaultNumerator###object() IF object IS object;
    initWhenNumeratorObject(object) += ASSIGNED(object IS object);

END

//------------------------------------------ Для двух объектов ---------------------------------------------//

META defineNumeratedObject(object1, object2)

    number###object1###object2 'Номер' = DATA STRING[18] (object1, object2) IN numberedGroup MINCHARWIDTH 7;
    series###object1###object2 'Серия' = DATA STRING[2] (object1, object2) IN numberedGroup FIXEDCHARWIDTH 3;
    seriesNumber###object1###object2 'Серия/Номер' (object1, object2) = ustring2(series###object1###object2(object1, object2), number###object1###object2(object1, object2)) MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;

    numerator###object1###object2 = DATA numerator (object1, object2);
    nameNumerator###object1###object2 'Нумератор' (object1, object2) = name(numerator###object1###object2(object1, object2)) IN numberedGroup MINCHARWIDTH 7 PREFCHARWIDTH 15;

    setValueSessionNumerated###object1###object2 'Сгенерировать номер' = ACTION (object1, object2) {
        SET number###object1###object2(object1, object2) <- curStringValueNumerator(numerator###object1###object2(object1, object2));
        SET series###object1###object2(object1, object2) <- seriesNumerator(numerator###object1###object2(object1, object2));
        EXEC incrementValueSessionNumerator(numerator###object1###object2(object1, object2));
    } IN numberedGroup SHORTCUT number###object1###object2;

    WHEN CHANGED(numerator###object1###object2(object1, object2)) AND
         NOT CHANGED(number###object1###object2(object1, object2)) AND
         NOT CHANGED(series###object1###object2(object1, object2))
         DO EXEC setValueSessionNumerated###object1###object2(object1, object2) ;

END

META defineNumeratedObjectDefault (object1, object2, property, caption, series)

    defaultNumerator###object1###object2 = DATA numerator ();
    nameDefaultNumerator###object1###object2 caption = name(defaultNumerator###object1###object2());

    EXTEND FORM defaultNumerators
        PROPERTIES() nameDefaultNumerator###object1###object2
    ;
    EXTEND DESIGN defaultNumerators {
        defaultContainer {
            ADD PROPERTY(nameDefaultNumerator###object1###object2);
        }
    }

    loadDefaultNumerators() += ACTION () {
        ADDOBJ numerator;
        FOR n == addedObject() DO {
            SET name(n) <- caption;
            SET seriesNumerator(n) <- series;
            SET minValueNumerator(n) <- 1L;
            SET maxValueNumerator(n) <- 99999L;
            SET stringLengthNumerator(n) <- 5;

            SET defaultNumerator###object1###object2() <- n;
        }
    };

    numerator###object1###object2 (object1, object2) <- defaultNumerator###object1###object2 () WHEN ASSIGNED(property(object1, object2));
END
