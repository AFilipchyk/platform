MODULE Excel;

REQUIRE System, MasterData;

CLASS Template 'Шаблон';
TABLE template(Template);

nameTemplate 'Имя' = DATA STRING[100] (Template);
fileTemplate 'Файл шаблона' (Template) = DATA EXCELFILE (Template);
loadFileTemplate 'Загрузить шаблон'(t) = ACTION LOADFILE fileTemplate(t);
openFileTemplate 'Открыть шаблон'(t) = ACTION OPENFILE fileTemplate(t);

processTemplate = ACTION CUSTOM 'fdk.utils.excel.ProcessTemplateActionProperty';

resultTemplate = DATA SESSION EXCELFILE ();
openResultTemplate () = ACTION OPENFILE resultTemplate();

openProcessTemplate 'Сгенерировать документ' = ACTION (template) {
    processTemplate(template);
    openResultTemplate();
}

CLASS TemplateEntry 'Строка шаблона';
TABLE templateEntry(TemplateEntry);

templateTemplateEntry 'Шаблон' = DATA Template (TemplateEntry);
keyTemplateEntry 'Параметр' = DATA STRING[100](TemplateEntry);
valueTemplateEntry 'Значение' = DATA SESSION TEXT (TemplateEntry);

FORM templates 'Шаблоны Excel'

    OBJECTS t = Template
    PROPERTIES (t) nameTemplate, loadFileTemplate, openFileTemplate, openProcessTemplate TOOLBAR, ADDOBJ, delete

    OBJECTS te = TemplateEntry
    PROPERTIES (te) keyTemplateEntry, valueTemplateEntry, ADDOBJ, delete

    FILTERS templateTemplateEntry (te) == t
;

NAVIGATOR {
    masterData {
        templatesNavigator {
            ADD templates;
        }
    }
}



