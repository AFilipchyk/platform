MODULE EvalScript;

REQUIRE System, Utils;

NAMESPACE System;

scriptStorage 'Скрипт' () = DATA TEXT ();

scriptExecution 'Выполнить скрипт' () = ACTION EVAL scriptStorage();

actionBodyExecution 'Выполнить action' () = ACTION () {
     LOCAL s = TEXT();
     s() <- toText('run() = ACTION {');
     s() <- s() + scriptStorage();
     s() <- s() + ';\n};';
     EVAL s();
}

formExecution 'Показать форму' () = ACTION {
     LOCAL s = TEXT();
     s() <- toText('FORM form');
     s() <- s() + ' ';
     s() <- s() + scriptStorage();
     s() <- s() + ';\nrun() = ACTION FORM form;';
     EVAL s();
}

FORM mainForm 'Скрипт'
    PROPERTIES() scriptStorage,
                 scriptExecution, actionBodyExecution, formExecution
;

DESIGN mainForm FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        NEW south BEFORE functions.box {
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(scriptExecution);
            ADD PROPERTY(actionBodyExecution);
            ADD PROPERTY(formExecution);
        }
        ADD PROPERTY(scriptStorage) BEFORE south {
            panelLabelAbove = TRUE;
            preferredSize = ( -1, 600);
            fillVertical = 1.0;
            fillHorizontal = 1.0;
        }
    }
}

NAVIGATOR {
    configuration {
        ADD mainForm;
    }
}

