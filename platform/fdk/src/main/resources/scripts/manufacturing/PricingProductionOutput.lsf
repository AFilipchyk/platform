MODULE  PricingProductionOutput;

REQUIRE ProductionOutput, Pricing;
NAMESPACE Production;

// ----------------  Расценка ------------------- //

@defineDocumentInterfaceHeaderPricingCommittee (output, stock);
@defineDocumentInterfaceCreate (output, createPricing, 'Создать акт расценки');

@defineDocumentInterfaceDetailPricePrefix(output, supplier, ' входная');
supplierPriceUserOutputDetail(detail) <- componentsPriceProductDetail(productDetailUserOutputDetail(detail))
    WHEN CHANGED(productDetailUserOutputDetail(detail));

@defineDocumentInterfaceDetailDataSumPrefix (output, supplier, ' входная');

@deriveDocumentDetailSumPrefix(userOutput, supplier, currency, quantity);

@defineDocumentHeaderSumCustomPrefix (output, outputDetail, supplier,' входная');

@defineDocumentInterfaceDetailPricePrefix(output, retail, ' выходная');
retailPriceUserOutputDetail(detail) <- productsPriceProductDetail(productDetailUserOutputDetail(detail))
                  WHEN CHANGED(productDetailUserOutputDetail(detail));
@defineDocumentInterfaceDetailVAT(output, countryStock, retail, );
valueRetailVATUserOutputDetail(detail) <- valueVATProductDetail(productDetailUserOutputDetail(detail))
                  WHEN CHANGED(productDetailUserOutputDetail(detail));


@defineDocumentInterfaceDetailMarkupPrefix (output, retail, );
retailMarkupUserOutputDetail(detail) <- [round2(((X/Z*100/(100+Y))-1)*100)](
                                        0.0 IF detail IS UserOutputDetail OR retailPriceUserOutputDetail(detail),
                                        supplierPriceUserOutputDetail(detail),
                                        0.0 IF detail IS UserOutputDetail OR valueRetailVATUserOutputDetail(detail))
        WHEN CHANGED(retailPriceUserOutputDetail(detail)) OR
             CHANGED(supplierPriceUserOutputDetail(detail)) OR
             CHANGED(retailPriceUserOutputDetail(detail))
;
//@changeDocumentDetailMarkupCustomPrefix(userOutputDetail, retail, , retail, retail);
//@changeDocumentDetailPriceCustomPrefix(userOutputDetail, retail, , retail, retail, currency);
@defineDocumentInterfaceDetailDataSumPrefix (output, retail, ' выходная');
@deriveDocumentDetailSumPrefix(userOutput, retail, currency, quantity);

@defineDocumentInterfaceHeaderSumPrefix (output, retail, ' выходная');

@defineDocumentInterfaceDetailVATDataSumPrefix (output, retail, ' розничная');
@defineDocumentInterfaceDetailMarkupSumPrefix (output, retail);

VATMarkupSumUserOutputDetail 'НДС и надбавка' (userOutputDetail) =  retailSumUserOutputDetail(userOutputDetail) (-) supplierSumUserOutputDetail(userOutputDetail);

retailVATSumUserOutputDetail(userOutputDetail) <- roundPriceCurrency([X*Y/(100+Y)](VATMarkupSumUserOutputDetail(userOutputDetail), valueRetailVATUserOutputDetail(userOutputDetail)), currencyUserOutputDetail(userOutputDetail))
        WHEN CHANGED(VATMarkupSumUserOutputDetail(userOutputDetail)) OR
             CHANGED(valueRetailVATUserOutputDetail(userOutputDetail)) OR
             CHANGED(currencyUserOutputDetail(userOutputDetail));

retailMarkupSumUserOutputDetail (userOutputDetail) <- VATMarkupSumUserOutputDetail(userOutputDetail) (-) retailVATSumUserOutputDetail(userOutputDetail)
        WHEN CHANGED(VATMarkupSumUserOutputDetail(userOutputDetail)) OR
             CHANGED(retailVATSumUserOutputDetail(userOutputDetail));

retailMarkupUserOutputDetail(userOutputDetail) <-[round2(X*100/Y)](retailMarkupSumUserOutputDetail(userOutputDetail),supplierSumUserOutputDetail(userOutputDetail))
        WHEN CHANGED(retailMarkupSumUserOutputDetail(userOutputDetail)) OR
             CHANGED(supplierSumUserOutputDetail(userOutputDetail));

@defineDocumentInterfaceHeaderSumPrefix (output, retailMarkup, ' надбавки');
@defineDocumentInterfaceHeaderSumPrefix (output, retailVAT, ' НДС');

//-- аггр.расценка на основе заказа
@defineDocumentHeaderCreate (order, createPricing, 'Создать акт расценки');
backgroundRetailOrder 'Цвет' (order) = RGB(224, 255, 255) IF order IS Order;

@defineDocumentHeaderPricingCommittee(order, productsStock, );

//-- Свойства для аггрегированных производств/расценок

componentsSumProductDetail 'Сумма компонентов произведенного изделия' (productDetail)= roundPriceCurrency((componentsPriceProductDetail(productDetail)*outputQuantityProductDetail(productDetail)),currencyProductDetail(productDetail));
VATMarkupSumProductDetail 'НДС и надбавка' (productDetail) =  outputProductsSumProductDetail(productDetail) (-) componentsSumProductDetail(productDetail);
retailVATSumProductDetail 'Сумма НДС' (productDetail) = roundPriceCurrency([X*Y/(100+Y)](VATMarkupSumProductDetail(productDetail), valueVATProductDetail(productDetail)), currencyProductDetail(productDetail));
retailMarkupSumProductDetail 'Сумма надбавки' (productDetail) =  VATMarkupSumProductDetail(productDetail) (-) retailVATSumProductDetail(productDetail);
retailMarkupProductDetail 'Надбавка, %'(productDetail) =[round2(X*100/Y)](retailMarkupSumProductDetail(productDetail), componentsSumProductDetail(productDetail));

pricingCommitteeOutput(output) += pricingCommitteeOrder(orderOrderOutput(output));
createPricingOutput(output) += createPricingOrder(orderOrderOutput(output));

supplierPriceOutputDetail(outputDetail) += componentsPriceProductDetail(productDetailOrderOutputDetail(outputDetail));
supplierSumOutputDetail(outputDetail) += componentsSumProductDetail(productDetailOrderOutputDetail(outputDetail));

valueRetailVATOutputDetail(outputDetail) += valueVATProductDetail(productDetailOrderOutputDetail(outputDetail));
retailMarkupOutputDetail(outputDetail) += retailMarkupProductDetail(productDetailOrderOutputDetail(outputDetail));
retailVATSumOutputDetail(outputDetail)+= retailVATSumProductDetail(productDetailOrderOutputDetail(outputDetail));
retailMarkupSumOutputDetail(outputDetail) += retailMarkupSumProductDetail(productDetailOrderOutputDetail(outputDetail));
retailPriceOutputDetail(outputDetail) += productsPriceProductDetail(productDetailOrderOutputDetail(outputDetail));
retailSumOutputDetail(outputDetail) += outputProductsSumProductDetail(productDetailOrderOutputDetail(outputDetail));

EXTEND FORM order
    PROPERTIES(o)  BACKGROUND backgroundRetailOrder(o) createPricingOrder, namePricingCommitteeOrder SHOWIF createPricingOrder(o)
    PROPERTIES(pd) BACKGROUND backgroundRetailOrder(o) SHOWIF createPricingOrder(o) READONLY BEFORE outputProductsSumProductDetail
                   componentsSumProductDetail, retailMarkupProductDetail, retailMarkupSumProductDetail, retailVATSumProductDetail

;
EXTEND DESIGN order {
    headerRow1 {
        NEW headerRow13 BEFORE headerRow12 {
            title = 'Расценка';
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY(createPricingOrder);
            ADD PROPERTY(namePricingCommitteeOrder);
        }
    }
}

EXTEND FORM orders
    PROPERTIES(o) READONLY BACKGROUND backgroundRetailOrder(o) createPricingOrder, namePricingCommitteeOrder
    PROPERTIES(pd) BACKGROUND backgroundRetailOrder(o) SHOWIF createPricingOrder(o) READONLY BEFORE outputProductsSumProductDetail
                   componentsSumProductDetail, retailMarkupProductDetail, retailMarkupSumProductDetail, retailVATSumProductDetail
;


//-- аггр.расценка на основе производства

backgroundRetailOutput 'Цвет' (output) = RGB(224, 255, 255) IF output IS Output;

EXTEND FORM userOutput
    PROPERTIES(o)  BACKGROUND backgroundRetailOutput(o) createPricingUserOutput, namePricingCommitteeUserOutput SHOWIF createPricingUserOutput(o)
    PROPERTIES (d) BEFORE delete(d) SHOWIF createPricingUserOutput(o) BACKGROUND backgroundRetailOutput(o)
                   supplierPriceUserOutputDetail, supplierSumUserOutputDetail, retailMarkupUserOutputDetail, retailMarkupSumUserOutputDetail,
                   valueRetailVATUserOutputDetail, retailVATSumUserOutputDetail, retailPriceUserOutputDetail, retailSumUserOutputDetail
;
EXTEND DESIGN userOutput {
    headerRow12 {
        NEW headerRow121 BEFORE headerRow122 {
            title = 'Расценка';
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY(createPricingUserOutput);
            ADD PROPERTY(namePricingCommitteeUserOutput);
        }
    }
}

EXTEND FORM outputs
    PROPERTIES(o) READONLY BACKGROUND backgroundRetailOutput(o) createPricingOutput, namePricingCommitteeOutput
    PROPERTIES (d) READONLY SHOWIF createPricingOutput(o) BACKGROUND backgroundRetailOutput(o) supplierPriceOutputDetail, supplierSumOutputDetail, retailMarkupOutputDetail, retailMarkupSumOutputDetail,
                   valueRetailVATOutputDetail, retailVATSumOutputDetail, retailPriceOutputDetail, retailSumOutputDetail
;

CLASS OutputPricing 'Акт расценки на основе производства' : Pricing;
CLASS OutputPricingPosted 'Проведенный акт расценки на основе производства': OutputPricing, PostedObject;
CLASS OutputPricingDetail 'Строка акта расценки на основе производства' : PricingDetail;

@defineDocumentTables(outputPricing);

@defineDocumentAggregation(output, outputPricing, createPricingOutput);

printPricingOutput 'Акт расценки' (output) = printPricing(outputPricingOutput(output)) IMAGE 'print.png' IN printGroup;
EXTEND FORM outputs
    PROPERTIES(o) FORCE PANEL printPricingOutput SHOWIF createPricingOutput(o)
;
EXTEND DESIGN outputs {printContainer { ADD o.printGroup;}}

pricingPricingDetail(detail) += outputPricingOutputPricingDetail(detail);
@defineDocumentDetailIndex(outputPricing);

datePricing(pricing) += dateOutputPricing(pricing);
timePricing(pricing) += timeOutputPricing(pricing);


departmentStorePricing(pricing) += stockOutput(outputOutputPricing(pricing)) IF stockOutput(outputOutputPricing(pricing)) IS DepartmentStore;
dataDepartmentStorePricingDetail(pricingDetail) += stockOutput(outputOutputPricing(outputPricingOutputPricingDetail(pricingDetail)))
                                                   IF stockOutput(outputOutputPricing(outputPricingOutputPricingDetail(pricingDetail))) IS DepartmentStore;

supplierPricing(pricing) += legalEntityStockOutput(outputOutputPricing(pricing));

@defineDocumentAggregationPosted(output, outputPricing);
isPostedPricing(pricing) += isPostedOutputPricing(pricing);

numberOutputPricing 'Номер документа' (outputPricing) = numberOutput(outputOutputPricing(outputPricing));
numberPricing(pricing) += numberOutputPricing(pricing);
seriesOutputPricing 'Серия документа' (outputPricing) = seriesOutput(outputOutputPricing(outputPricing));
seriesPricing(pricing) += seriesOutputPricing(pricing);
seriesNumberOutputPricing 'Серия/номер документа' (outputPricing) = seriesNumberOutput(outputOutputPricing(outputPricing));


notePricing(pricing) += noteOutput(outputOutputPricing(pricing));
currencyPricing (pricing) += currencyOutput(outputOutputPricing(pricing));
pricingCommitteePricing(pricing) += pricingCommitteeOutput(outputOutputPricing(pricing));

@defineDocumentDescription(outputPricing, outputPricingDetail, seriesNumberOutputPricing, 'Акт расценки на основе производства');
descriptionPricing (pricing) += descriptionOutputPricing(pricing);

skuPricingDetail(pricingDetail) +=  skuOutputDetail(outputDetailOutputPricingDetail(pricingDetail));
quantityPricingDetail(pricingDetail) += quantityOutputDetail(outputDetailOutputPricingDetail(pricingDetail));

//-------------------------- Ценовые характеристики------------------------------------------
@defineDocumentAggregationDetailProperty (output, outputPricing, supplierPrice, 'Цена входная');
pricePricingDetail(pricingDetail) += supplierPriceOutputPricingDetail(pricingDetail);
@defineDocumentAggregationDetailProperty (output, outputPricing, supplierSum, 'Сумма входная');
sumPricingDetail(pricingDetail) += supplierSumOutputPricingDetail(pricingDetail);

@defineDocumentAggregationDetailProperty (output, outputPricing, retailPrice, 'Цена выходная');
retailPricePricingDetail(pricingDetail) += retailPriceOutputPricingDetail(pricingDetail);
@defineDocumentAggregationDetailProperty (output, outputPricing, retailSum, 'Сумма выходная');
retailSumPricingDetail(pricingDetail) += retailSumOutputPricingDetail(pricingDetail);

@defineDocumentAggregationDetailProperty (output, outputPricing, retailMarkup, 'Надбавка');
retailMarkupPricingDetail(pricingDetail) += retailMarkupOutputPricingDetail(pricingDetail);
@defineDocumentAggregationDetailProperty (output, outputPricing, retailMarkupSum, 'Сумма надбавки');
retailMarkupSumPricingDetail(pricingDetail) += retailMarkupSumOutputPricingDetail(pricingDetail);

@defineDocumentAggregationDetailProperty (output, outputPricing, valueRetailVAT, 'НДС,%');      // пока только значение ставки, без самой шкалы.!!!!!
valueRetailVATPricingDetail(pricingDetail) += valueRetailVATOutputPricingDetail(pricingDetail);
@defineDocumentAggregationDetailProperty (output, outputPricing, retailVATSum, 'Сумма НДС');
retailVATSumPricingDetail(pricingDetail) += retailVATSumOutputPricingDetail(pricingDetail);

//--
quantityOutputOutputPricing (output, outputPricing) = GROUP SUM quantityOutputDetail(outputDetailOutputPricingDetail(detail)) BY outputOutputDetail(outputDetailOutputPricingDetail(detail)), outputPricingOutputPricingDetail(detail);

outputsOutputPricing 'Документы производства' (outputPricing) = GROUP CONCAT toString255(descriptionOutput(output)) IF quantityOutputOutputPricing (output, outputPricing) , ', '
                                                BY outputPricing
                                                ORDER output MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;
//descriptionDocumentPricing(pricing) += outputsOutputPricing(pricing);
//EXTEND FORM pricings
//    PROPERTIES(p)  outputsOutputPricing
//;

printPricingOrder 'Акт расценки' (order) = printPricingOutput(orderOutputOrder(order)) IMAGE 'print.png' IN printGroup;

EXTEND FORM orders
    PROPERTIES(o) FORCE PANEL printPricingOrder SHOWIF createPricingOrder(o)
;
EXTEND DESIGN orders {printContainer { ADD o.printGroup;}}



