MODULE  ProductionOrder;

REQUIRE Substitute, PriceList;
NAMESPACE Production;

// ---------------- Производственный заказ ------------------- //
CLASS order 'Производственный заказ' : historizable, numeratedObject;
CLASS orderPosted 'Проведенный производственный заказ' : order, postedObject;
TABLE order(order);

@defineDocumentHeaderTime(order);
@defineDocumentHeaderNote(order);

@defineDocumentHeaderPosted (order);
@defineDocumentHeaderPriceListType (order);

@defineDocumentHeaderStock (order, stock, 'Склад сырья', raw);
@defineDocumentHeaderStock (order, stock, 'Склад готовой продукции', goods);

@defineDocumentHeaderDatePrefix (order, from, ' действия с');
@defineDocumentHeaderDatePrefix (order, to, ' действия по');

@defineDocumentHeaderDescription(order, 'Производственный заказ');
@defineDocumentHeaderCurrency (order);
@deriveDocumentCurrency (order, rawStock, currencyStock);

@defineNumeratedObjectDefault(order, 'Нумератор для производственных заказов', 'ПЗ');

// ---------------- Изделие ------------------- //
CLASS productDetail 'Строка изделия' : named;
TABLE productDetail (productDetail);

@defineDocumentRelation (order, productDetail, );
@defineDocumentDetailIndex (order, productDetail);
@defineDocumentDetailActionsCustom (order, productDetail);
@defineDocumentDetailPosted (order, productDetail);
@defineDocumentDetailTime (order, productDetail);
@defineDocumentDetailDescription(order, productDetail);
@defineDocumentDetailDatePrefix (order, productDetail, from, ' действия с');
fromDateOrder(order) <- currentDate() WHEN ASSIGNED(order IS order);
@defineDocumentDetailDatePrefix (order, productDetail, to, ' действия по');

@defineDocumentDetailQuantityCustom (productDetail);
@defineDocumentHeaderQuantityCustomPrefix (order, productDetail, , ' (изделие)');

@defineDocumentDetailSkuCustom (productDetail, sku);

productProductDetail = DATA product (productDetail);
nameProductProductDetail 'Изделие' (productDetail) = nameMaterial(productProductDetail(productDetail)) IN recognize;

UOMProductDetail = DATA UOM (productDetail);
shortNameUOMProductDetail 'Ед.изм.' (productDetail) = shortName(UOMProductDetail(productDetail)) IN recognize;
UOMProductDetail(productDetail) <-UOMProduct(productProductDetail(productDetail)) WHEN CHANGED (productProductDetail(productDetail));

CONSTRAINT skuProductDetail(productDetail) != skuProduct(productProductDetail(productDetail)) CHECKED BY productProductDetail
    MESSAGE 'Sku изделия и строки изделия не соответствуют друг другу';

@defineDocumentDetailPriceCustomPrefix (productDetail, , );

currencyProductDetail (productDetail)= currencyOrder(orderProductDetail(productDetail));

sumProductDetail 'Сумма' = DATA NUMERIC[16,2] (productDetail);
sumProductDetail(productDetail) <- toNumeric16p2(roundCurrency((quantityProductDetail(productDetail) * priceProductDetail(productDetail)), currencyProductDetail(productDetail)))
    WHEN CHANGED (quantityProductDetail(productDetail)) OR
         CHANGED (priceProductDetail(productDetail)) OR
         CHANGED(currencyProductDetail(productDetail));

// ------------- Компоненты ----------- //
CLASS componentDetail 'Строка компонента' : named;
TABLE componentDetail (componentDetail);

@defineDocumentRelation (order, componentDetail, );
@defineDocumentDetailIndex (order, componentDetail);
@defineDocumentDetailActionsCustom (order, componentDetail);
@defineDocumentDetailPosted (order, componentDetail);
@defineDocumentDetailTime (order, componentDetail);
@defineDocumentDetailDescription(order, componentDetail);
@defineDocumentDetailDatePrefix (order, componentDetail, from, ' действия с');
@defineDocumentDetailDatePrefix (order, componentDetail, to, ' действия по');

@defineDocumentDetailQuantityCustomPrefix (componentDetail, brutto, ' (брутто)');
@defineDocumentHeaderQuantityCustomPrefix (order, componentDetail, brutto, ' (материал брутто)');

@defineDocumentDetailSkuCustom (componentDetail, sku);

componentComponentDetail = DATA component (componentDetail);
nameComponentComponentDetail 'Компонент' (componentDetail) = nameComponent(componentComponentDetail(componentDetail));

UOMComponentDetail = DATA UOM (componentDetail);
shortNameUOMComponentDetail 'Ед. изм.' (componentDetail) = shortName(UOMComponentDetail(componentDetail));
UOMComponentDetail(componentDetail) <- UOMComponent(componentComponentDetail(componentDetail)) WHEN CHANGED (componentComponentDetail(componentDetail));

CONSTRAINT materialComponent(componentComponentDetail(componentDetail)) IS sku AND NOT skuComponentDetail(componentDetail) == materialComponent(componentComponentDetail(componentDetail))
    CHECKED BY componentComponentDetail MESSAGE 'Sku компонента и строки компонента не соответствуют друг другу';

productComponentDetail = DATA product (componentDetail);
nameMaterialComponentDetail 'Изделие' (componentDetail) = nameMaterial(productComponentDetail(componentDetail));

CONSTRAINT skuComponentDetail(componentDetail) != skuProduct(productComponentDetail(componentDetail))
    CHECKED BY productComponentDetail MESSAGE 'Sku изделия и строки компонента не соответствуют друг другу';

//--

quantityBOMOrder 'Кол-во спецификаций' (BOM, order) = ceil([GROUP MAX quantityProductDetail(productDetail) / quantityProduct(productProductDetail(productDetail))
    BY BOMProduct(productProductDetail(productDetail)), orderProductDetail(productDetail)](BOM, order));

createComponentOrder 'Заполнить компоненты заказа' = ACTION (order)  {

    FOR quantityBOMOrder(BOM, order) DO {

        FOR  BOMComponent(component) == BOM  DO {
            FOR ADDOBJ d=componentDetail DO {
                SET orderComponentDetail(d) <- order;
                SET componentComponentDetail(d) <- component;
                SET bruttoQuantityComponentDetail(d) <- bruttoQuantityComponentDate(component, dateOrder(order))*quantityBOMOrder(BOM,order);

                IF materialComponent(component) AS sku THEN {
                    SET skuComponentDetail(d) <- materialComponent(component);
                } ELSE {
                    IF materialComponent(component) AS product THEN {
                        SET productComponentDetail(d) <- materialComponent(component);
                        SET skuComponentDetail(d) <- skuProduct(materialComponent(component));
                    }
                }
            }
        }
    }

} TOOLBAR;

createComponentDetail 'Заполнить компоненты изделия' = ACTION (componentDetail)  {

    FOR  BOMComponent(component) == BOMProduct(productComponentDetail(componentDetail))  DO {
        FOR ADDOBJ d=componentDetail DO {
            SET orderComponentDetail(d) <- orderComponentDetail(componentDetail);
            SET componentComponentDetail(d) <- component;
            SET bruttoQuantityComponentDetail(d) <- bruttoQuantityComponentDate(component, dateOrder(orderComponentDetail(componentDetail)))*
                ceil(bruttoQuantityComponentDetail(componentDetail)/quantityProduct(productComponentDetail(componentDetail)));

            IF materialComponent(component) AS sku THEN {
                SET skuComponentDetail(d) <- materialComponent(component);
            } ELSE {
                IF materialComponent(component) AS product THEN {
                    SET productComponentDetail(d) <- materialComponent(component);
                    SET skuComponentDetail(d) <- skuProduct(materialComponent(component));
                }
            }
        }
    }
    EXEC delete(componentDetail);
} TOOLBAR;


recBruttoQuantityOrderSku 'Рекур.кол-во' (order, sku) = GROUP SUM quantityBOMOrder(BOM, order) * recBruttoQuantityBOMSkuDate(BOM, sku, dateOrder(order)) BY order, sku;
createRecComponentOrder 'Заполнить компоненты заказа рекурсивно' = ACTION (order)  {

    FOR q == recBruttoQuantityOrderSku(order, sku) ADDOBJ d = componentDetail DO {
        SET orderComponentDetail(d) <- order;
        SET skuComponentDetail(d) <- sku;
        SET bruttoQuantityComponentDetail(d) <- q;
    }
} TOOLBAR;

backgroundComponentDetail 'Цвет' (componentDetail) = RGB(255,255,229) IF productComponentDetail(componentDetail);

//--  Сбивка по sku

bruttoQuantitySkuOrder 'Количество брутто' (sku,order)= GROUP SUM bruttoQuantityComponentDetail(componentDetail) BY skuComponentDetail(componentDetail), orderComponentDetail(componentDetail);

priceSkuOrder 'Цена' = DATA NUMERIC[14,2] (sku, order);
priceSkuOrder(sku,order) <- pricePriceListTypeSkuStockDateTime(priceListTypeOrder(order), // или prevPricePriceListTypeSkuStockDateTime
                                                               sku,
                                                               rawStockOrder(order),
                                                               dateTimeOrder(order))
                                    WHEN CHANGED(priceListTypeOrder(order)) OR
                                         CHANGED(rawStockOrder(order)) OR
                                         CHANGED(dateTimeOrder(order));

sumSkuOrder 'Сумма' = DATA NUMERIC[16,2] (sku, order);

sumSkuOrder(sku, order) <- toNumeric16p2(roundCurrency((bruttoQuantitySkuOrder(sku, order) * priceSkuOrder(sku, order)), currencyOrder(order)))
    WHEN CHANGED (bruttoQuantitySkuOrder(sku, order)) OR
         CHANGED (priceSkuOrder(sku, order)) OR
         CHANGED(currencyOrder(order) AND sku IS sku);


recPriceProductOrder (product, order) = GROUP SUM recPricePercentProductSkuDate (product, sku, dateOrder(order))*priceSkuOrder(sku, order)/100
    BY product, order;
recPricePercentProductDetailSkuOrder 'Коэф.цены' (productDetail, sku, order)= recPricePercentProductSkuDate (productProductDetail(productDetail), sku, dateOrder(order))/100;

recPriceProductDetail 'Цена расч.'(productDetail) = recPriceProductOrder (productProductDetail(productDetail), orderProductDetail(productDetail));

fillPriceProductsOrder 'Заполнить цены для изделий по с/с' = ACTION (order)  {

    FOR orderProductDetail(productDetail) == order DO {
        SET priceProductDetail(productDetail) <- recPriceProductDetail(productDetail);
    }
} TOOLBAR;


// ------------- Формы ----------- //

FORM order 'Производственный заказ'

    OBJECTS o = order FIXED PANEL
    PROPERTIES(o) objectClassName, nameRawStockOrder, nameGoodsStockOrder, nameNumeratorObject, numberObject, seriesObject,
                  dateOrder, timeOrder, fromDateOrder, toDateOrder, noteOrder, quantityProductDetailOrder, bruttoQuantityComponentDetailOrder

    OBJECTS pd=productDetail
    PROPERTIES(pd) indexProductDetail, nameSkuProductDetail,  nameProductProductDetail, shortNameUOMProductDetail, quantityProductDetail,
                   priceProductDetail, sumProductDetail, recPriceProductDetail
    PROPERTIES(pd) ADDOBJ, delete
    PROPERTIES(o) TODRAW pd fillPriceProductsOrder, deleteProductDetailOrder

    FILTERS orderProductDetail(pd)==o

    OBJECTS cd = componentDetail
    PROPERTIES(cd) BACKGROUND backgroundComponentDetail(cd) indexComponentDetail, nameSkuComponentDetail, nameMaterialComponentDetail, nameComponentComponentDetail,
                   shortNameUOMComponentDetail, bruttoQuantityComponentDetail
    PROPERTIES(o) TODRAW cd createComponentOrder, createRecComponentOrder
    PROPERTIES(cd) TODRAW cd createComponentDetail SHOWIF productComponentDetail(cd)
    PROPERTIES(cd) ADDOBJ, delete
    PROPERTIES(o) TODRAW cd deleteComponentDetailOrder

    FILTERS orderComponentDetail(cd)==o

    OBJECTS s = sku
    PROPERTIES(s) READONLY nameSku, idBarcodeSku, shortNameUOMSku
    PROPERTIES(s,o) READONLY bruttoQuantitySkuOrder
    PROPERTIES(s,o) priceSkuOrder, sumSkuOrder
    PROPERTIES(o) TODRAW s namePriceListTypeOrder FORCE PANEL
    FILTERS bruttoQuantitySkuOrder(s,o)

    PROPERTIES    recPricePercentProductDetailSkuOrder(pd,s,o), recBruttoQuantityOrderSku(o,s)

    EVENTS
        ON OK EXEC prePostOrder(o)
    EDIT order OBJECT o
;

DESIGN order FROM DEFAULT{

    main {
        preferredSize = (1024, 768);
        NEW specification.box BEFORE functions.box{
            type = TABBED;
            ADD pd.box { title = 'Изделия';}
            ADD cd.box { title = 'Компоненты';}
            ADD s.box { title = 'Потребность';}
        }

        NEW header.box BEFORE specification.box {
            childConstraints = TO THE RIGHT;

            NEW headerRow1 {
                childConstraints = TO THE BOTTOM;
                ADD o.documentHeaderGroup {
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD PROPERTY(objectClassName) { preferredCharWidth = 10; }
                    ADD PROPERTY(nameNumeratorObject);
                    ADD PROPERTY(numberObject);
                    ADD PROPERTY(seriesObject);
                    ADD PROPERTY(dateOrder);
                    ADD PROPERTY(timeOrder);
                }
                NEW headerRow11 {
                    title = 'Склады';
                    childConstraints = TO THE RIGHT;
                    ADD PROPERTY(nameRawStockOrder);
                    ADD PROPERTY(nameGoodsStockOrder);
                }
                NEW headerRow12 {
                    childConstraints = TO THE RIGHTBOTTOM;
                    ADD o.documentPrmGroup {
                        childConstraints = TO THE RIGHTBOTTOM;
                    }
                    NEW timeRow {
                        title = 'Срок действия';
                        childConstraints = TO THE RIGHTBOTTOM;
                        ADD PROPERTY(fromDateOrder);
                        ADD PROPERTY(toDateOrder);
                    }
                    NEW priceRow {
                        title = 'Вид цены материала';
                        childConstraints = TO THE RIGHTBOTTOM;
                        ADD PROPERTY(namePriceListTypeOrder);
                    }
                }
            }

            ADD o.documentSumGroup {
                childConstraints = TO THE BOTTOM;
            }
        }

        PROPERTY(formOk) {
            caption = 'Провести';
        }
    }
}

FORM orders 'Производственные заказы'

    OBJECTS o = order
    PROPERTIES(o) READONLY isPostedOrder FORCE GRID, objectClassName, nameRawStockOrder, nameGoodsStockOrder, nameNumeratorObject, numberObject, seriesObject,
                  dateOrder, timeOrder, fromDateOrder, toDateOrder, noteOrder, quantityProductDetailOrder, bruttoQuantityComponentDetailOrder, namePriceListTypeOrder
    PROPERTIES(o) ADDFORM, EDITFORM, delete
    PROPERTIES (o) READONLY FORCE PANEL nameUserCreatedHistorizable, timeCreatedHistorizable, hostnameComputerCreatedHistorizable, nameUserClosed, timeClosed, hostnameComputerClosed

    OBJECTS pd=productDetail
    PROPERTIES(pd) READONLY indexProductDetail, nameSkuProductDetail, nameProductProductDetail, shortNameUOMProductDetail,
                   quantityProductDetail, priceProductDetail, sumProductDetail
    FILTERS orderProductDetail(pd)==o

    OBJECTS cd = componentDetail
    PROPERTIES(cd) READONLY indexComponentDetail, nameSkuComponentDetail, nameComponentComponentDetail, shortNameUOMComponentDetail, bruttoQuantityComponentDetail

    FILTERS orderComponentDetail(cd)==o
;
DESIGN orders FROM DEFAULT {
    PROPERTY (delete(o)) {
        askConfirm = TRUE;
    }

    NEW documentContainer BEFORE functions.box {
        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        ADD o.box;

        NEW documentDetail {
            type = TABBED;

            ADD pd.box {
                title = 'Изделия';
            }
            ADD cd.box {
                title = 'Компоненты';
            }
            NEW documentHistory {
                title = 'История';

                ADD o.historyGroup;
                ADD o.postedGroup;
            }
            NEW printTab {
                title = 'Печатные формы';
                NEW printContainer {
                    title = 'Печать';
                    childConstraints = TO THE BOTTOM;
                    fillVertical = 1.0;
                }
            }
        }
    }
}



NAVIGATOR {
    manufacturingDocuments {
        ADD orders;
    }
}