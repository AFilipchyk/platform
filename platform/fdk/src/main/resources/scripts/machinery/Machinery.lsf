MODULE Machinery;

REQUIRE System, Stock, Barcode, PriceList, DefaultData;

//---------------------------- Сервера управления оборудования ----------------------------//

CLASS EquipmentServer 'Сервер оборудования' : Named;
TABLE equipmentServer (EquipmentServer);

sidEquipmentServer 'Идентификатор' = DATA STRING[20] (EquipmentServer) IN base;
sidToEquipmentServer(equipmentServer) = GROUP UNIQUE equipmentServer BY sidEquipmentServer (equipmentServer) WHERE equipmentServer IS EquipmentServer;
delayEquipmentServer 'Период обновления (в миллисекундах)' = DATA INTEGER (EquipmentServer) IN base;

//---------------------------- Ошибки сервера оборудования ----------------------------//

CLASS EquipmentServerError 'Ошибки';
TABLE equipmentServerError (EquipmentServerError);

dataEquipmentServerError 'Сообщение об ошибке' = DATA STRING[200] (EquipmentServerError) IN base;
erTraceEquipmentServerError 'След ошибки' = DATA TEXT (EquipmentServerError) IN base;
dateEquipmentServerError 'Время возникновения' = DATA DATETIME (EquipmentServerError) IN base;
equipmentServerEquipmentServerError 'Сервер оборудования (ID)' = DATA EquipmentServer(EquipmentServerError) IN base;

//---------------------------- Лог сервера оборудования ----------------------------//

CLASS EquipmentServerLog 'Лог';
TABLE equipmentServerLog (EquipmentServerLog);

dataEquipmentServerLog 'Сообщение' = DATA TEXT (EquipmentServerLog) IN base;
dateEquipmentServerLog 'Время' = DATA DATETIME (EquipmentServerLog) IN base;
equipmentServerEquipmentServerLog 'Сервер оборудования (ID)' = DATA EquipmentServer(EquipmentServerLog) IN base;

//---------------------------- Формы для серверов оборудования ----------------------------//

FORM equipmentServers 'Серверы оборудования'
    OBJECTS es = EquipmentServer
    PROPERTIES(es)  READONLY name, sidEquipmentServer, delayEquipmentServer
    PROPERTIES(es)  ADDFORM, EDITFORM, delete

    OBJECTS e = EquipmentServerError
    PROPERTIES(e) READONLY dataEquipmentServerError, dateEquipmentServerError
    PROPERTIES(e) delete
    PROPERTIES(e) READONLY FORCE PANEL erTraceEquipmentServerError

    OBJECTS l = EquipmentServerLog
    PROPERTIES(l) READONLY dataEquipmentServerLog, dateEquipmentServerLog
    PROPERTIES(l) delete

    FILTERS equipmentServerEquipmentServerError (e) == es
    FILTERS equipmentServerEquipmentServerLog (l) == es
;

DESIGN equipmentServers FROM DEFAULT {
    main {
        NEW topContainer {
            type = SPLITV;
            childConstraints =  TO THE BOTTOM;

            ADD es.box;

            NEW specContainer {
                type = TABBED;

                NEW errorContainer {
                    title = 'Ошибки';
                    ADD e.box;
                    PROPERTY(erTraceEquipmentServerError(e)) {
                        fillHorizontal = 1;
                        panelLabelAbove = TRUE;
                    }
                }
                ADD l.box;
            }
        }
        ADD functions.box;
    }
}

//---------------------------- группы оборудования ----------------------------------------//

CLASS ABSTRACT GroupMachinery 'Группы оборудования';
TABLE groupMachinery (GroupMachinery);

nameGroupMachinery 'Наименование' = DATA STRING[200] (GroupMachinery) IN base MINCHARWIDTH 10 PREFCHARWIDTH 20;

equipmentServerGroupMachinery = DATA EquipmentServer (GroupMachinery);
nameEquipmentServerGroupMachinery 'Сервер оборудования' (groupMachinery) = name(equipmentServerGroupMachinery(groupMachinery));
sidEquipmentServerGroupMachinery 'ИД сервера оборудования' (groupMachinery) = sidEquipmentServer(equipmentServerGroupMachinery(groupMachinery));

// -------------------------------- Склады --------------------------------------------- //

stockGroupMachinery = ABSTRACT Stock (GroupMachinery) PERSISTENT;
nameStockGroupMachinery 'Склад' (groupMachinery) = name(stockGroupMachinery(groupMachinery));

// фильтрация по группам товаров

filterSkuGroupMachinery 'Фильтровать по классификатору' = DATA BOOLEAN (GroupMachinery);

TABLE groupMachinerySkuGroup (GroupMachinery, SkuGroup);
inGroupMachinerySkuGroup 'Вкл' = DATA BOOLEAN (GroupMachinery, SkuGroup);

FORM filterSkuGroupMachinery 'Фильтрация по классификатору'
    OBJECTS gm = GroupMachinery FIXED PANEL
    PROPERTIES(gm) READONLY nameStockGroupMachinery, nameEquipmentServerGroupMachinery, nameGroupMachinery

    TREE treeGroups g=SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY name(g)
    ORDER BY name

    OBJECTS cg=SkuGroup
    PROPERTIES(cg) READONLY canonicalNameSkuGroup
    PROPERTIES(gm, cg)      inGroupMachinerySkuGroup
    ORDER BY canonicalNameSkuGroup
    FILTERS isParentLeafSkuGroupSkuGroup(cg, g)

    FILTERGROUP filters
        FILTER 'Только выбранные группы' 'F10' inGroupMachinerySkuGroup(gm, cg)
;

DESIGN filterSkuGroupMachinery FROM DEFAULT {
    POSITION treeGroups.tree.box TO THE LEFT cg.box;

    treeGroups.tree {
        fillHorizontal = 0.5;
    }
}

showFilterSkuGroupMachinery 'Выбрать группы' (groupMachinery) = ACTION FORM filterSkuGroupMachinery OBJECTS gm = groupMachinery MODAL;

//---------------------------- Модели оборудования ----------------------------------------//
CLASS ABSTRACT Model 'Модель' : Named;
TABLE model (Model);

sidModel 'Код' = DATA STRING[20] (Model) IN base;

noteModel 'Примечание' = DATA STRING[200] (Model) IN base;
handlerModel 'Обработчик' = DATA STRING[200] (Model) IN base;

maxProductModel 'MAX допустимое колич. товаров' = DATA INTEGER (Model) IN base;

//---------------------------- типы оборудования  ----------------------------------------//
CLASS ABSTRACT Machinery 'Оборудование';
TABLE machinery(Machinery);

groupMachineryMachinery = ABSTRACT GroupMachinery (Machinery) PERSISTENT;
nameGroupMachineryMachinery 'Группа' (machinery) = nameGroupMachinery(groupMachineryMachinery(machinery));

modelMachinery = ABSTRACT Model (Machinery) PERSISTENT;
sidModelMachinery 'Код' (machinery) = sidModel(modelMachinery(machinery)) IN base;
nameModelMachinery 'Модель' (machinery) = name(modelMachinery(machinery)) IN base;
handlerModelMachinery 'Обработчик' (machinery) = handlerModel(modelMachinery(machinery)) IN base;

nppMachinery 'Порядковый номер' = DATA INTEGER (Machinery) IN base;
descriptionMachinery 'Описание' = DATA STRING[200] (Machinery) IN base;
portMachinery 'Адрес/порт' = DATA STRING[100] (Machinery) IN base;

//---------------------------- Загрузка в ВУ  ----------------------------------------//
CLASS ABSTRACT PriceTransactionDocument 'Документ, требующий загрузки в оборудование';
TABLE priceTransactionDocument (PriceTransactionDocument);

isDraftPriceTransactionDocument 'Не проведен' = ABSTRACT BOOLEAN (PriceTransactionDocument) PERSISTENT;
descriptionPriceTransactionDocument 'Название документа загрузки' = ABSTRACT STRING[200] (PriceTransactionDocument) PERSISTENT;
skipPriceTransactionDocument 'Не загружать' = ABSTRACT BOOLEAN (PriceTransactionDocument) PERSISTENT;

sentPriceTransactionDocument 'Принят к загрузке в оборудование' = DATA BOOLEAN (PriceTransactionDocument);
sentPriceTransactionDocument(document) <- NULL WHEN ASSIGNED(isDraftPriceTransactionDocument(document));

META implementPriceTransactionDocument(concrete)
    EXTEND CLASS concrete : Machinery.PriceTransactionDocument;
    Machinery.isDraftPriceTransactionDocument (document) += isDraft###concrete(document);
    Machinery.descriptionPriceTransactionDocument (document) += description###concrete(document);
END
//----------------------------------- Загрузка в ВУ -------------------------------------------------------

CLASS ABSTRACT MachineryPriceTransaction 'Загрузка прайса в оборудование' : Historizable;
TABLE machineryPriceTransaction (MachineryPriceTransaction);

groupMachineryMachineryPriceTransaction (transaction) = ABSTRACT GroupMachinery (MachineryPriceTransaction);
nameGroupMachineryMachineryPriceTransaction 'Группа оборудования' (transaction) = nameGroupMachinery(groupMachineryMachineryPriceTransaction(transaction));

stockMachineryPriceTransaction (transaction) = stockGroupMachinery(groupMachineryMachineryPriceTransaction(transaction));

equipmentServerMachineryPriceTransaction (transaction) = equipmentServerGroupMachinery(groupMachineryMachineryPriceTransaction(transaction));
sidEquipmentServerMachineryPriceTransaction (transaction) = sidEquipmentServer(equipmentServerMachineryPriceTransaction (transaction));

// Дата/время
dateMachineryPriceTransaction 'Дата' = DATA DATE (MachineryPriceTransaction);
dateMachineryPriceTransaction (transaction) <- currentDate() WHEN ASSIGNED(transaction IS MachineryPriceTransaction);

timeMachineryPriceTransaction 'Время' = DATA TIME (MachineryPriceTransaction);
timeMachineryPriceTransaction (transaction) <- currentTime() WHEN ASSIGNED(transaction IS MachineryPriceTransaction);

dateTimeMachineryPriceTransaction 'Дата/время' (transaction) = dateTimeToDateTime(dateMachineryPriceTransaction(transaction), timeMachineryPriceTransaction(transaction));

orderMachineryPriceTransaction (transaction) = STRUCT(dateTimeMachineryPriceTransaction(transaction), transaction) PERSISTENT;

// Статус
snapshotMachineryPriceTransaction 'Целиком' = DATA BOOLEAN (MachineryPriceTransaction);

succeededMachineryPriceTransaction 'Загружена' = DATA BOOLEAN (MachineryPriceTransaction);
canceledMachineryPriceTransaction 'Отменена' = DATA BOOLEAN (MachineryPriceTransaction);
processMachineryPriceTransaction 'Требуется загрузка' (machineryPriceTransaction) =
    machineryPriceTransaction IS MachineryPriceTransaction AND NOT
    succeededMachineryPriceTransaction(machineryPriceTransaction) AND NOT
    canceledMachineryPriceTransaction(machineryPriceTransaction);

// Основание
commentMachineryPriceTransaction 'Примечание' = DATA STRING[30] (MachineryPriceTransaction);

priceTransactionDocumentMachineryPriceTransaction = DATA PriceTransactionDocument (MachineryPriceTransaction);
descriptionPriceTransactionDocumentMachineryPriceTransaction 'Основание загрузки' (transaction) =
    descriptionPriceTransactionDocument(priceTransactionDocumentMachineryPriceTransaction(transaction));

// todo : persistent почему-то не работает
descriptionMachineryPriceTransaction 'Основание' (document) = UNION OVERRIDE descriptionPriceTransactionDocumentMachineryPriceTransaction(document),
                                                                             commentMachineryPriceTransaction(document);

countProcessPriceTransactionDocument 'Кол-во ожидающих транзакций' (document) = GROUP SUM 1 IF processMachineryPriceTransaction(transaction)
                                                                                 BY priceTransactionDocumentMachineryPriceTransaction(transaction) PERSISTENT;

succeededPriceTransactionDocument 'Загружен в оборудование' (document) = sentPriceTransactionDocument (document) AND NOT countProcessPriceTransactionDocument(document);

statusMachineryPriceTransactionDocument 'Статус загрузки в оборудование' (document) =
                                                        CASE
                                                            WHEN skipPriceTransactionDocument(document) THEN 'Загрузка не требуется' IF document IS PriceTransactionDocument
                                                            WHEN succeededPriceTransactionDocument(document) THEN 'Успешно загружен' IF document IS PriceTransactionDocument
                                                            WHEN sentPriceTransactionDocument(document) THEN 'Принят к загрузке' IF document IS PriceTransactionDocument
                                                            DEFAULT 'Не загружен' IF document IS PriceTransactionDocument
                                                        END;

// Загруженные штрих-коды
TABLE machineryPriceTransactionBarcode (MachineryPriceTransaction, Barcode);

inMachineryPriceTransactionBarcode 'Вкл' = DATA BOOLEAN (MachineryPriceTransaction, Barcode);
nameMachineryPriceTransactionBarcode 'Наименование' = DATA STRING[255] (MachineryPriceTransaction, Barcode);
priceMachineryPriceTransactionBarcode 'Цена' = DATA NUMERIC[14,2] (MachineryPriceTransaction, Barcode);

orderMachineryPriceTransactionBarcode(transaction, barcode) = orderMachineryPriceTransaction(transaction)
                                                              AND inMachineryPriceTransactionBarcode(transaction, barcode)
                                                              AND NOT canceledMachineryPriceTransaction(transaction) PERSISTENT INDEXED;

// Текущие загруженные штрих-коды
TABLE barcodeGroupMachinery (Barcode, GroupMachinery);

lastOrderTransactionBarcodeGroupMachinery (barcode, groupMachinery) = GROUP MAX orderMachineryPriceTransactionBarcode(transaction, barcode)
                                                                            BY barcode, groupMachineryMachineryPriceTransaction(transaction);
lastTransactionBarcodeGroupMachinery (barcode, groupMachinery) = lastOrderTransactionBarcodeGroupMachinery(barcode, groupMachinery) [2] PERSISTENT;

transactionNameBarcodeGroupMachinery 'Текущее наименование в оборудовании' (barcode, groupMachinery)  =
    nameMachineryPriceTransactionBarcode(lastTransactionBarcodeGroupMachinery(barcode, groupMachinery), barcode);
transactionPriceBarcodeGroupMachinery 'Текущая цена в оборудовании' (barcode, groupMachinery)  =
    priceMachineryPriceTransactionBarcode(lastTransactionBarcodeGroupMachinery(barcode, groupMachinery), barcode) PERSISTENT;

//--------------------Сообщения об ошибках транзакций--------------------//
CLASS MachineryPriceTransactionError 'Ошибка';
TABLE machineryPriceTransactionError (MachineryPriceTransactionError);

dataMachineryPriceTransactionError 'Сообщение об ошибке' = DATA STRING[200] (MachineryPriceTransactionError) IN base;
dateMachineryPriceTransactionError 'Время возникновения' = DATA DATETIME (MachineryPriceTransactionError) IN base;
errorTraceMachineryPriceTransactionError 'След исключения' = DATA TEXT (MachineryPriceTransactionError) IN base;
machineryPriceTransactionMachineryPriceTransactionError 'Транзакция (ID)' = DATA MachineryPriceTransaction(MachineryPriceTransactionError) IN base;
quantityMachineryPriceTransactionErrorMachineryPriceTransaction 'Количество ошибок' (MachineryPriceTransaction) = GROUP SUM 1 IF machineryPriceTransactionMachineryPriceTransactionError (machineryPriceTransactionError) == MachineryPriceTransaction
    BY machineryPriceTransactionMachineryPriceTransactionError (machineryPriceTransactionError) IN base;

// Загрузка в конкретные устройства
TABLE machineryPriceTransactionMachinery (MachineryPriceTransaction, Machinery);
inMachineryPriceTransactionMachinery 'Вкл' = DATA BOOLEAN (MachineryPriceTransaction, Machinery);
nppsMachineryPriceTransaction 'Номера устройств' (transaction) =
    GROUP CONCAT toString3(nppMachinery(machinery)) IF inMachineryPriceTransactionMachinery(transaction, machinery), ','
          BY transaction
          ORDER machinery MINCHARWIDTH 10 PREFCHARWIDTH 20 PERSISTENT;

// перезагрузка прайса целиком
selectedMachinery 'Вкл' = DATA SESSION BOOLEAN (Machinery);

GROUP snapshotMachineryPriceGroup 'Перезагрузка прайса' : public;

FORM groupMachineryInput 'Выбор оборудования для перезагрузки прайса'

    OBJECTS s = Stock FIXED PANEL
    PROPERTIES(s) READONLY sname = name

    OBJECTS g = GroupMachinery
    PROPERTIES(g) READONLY nameGroupMachinery, nameStockGroupMachinery, nameEquipmentServerGroupMachinery
    FILTERS stockGroupMachinery(g) == s

    OBJECTS m = Machinery
    PROPERTIES(m)          selectedMachinery
    PROPERTIES(m) READONLY nppMachinery, descriptionMachinery, portMachinery

    FILTERS groupMachineryMachinery(m) == g
;

DESIGN groupMachineryInput FROM DEFAULT {
    main {
        preferredSize = (1024, 768);
    }
    PROPERTY(sname) {
        focusable = FALSE;
    }
}

// ---------------------------------------- Формы загрузки прайса --------------------------------------- //

FORM machineryPriceTransaction 'Загрузка прайса в оборудование'
    OBJECTS t = MachineryPriceTransaction FIXED PANEL
    PROPERTIES(t) nameGroupMachineryMachineryPriceTransaction, dateMachineryPriceTransaction, timeMachineryPriceTransaction,
                  snapshotMachineryPriceTransaction, succeededMachineryPriceTransaction, canceledMachineryPriceTransaction

    OBJECTS b = Barcode
    PROPERTIES(b) READONLY idBarcode
    PROPERTIES(t, b)       inMachineryPriceTransactionBarcode, nameMachineryPriceTransactionBarcode, priceMachineryPriceTransactionBarcode FORCE PANEL
    FILTERS inMachineryPriceTransactionBarcode(t, b)

    EDIT MachineryPriceTransaction OBJECT t;
;

FORM machineryPriceTransactions 'Загрузки прайса в оборудование'
    OBJECTS s = Stock FIXED PANEL
    PROPERTIES name(s) SELECTOR

    OBJECTS t = MachineryPriceTransaction
    PROPERTIES(t) READONLY snapshotMachineryPriceTransaction, nameGroupMachineryMachineryPriceTransaction, dateMachineryPriceTransaction, timeMachineryPriceTransaction,
                           descriptionMachineryPriceTransaction, succeededMachineryPriceTransaction, canceledMachineryPriceTransaction,
                           quantityMachineryPriceTransactionErrorMachineryPriceTransaction BACKGROUND quantityMachineryPriceTransactionErrorMachineryPriceTransaction(t),
                           nppsMachineryPriceTransaction FORCE PANEL SHOWIF snapshotMachineryPriceTransaction(t)
    PROPERTIES(t) READONLY nameUserCreatedHistorizable, hostnameComputerCreatedHistorizable
    PROPERTIES(t)          ADDFORM, EDITFORM, delete
    FILTERS stockMachineryPriceTransaction(t) == s

    OBJECTS b = Barcode
    PROPERTIES(b)    idBarcode
    PROPERTIES(t, b) nameMachineryPriceTransactionBarcode, priceMachineryPriceTransactionBarcode

    FILTERS inMachineryPriceTransactionBarcode(t, b)

    OBJECTS e = MachineryPriceTransactionError
    PROPERTIES(e)    dataMachineryPriceTransactionError, dateMachineryPriceTransactionError
    PROPERTIES(e)  FORCE PANEL  errorTraceMachineryPriceTransactionError
    FILTERS machineryPriceTransactionMachineryPriceTransactionError(e) == t
;
@extendFormFilterStockAccess(Stock, s, machineryPriceTransactions);

DESIGN machineryPriceTransactions FROM DEFAULT {
    main {
        NEW topContainer {
            type = SPLITV;
            childConstraints = TO THE BOTTOM;

            ADD t.box {
                preferredSize = ( -1, 300);
                minimumSize = ( -1, 300);
                maximumSize = ( -1, 300);
            }

            NEW specContainer{
                type = TABBED;
                ADD b.box;
                NEW errorContainer {
                   title = 'Сообщения об ошибках';
                   ADD e.box;
                   PROPERTY(errorTraceMachineryPriceTransactionError(e)) {
                      fillHorizontal = 1;
                      panelLabelAbove = TRUE;
                   }
                }
            }
        }
        ADD functions.box;
    }
}

// ------------------------------------------------ Действия по загрузке ------------------------------------ //

createMachineryPriceTransactionGroupMachinery = ABSTRACT ACTION (groupMachinery);

overInGroupMachinerySku = ABSTRACT BOOLEAN (GroupMachinery, Sku);
inGroupMachinerySku 'Вкл' (groupMachinery, sku) = groupMachinery IS GroupMachinery
                                                    AND sku IS Sku
                                                         AND NOT (filterSkuGroupMachinery(groupMachinery) AND NOT inGroupMachinerySkuGroup(groupMachinery, skuGroupSku(sku)))
                                                         AND NOT overInGroupMachinerySku(groupMachinery, sku);

// Перезагрузка целиком
snapshotAttributeMachineryPriceTransactionGroupMachinery = ABSTRACT ACTION (machineryPriceTransaction, groupMachinery);

snapshotGroupMachineryDepartmentStore 'Перезагрузить прайс' = ACTION (stock) NEWSESSION {
    FORM groupMachineryInput OBJECTS s = (stock AS Stock) MODAL;
    IF formResult() == FormResult.ok THEN {
        LOCAL groupMachinery = GroupMachinery ();
        SET groupMachinery() <- chosenObject('g');
        EXEC createMachineryPriceTransactionGroupMachinery(groupMachinery());
        FOR t == addedObject() DO {
            SET groupMachineryMachineryPriceTransaction(t) <- groupMachinery();
            SET commentMachineryPriceTransaction(t) <- 'Перезагрузка прайса целиком';
            SET snapshotMachineryPriceTransaction(t) <- TRUE;
            SET inMachineryPriceTransactionMachinery(t, machinery) <- selectedMachinery(machinery);
            SET inMachineryPriceTransactionBarcode(t, barcode) <- TRUE AND lastTransactionBarcodeGroupMachinery(barcode, groupMachinery())
                                                                       AND inGroupMachinerySku(groupMachinery(), skuBarcode(barcode)) // важно, что товар должен быть активным на текущий момент
                                                                       AND activeBarcode(barcode);
            SET nameMachineryPriceTransactionBarcode(t, barcode) <- transactionNameBarcodeGroupMachinery(barcode, groupMachinery());
            EXEC snapshotAttributeMachineryPriceTransactionGroupMachinery(t, groupMachinery());
            SET priceMachineryPriceTransactionBarcode(t, barcode) <- transactionPriceBarcodeGroupMachinery(barcode, groupMachinery());
        }
        EXEC apply();
    }
} CONFIRM IN snapshotMachineryPriceGroup;

// Инкрементная загрузка
GROUP incrementMachineryPriceGroup 'Инкрементная загрузка' : public;

// ------------------------------- Определение цены, по которой загружать ------------------------------- //

priceListTypeGroupMachinery = DATA PriceListType (GroupMachinery);
namePriceListTypeGroupMachinery 'Вид цен для POS' (groupMachinery) = name(priceListTypeGroupMachinery(groupMachinery));

priceBarcodeGroupMachinery 'Цена' (barcode, groupMachinery) = pricePriceListTypeSkuStockDateTime(priceListTypeGroupMachinery(groupMachinery),
                                                                                                 skuBarcode(barcode),
                                                                                                 stockGroupMachinery(groupMachinery),
                                                                                                 currentDateTime());

createMachineryPriceTransactionSnapshot = DATA SESSION BOOLEAN ();

createMachineryPriceTransactionSku = DATA SESSION BOOLEAN (Sku);
createMachineryPriceTransactionBarcodeGroupMachinery (barcode, groupMachinery) = createMachineryPriceTransactionSku(skuBarcode(barcode)) // если передали параметром
                                                                                 AND inGroupMachinerySku(groupMachinery, skuBarcode(barcode)) // если на эту группу оборудования должна закачиваться
                                                                                 AND activeBarcode(barcode) // если активный штрих-код
                                                                                 AND priceBarcodeGroupMachinery(barcode, groupMachinery); // если есть цена розничная

createMachineryPriceTransactionDocument = DATA SESSION PriceTransactionDocument();
createMachineryPriceTransactionComment = DATA SESSION STRING[20] ();

createAttributeMachineryPriceTransaction = ABSTRACT ACTION (machineryPriceTransaction);
createAttributeMachineryPriceTransactionStock = ABSTRACT ACTION (machineryPriceTransaction, stock);
createAttributeMachineryPriceTransactionGroupMachinery = ABSTRACT ACTION (machineryPriceTransaction, groupMachinery);

createMachineryPriceTransaction 'Создать транзакцию' = ACTION (stock) {
    LOCAL message = STRING[3000] ();
    LOCAL sentSomething = BOOLEAN();
    SET message() <- 'Принято к загрузке в оборудование : \n';

    FOR stockGroupMachinery(groupMachinery) == stock DO {
        LOCAL inBarcode = BOOLEAN (Barcode);
        IF createMachineryPriceTransactionSnapshot() THEN
            SET inBarcode(barcode) <- createMachineryPriceTransactionBarcodeGroupMachinery(barcode, groupMachinery)
        ELSE
            SET inBarcode(barcode) <- createMachineryPriceTransactionBarcodeGroupMachinery(barcode, groupMachinery)
                                        AND NOT (priceBarcodeGroupMachinery(barcode, groupMachinery) == transactionPriceBarcodeGroupMachinery(barcode, groupMachinery)); // если цена изменилась

        LOCAL countBarcode = INTEGER();
        SET countBarcode() <- [GROUP SUM 1 IF inBarcode(barcode)]();
        IF countBarcode() THEN {
            EXEC createMachineryPriceTransactionGroupMachinery(groupMachinery);
            FOR t == addedObject() DO {
                SET groupMachineryMachineryPriceTransaction(t) <- groupMachinery AS GroupMachinery;
                SET priceTransactionDocumentMachineryPriceTransaction(t) <- createMachineryPriceTransactionDocument();
                SET commentMachineryPriceTransaction(t) <- createMachineryPriceTransactionComment();
                SET inMachineryPriceTransactionBarcode(t, barcode) <- inBarcode(barcode);
                EXEC createAttributeMachineryPriceTransaction(t);
                EXEC createAttributeMachineryPriceTransactionStock(t, stock AS Stock); //добавлен AS Stock
                EXEC createAttributeMachineryPriceTransactionGroupMachinery(t, groupMachineryMachineryPriceTransaction(t));
                SET priceMachineryPriceTransactionBarcode(t, barcode) <- priceBarcodeGroupMachinery(barcode, groupMachinery) WHERE inMachineryPriceTransactionBarcode(t, barcode);
            }
            SET message() <- [FORMULA STRING[3000] '$1 || $2 || \' - \' || CAST($3 as text) || \' товаров.\'\n']
                             (message(), nameGroupMachinery(groupMachinery), countBarcode());
            SET sentSomething() <- TRUE;
        }
    }

    IF sentSomething() THEN
        MESSAGE message()
    ELSE
        MESSAGE 'Загрузка прайса в оборудование не требуется.';
};

createBalanceMachineryPriceTransaction 'Остатки' = ACTION (stock) NEWSESSION AUTOAPPLY {
    SET createMachineryPriceTransactionSku(sku) <- TRUE IF currentBalanceSkuStock(sku, stock);
    SET createMachineryPriceTransactionComment() <- 'Инкрементная загрузка остатков';
    EXEC createMachineryPriceTransaction(stock AS Stock);
} CONFIRM IN incrementMachineryPriceGroup;

createPriceMachineryPriceTransaction 'Все товары' = ACTION (stock) NEWSESSION AUTOAPPLY {
    SET createMachineryPriceTransactionSku(sku) <- TRUE;
    SET createMachineryPriceTransactionComment() <- 'Инкрементная загрузка товаров';
    EXEC createMachineryPriceTransaction(stock AS Stock);
} CONFIRM IN incrementMachineryPriceGroup;

EXTEND FORM machineryPriceTransactions
    PROPERTIES(s) createBalanceMachineryPriceTransaction, createPriceMachineryPriceTransaction, snapshotGroupMachineryDepartmentStore
;

// -------------------- Создание новых атрибутов ----------------- //

META defineMachineryPriceTransactionAttributeBase(object, caption, nameProp)
    transaction###object##BarcodeGroupMachinery caption###' (тек.)' (barcode, groupMachinery)  =
        object##MachineryPriceTransactionBarcode(lastTransactionBarcodeGroupMachinery(barcode, groupMachinery), barcode);

    snapshotAttributeMachineryPriceTransactionGroupMachinery(t, groupMachinery) +=
        ACTION SET object##MachineryPriceTransactionBarcode(t, barcode) <- transaction###object##BarcodeGroupMachinery(barcode, groupMachinery);

    EXTEND FORM machineryPriceTransaction PROPERTIES(t, b) nameProp###object##MachineryPriceTransactionBarcode;
    EXTEND FORM machineryPriceTransactions PROPERTIES(t, b) READONLY nameProp###object##MachineryPriceTransactionBarcode;
END

META defineMachineryPriceTransactionAttributeAction(object, caption, nameProp)
    @defineMachineryPriceTransactionAttributeBase(object, caption, nameProp);
    createAttributeMachineryPriceTransaction(t) += ACTION SET object##MachineryPriceTransactionBarcode(t, barcode) <- object##Barcode(barcode)
                                                          WHERE inMachineryPriceTransactionBarcode(t, barcode);
END

META defineMachineryPriceTransactionAttribute(object, caption, type)
    object###MachineryPriceTransactionBarcode caption = DATA type (MachineryPriceTransaction, Barcode);
    @defineMachineryPriceTransactionAttributeAction(object, caption, );
END

META defineMachineryPriceTransactionAttribute(object, caption, type, nameProp)
    object###MachineryPriceTransactionBarcode = DATA type (MachineryPriceTransaction, Barcode);
    nameProp###object###MachineryPriceTransactionBarcode caption (transaction, barcode) =
        nameProp###object(object###MachineryPriceTransactionBarcode(transaction, barcode));
    @defineMachineryPriceTransactionAttributeAction(object, caption, nameProp);
END

META defineMachineryPriceTransactionAttributeStockAction(object, caption, nameProp)
    @defineMachineryPriceTransactionAttributeBase(object, caption, nameProp);
    createAttributeMachineryPriceTransactionStock(t, stock) += ACTION SET object##MachineryPriceTransactionBarcode(t, barcode) <- object##BarcodeStock(barcode, stock)
                                                                      WHERE inMachineryPriceTransactionBarcode(t, barcode);
END
META defineMachineryPriceTransactionAttributeStock(object, caption, type)
    object###MachineryPriceTransactionBarcode caption = DATA type (MachineryPriceTransaction, Barcode);
    @defineMachineryPriceTransactionAttributeStockAction(object, caption, );
END

// --------------- Атрибуты по умолчанию ------------------ //
@defineMachineryPriceTransactionAttribute(skuGroup, 'Группа товара', SkuGroup, canonicalName);
@defineMachineryPriceTransactionAttribute(isWeight, 'Весовой товар', BOOLEAN);
@defineMachineryPriceTransactionAttributeStock(expiryDate, 'Срок годности (дата)', DATE);

// Подключение свойств к документу
META defineDocumentMachineryPriceTransaction (document, skuProp, stockProp)
    create###document##MachineryPriceTransaction 'Загрузить в оборудование' = ACTION (document) NEWSESSION AUTOAPPLY {
        SET createMachineryPriceTransactionSku(sku) <- TRUE IF quantity###document##Detail###skuProp###document(sku, document);
        SET createMachineryPriceTransactionDocument() <- document AS Machinery.PriceTransactionDocument;
        EXEC createMachineryPriceTransaction(stockProp###document(document));
        SET sentPriceTransactionDocument(document) <- TRUE;
    } TOOLBAR CONFIRM;

    createSnapshot###document##MachineryPriceTransaction 'Перегрузить все позиции' = ACTION (document) NEWSESSION AUTOAPPLY {
        SET createMachineryPriceTransactionSnapshot() <- TRUE;
        SET createMachineryPriceTransactionSku(sku) <- TRUE IF quantity###document##Detail###skuProp###document(sku, document);
        SET createMachineryPriceTransactionDocument() <- document AS Machinery.PriceTransactionDocument;
        EXEC createMachineryPriceTransaction(stockProp###document(document));
        SET sentPriceTransactionDocument(document) <- TRUE;
    } CONFIRM;

    showCreateMachineryPriceTransaction###document 'Показывать' (document) = isPosted###document(document) AND NOT Machinery.skipPriceTransactionDocument(document)
                                                                                                           AND NOT sentPriceTransactionDocument(document);
    backgroundCreateMachineryPriceTransaction###document 'Цвет' (document) = IF countProcessPriceTransactionDocument(document) THEN
                                                                                RGB(255,255,128)
                                                                             ELSE
                                                                                RGB(212,255,212) IF showCreateMachineryPriceTransaction###document(document);
END

META extendFormDocumentMachineryPriceTransaction(form, object, document)
    EXTEND FORM form
        PROPERTIES(object) statusMachineryPriceTransactionDocument ON SHORTCUT createSnapshot###document##MachineryPriceTransaction(object) BACKGROUND backgroundCreateMachineryPriceTransaction###document(object) READONLY,
                           create###document###MachineryPriceTransaction FORCE PANEL SHOWIF showCreateMachineryPriceTransaction###document(object)
    ;
END

// ------------------------------------------------ Стандартные значения ------------------------------------ //

EXTEND FORM defaultData
    OBJECTS         es=EquipmentServer FIXED PANEL
    PROPERTIES(es)  SELECTOR name

    OBJECTS         pt=PriceListType FIXED PANEL
    PROPERTIES(pt)  SELECTOR name

    OBJECTS         s=Stock FIXED PANEL
    PROPERTIES(s)   SELECTOR name
;

EXTEND DESIGN defaultData {
    pane {
        NEW machinery {
            title = 'Оборудование';
            childConstraints = TO THE BOTTOM;

            ADD PROPERTY(name(es)) {
                caption = 'Сервер оборудования';
            }
            ADD PROPERTY(name(pt)) {
                caption = 'Вид цен';
            }
            ADD PROPERTY(name(s)) {
                caption = 'Склад';
            }
        }
    }
}

NAVIGATOR {
    NEW machineryNavigator 'Оборудование' BEFORE administration TO toolbar IMAGE '/images/cogwheel.png' {
        NEW machineryExport 'Загрузка в оборудование' {
            ADD machineryPriceTransactions;
        }
        NEW machineryMasterData 'Справочники' {
            ADD equipmentServers;
        }
    }
}