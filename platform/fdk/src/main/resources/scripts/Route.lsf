MODULE Route;

REQUIRE Transport, Customer, Numerator;

// ------------------------------- Route ----------------------------------- //
CLASS route 'Маршрут' : numeratedObject;

truckRoute = DATA truck(route);
sidTruckRoute 'Машина' (route) = sidTruck(truckRoute(route));

FORM route 'Маршрут'
    OBJECTS r=route FIXED PANEL
    PROPERTIES(r) nameNumeratorObject, numberObject, seriesObject, sidTruckRoute

    EDIT route OBJECT r
;

FORM routes 'Маршруты'
    OBJECTS r=route
    PROPERTIES(r) READONLY nameNumeratorObject, numberObject, seriesObject, sidTruckRoute
    PROPERTIES(r) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR
;


// ------------------------------- Node ------------------------------------ //
CLASS ABSTRACT node 'Пункт';

nameNode 'Наименование' = ABSTRACT STRING[110](node);
addressNode 'Адрес' = ABSTRACT STRING[110](node);

inNodeRoute 'Вкючен' = DATA BOOLEAN(node, route);
numberNodeRoute 'Порядковый номер' = DATA INTEGER(node, route);

EXTEND CLASS customer : node;
EXTEND CLASS stock : node;
//EXTEND CLASS supplier : node;

nameNode(customer) += name(customer) IF customer IS customer;
nameNode(stock) += name(stock) IF stock IS stock;

addressNode(customer) += addressLegalEntity(customer) IF customer IS customer;
addressNode(stock) += addressStock(stock) IF stock IS stock;

//nameNode(supplier) += name(supplier) IF supplier IS supplier;

EXTEND FORM route

    OBJECTS n=node
    PROPERTIES (n) READONLY nameNode, addressNode
    PROPERTIES (n, r) inNodeRoute, numberNodeRoute
;

EXTEND FORM routes
    OBJECTS n=node
    PROPERTIES (n) READONLY nameNode, addressNode
    PROPERTIES(n, r) READONLY numberNodeRoute
    FILTERS inNodeRoute(n, r)
;

