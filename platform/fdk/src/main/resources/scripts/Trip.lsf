MODULE Trip;

REQUIRE Order, Shipment, Route;

NAMESPACE Trip;

CLASS trip 'Рейс' : numeratedObject;

@defineDocumentHeaderTime(trip);

truckTrip = DATA truck(trip);
sidTruckTrip 'Машина (номер)'(trip) = sidTruck(truckTrip(trip));
weightTruckTrip 'Грузоподъёмность (тонн)'(trip) = weightTruck(truckTrip(trip));
ownerTruckTrip 'Владелец'(trip) = ownerTruck(truckTrip(trip));
markTruckTrip (trip) = markTruck(truckTrip(trip));
nameMarkTruckTrip 'Марка' (trip) = name(markTruckTrip(trip));
trailerTruckTrip 'Прицеп' (trip) = trailerTruck(truckTrip(trip));

driverTrip = DATA driver(trip);
nameDriverTrip 'Водитель' (trip) = commonName(driverTrip(trip));
sidDriverTrip 'Табельный номер' (trip) = sidDriver(driverTrip(trip));
typeDriverTrip 'Класс' (trip) = typeDriver(driverTrip(trip));

// заказы
tripOrder = DATA trip(order);
inTripOrder 'Включен' (trip, order) = tripOrder(order)==trip;

filterTripOrder(trip, order) = tripOrder(order)==trip OR (trip IS trip AND order IS order AND NOT tripOrder(order));

quantityTripStockFrom (trip, stock) = GROUP SUM 1 BY tripOrder(order), fromStockOrder(order);
quantityTripStockTo (trip, stock) = GROUP SUM 1 BY tripOrder(order), toStockOrder(order);
quantityTripStock(trip, stock) = UNION SUM quantityTripStockFrom(trip, stock), quantityTripStockTo(trip, stock);

//inNodeTrip 'Включен' (node, trip) = TRUE IF quantityTripNode(trip, node);
numberStockTrip 'Порядковый номер' = DATA INTEGER(stock, trip);
numberStockTrip(stock, trip) => quantityTripStock(trip, stock)>0 RESOLVE FALSE;

maxNumberTrip(trip) = GROUP MAX numberStockTrip(stock, trip) BY trip;
indexTripOrder 'Номер' (trip, order) = maxNumberTrip(trip) - numberStockTrip(toStockOrder(order), trip) + 1 IF inTripOrder(trip, order);

CONSTRAINT numberStockTrip(fromStockOrder(order), tripOrder(order)) >= numberStockTrip(toStockOrder(order), tripOrder(order))
    MESSAGE 'Номер не соответствует направлению заказа';

grossWeightOrderedTrip 'Суммарный вес заказов' (trip) = GROUP SUM grossWeightOrderDetailOrder(order) BY tripOrder(order);

quantityOrderedTripSku 'Кол-во в заказах' (trip, sku) = GROUP SUM quantityOrderDetail(orderDetail) BY tripOrder(orderOrderDetail(orderDetail)), skuOrderDetail(orderDetail);

//netWeightSkuOrderDetail 'Вес нетто' (orderDetail) = netWeightSku(skuOrderDetail(orderDetail));

// поставки
tripShipment = DATA trip(shipment);
inTripShipment 'Включен' (trip, shipment) = tripShipment(shipment)==trip;

filterTripShipment(trip, shipment) = tripShipment(shipment)==trip OR (trip IS trip AND shipment IS shipment AND NOT tripShipment(shipment));

grossWeightShipmentedTrip 'Суммарный вес поставок' (trip) = GROUP SUM grossWeightShipmentDetailShipment(shipment) BY tripShipment(shipment);

quantityShipmentDetailTripShipment (trip, shipment) = GROUP SUM quantityShipmentDetailOrderShipment (order, shipment) BY tripOrder(order), shipment;

quantityShipmentedTripSku 'Кол-во в поставках' (trip, sku) = GROUP SUM quantityShipmentDetail(shipmentDetail) BY tripShipment(shipmentShipmentDetail(shipmentDetail)), skuShipmentDetail(shipmentDetail);

createShipmentsTrip 'Присоединить поставки' (trip) = ACTION (trip) {
    SET tripShipment(shipment) <- NULL WHERE tripShipment(shipment) == trip;
    SET tripShipment(shipment) <- trip WHERE quantityShipmentDetailTripShipment(trip, shipment);
} TOOLBAR CONFIRM;

FORM waybill PRINT

    OBJECTS t=trip FIXED PANEL
    PROPERTIES(t) dateTrip, numberObject, sidTruckTrip, nameMarkTruckTrip, trailerTruckTrip, sidDriverTrip, typeDriverTrip

    OBJECTS st=stock
    PROPERTIES(st) name, addressStock, nameLegalEntityStock
    FILTERS quantityTripStock(t, st)>0
;

printWaybill 'Печать путевого листа' (trip) = ACTION (trip) {
    FORM waybill OBJECTS t = trip;
} CONFIRM TOOLBAR;


FORM ordersTrip PRINT
    OBJECTS t=trip FIXED PANEL
    PROPERTIES (t) nameNumeratorObject, numberObject, seriesObject, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameMarkTruckTrip, trailerTruckTrip

    OBJECTS o=order FIXED GRID
    FILTERS isPostedOrder(o), inTripOrder(t, o)

    OBJECTS s=sku
    PROPERTIES READONLY idBarcodeSku(s), nameSku(s), shortNameUOMSku(s)
    PROPERTIES READONLY quantityOrderedTripSku(t, s)
    PROPERTIES READONLY quantityOrderDetailSkuOrder(s, o) COLUMNS (o) HEADER seriesNumberObject(o)
    FILTERS quantityOrderedTripSku(t, s) > 0

    //PROPERTIES(o) READONLY numberOrder, seriesOrder, dateOrder, nameFromStockOrder, nameToStockOrder, grossWeightOrderDetailOrder
;

printOrdersTrip 'Печать заказов' (trip) = ACTION (trip) {
    FORM ordersTrip OBJECTS t = trip;
} CONFIRM TOOLBAR;

FORM trip 'Рейс'
    OBJECTS t=trip FIXED PANEL
    PROPERTIES (t) nameNumeratorObject, numberObject, seriesObject, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameMarkTruckTrip, trailerTruckTrip, weightTruckTrip, printWaybill
    PROPERTIES (t) READONLY grossWeightOrderedTrip, grossWeightShipmentedTrip

    OBJECTS o=order
    PROPERTIES(t, o) inTripOrder
    PROPERTIES(o) READONLY numberOrder, seriesOrder, dateOrder, nameFromStockOrder, nameToStockOrder, grossWeightOrderDetailOrder
    PROPERTIES(o) editOrder
    PROPERTIES printOrdersTrip(t) TODRAW o FORCE PANEL

    FILTERS isPostedOrder(o)

    FILTERGROUP filterDateOrder
        FILTER 'Заказы к отправке' 'F10' dateOrder(o) <= dateTrip(t) DEFAULT
    FILTERGROUP filterOrder
        FILTER 'Не расписанные заказы или в текущем рейсе' 'F11' filterTripOrder(t, o) DEFAULT

    OBJECTS od=orderDetail
    PROPERTIES(od) READONLY idBarcodeSkuOrderDetail, nameSkuOrderDetail, shortNameUOMSkuOrderDetail, quantityOrderDetail//, grossWeightSkuOrderDetail
    FILTERS orderOrderDetail(od)==o

    OBJECTS s=shipment
    PROPERTIES (t, s) inTripShipment
    PROPERTIES(s) READONLY numberShipment, seriesShipment, dateShipment, nameFromStockShipment, nameToStockShipment, grossWeightShipmentDetailShipment
    PROPERTIES (s) editShipment
    //PROPERTIES(s) printConsignmentSimpleVertical, printConsignmentSimpleHorizontal, printConsignmentSimpleAttach,
    //              printConsignmentVerticalA, printConsignmentHorizontalA, printConsignmentVerticalB,
    //              printConsignmentHorizontalB, printConsignmentAttach, editConsignment
    FILTERS isPostedShipment(s)

    PROPERTIES createShipmentsTrip(t) TODRAW s FORCE PANEL

    FILTERGROUP filterDateShipment
        FILTER 'Поставки к отправке' 'F10' dateShipment(s) <= dateTrip(t) DEFAULT
    FILTERGROUP filterShipment
        FILTER 'Не расписанные поставки или в текущем рейсе' 'F11' filterTripShipment(t, s) DEFAULT

    OBJECTS sd=shipmentDetail
    PROPERTIES(sd) READONLY idBarcodeSkuShipmentDetail, nameSkuShipmentDetail, shortNameUOMSkuShipmentDetail, quantityShipmentDetail//, netWeightSkuShipmentDetail
    FILTERS shipmentShipmentDetail(sd)==s

    OBJECTS sk=sku
    PROPERTIES(sk) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    PROPERTIES(t, sk) READONLY quantityOrderedTripSku, quantityShipmentedTripSku

    FILTERGROUP filterSku
        FILTER 'Товары в заказах или поставках' 'F11' quantityOrderedTripSku(t, sk)>0 OR quantityShipmentedTripSku(t, sk)>0 DEFAULT
        FILTER 'Товары в заказах' 'F9' quantityOrderedTripSku(t, sk)>0
        FILTER 'Товары в поставках' 'F10' quantityShipmentedTripSku(t, sk)>0
        FILTER 'Товары в заказах и поставках' 'F8' quantityOrderedTripSku(t, sk)>0 AND quantityShipmentedTripSku(t, sk)>0

    OBJECTS o2=order
    PROPERTIES(o2) READONLY numberOrder, seriesOrder, dateOrder, nameFromStockOrder, nameToStockOrder
    PROPERTIES(o2) editOrder
    PROPERTIES(sk, o2) READONLY quantityOrderDetailSkuOrder
    FILTERS inTripOrder(t, o2), quantityOrderDetailSkuOrder(sk, o2)>0

    OBJECTS s2=shipment
    PROPERTIES(s2) READONLY numberShipment, seriesShipment, dateShipment, nameFromStockShipment, nameToStockShipment
    PROPERTIES(s2)  editShipment
    PROPERTIES(sk, s2) READONLY quantityShipmentDetailSkuShipment
    FILTERS inTripShipment(t, s2), quantityShipmentDetailSkuShipment(sk, s2)>0

    OBJECTS st=stock
    PROPERTIES(st) READONLY name, addressStock, nameLegalEntityStock
    PROPERTIES(st, t) numberStockTrip
    FILTERS quantityTripStock (t, st)>0

    EDIT trip OBJECT t
;


DESIGN trip FROM DEFAULT{
    main {
        NEW topContainer {
            caption = 'Шапка документа';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(nameNumeratorObject);
            ADD PROPERTY(numberObject);
            ADD PROPERTY(seriesObject);
            ADD PROPERTY(dateTrip);
            ADD PROPERTY(timeTrip);
        }

        NEW midContainer {
            caption = 'Параметры машины';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(nameDriverTrip);
            ADD PROPERTY(sidTruckTrip);
            ADD PROPERTY(nameMarkTruckTrip);
            ADD PROPERTY(trailerTruckTrip);
            ADD PROPERTY(weightTruckTrip);
            ADD PROPERTY(grossWeightOrderedTrip);
            ADD PROPERTY(grossWeightShipmentedTrip);
        }

        NEW bottomContainer {
            type = TABBED;
            NEW orderContainer {
                caption = 'Заказы';
                childConstraints = TO THE BOTTOM;
                ADD o.box;
                ADD od.box;
            }

            NEW shipmentContainer {
                caption = 'Поставки';
                childConstraints = TO THE BOTTOM;
                ADD s.box;
                ADD sd.box;
            }

            NEW skuContainer {
                caption = 'Итоги по товарам';
                childConstraints = TO THE RIGHT;
                ADD sk.box;

                NEW documentContainer {
                    childConstraints = TO THE BOTTOM;
                    type = SPLITV;
                    ADD o2.box;
                    ADD s2.box;
                }
            }

            ADD st.box;
        }

        ADD PROPERTY(printWaybill);
        ADD functions.box;
    }
}

FORM trips 'Рейсы'
    OBJECTS t=trip
    PROPERTIES(t) READONLY numberObject, seriesObject, dateTrip, timeTrip, nameDriverTrip, sidTruckTrip, nameMarkTruckTrip, trailerTruckTrip, weightTruckTrip
    PROPERTIES(t) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    OBJECTS o=order
    PROPERTIES(o) READONLY numberOrder, seriesOrder, dateOrder, nameFromStockOrder, nameToStockOrder, grossWeightOrderDetailOrder
    FILTERS inTripOrder(t, o)

    OBJECTS s=shipment
    PROPERTIES(s) READONLY numberShipment, seriesShipment, dateShipment, nameFromStockShipment, nameToStockShipment, grossWeightShipmentDetailShipment
    FILTERS inTripShipment(t, s)

    OBJECTS sk=sku
    PROPERTIES(sk) READONLY idBarcodeSku, nameSku, shortNameUOMSku
    PROPERTIES(t, sk) READONLY quantityOrderedTripSku, quantityShipmentedTripSku

    FILTERGROUP filterSku
        FILTER 'Товары в заказах или поставках' 'F11' quantityOrderedTripSku(t, sk)>0 OR quantityShipmentedTripSku(t, sk)>0 DEFAULT
        FILTER 'Товары в заказах' 'F9' quantityOrderedTripSku(t, sk)>0
        FILTER 'Товары в поставках' 'F10' quantityShipmentedTripSku(t, sk)>0
        FILTER 'Товары в заказах и поставках' 'F8' quantityOrderedTripSku(t, sk)>0 AND quantityShipmentedTripSku(t, sk)>0

    OBJECTS st=stock
    PROPERTIES(st) READONLY name, addressStock, nameLegalEntityStock
    PROPERTIES(st, t) READONLY numberStockTrip
    FILTERS quantityTripStock(t, st)>0
;

DESIGN trips FROM DEFAULT{
    main {
        ADD t.box;

        NEW bottomContainer {
            type = TABBED;
            ADD o.box;
            ADD s.box;
            ADD sk.box;
            ADD st.box;
        }

        ADD functions.box;
    }
}
