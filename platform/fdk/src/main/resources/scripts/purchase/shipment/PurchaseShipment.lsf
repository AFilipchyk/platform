MODULE PurchaseShipment;

REQUIRE Shipment, PurchaseInvoice, PurchaseOperation;

NAMESPACE Purchase;

//----------------------------------------------- Поставка ---------------------------------------------------//

@defineShipment(' (закупка)', customerStock, showPurchasePack, 'Упаковка', Purchase);
@defineShipmentBatch(customerStock);

@defineShipmentStockDestination(supplierStock, customerStock);

//----------------------------------------------- Операции -----------------------------------------------------//

@defineDocumentOperation(shipment, s);
@defineDocumentOperationLegalEntity(userShipment, supplier, 'Поставщик');
@defineDocumentOperationLegalEntity(userShipment, customer, 'Покупатель');

createShipmentUserInvoice(invoice) <- createShipmentOperation(operationUserInvoice(invoice))
    WHEN CHANGED(operationUserInvoice(invoice));
    
//------------------------------ Ограничение на выбор контрагентов -----------------------------//

CONSTRAINT supplierUserShipment(userShipment) AND NOT isSupplierLegalEntity(supplierUserShipment(userShipment))
    CHECKED BY supplierUserShipment MESSAGE 'Для поставки выбрано в качестве поставщика организация, не являющаяся поставщиком';
CONSTRAINT customerUserShipment(userShipment) AND NOT isCompanyLegalEntity(customerUserShipment(userShipment))
    CHECKED BY customerUserShipment MESSAGE 'Для поставки выбрано в качестве покупателя организация, не являющаяся компанией';

//------------------------------ Автоматическое проставление свойств -----------------------------//

supplierUserShipment(userShipment) <- defaultSupplierEmployee(currentUser()) IF countAccessSupplierEmployee (currentUser()) == 1
    WHEN ASSIGNED(userShipment IS userShipment);
customerUserShipment(userShipment) <- defaultCompanyEmployee(currentUser()) IF countAccessCompanyEmployee (currentUser()) == 1
    WHEN ASSIGNED(userShipment IS userShipment);

supplierStockUserShipment(userShipment) <- defaultStockEmployeeLegalEntity(currentUser(), supplierUserShipment(userShipment))
    IF countAccessStockEmployeeLegalEntity (currentUser(), supplierUserShipment(userShipment)) == 1
        WHEN CHANGED(supplierUserShipment(userShipment));
customerStockUserShipment(userShipment) <- defaultStockEmployeeLegalEntity(currentUser(), customerUserShipment(userShipment))
    IF countAccessStockEmployeeLegalEntity (currentUser(), customerUserShipment(userShipment)) == 1
        WHEN CHANGED(customerUserShipment(userShipment));

// ------------------------------- Расчет учетной цены для поставки ------------------------ //
overShipmentPriceUserInvoiceDetail = ABSTRACT NUMERIC[14,2] (userInvoiceDetail) PERSISTENT;
calcShipmentPriceUserInvoiceDetail (detail) = costPriceUserInvoiceDetail(detail) OR overShipmentPriceUserInvoiceDetail(detail) PERSISTENT;

extraShipmentPriceUserInvoiceDetail = ABSTRACT NUMERIC[14,2] (userInvoiceDetail) PERSISTENT;

// Цены учетные
priceUserShipmentDetail (detail) <- costPriceInvoiceDetail(invoiceDetailUserShipmentDetail(detail))
    WHEN CHANGED(priceInvoiceDetail(invoiceDetailUserShipmentDetail(detail)));
shipmentPriceUserInvoiceDetail(detail) <- calcShipmentPriceUserInvoiceDetail(detail) (+) extraShipmentPriceUserInvoiceDetail(detail) WHEN
    (CHANGED(calcShipmentPriceUserInvoiceDetail(detail)) OR
     CHANGED(extraShipmentPriceUserInvoiceDetail(detail)) OR
     CHANGED(createShipmentUserInvoiceDetail(detail)))
        AND createShipmentUserInvoiceDetail(detail);

// ------------------------------- Проведение по регистру остатков ------------------------ //

overCreateBatchShipmentDetail = ABSTRACT BOOLEAN (shipmentDetail);
createBatchShipmentDetail (detail) = (detail IS shipmentDetail OR overCreateBatchShipmentDetail(detail)) AND NOT batchShipmentDetail(detail);

// Создаем партию
CLASS shipmentBatch 'Партия на основе закупки';
@defineAggregation(shipmentDetail, shipmentBatch, createBatchShipmentDetail);

@defineSkuLedgerAggregation(shipmentDetail, shipmentBatch, sku, customerStock);
priceShipmentBatch (batch) = priceShipmentDetail(shipmentDetailShipmentBatch(batch));

@implementBatch(shipmentBatch, sku, stock, price);
quantityBatch (ledger) += quantityShipmentDetail(shipmentDetailShipmentBatch(ledger));
sumInSkuLedger (ledger) += sumShipmentDetail(shipmentDetailShipmentBatch(ledger));

ledgerBatchShipmentDetail (detail) = shipmentBatchShipmentDetail(detail) OR batchShipmentDetail(detail) PERSISTENT;

batchPriceListLedger(ledger) += ledgerBatchShipmentDetail(invoiceShipmentDetailInvoiceDetail(ledger)) OR batchInvoiceDetail(ledger);
batchPriceListLedger(ledger) += ledgerBatchShipmentDetail(invoiceShipmentDetailInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) OR batchInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger));

// Проводим по регистру учетных цен
@implementSystemLedgerPriceListTypeBatch(account, shipmentBatch, stock);

// Создаем просто inLIFOSkuLedger
createSkuLedgerShipmentDetail (detail) = detail IS shipmentDetail AND NOT createBatchShipmentDetail(detail);

CLASS shipmentSkuLedger 'Изменение остатка на основе закупки';
@defineAggregation(shipmentDetail, shipmentSkuLedger, createSkuLedgerShipmentDetail);

@defineSkuLedgerAggregation(shipmentDetail, shipmentSkuLedger, sku, customerStock);
priceShipmentSkuLedger (ledger) = priceShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));

@implementSkuLedgerInLIFO(shipmentSkuLedger, sku, stock);
quantityInLIFOSkuLedger (ledger) += quantityShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));
limitInLIFOSkuLedgerBatch(ledger, batch) += quantityShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)) IF batchShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)) == batch;
changedDataSkuLedger(ledger) += CHANGED(batchShipmentDetail(shipmentDetailShipmentSkuLedger(ledger)));
sumInSkuLedger (ledger) += sumShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));

costShipmentDetailBatch(detail, batch) += IF createBatchShipmentDetail(detail) THEN
                                             costSkuLedgerBatch(shipmentBatchShipmentDetail(detail), batch)
                                          ELSE
                                             costSkuLedgerBatch(shipmentSkuLedgerShipmentDetail(detail), batch);

// ------------------------------- Проведение регистру учетных цен ------------------------ //

@implementSystemLedgerPriceListType(account, shipmentSkuLedger, stock);
batchPriceListLedger(ledger) += batchShipmentDetail(shipmentDetailShipmentSkuLedger(ledger));

// ------------------------------- Проведение по товарному отчету ------------------------ //
@implementStockDocumentLedgerInc(shipment, customerStock);
sumIncStockDocumentLedger (ledger) += sumShipmentDetailShipment(ledger);
sumItemIncStockDocumentLedger (ledger) += sumItemShipmentDetailShipment(ledger);
sumContainerIncStockDocumentLedger (ledger) += sumContainerShipmentDetailShipment(ledger);


NAVIGATOR {
    purchasePurchaseNavigator {
        ADD shipments;
    }
}