MODULE PurchaseReturnShipment;

REQUIRE Shipment, PurchaseReturnInvoice;

NAMESPACE PurchaseReturn;

//----------------------------------------------- Поставка ---------------------------------------------------//
@defineShipment(' (закупка-возврат)', customerStock, showPurchasePack, 'Отображать упаковку', purchasePack);
@defineShipmentBatch(customerStock);

@defineShipmentStockDestination(customerStock, supplierStock);

//----------------------------------------------- Операции -----------------------------------------------------//

@defineDocumentOperation(purchase, shipment, s);
@defineDocumentOperationLegalEntity(purchase, userShipment, supplier, 'Поставщик');
@defineDocumentOperationLegalEntity(purchase, userShipment, customer, 'Покупатель');

createShipmentUserInvoice(invoice) <- createShipmentPurchaseOperation(purchaseOperationUserInvoice(invoice))
    WHEN CHANGED(purchaseOperationUserInvoice(invoice));

//------------------------------ Ограничение на выбор контрагентов -----------------------------//

CONSTRAINT supplierUserShipment(userShipment) AND NOT isSupplierLegalEntity(supplierUserShipment(userShipment))
    CHECKED BY supplierUserShipment MESSAGE 'Для поставки выбрано в качестве поставщика организация, не являющаяся поставщиком';
CONSTRAINT customerUserShipment(userShipment) AND NOT isCompanyLegalEntity(customerUserShipment(userShipment))
    CHECKED BY customerUserShipment MESSAGE 'Для поставки выбрано в качестве покупателя организация, не являющаяся компанией';

//------------------------------ Автоматическое проставление свойств -----------------------------//

supplierUserShipment(userShipment) <- defaultSupplierEmployee(currentUser()) IF countAccessSupplierEmployee (currentUser()) == 1
    WHEN ASSIGNED(userShipment IS userShipment);
customerUserShipment(userShipment) <- defaultCompanyEmployee(currentUser()) IF countAccessCompanyEmployee (currentUser()) == 1
    WHEN ASSIGNED(userShipment IS userShipment);

supplierStockUserShipment(userShipment) <- defaultStockEmployeeLegalEntity(currentUser(), supplierUserShipment(userShipment))
    IF countAccessStockEmployeeLegalEntity (currentUser(), supplierUserShipment(userShipment)) == 1
        WHEN CHANGED(supplierUserShipment(userShipment));
customerStockUserShipment(userShipment) <- defaultStockEmployeeLegalEntity(currentUser(), customerUserShipment(userShipment))
    IF countAccessStockEmployeeLegalEntity (currentUser(), customerUserShipment(userShipment)) == 1
        WHEN CHANGED(customerUserShipment(userShipment));

// --------------------------- Расчет учетной цены для поставки --------------------- //

@deriveDocumentDetailPriceSystemLedgerPriceListTypeBatch (userShipment, accountPriceListType, , , sku, supplierStock);
shipmentPriceUserInvoiceDetail(detail) <- calcShipmentPriceUserInvoiceDetail(detail) (+) extraShipmentPriceUserInvoiceDetail(detail) WHEN
    (CHANGED(calcShipmentPriceUserInvoiceDetail(detail)) OR CHANGED(extraShipmentPriceUserInvoiceDetail(detail)) OR CHANGED(createShipmentUserInvoiceDetail(detail)))
        AND createShipmentUserInvoiceDetail(detail);

// ------------------------------- Проведение по регистру остатков ------------------------ //

@implementSkuLedgerOutFIFO(shipmentDetail, sku, customerStock);
quantityOutFIFOSkuLedger (ledger) += quantityShipmentDetail(ledger);
@implementSkuLedgerOutFIFOBatchBalance(shipmentDetail, customerStock);
sumOutSkuLedger (ledger) += sumShipmentDetail(ledger);

// ------------------------------- Проведение по товарному отчету ------------------------ //

@implementStockDocumentLedgerOut(shipment, customerStock);
sumOutStockDocumentLedger (ledger) += sumShipmentDetailShipment(ledger);
sumItemOutStockDocumentLedger (ledger) += sumItemShipmentDetailShipment(ledger);
sumContainerOutStockDocumentLedger (ledger) += sumContainerShipmentDetailShipment(ledger);


NAVIGATOR {
    purchaseReturnNavigator {
        ADD shipments;
    }
}
