MODULE PurchaseInvoiceCharge;

REQUIRE PurchaseInvoice;

NAMESPACE Purchase;

chargeSumUserInvoice 'Сумма услуг в документе' (userInvoice) = GROUP SUM sumUserInvoiceDetail(idetail) AND isChargeSkuUserInvoiceDetail(idetail)
    BY userInvoiceUserInvoiceDetail(idetail) IN documentSumGroup;
chargeSumInvoice 'Сумма услуг в документе' (invoice) = GROUP SUM sumInvoiceDetail(idetail) AND isChargeSkuInvoiceDetail(idetail)
    BY invoiceInvoiceDetail(idetail) IN documentSumGroup;

itemSumUserInvoice 'Сумма товара в документе' (userInvoice) = GROUP SUM sumUserInvoiceDetail(idetail) AND isStockSkuUserInvoiceDetail(idetail)
    BY userInvoiceUserInvoiceDetail(idetail) IN documentSumGroup;
itemSumInvoice 'Сумма товара в документе' (invoice) = GROUP SUM sumInvoiceDetail(idetail) AND isStockSkuInvoiceDetail(idetail)
    BY invoiceInvoiceDetail(idetail) IN documentSumGroup;

calcChargePriceUserInvoiceDetail  (detail) = [(X*Y)/(Z*W)](
    sumUserInvoiceDetail(detail),
    chargeSumUserInvoice(userInvoiceUserInvoiceDetail(detail)),
    itemSumUserInvoice(userInvoiceUserInvoiceDetail(detail)),
    quantityUserInvoiceDetail(detail)
) AND isStockSkuInvoiceDetail(detail);

@defineDocumentInterfaceDetailPriceCustomPrefix (invoiceDetail, charge, ' услуг за ед.');

deriveChargePriceUserInvoice 'Расписать сумму услуг по товарам' = ACTION (userInvoice) {
    SET chargePriceUserInvoiceDetail(detail) <- calcChargePriceUserInvoiceDetail(detail) WHERE userInvoiceUserInvoiceDetail(detail) == userInvoice;
}

EXTEND CLASS SystemLedgerPriceListType {chargePriceStockPriceListType 'Услуги (последняя по складу)' }
batchLedgerPriceListType(type) += type == SystemLedgerPriceListType. chargePriceStockPriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += chargePriceInvoiceDetail(ledger) WHEN CLASS(chargePriceInvoiceDetail(ledger)) AND type == SystemLedgerPriceListType.chargePriceStockPriceListType;

EXTEND CLASS SystemLedgerPriceListType { chargePricePriceListType 'Услуги (последняя по ценовой группе)' }
batchLedgerPriceListType(type) += type == SystemLedgerPriceListType.chargePricePriceListType;
pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger)) WHEN CLASS(chargePriceInvoiceDetail(invoiceDetailInvoicePriceListLedger(ledger))) AND type == SystemLedgerPriceListType.chargePricePriceListType;

extraCostPriceUserInvoiceDetail(detail) += chargePriceUserInvoiceDetail(detail);

EXTEND FORM userInvoice
    PROPERTIES(i) chargeSumUserInvoice, deriveChargePriceUserInvoice TODRAW d FORCE PANEL TOOLBAR
    PROPERTIES(d) chargePriceUserInvoiceDetail BEFORE numberVATUserInvoiceDetail(d)
;

EXTEND FORM invoices
    PROPERTIES(i) chargeSumInvoice
    PROPERTIES(d) chargePriceInvoiceDetail BEFORE numberVATInvoiceDetail(d)
;