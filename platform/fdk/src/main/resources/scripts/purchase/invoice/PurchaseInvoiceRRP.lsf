MODULE PurchaseInvoiceRRP;

REQUIRE PurchaseInvoice;

NAMESPACE Purchase;

//------------------------- Рекомендуемая розничная цена-------------------------//
@defineDocumentInterfaceHeaderCreate (invoice, toShowRRPPrice, 'Отображать RRP');
@defineDocumentInterfaceDetailPriceListType (invoice, priceListType, RRP, ' (RRP)');

RRPPriceListTypeUserInvoiceDetail(userInvoiceDetail) <- RRPPriceListTypeAgreement(agreementUserInvoice(userInvoiceUserInvoiceDetail(userInvoiceDetail)))
    WHEN CHANGED (agreementUserInvoice(userInvoiceUserInvoiceDetail(userInvoiceDetail)));

currencyRRPUserInvoiceDetail (userInvoiceDetail) = currencyPriceListType(RRPPriceListTypeUserInvoiceDetail(userInvoiceDetail));
nameCurrencyRRPUserInvoiceDetail 'Валюта (RRP)' (userInvoiceDetail) = name(currencyRRPUserInvoiceDetail(userInvoiceDetail));

currencyRRPInvoiceDetail (invoiceDetail) = currencyPriceListType(RRPPriceListTypeInvoiceDetail(invoiceDetail));
nameCurrencyRRPInvoiceDetail 'Валюта (RRP)' (invoiceDetail) = name(currencyRRPInvoiceDetail(invoiceDetail));

@defineDocumentInterfaceDetailPriceCustomPrefix (invoiceDetail, RRP, ' (RRP)');
RRPPriceUserInvoiceDetail (detail)  <-  prevPricePriceListTypeSkuStockDateTime(RRPPriceListTypeUserInvoiceDetail(detail),
                                                                               skuUserInvoiceDetail(detail),
                                                                               customerStockUserInvoiceDetail(detail),
                                                                               dateTimeUserInvoiceDetail(detail))
                                                    WHEN CHANGED(RRPPriceListTypeUserInvoiceDetail(detail)) OR
                                                         CHANGED(skuUserInvoiceDetail(detail)) OR
                                                         CHANGED(customerStockUserInvoiceDetail(detail)) OR
                                                         CHANGED(dateTimeUserInvoiceDetail(detail));

// todo: откуда брать тип обмена????
RRPTypeExchangeInvoiceDetail  (invoiceDetail) = ABSTRACT typeExchange (invoiceDetail) PERSISTENT;
nameRRPTypeExchangeInvoiceDetail 'Тип обмена (RRP)' (invoiceDetail)= name(RRPTypeExchangeInvoiceDetail(invoiceDetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;

RRPTypeExchangeUserInvoiceDetail (userInvoiceDetail) = DATA typeExchange (userInvoiceDetail);
nameRRPTypeExchangeUserInvoiceDetail 'Тип обмена (RRP)' (userInvoiceDetail)= name(RRPTypeExchangeUserInvoiceDetail(userInvoiceDetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;

CONSTRAINT currencyRRPUserInvoiceDetail(detail) != currencyTypeExchange(RRPTypeExchangeUserInvoiceDetail(detail))
    CHECKED BY RRPTypeExchangeUserInvoiceDetail MESSAGE 'В накладной валюта типа обмена RRP не соавпадает с валютой вида цены RRP';

RRPTypeExchangeInvoiceDetail(invoiceDetail) += RRPTypeExchangeUserInvoiceDetail(invoiceDetail);

@defineDocumentInterfaceDetailPriceCustomPrefix (invoiceDetail, RRPExchange, ' (RRP) конвертированная');

RRPExchangePriceUserInvoiceDetail (detail)  <-  roundCurrency(           // Какое ставить округление?????
    RRPPriceUserInvoiceDetail (detail)*rateTypeExchangeCurrencyDate(
        RRPTypeExchangeUserInvoiceDetail(detail), currencyUserInvoiceDetail(detail), dateUserInvoiceDetail(detail)),
            currencyUserInvoiceDetail(detail))

        WHEN CHANGED(RRPTypeExchangeUserInvoiceDetail(detail)) OR
             CHANGED(currencyUserInvoiceDetail(detail)) OR
             CHANGED(dateUserInvoiceDetail(detail)) OR
             CHANGED(RRPPriceUserInvoiceDetail (detail));

toShowRRPExchangePriceInvoice(invoice) = [GROUP SUM 1 AND NOT currencyInvoiceDetail(invoiceDetail) == currencyRRPInvoiceDetail(invoiceDetail) BY invoiceInvoiceDetail(invoiceDetail)](
    invoice) AND toShowRRPPriceInvoice(invoice);

EXTEND FORM userInvoice
    PROPERTIES(i) toShowRRPPriceUserInvoice
    PROPERTIES(d) SHOWIF toShowRRPPriceUserInvoice(i) BEFORE delete(d) nameRRPPriceListTypeUserInvoiceDetail, nameCurrencyRRPUserInvoiceDetail READONLY, RRPPriceUserInvoiceDetail
    PROPERTIES(d) SHOWIF toShowRRPExchangePriceInvoice(i) BEFORE delete(d) nameRRPTypeExchangeUserInvoiceDetail, RRPExchangePriceUserInvoiceDetail
;
EXTEND FORM invoices
    PROPERTIES(i) toShowRRPPriceInvoice
    PROPERTIES(d) READONLY SHOWIF toShowRRPPriceInvoice(i) nameRRPPriceListTypeInvoiceDetail, nameCurrencyRRPInvoiceDetail, RRPPriceInvoiceDetail
    PROPERTIES(d) READONLY SHOWIF toShowRRPExchangePriceInvoice(i) nameRRPTypeExchangeInvoiceDetail, RRPExchangePriceInvoiceDetail
;


