MODULE POS;

REQUIRE System,
        Utils,
        Document,
        Currency,
        Store,
        Stock,
        CashRegister,
        CashOperation,
        Sales,
        RetailCRM,
        PriceInterval,
        AccountDocument; // todo : эту зависимость надо будет убить, но сейчас не получается из-за того что нельзя донаследовать классы

// ------------------------------------- Объявление Z-отчета ---------------------------------------- //

CLASS zReport 'Z-отчет (открытый)' : incomeCashOrder, outAccountDocumentLedger, historyObject, numberedObject;
CLASS zReportPosted 'Z-отчет (закрытый)' : zReport, postedObject;

TABLE zReport (zReport);

@defineDocumentHeaderPosted(zReport);
@defineDocumentHeaderTime(zReport);
@defineDocumentHeaderDepartmentStore(zReport);

castBasisZReport =
    FORMULA STRING[100] '\'Z-отчет №\' || CAST($1 AS TEXT) || \' с кассы \' || CAST($2 AS TEXT) || \' от \' || CAST($3 AS TEXT)';

numberZReport 'Номер Z-отчета' (zReport) = DATA STRING[21](zReport) IN documentPrmGroup;
cashRegisterZReport (zReport) = DATA cashRegister(zReport);
numberCashRegisterZReport 'Касса Z-отчета' (zReport) = numberCashRegister(cashRegisterZReport(zReport)) IN documentPrmGroup;
numberNumberCashRegisterToZReportPosted (numberZReport, numberCashRegister) = GROUP UNIQUE zReportPosted BY numberZReport (zReportPosted), numberCashRegisterZReport (zReportPosted) WHERE zReportPosted IS zReportPosted;
maxNumberZReport(cashRegister) = GROUP MAX numberZReport(zReport)  BY cashRegisterZReport (zReport);
basisZReport 'Основание' (zReport) = castBasisZReport(numberZReport(zReport), numberCashRegisterZReport(zReport), dateZReport(zReport)) IN documentPrmGroup;

dateCashDocument(zReport) += dateZReport(zReport);
timeCashDocument(zReport) += timeZReport(zReport);
dateTimeCashDocument(zReport) += dateTimeZReport(zReport);
numberCashDocument(zReport) += numberZReport(zReport);
departmentStoreCashDocument(zReport) += departmentStoreZReport(zReport);
basisCashDocument(zReport) += basisZReport(zReport);
cashRegisterCashDocument(zReport) += cashRegisterZReport(zReport);

departmentStoreZReport(zReport) <- departmentStoreCashRegister(cashRegisterZReport(zReport))
    WHEN CHANGED(cashRegisterZReport(zReport));

descriptionZReport (zReport) =
    [FORMULA STRING[200] '\'Продажа по кассе \' || CAST($1 AS TEXT) || \' отдела \' || CAST($2 AS TEXT) ||  \' от \' || CAST($3 AS TEXT)'](
    numberCashRegisterZReport(zReport), nameDepartmentStoreZReport(zReport), dateZReport(zReport));

//----------------------------------------------- Чеки ---------------------------------------------------//

CLASS receipt 'Чек' : historyObject;

CLASS ABSTRACT receiptDetail 'Строка чека';
CLASS receiptSaleDetail 'Строка продажи' : receiptDetail, outFIFOSkuLedger, salesLedger;
CLASS receiptReturnDetail 'Строка возврата' : receiptDetail, inLIFOSkuLedger;

TABLE receipt (receipt);
TABLE receiptDetail (receiptDetail);
TABLE receiptSaleDetail (receiptSaleDetail);
TABLE receiptReturnDetail (receiptReturnDetail);

@defineDocumentRelation(zReport, receipt);
cashRegisterReceipt (receipt) = cashRegisterZReport(zReportReceipt(receipt));
numberCashRegisterReceipt 'Номер кассы' (receipt) = numberCashRegister(cashRegisterReceipt(receipt)) IN recognizeGroup;

cashRegisterModelReceipt (receipt) = cashRegisterModelCashRegister(cashRegisterReceipt(receipt));
sidCashRegisterModelReceipt 'Код модели' (receipt) = sidModel(cashRegisterModelReceipt(receipt));

numberZReportReceipt 'Номер Z-отчета' (receipt) = numberZReport(zReportReceipt(receipt)) IN recognizeGroup;

userReceipt = DATA customUser(receipt);
nameUserReceipt 'Кассир' (receipt) = commonName(userReceipt(receipt));
userReceipt(receipt) <- currentUser() WHEN ASSIGNED(receipt IS receipt);

@defineDocumentHeaderTime(receipt);

@defineDocumentDetailDepartmentStoreCustom(zReport, receipt);
@defineDocumentDetailPosted(zReport, receipt);

@defineDocumentRelation(receipt, receiptSaleDetail);
@defineDocumentRelation(receipt, receiptReturnDetail);

@defineDocumentHeaderCurrency (receipt);
@deriveDocumentCurrency (receipt, departmentStore);

@defineDocumentDetailCurrency (receipt, receiptSaleDetail);
@defineDocumentDetailCurrency (receipt, receiptReturnDetail);

receiptReceiptDetail (receiptDetail) = UNION EXCLUSIVE receiptReceiptReturnDetail(receiptDetail), receiptReceiptSaleDetail(receiptDetail) PERSISTENT;

zReportReceiptDetail (receiptDetail) = zReportReceipt(receiptReceiptDetail(receiptDetail));
sidCashRegisterModelReceiptDetail 'Код модели' (receiptDetail) = sidCashRegisterModelReceipt(receiptReceiptDetail(receiptDetail));

typeReceiptDetail 'Тип' (receiptDetail) = UNION EXCLUSIVE 'Продажа' AND receiptDetail IS receiptSaleDetail, 'Возврат' AND receiptDetail IS receiptReturnDetail FIXEDCHARWIDTH 8;

@defineDocumentDetailIndex(receipt, receiptDetail);

@defineDocumentDetailTime(receipt, receiptSaleDetail);
@defineDocumentDetailTime(receipt, receiptReturnDetail);
dateReceiptDetail 'Дата' (receiptDetail) = UNION EXCLUSIVE dateReceiptReturnDetail(receiptDetail), dateReceiptSaleDetail(receiptDetail) PERSISTENT;
dateTimeReceiptDetail 'Дата/время' (receiptDetail) = UNION EXCLUSIVE dateTimeReceiptReturnDetail(receiptDetail), dateTimeReceiptSaleDetail(receiptDetail) PERSISTENT;

@defineDocumentDetailPosted(receipt, receiptSaleDetail);
@defineDocumentDetailPosted(receipt, receiptReturnDetail);
isPostedReceiptDetail 'Проведен' (receiptDetail) = UNION EXCLUSIVE isPostedReceiptReturnDetail(receiptDetail), isPostedReceiptSaleDetail(receiptDetail) PERSISTENT;

@defineDocumentDetailDepartmentStoreCustom(receipt, receiptSaleDetail);
@defineDocumentDetailDepartmentStoreCustom(receipt, receiptReturnDetail);
departmentStoreReceiptDetail (receiptDetail) = UNION EXCLUSIVE departmentStoreReceiptReturnDetail(receiptDetail), departmentStoreReceiptSaleDetail(receiptDetail) PERSISTENT;

@defineDocumentDetailSku(receiptSale, sku);
@defineDocumentDetailSku(receiptReturn, sku);
skuReceiptDetail (receiptDetail) = skuReceiptReturnDetail(receiptDetail) OR skuReceiptSaleDetail(receiptDetail);
nameSkuReceiptDetail 'Товар' (receiptDetail) = nameSkuStock(skuReceiptDetail(receiptDetail), departmentStoreReceiptDetail(receiptDetail)) IN recognizeGroup;

numberReceipt 'Номер чека' (receipt) = DATA INTEGER (receipt) IN documentHeaderGroup;
numberReceiptReceiptDetail 'Номер чека' (receiptDetail) = numberReceipt(receiptReceiptDetail(receiptDetail));

maxNumberReceiptZReport 'Максимальный номер чека' (zReport) = GROUP MAX numberReceipt(receipt) BY zReportReceipt(receipt) PERSISTENT;

zReportReceiptToReceipt (zReport, receipt, cashRegister) = GROUP UNIQUE receipt BY numberZReport(zReportReceipt(receipt)), numberReceipt(receipt), numberCashRegister(cashRegisterZReport(zReportReceipt(receipt))) WHERE receipt IS receipt;

GROUP receiptDiscountGroup 'Дисконтная карта' : publicGroup;

discountCardReceipt (receipt) = DATA discountCard (receipt);
numberDiscountCardReceipt 'Номер дисконтной карты' (receipt) = seriesNumberObject(discountCardReceipt(receipt)) IN receiptDiscountGroup;
nameClientDiscountCardReceipt 'Держатель дисконтной карты' (receipt) = nameClientDiscountCard(discountCardReceipt(receipt)) IN receiptDiscountGroup;
numberDiscountCardReceiptDetail 'Номер дисконтной карты' (receiptDetail) = numberDiscountCardReceipt(receiptReceiptDetail(receiptDetail));
nameClientDiscountCardReceiptDetail 'Держатель дисконтной карты' (receiptDetail) = nameClientDiscountCardReceipt(receiptReceiptDetail(receiptDetail));

idBarcodeReceiptDetail 'Штрих-код' (receiptDetail) = DATA STRING[14] (receiptDetail) FIXEDCHARWIDTH 14 INDEXED;

// Возвраты

receiptSaleDetailReceiptReturnDetail = DATA receiptSaleDetail (receiptReturnDetail);
receiptSaleReceiptReturnDetail (returnDetail) = receiptReceiptSaleDetail(receiptSaleDetailReceiptReturnDetail(returnDetail));

descriptionSaleReceiptReturnDetail 'Чек продажи' (returnDetail) =
    [FORMULA STRING[200] '\'Чек № \' || CAST($1 AS TEXT) || \' от \' || CAST($2 AS TEXT) ||  \' позиция \' || CAST($3 AS TEXT)'](
    numberReceipt(receiptSaleReceiptReturnDetail(returnDetail)), dateTimeReceipt(receiptSaleReceiptReturnDetail(returnDetail)), indexReceiptDetail(receiptSaleDetailReceiptReturnDetail(returnDetail)));

// Количества и суммы

quantityReceiptSaleDetail 'Количество' (receiptDetail) = DATA NUMERIC[14,3] (receiptSaleDetail);
priceReceiptSaleDetail 'Цена' (receiptDetail) = DATA NUMERIC[14,2] (receiptSaleDetail);
sumReceiptSaleDetail 'Сумма' (receiptDetail) = DATA NUMERIC[16,2] (receiptSaleDetail);
discountPercentReceiptSaleDetail 'Процент скидки' (receiptDetail) = DATA NUMERIC[6,2] (receiptSaleDetail);
discountSumReceiptSaleDetail 'Сумма скидки' (receiptDetail) = DATA NUMERIC[16,2] (receiptSaleDetail);

priceSumReceiptSaleDetail 'Сумма без скидки' = quantityReceiptSaleDetail(detail) * priceReceiptSaleDetail(detail);

calcDiscountSumReceiptSaleDetail (detail) = priceSumReceiptSaleDetail(detail) * discountPercentReceiptSaleDetail (detail) / 100;
discountSumReceiptSaleDetail (detail) <- round(calcDiscountSumReceiptSaleDetail (detail), priceRoundCurrency(currencyReceiptSaleDetail (detail)))
    WHEN CHANGED (calcDiscountSumReceiptSaleDetail(detail)) OR CHANGED (currencyReceiptSaleDetail (detail));

calcSumReceiptSaleDetail (detail) = priceSumReceiptSaleDetail(detail) (-) discountSumReceiptSaleDetail(detail);
sumReceiptSaleDetail (detail) <- round(calcSumReceiptSaleDetail(detail), priceRoundCurrency(currencyReceiptSaleDetail (detail)))
    WHEN CHANGED (calcSumReceiptSaleDetail(detail)) OR CHANGED (currencyReceiptSaleDetail (detail));

quantityReceiptReturnDetail 'Количество' (receiptDetail) = DATA NUMERIC[14,3] (receiptReturnDetail);
priceReceiptReturnDetail 'Цена' (receiptDetail) = DATA NUMERIC[14,2] (receiptReturnDetail);
sumReceiptReturnDetail 'Сумма' (receiptDetail) = DATA NUMERIC[16,2] (receiptReturnDetail);
discountSumReceiptReturnDetail 'Сумма скидки' (receiptDetail) = DATA NUMERIC[16,2] (receiptReturnDetail);

quantityReturnedReceiptSaleDetail 'Возвращено' (saleDetail) = GROUP SUM quantityReceiptReturnDetail(returnDetail) BY receiptSaleDetailReceiptReturnDetail(returnDetail) PERSISTENT;
CONSTRAINT quantityReturnedReceiptSaleDetail(detail) > quantityReceiptSaleDetail (detail) MESSAGE 'Количество возвратов по строке чека превышает проданное количество';

calcSumReceiptReturnDetail(detail) = round(quantityReceiptReturnDetail(detail) * sumReceiptSaleDetail(receiptSaleDetailReceiptReturnDetail(detail)) / quantityReceiptSaleDetail(receiptSaleDetailReceiptReturnDetail(detail)),
    priceRoundCurrency(currencyReceiptReturnDetail (detail)));
sumReceiptReturnDetail (detail) <- calcSumReceiptReturnDetail(detail) WHEN CHANGED (calcSumReceiptReturnDetail(detail));

calcDiscountSumReceiptReturnDetail(detail) = round(quantityReceiptReturnDetail(detail) * discountSumReceiptSaleDetail(receiptSaleDetailReceiptReturnDetail(detail)) / quantityReceiptSaleDetail(receiptSaleDetailReceiptReturnDetail(detail)),
   priceRoundCurrency(currencyReceiptSaleDetail(receiptSaleDetailReceiptReturnDetail(detail))));
discountSumReceiptReturnDetail (detail) <- calcDiscountSumReceiptReturnDetail(detail) WHEN CHANGED (calcDiscountSumReceiptReturnDetail(detail));

quantityReceiptDetail 'Количество' (receiptDetail) = UNION EXCLUSIVE quantityReceiptReturnDetail(receiptDetail), quantityReceiptSaleDetail(receiptDetail) PERSISTENT;
priceReceiptDetail 'Цена' (receiptDetail) = UNION EXCLUSIVE priceReceiptReturnDetail(receiptDetail), priceReceiptSaleDetail(receiptDetail) PERSISTENT;
sumReceiptDetail 'Сумма' (receiptDetail) = UNION EXCLUSIVE sumReceiptReturnDetail(receiptDetail), sumReceiptSaleDetail(receiptDetail) PERSISTENT;
discountSumReceiptDetail 'Сумма скидки' (receiptDetail) = UNION EXCLUSIVE discountSumReceiptReturnDetail(receiptDetail), discountSumReceiptSaleDetail(receiptDetail) PERSISTENT;

sumReceiptSaleDetailReceipt 'Сумма продажи' (receipt) = GROUP SUM sumReceiptSaleDetail(receiptDetail)
    BY receiptReceiptSaleDetail (receiptDetail) IN documentSumGroup PERSISTENT;

sumReceiptReturnDetailReceipt 'Сумма возврата' (receipt) = GROUP SUM sumReceiptReturnDetail(receiptDetail)
    BY receiptReceiptReturnDetail (receiptDetail) IN documentSumGroup PERSISTENT;

sumReceiptDetailReceipt 'Сумма чека' (receipt) = sumReceiptSaleDetailReceipt(receipt) (-) sumReceiptReturnDetailReceipt(receipt) PERSISTENT;
sumReceiptDetailReceiptDetail 'Сумма чека' (receiptDetail) = sumReceiptDetailReceipt(receiptReceiptDetail(receiptDetail));

discountSumSaleReceiptDetailSaleReceipt 'Сумма скидки (продажи)' (receipt) = GROUP SUM discountSumReceiptSaleDetail(receiptDetail)
    BY receiptReceiptSaleDetail (receiptDetail) IN documentSumGroup;
discountSumSaleReceipt 'Сумма скидки (продажи) по чеку' (receipt) = DATA NUMERIC[16,2] (receipt);

discountSumReturnReceiptDetailReturnReceipt 'Сумма скидки (возврат)' (receipt) = GROUP SUM discountSumReceiptReturnDetail(receiptDetail)
    BY receiptReceiptReturnDetail (receiptDetail) IN documentSumGroup;
discountSumReturnReceipt 'Сумма скидки (возврат) по чеку' (receipt) = DATA NUMERIC[16,2] (receipt);

discountSumReceiptDetailReceipt 'Сумма скидки' (receipt) = discountSumSaleReceiptDetailSaleReceipt(receipt) (-) discountSumReturnReceiptDetailReturnReceipt(receipt);
discountSumReceipt 'Сумма скидки по чеку' (receipt) = discountSumSaleReceipt(receipt) (-) discountSumReturnReceipt(receipt);

@defineDocumentHeaderCount(receipt);

@defineDocumentHeaderQuantity(receipt);
@defineDocumentHeaderSkuQuantity(receipt, sku);
@defineDocumentHeaderSkuQuantityCustom(receipt, receiptSaleDetail, sku);

// -------------------------------------------------- НДС ------------------------------------------------ //

VATRetailSkuDepartmentStoreDateTime = ABSTRACT range (sku, departmentStore, DATETIME);
currentVATRetailSkuDepartmentStore (sku, departmentStore) = VATRetailSkuDepartmentStoreDateTime(sku, departmentStore, currentDateTime());

VATReceiptSaleDetail = DATA range (receiptSaleDetail);
CONSTRAINT taxRange(VATReceiptSaleDetail(detail)) != tax.taxVAT OR
           countryRange(VATReceiptSaleDetail(detail)) != countryStock(departmentStoreReceiptSaleDetail(detail))
           CHECKED BY VATReceiptSaleDetail
           MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС и стране магазина';

VATSumSaleReceipt 'Сумма налогов по продажам' (receipt) = DATA DOUBLE (receipt);

VATReceiptReturnDetail = DATA range (receiptReturnDetail);
CONSTRAINT taxRange(VATReceiptReturnDetail(detail)) != tax.taxVAT OR
           countryRange(VATReceiptReturnDetail(detail)) != countryStock(departmentStoreReceiptReturnDetail(detail))
           CHECKED BY VATReceiptReturnDetail
           MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС и стране магазина';

VATSumReturnReceipt 'Сумма налогов по возвратам' (receipt) = DATA DOUBLE (receipt);

VATReceiptDetail (receiptDetail) = UNION EXCLUSIVE VATReceiptReturnDetail(receiptDetail), VATReceiptSaleDetail(receiptDetail) PERSISTENT;
numberVATReceiptDetail 'НДС, номер' (receiptDetail) = numberRange(VATReceiptDetail(receiptDetail));
valueVATReceiptDetail 'НДС, %' (receiptDetail) = valueRateRangeDate(VATReceiptDetail(receiptDetail), dateReceiptDetail(receiptDetail));

// используется только для приема реализации из внешних касс
numberReceiptDetail 'Номер позиции чека' = DATA INTEGER (receiptDetail);
zReportReceiptReceiptDetailToReceiptDetail (zReport, receipt, receiptDetail, cashRegister) = GROUP UNIQUE receiptDetail BY numberZReport(zReportReceiptDetail(receiptDetail)), numberReceipt(receiptReceiptDetail(receiptDetail)), numberReceiptDetail(receiptDetail), numberCashRegister(cashRegisterZReport(zReportReceiptDetail(receiptDetail))) WHERE receiptDetail IS receiptDetail;
//

descriptionReceipt 'Название документа' (receipt) = [FORMULA STRING[200] '\'Чек № \' || CAST($1 AS TEXT) || \' от \' || CAST($2 AS TEXT) '](
                                               numberReceipt(receipt), dateTimeReceipt(receipt));

descriptionReceiptSaleDetail (receiptDetail) = [FORMULA STRING[200] '\'Продажа № \' || CAST($1 AS TEXT) || \' от \' || CAST($2 AS TEXT) '] (
                                          numberReceipt(receiptReceiptSaleDetail(receiptDetail)), dateTimeReceipt(receiptReceiptSaleDetail(receiptDetail)));

descriptionReceiptReturnDetail (receiptDetail) = [FORMULA STRING[200] '\'Возврат № \' || CAST($1 AS TEXT) || \' от \' || CAST($2 AS TEXT) '] (
                                            numberReceipt(receiptReceiptReturnDetail(receiptDetail)), dateTimeReceipt(receiptReceiptReturnDetail(receiptDetail)));

descriptionReceiptDetail (receiptDetail) = descriptionReceiptReturnDetail(receiptDetail) OR descriptionReceiptSaleDetail(receiptDetail);

@defineAddDetailDialogSkuStockCustom(receipt, receiptSaleDetail, ' (продажа)', sku, departmentStore, dialogSku);
@defineAddDetailDialogSkuStockCustom(receipt, receiptReturnDetail, ' (возврат)', sku, departmentStore, dialogSku);

// -------------------------------------------- Проведение по регистрам --------------------------------------- //

META implementSkuLedgerReceiptDetail(dumb)
    @implementSkuLedger(receipt, sku, departmentStore);

    quantityOutFIFOSkuLedger (ledger) += quantityReceiptSaleDetail(ledger);
    @implementSkuLedgerOutFIFOBalance(receiptSale, sku, departmentStore);
    sumOutSkuLedger (ledger) += sumReceiptSaleDetail(ledger);

    quantityInLIFOSkuLedger (ledger) += quantityReceiptReturnDetail(ledger);
    @implementSkuLedgerInLIFOBalance(receiptReturn, sku, departmentStore);
    sumInSkuLedger (ledger) += sumReceiptReturnDetail(ledger);
END

@implementSalesLedger(receiptSale, sku, departmentStore);
quantitySalesLedger (ledger) += quantityReceiptSaleDetail(ledger);
VATSalesLedger (ledger) += VATReceiptSaleDetail(ledger);
sumSalesLedger (ledger) += sumReceiptSaleDetail(ledger);

// ----------------- Оплаты по чеку ------------------------------

CLASS payment 'Оплата по чеку';
TABLE payment (payment);

CLASS STATIC paymentMeans 'Форма оплаты'{
    paymentMeansCash 'Наличные',
    paymentMeansCard 'Карточка'
};
CLASS paymentType 'Тип платежа' : named;
TABLE paymentType (paymentType);

paymentMeansPaymentType (paymentType) = DATA paymentMeans (paymentType);
namePaymentMeansPaymentType 'Форма оплаты' (paymentType) = name(paymentMeansPaymentType(paymentType)) IN baseGroup;

sidPaymentType 'Идентификатор' = DATA STRING[10] (paymentType) IN baseGroup;
sidToTypePayment (tp) = GROUP UNIQUE paymentType BY sidPaymentType(paymentType) WHERE paymentType IS paymentType;

receiptPayment (payment) = DATA receipt (payment) NOT NULL DELETE;

countPaymentReceipt 'Кол-во типов платежей' (receipt) = GROUP SUM 1 IF receiptPayment(payment) == receipt BY receipt;

paymentTypePayment (payment) = DATA paymentType(payment);
namePaymentTypePayment 'Тип платежа' (payment) = name(paymentTypePayment(payment));

minCashPaymentType () = GROUP MIN paymentType IF paymentMeansPaymentType(paymentType) == paymentMeans.paymentMeansCash;
minCardPaymentType () = GROUP MIN paymentType IF paymentMeansPaymentType(paymentType) == paymentMeans.paymentMeansCard;

paymentMeansPayment (payment) = paymentMeansPaymentType(paymentTypePayment(payment));
namePaymentMeansPayment 'Форма оплаты' (payment) = name(paymentMeansPayment(payment));

numberPayment 'Номер платежа' (payment) = DATA INTEGER (payment);
zReportReceiptPaymentToPayment (zReport, receipt, number, cashRegister) = GROUP UNIQUE payment BY numberZReport(zReportReceipt(receiptPayment(payment))), numberReceipt(receiptPayment(payment)), numberPayment(payment), numberCashRegister(cashRegisterZReport(zReportReceipt(receiptPayment(payment)))) WHERE payment IS payment;

sumPayment 'Сумма платежа' (payment) = DATA NUMERIC[16,2] (payment);

sumPaymentReceipt (receipt) = GROUP SUM sumPayment(payment) BY receiptPayment(payment);

sumInCashPaymentReceipt (receipt) = GROUP SUM sumPayment(payment) AND paymentMeansPayment(payment) == paymentMeans.paymentMeansCash  BY receiptPayment(payment);

changePaymentReceipt 'Сдача' (receipt) = sumPaymentReceipt(receipt) - sumReceiptDetailReceipt(receipt);

sumCashPaymentReceipt (receipt) = sumInCashPaymentReceipt(receipt) - changePaymentReceipt(receipt);
sumNotCashPaymentReceipt (receipt) = GROUP SUM sumPayment(payment) AND NOT paymentMeansPayment(payment) == paymentMeans.paymentMeansCash BY receiptPayment(payment);

CONSTRAINT receipt IS receipt AND NOT sumPaymentReceipt(receipt) MESSAGE 'По чеку не указаны платежи';
CONSTRAINT sumReceiptDetailReceipt(receipt) > sumPaymentReceipt(receipt) MESSAGE 'Сумма платежей по чеку меньше суммы чека';
CONSTRAINT sumReceiptDetailReceipt(receipt) < sumNotCashPaymentReceipt(receipt) MESSAGE 'Сумма платежей по безналичному расчету больше суммы чека';

// Итоги по Z-отчету

@defineDocumentHeaderQuantityCustom(zReport, receiptDetail);

sumCashZReport 'Сумма наличных Z-отчета' (zReport) = GROUP SUM sumCashPaymentReceipt(receipt) BY zReportReceipt(receipt) PERSISTENT IN documentSumGroup;
sumCashIncomeCashOrder(zReport) += sumCashZReport(zReport);
sumReceiptDetailZReport 'Сумма Z-отчета' (zReport) = GROUP SUM sumReceiptDetailReceipt(receipt) BY zReportReceipt(receipt) PERSISTENT IN documentSumGroup;
discountSumReceiptDetailZReport 'Сумма скидок Z-отчета' (zReport) = GROUP SUM discountSumReceiptDetailReceipt(receipt) BY zReportReceipt(receipt) PERSISTENT IN documentSumGroup;
discountSumReceiptZReport 'Сумма скидок по чекам Z-отчета' (zReport) = GROUP SUM discountSumReceipt(receipt) BY zReportReceipt(receipt) PERSISTENT IN documentSumGroup;
discountSumZReport 'Сумма скидок Z-отчета' (zReport) = discountSumReceiptDetailZReport(zReport) (+) discountSumReceiptZReport(zReport);

sumCardDepartmentStoreDateFromTo 'Продано с использованием банк. карточек' (departmentStore, dateFrom, dateTo) = GROUP SUM sumPayment(payment) AND paymentMeansPayment(payment)==paymentMeans.paymentMeansCard
    AND dateReceipt(receiptPayment(payment)) >= (dateFrom AS DATE) AND dateReceipt(receiptPayment(payment)) <= (dateTo AS DATE) AND isPostedReceipt(receiptPayment(payment))
        BY departmentStoreReceipt(receiptPayment(payment)), dateFrom, dateTo;

sumSaleDepartmentStoreDateFromTo 'Продано по кассе' (departmentStore, dateFrom, dateTo) = GROUP SUM sumReceiptDetailReceipt(receipt)
    AND dateReceipt(receipt) >= (dateFrom AS DATE) AND dateReceipt(receipt) <= (dateTo AS DATE) AND isPostedReceipt(receipt)
        BY departmentStoreReceipt(receipt), dateFrom, dateTo;

// ----------------------------------- Проведение по товарному отчету -------------------------- //

META implementAccountDocumentLedgerZReport(dumb)
    @implementAccountDocumentLedgerZReportCustom(sumReceiptDetailZReport);
END

META implementAccountDocumentLedgerZReportCustom (sumProp)
    @implementAccountDocumentLedger(zReport, departmentStore);
    sumOutAccountDocumentLedger (ledger) += sumProp (ledger);
    sumItemOutAccountDocumentLedger (ledger) += sumProp (ledger);
    sumContainerOutAccountDocumentLedger (ledger) += 0.0 IF ledger IS zReport;
END
//----------------------------------- Формы -------------------------------------------------//

FORM zReport 'Z-отчет'
    OBJECTS z=zReport FIXED PANEL
    PROPERTIES (z) numberCashRegisterZReport, nameDepartmentStoreZReport READONLY, dateZReport, timeZReport, numberZReport,
                   basisZReport, countReceiptZReport, quantityReceiptDetailZReport, sumCashZReport, sumReceiptDetailZReport,
                   discountSumReceiptDetailZReport

    OBJECTS b=receipt, d=receiptDetail
    PROPERTIES(b) numberReceipt, dateReceipt, timeReceipt, nameUserReceipt,
                  numberDiscountCardReceipt, nameClientDiscountCardReceipt, sumReceiptDetailReceipt, discountSumReceiptDetailReceipt,
                  discountSumReceipt, countReceiptDetailReceipt, quantityReceiptDetailReceipt, ADDOBJ, delete
    FILTERS zReportReceipt(b)==z

    PROPERTIES(b) TODRAW d addDetailDialogSkuStockReceiptSaleDetailReceipt, addDetailDialogSkuStockReceiptReturnDetailReceipt
                           // todo : добавить операции возврата по штрих-коду

    PROPERTIES(d) typeReceiptDetail, idBarcodeReceiptDetail, nameSkuReceiptDetail, quantityReceiptDetail, quantityReturnedReceiptSaleDetail FORCE GRID, priceReceiptDetail,
                  sumReceiptDetail, discountPercentReceiptSaleDetail FORCE GRID, discountSumReceiptDetail, descriptionSaleReceiptReturnDetail, numberVATReceiptDetail, ADDOBJ, delete

    FILTERS receiptReceiptDetail(d)==b

    OBJECTS p=payment
    PROPERTIES(p) namePaymentTypePayment, namePaymentMeansPayment, sumPayment, ADDOBJ, delete

    FILTERS receiptPayment(p)==b

    EDIT zReport OBJECT z
;

DESIGN zReport FROM DEFAULT{

    NEW topContainer{

        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        NEW firstCase {

            type = SPLITV;

            ADD z.box;
            ADD b.box;
        }

        NEW secondCase{

            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD d.box{
                title = 'Строка чека';
                fillHorizontal = 3;
            }
            ADD p.box;
        }
    }

    ADD functions.box;
}

FORM zReports 'Z-отчеты'
    OBJECTS z=zReport
    PROPERTIES (z) READONLY objectClassName, numberCashRegisterZReport, nameDepartmentStoreZReport, dateZReport, timeZReport,
                            numberZReport, basisZReport, countReceiptZReport, quantityReceiptDetailZReport, sumCashZReport,
                            sumReceiptDetailZReport, discountSumReceiptDetailZReport
    PROPERTIES (z) ADDFORM, EDITFORM SHOWIF isDraftZReport(z), delete FORCE PANEL DRAWTOTOOLBAR SHOWIF isDraftZReport(z),
                            postZReport SHOWIF isDraftZReport(z), unpostZReport SHOWIF isPostedZReport(z)

    OBJECTS b=receipt
    PROPERTIES(b) READONLY  numberReceipt, dateReceipt, timeReceipt, nameUserReceipt,
                            numberDiscountCardReceipt, nameClientDiscountCardReceipt, sumReceiptDetailReceipt, discountSumReceiptDetailReceipt,
                            discountSumReceipt, countReceiptDetailReceipt, quantityReceiptDetailReceipt

    FILTERS zReportReceipt(b)==z
    ORDER BY                numberReceipt

    OBJECTS d=receiptDetail
    PROPERTIES(d) READONLY  typeReceiptDetail, idBarcodeReceiptDetail, nameSkuReceiptDetail, quantityReceiptDetail, quantityReturnedReceiptSaleDetail FORCE GRID, priceReceiptDetail,
                            costSumSkuLedger, sumReceiptDetail, discountPercentReceiptSaleDetail FORCE GRID, discountSumReceiptDetail, descriptionSaleReceiptReturnDetail

    FILTERS receiptReceiptDetail(d)==b

    OBJECTS p=payment
    PROPERTIES(p) READONLY  namePaymentTypePayment, namePaymentMeansPayment, sumPayment

    FILTERS receiptPayment(p)==b
;

DESIGN zReports FROM DEFAULT{

    NEW topContainer{

        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        NEW firstCase {

            type = SPLITV;

            ADD z.box;
            ADD b.box;
        }

        NEW secondCase{

            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD d.box{
                title = 'Строка чека';
                fillHorizontal = 3;
            }
            ADD p.box;
        }
    }

    PROPERTY(objectClassName) {
        preferredCharWidth = 15;
    }
    PROPERTY (delete(z)) {
        askConfirm = TRUE;
    }

    ADD functions.box;
}

// ----------------------------- Кассовый модуль ---------------- //

POSPriceSkuDepartmentStoreDateTime 'Цена в кассе' = ABSTRACT NUMERIC[14,2] (sku, departmentStore, DATETIME);
currentPOSPriceSkuDepartmentStore 'Текущая цена' (sku, departmentStore) = POSPriceSkuDepartmentStoreDateTime(sku, departmentStore, currentDateTime());

zReportCashRegister = DATA zReport (cashRegister);

currentCashRegister () = cashRegisterComputer(currentComputer());

GROUP receiptZReportGroup 'Z-отчет' : publicGroup;

currentZReport () = zReportCashRegister(currentCashRegister());
numberCurrentZReport 'Номер текущего Z-отчета' () = numberZReport(currentZReport()) IN receiptZReportGroup;

closedCurrentZReport () = TRUE AND NOT currentZReport();

// Операции с чеками

createCurrentReceipt 'Создать новый чек' = ACTION () {
    IF currentZReport() THEN {
        FOR ADDOBJ r = receipt DO {
            EXEC seek(r);
            SET zReportReceipt(r) <- currentZReport();
            SET numberReceipt(r) <- maxNumberReceiptZReport(currentZReport()) (+) 1;
        }
    }
}

// Вывод на дисплей

fiscalDisplayTextReceiptDetail 'Вывести текст на дисплей'(receiptDetail) = ABSTRACT ACTION (receiptDetail);
WHEN CHANGED (quantityReceiptDetail(receiptDetail)) OR CHANGED(sumReceiptDetail(receiptDetail)) DO EXEC fiscalDisplayTextReceiptDetail(receiptDetail) SESSION;

// Проведение чека

GROUP receiptActionGroup 'Оплата' : publicGroup;

postPrintReceipt 'Напечатать фискальный чек' = ABSTRACT ACTION (receipt);
noFiscalPostPrintReceipt 'Создать чек (без фискального регистратора)' = ACTION (receipt) {
    IF receipt IS receipt THEN {
        EXEC apply();
        EXEC createCurrentReceipt();
    }
}
postPrintReceipt(receipt) += noFiscalPostPrintReceipt(receipt) IF NOT sidCashRegisterModelReceipt(receipt);

FORM postReceiptPayment 'Оплата по чеку'
    OBJECTS r = receipt FIXED PANEL
    PROPERTIES(r) READONLY changePaymentReceipt

    OBJECTS p = payment FIXED GRID
    PROPERTIES(p) sumPayment, namePaymentTypePayment, ADDOBJ, delete
    FILTERS receiptPayment(p) == r
;

DESIGN postReceiptPayment FROM DEFAULT {

    main {
        preferredSize = (600, 250);
        NEW payContainer {
            childConstraints = TO THE RIGHT;
            ADD p.box {
                PROPERTY(sumPayment) { font = 'Tahoma bold 36'; }
                PROPERTY(namePaymentTypePayment) { font = 'Tahoma bold 36'; }
                PROPERTY(delete) { font = 'Tahoma bold 36'; }
            }
            NEW rightContainer {
                childConstraints = TO THE BOTTOM;
                ADD PROPERTY(changePaymentReceipt) { panelLabelAbove = TRUE; focusable = FALSE; font = 'Tahoma bold 72'; }
                ADD functions.box;
            }
        }
    }
}

postReceipt 'Смешанный платеж' = ACTION (receipt) {
    SET zReportReceipt(receipt) <- currentZReport();
    IF sumReceiptDetailReceipt(receipt) THEN {
        ADDOBJ payment;
        FOR p == addedObject() DO {
            SET receiptPayment(p) <- receipt;
            SET paymentTypePayment(p) <- minCashPaymentType();
        }
        ADDOBJ payment;
        FOR p == addedObject() DO {
            SET receiptPayment(p) <- receipt;
            SET paymentTypePayment(p) <- minCardPaymentType();
        }
        FORM postReceiptPayment OBJECTS r = receipt MODAL CHECK;
        IF formResult() == formResult.ok THEN {
            EXEC postPrintReceipt(receipt);
        } ELSE {
            FOR receiptPayment(p) == receipt DO {
                EXEC delete(p);
            }
        }
    }
} EDITKEY 'F9' IN receiptActionGroup;

postCardReceipt 'Карточкой' = ACTION (receipt) {
    SET zReportReceipt(receipt) <- currentZReport();
    IF sumReceiptDetailReceipt(receipt) THEN {
        ADDOBJ payment;
        FOR p == addedObject() DO {
            SET receiptPayment(p) <- receipt;
            SET paymentTypePayment(p) <- minCardPaymentType();
            SET sumPayment(p) <- sumReceiptDetailReceipt(receipt);
        }
        EXEC postPrintReceipt(receipt);
    }
} EDITKEY 'F10' CONFIRM IN receiptActionGroup;

postCashReceipt 'Наличными' = ACTION (receipt) {
    SET zReportReceipt(receipt) <- currentZReport();
    IF sumReceiptDetailReceipt(receipt) THEN {
        ADDOBJ payment;
        FOR p == addedObject() DO {
            SET receiptPayment(p) <- receipt;
            SET paymentTypePayment(p) <- minCashPaymentType();
            SET sumPayment(p) <- sumReceiptDetailReceipt(receipt);
        }
        FORM postReceiptPayment OBJECTS r = receipt MODAL CHECK;
        IF formResult() == formResult.ok THEN {
            EXEC postPrintReceipt(receipt);
        } ELSE {
            FOR receiptPayment(p) == receipt DO {
                EXEC delete(p);
            }
        }
    }
} EDITKEY 'F11' IN receiptActionGroup;

cancelReceipt 'Отменить чек' = ACTION (receipt) {
    IF sumReceiptDetailReceipt(receipt) THEN {
        EXEC cancel();
        EXEC createCurrentReceipt();
    }
} EDITKEY 'shift F11' CONFIRM IN receiptActionGroup;

// Операции по добавлению строк в чек

// Продажи
createReceiptSaleDetail 'Добавить строку продажи' = ACTION (receipt, sku, barcode) {
    ADDOBJ receiptSaleDetail;
    FOR d == addedObject() DO {
        SET receiptReceiptSaleDetail(d) <- receipt;
        SET idBarcodeReceiptDetail(d) <- barcode;
        SET skuReceiptSaleDetail(d) <- sku;
        SET quantityReceiptSaleDetail(d) <- 1.0;
        SET priceReceiptSaleDetail(d) <- POSPriceSkuDepartmentStoreDateTime(sku, departmentStoreReceipt(receipt), currentDateTime());
        SET VATReceiptSaleDetail(d) <- VATRetailSkuDepartmentStoreDateTime(sku, departmentStoreReceipt(receipt), currentDateTime());
    }
}

GROUP receiptSaleActionGroup 'Продажа' : publicGroup;

// По штрих-коду
changeBarcodeSaleReceipt = ACTION (receipt) {
    REQUEST STRING[14] INPUT;
    LOCAL dialogBarcodeSku = sku();
    SET dialogBarcodeSku() <- skuBarcodeIdDate(requestedString(), currentDate());

    IF dialogBarcodeSku() IS sku THEN {
        EXEC createReceiptSaleDetail(receipt, dialogBarcodeSku(), requestedString());
    } ELSE
        MESSAGE 'Не найден штрих-код';
};

// Поиск подбором
FORM searchSkuSaleReceipt 'Поиск SKU'
    OBJECTS st = departmentStore

    TREE skuTree sk = skuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = nameSkuGroupStock(sk, st)
    ORDER BY skuTreeName

    OBJECTS           s=sku
    PROPERTIES        READONLY nameSkuStock(s, st), idBarcodeSku(s)
    FILTERS           isParentSkuGroupSku(sk, s)
    ORDER BY          nameSkuStock

    PROPERTIES(s, st) READONLY currentBalanceSkuStock HINTNOUPDATE, currentPOSPriceSkuDepartmentStore

    FILTERGROUP filters
        FILTER 'Товары с остатком' 'F10' currentBalanceSkuStock(s, st) DEFAULT

;

DESIGN searchSkuSaleReceipt FROM DEFAULT {
    main{
        preferredSize = (1024, 768);
        NEW topContainer BEFORE functions.box {
            type = SPLITH;
            ADD skuTree.tree.box { title = 'Группы'; }
            ADD s.box {
                fillHorizontal = 2;
                s.grid {
                    defaultComponent = TRUE;
                }
            }
        }
    }
}

searchSkuSaleReceipt 'Поиск' = ACTION (receipt) {
    SET zReportReceipt(receipt) <- currentZReport();
    FORM searchSkuSaleReceipt OBJECTS st = departmentStoreReceipt(receipt) MODAL;
    IF formResult() == formResult.ok THEN {
        EXEC createReceiptSaleDetail(receipt, chosenObject('s'), idBarcodeSku(chosenObject('s')));
    }
} EDITKEY 'F3' IN receiptSaleActionGroup;

// Возвраты

createReceiptReturnDetail 'Добавить строку возврата' = ACTION (receipt, receiptSaleDetail, barcode) {
    ADDOBJ receiptReturnDetail;
    FOR d == addedObject() DO {
        SET receiptReceiptReturnDetail(d) <- receipt;
        SET idBarcodeReceiptDetail(d) <- barcode;
        SET receiptSaleDetailReceiptReturnDetail(d) <- receiptSaleDetail;
        SET skuReceiptReturnDetail(d) <- skuReceiptSaleDetail(receiptSaleDetail);
        SET quantityReceiptReturnDetail(d) <- 1.0;
        SET priceReceiptReturnDetail(d) <- priceReceiptSaleDetail(receiptSaleDetail);
        SET VATReceiptReturnDetail(d) <- VATReceiptSaleDetail(receiptSaleDetail);
    }
}

GROUP receiptReturnActionGroup 'Возврат' : publicGroup;

// По штрих-коду
FORM scanBarcodeReturnReceipt 'Выбор строки возврата'
    OBJECTS d = departmentStore FIXED PANEL

    OBJECTS s = sku FIXED PANEL
    PROPERTIES idBarcodeSku(s) READONLY, nameSkuStock(s, d)

    OBJECTS r = receiptSaleDetail FIXED GRID
    PROPERTIES(r) READONLY numberReceiptReceiptDetail, dateTimeReceiptSaleDetail, numberDiscountCardReceiptDetail, nameClientDiscountCardReceiptDetail, sumReceiptDetailReceiptDetail,
                           indexReceiptDetail, quantityReceiptDetail, quantityReturnedReceiptSaleDetail, priceReceiptDetail,
                           sumReceiptDetail, discountPercentReceiptSaleDetail FORCE GRID, discountSumReceiptDetail
    FILTERS departmentStoreReceiptSaleDetail(r) == d,
            skuReceiptSaleDetail(r) == s
;

DESIGN scanBarcodeReturnReceipt FROM DEFAULT {
    PROPERTY(idBarcodeSku) {
        focusable = FALSE;
    }
    PROPERTY(nameSkuStock) {
        focusable = FALSE;
    }
}

scanBarcodeReturnReceipt 'Ввести штрих-код' = ACTION (receipt) {
    SET zReportReceipt(receipt) <- currentZReport();
    FORM barcodeInput MODAL;
    IF formResult() == formResult.ok THEN {
        LOCAL dialogBarcodeSku = sku();
        SET dialogBarcodeSku() <- skuBarcodeIdDate(chosenString('barcode'), currentDate());

        IF dialogBarcodeSku() IS sku THEN {
            FORM scanBarcodeReturnReceipt OBJECTS d = departmentStoreReceipt(receipt), s = dialogBarcodeSku() MODAL;
            IF formResult() == formResult.ok THEN {
                EXEC createReceiptReturnDetail(receipt, chosenObject('r'), chosenString('barcode'));
            }
        } ELSE
            MESSAGE 'Не найден штрих-код';
    }
} EDITKEY 'F7' IN receiptReturnActionGroup;

// Поиск

FORM searchSkuReturnReceipt 'Поиск строки возврата'
    OBJECTS st = departmentStore FIXED PANEL

    OBJECTS s = receipt FIXED GRID
    PROPERTIES(s) READONLY  numberReceipt, dateReceipt, timeReceipt,
                            numberDiscountCardReceipt, nameClientDiscountCardReceipt, sumReceiptDetailReceipt, discountSumReceiptDetailReceipt,
                            countReceiptDetailReceipt, quantityReceiptDetailReceipt
    FILTERS departmentStoreReceipt(s) == st

    OBJECTS r = receiptSaleDetail FIXED GRID
    PROPERTIES(r) READONLY  idBarcodeReceiptDetail, nameSkuReceiptDetail, quantityReceiptDetail, quantityReturnedReceiptSaleDetail, priceReceiptDetail,
                            sumReceiptDetail, discountPercentReceiptSaleDetail, discountSumReceiptDetail
    FILTERS receiptReceiptSaleDetail(r) == s
;

searchSkuReturnReceipt 'Поиск' = ACTION (receipt) {
    SET zReportReceipt(receipt) <- currentZReport();
    FORM searchSkuReturnReceipt OBJECTS st = departmentStoreReceipt(receipt) MODAL;
    IF formResult() == formResult.ok THEN {
        EXEC createReceiptReturnDetail(receipt, chosenObject('r'), idBarcodeSku(skuReceiptSaleDetail(chosenObject('r'))));
    }
} EDITKEY 'F6' IN receiptReturnActionGroup;

// Дисконтные карты

FORM inputDiscountCardNumberReceipt 'Ввод номера дисконтной карты'
    OBJECTS (n = STRING[18], s = STRING[2]) FIXED PANEL
    PROPERTIES number = OBJVALUE(n), series = OBJVALUE(s)
;

DESIGN inputDiscountCardNumberReceipt FROM DEFAULT{
    NEW centerContainer BEFORE functions.box {
        childConstraints = TO THE RIGHT;
        ADD PROPERTY(number) { caption = 'Номер'; font = 'Tahoma bold 72'; }
        ADD PROPERTY(series) { minimumCharWidth = 3; caption = 'Серия'; font = 'Tahoma bold 72'; }
    }
}

inputDiscountCardNumberReceipt 'Ввести номер' = ACTION (receipt) {
    FORM inputDiscountCardNumberReceipt MODAL;
    IF formResult() == formResult.ok THEN {
        IF TRUE AND NOT chosenString('s') == '' THEN {
            IF discountCardSeriesNumber(ustring2(chosenString('s'),chosenString('n'))) THEN
                SET discountCardReceipt(receipt) <- discountCardSeriesNumber(ustring2(chosenString('s'),chosenString('n')))
            ELSE
                MESSAGE 'Дисконтная карта с такой серией и номером не найдена';
        } ELSE {
            IF discountCardNumber(chosenString('n')) THEN
                SET discountCardReceipt(receipt) <- discountCardNumber(chosenString('n'))
            ELSE
                MESSAGE 'Дисконтная карта с таким номером не найдена';
        }
    }
} IN receiptDiscountGroup EDITKEY 'F5';

clearDiscountCardReceipt 'Сбросить' = ACTION (receipt) {
    SET discountCardReceipt(receipt) <- NULL;
} IN receiptDiscountGroup EDITKEY 'shift F5';

// Операции с Z-отчетами

openCurrentZReport 'Открыть Z-отчет' = ACTION () {
    ADDOBJ zReportPosted;
    FOR z == addedObject() DO {
        SET numberZReport(z) <- castToString255(z);
        SET cashRegisterZReport(z) <- currentCashRegister();
        SET departmentStoreZReport(z) <- departmentStoreCashRegister(currentCashRegister()); // todo : приходится ставить из-за того, что на ADDOBJ срабатывает resolveAdd
        SET zReportCashRegister(cashRegister) IF cashRegister == currentCashRegister() <- z AS zReport;
        EXEC apply();
    }
} CONFIRM IN receiptZReportGroup;

closeCurrentZReport 'Закрыть Z-отчет' = ACTION () {
    EXEC cancel();
    SET zReportCashRegister(cashRegister) IF cashRegister == currentCashRegister() <- NULL;
    EXEC apply();
} CONFIRM IN receiptZReportGroup;

// --------------------------------- Применение акций ----------------------------- //

// Накопленные суммы

posSumDiscountCard 'Сумма продаж' (discountCard) = GROUP SUM sumReceiptDetailReceipt (receipt) BY discountCardReceipt(receipt) PERSISTENT;
cumulativeSumDiscountCard 'Накопленная сумма' (discountCard) = initialSumDiscountCard(discountCard) (+) posSumDiscountCard(discountCard) PERSISTENT;

prevCumulativeSumReceipt (receipt) = (0 AND receipt IS receipt) OR [PREV(cumulativeSumDiscountCard(discountCard))](discountCardReceipt(receipt));

EXTEND FORM discountCards
    PROPERTIES(d) posSumDiscountCard, cumulativeSumDiscountCard
;

// Расчет сумм скидок

TABLE receiptSaleDetailPromotionCondition(receiptSaleDetail, promotionCondition);
quantityReceiptSaleDetailPromotionCondition 'Кол-во по акции' = DATA NUMERIC[14,3] (receiptSaleDetail, promotionCondition) INDEXED;
promotionSumReceiptSaleDetailPromotionCondition 'Сумма скидки' = DATA NUMERIC[14,3] (receiptSaleDetail, promotionCondition);

promotionSumReceiptSaleDetail (detail) = GROUP SUM promotionSumReceiptSaleDetailPromotionCondition(detail, condition)
                                               BY detail;

inReceiptPromotion (receipt, promotion) = inPromotionStore(promotion, storeReceipt(receipt)) AND
                                          dateReceipt(receipt) >= dateFromPromotion(promotion) AND dateReceipt(receipt) <= dateToPromotion(promotion) AND
                                          NOT timeReceipt(receipt) < timeOfFromPromotion(promotion) AND NOT timeReceipt(receipt) > timeOfToPromotion(promotion) AND
                                          NOT sumReceiptDetailReceipt(receipt) < minSumBillPromotion(promotion) AND NOT sumReceiptDetailReceipt(receipt) > maxSumBillPromotion(promotion)
                                          AND NOT skipPromotionDOW(promotion, DOWInDate(dateReceipt(receipt)))
                                          AND NOT prevCumulativeSumReceipt(receipt) < minCumulativeSumPromotion(promotion) AND NOT prevCumulativeSumReceipt(receipt) > maxCumulativeSumPromotion(promotion)
                                          AND NOT (hasDiscountCardPromotion(promotion) AND NOT inPromotionDiscountCard(promotion, discountCardReceipt(receipt)));

maxSalePriceReceiptSku (receipt, sku) = GROUP MAX priceReceiptSaleDetail(detail) BY receiptReceiptSaleDetail(detail), skuReceiptSaleDetail(detail);

calculatePromotionsReceipt 'Рассчитать акции' = ACTION (receipt) {

    SET quantityReceiptSaleDetailPromotionCondition(detail, condition) IF receiptReceiptSaleDetail(detail) == receipt <- NULL;
    SET promotionSumReceiptSaleDetailPromotionCondition(detail, condition) IF receiptReceiptSaleDetail(detail) == receipt <- NULL;
    SET discountSumReceiptSaleDetail(detail) IF receiptReceiptSaleDetail(detail) == receipt <- NULL;

    LOCAL maxSalePriceSku = NUMERIC[14,2] (sku);
    SET maxSalePriceSku(sku) <- maxSalePriceReceiptSku(receipt, sku);

    FOR promotionGroup IS promotionGroup DO {
        LOCAL leftSku = NUMERIC[14,3] (sku);
        SET leftSku(sku) <- quantityReceiptSaleDetailSkuReceipt (sku, receipt);

        LOCAL leftQuantityReceiptSaleDetail = NUMERIC[14,3] (receiptSaleDetail);
        SET leftQuantityReceiptSaleDetail(detail) <- quantityReceiptSaleDetail(detail) IF receiptReceiptSaleDetail(detail) == receipt;

        LOCAL leftSumReceiptSaleDetail = NUMERIC[16,3] (receiptSaleDetail);
        SET leftSumReceiptSaleDetail(detail) <- (priceSumReceiptSaleDetail(detail) (-) promotionSumReceiptSaleDetail(detail)) IF receiptReceiptSaleDetail(detail) == receipt;

        FOR promotionGroupPromotion(promotion) == promotionGroup AND inReceiptPromotion(receipt, promotion) ORDER orderPromotion(promotion) DO {
            LOCAL quantityPromotionCondition = NUMERIC[14,3] (promotionCondition);
            SET quantityPromotionCondition(promotionCondition) <- [GROUP SUM leftSku (sku)
                                                                             IF inPromotionConditionSku(promotionCondition, sku)
                                                                             BY promotionCondition](promotionCondition) AND
                                                                                promotionPromotionCondition(promotionCondition) == promotion;

            LOCAL sumPromotionCondition = NUMERIC[14,3] (promotionCondition);
            SET sumPromotionCondition(promotionCondition) <- [GROUP SUM leftSku (sku) * maxSalePriceSku(sku)
                                                                    IF inPromotionConditionSku(promotionCondition, sku)
                                                                    BY promotionCondition](promotionCondition) AND
                                                                       promotionPromotionCondition(promotionCondition) == promotion;

            LOCAL countPromotionCondition = INTEGER (promotionCondition);
            SET countPromotionCondition(promotionCondition) <- -(UNION MAX -divideInteger(quantityPromotionCondition (promotionCondition), minQuantityPromotionCondition (promotionCondition)),
                                                                           -divideInteger(sumPromotionCondition(promotionCondition), minSumPromotionCondition(promotionCondition)));

            LOCAL countPromotion = INTEGER ();
            SET countPromotion () <- [GROUP MIN countPromotionCondition(promotionCondition)]();

            // Если все условия сработали хотя бы один раз
            IF countPromotion() > 0 AND [GROUP SUM 1 IF countPromotionCondition(promotionCondition) > 0]() == countPromotionConditionPromotion(promotion) THEN {

//                MESSAGE name(promotion);
//                MESSAGE castToString255(cumulativeSumReceipt(receipt));
//                MESSAGE castToString255(minCumulativeSumPromotion(promotion));
//                MESSAGE castToString255(cumulativeSumReceipt(receipt) < minCumulativeSumPromotion(promotion));
//
                LOCAL takenPromotionCondition = NUMERIC[14,3] (promotionCondition);
                SET takenPromotionCondition(promotionCondition) <- min(countPromotion() * quantityDiscountPromotionCondition(promotionCondition), quantityPromotionCondition(promotionCondition));

                LOCAL takenSkuPromotionCondition = NUMERIC[14,3] (sku, promotionCondition);
                SET takenSkuPromotionCondition(sku, promotionCondition) <- [PARTITION UNGROUP takenPromotionCondition
                                                                                      LIMIT leftSku (sku)
                                                                                            IF inPromotionConditionSku(promotionCondition, sku)
                                                                                      BY promotionCondition
                                                                                      ORDER maxSalePriceSku(sku), sku] (sku, promotionCondition);

                LOCAL takenSku = NUMERIC[14,3] (sku);
                SET takenSku(sku) <- [GROUP SUM takenSkuPromotionCondition(sku, promotionCondition) BY sku] (sku);

                SET leftSku(sku) <- leftSku(sku) (-) takenSku(sku);

                LOCAL takenReceiptSaleDetailPromotionCondition = NUMERIC[14,3] (receiptSaleDetail, promotionCondition);
                SET takenReceiptSaleDetailPromotionCondition (detail, condition) <- [PARTITION UNGROUP takenSkuPromotionCondition
                                                                                               LIMIT leftQuantityReceiptSaleDetail(detail)
                                                                                               BY skuReceiptSaleDetail(detail), promotionCondition
                                                                                               ORDER DESC detail](detail, condition);

                LOCAL takenReceiptSaleDetail = NUMERIC[14,3] (receiptSaleDetail);
                SET takenReceiptSaleDetail(detail) <- [GROUP SUM takenReceiptSaleDetailPromotionCondition(detail, condition) BY detail](detail);

                SET leftQuantityReceiptSaleDetail(detail) <- leftQuantityReceiptSaleDetail(detail) (-) takenReceiptSaleDetail(detail);

                SET quantityReceiptSaleDetailPromotionCondition(detail, condition) <- takenReceiptSaleDetailPromotionCondition(detail, condition)
                    WHERE takenReceiptSaleDetailPromotionCondition(detail, condition);

                SET promotionSumReceiptSaleDetailPromotionCondition (detail, condition) <-
                    IF resultPricePromotionCondition(condition) THEN
                        takenReceiptSaleDetailPromotionCondition(detail, condition) * (leftSumReceiptSaleDetail(detail) / quantityReceiptSaleDetail(detail) - resultPricePromotionCondition(condition))
                    ELSE
                        takenReceiptSaleDetailPromotionCondition(detail, condition) * (leftSumReceiptSaleDetail(detail) / quantityReceiptSaleDetail(detail) * percentPromotionCondition(condition) / 100)
                    WHERE takenReceiptSaleDetailPromotionCondition(detail, condition);
            }
        }
    }

    SET discountSumReceiptSaleDetail(detail) IF receiptReceiptSaleDetail(detail) == receipt <- promotionSumReceiptSaleDetail(detail);
}

WHEN CHANGED(quantityReceiptDetailReceipt(receipt)) OR CHANGED(discountCardReceipt(receipt)) DO EXEC calculatePromotionsReceipt(receipt) SESSION;

// Форма POS-терминала

prevCurrentBalanceSkuReceipt (sku, receipt) = [PREV(currentBalanceSkuStock(sku, stock))] (sku, departmentStoreReceipt(receipt));
prevCurrentBalanceReceiptDetail 'Тек. остаток' (receiptDetail) = prevCurrentBalanceSkuReceipt(skuReceiptDetail(receiptDetail), receiptReceiptDetail(receiptDetail));

closedCurrentZReportMessage '' = 'Не открыт Z-отчет';

FORM POS
    PROPERTIES() READONLY hostnameCurrentComputer, currentDateTime, currentUserName

    PROPERTIES() READONLY SHOWIF closedCurrentZReport() closedCurrentZReportMessage

    OBJECTS r = receipt FIXED PANEL
    PROPERTIES(r) SHOWIF currentZReport() discountSumReceiptDetailReceipt, sumReceiptDetailReceipt,
                                          searchSkuSaleReceipt,
                                          searchSkuReturnReceipt, scanBarcodeReturnReceipt,
                                          postReceipt, postCardReceipt, postCashReceipt, cancelReceipt,
                                          numberDiscountCardReceipt, nameClientDiscountCardReceipt,
                                          inputDiscountCardNumberReceipt, clearDiscountCardReceipt

    OBJECTS d = receiptDetail FIXED GRID
    PROPERTIES(d) SHOWIF currentZReport() READONLY indexReceiptDetail, typeReceiptDetail, idBarcodeReceiptDetail, nameSkuReceiptDetail, prevCurrentBalanceReceiptDetail
    PROPERTIES(d) SHOWIF currentZReport()          quantityReceiptDetail, priceReceiptDetail, sumReceiptDetail, discountPercentReceiptSaleDetail FORCE GRID, discountSumReceiptDetail,
                                                   numberVATReceiptDetail, valueVATReceiptDetail, delete
    FILTERS receiptReceiptDetail(d) == r

    OBJECTS bs = STRING[14] FIXED PANEL
    PROPERTIES(bs) SHOWIF currentZReport() barcodeSale = OBJVALUE ON CHANGE EXEC changeBarcodeSaleReceipt(r)

    OBJECTS pc = promotionCondition
    PROPERTIES(pc) SHOWIF currentZReport() READONLY namePromotionPromotionCondition, name, dateFromPromotionCondition, dateToPromotionCondition, percentPromotionCondition
    PROPERTIES(d, pc) SHOWIF currentZReport()       quantityReceiptSaleDetailPromotionCondition, promotionSumReceiptSaleDetailPromotionCondition
    FILTERS quantityReceiptSaleDetailPromotionCondition(d, pc)

    PROPERTIES() fiscalAdvancePaper, fiscalCutReceipt

    EVENTS
        ON INIT EXEC createCurrentReceipt()
;

DESIGN POS FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;

        REMOVE functions.box CASCADE;

        NEW northContainer {
            ADD PROPERTY(closedCurrentZReportMessage()) {
                focusable = FALSE;
                font = 'Tahoma 100';
                fillVertical = 1.0;
            }
        }
        NEW centerContainer {
            childConstraints = TO THE RIGHT;
            NEW receiptContainer {
                type = SPLITV;
                childConstraints = TO THE BOTTOM;
                ADD d.box {
                    fillVertical = 3;
                    PROPERTY(indexReceiptDetail) { focusable = FALSE; font = 'Tahoma 14'; }
                    PROPERTY(typeReceiptDetail) { focusable = FALSE; font = 'Tahoma 14'; }
                    PROPERTY(idBarcodeReceiptDetail) { focusable = FALSE; font = 'Tahoma 14'; }
                    PROPERTY(nameSkuReceiptDetail) { focusable = FALSE; font = 'Tahoma 14'; }
                    PROPERTY(prevCurrentBalanceReceiptDetail) { focusable = FALSE; font = 'Tahoma 14'; }
                    PROPERTY(quantityReceiptDetail) { font = 'Tahoma 14'; }
                    PROPERTY(priceReceiptDetail) { font = 'Tahoma 14'; }
                    PROPERTY(sumReceiptDetail) { font = 'Tahoma 14'; }
                    PROPERTY(discountPercentReceiptSaleDetail) { font = 'Tahoma 14'; }
                    PROPERTY(discountSumReceiptDetail) { font = 'Tahoma 14'; }
                    PROPERTY(delete) { font = 'Tahoma 14'; }
                }
                ADD pc.box {
                    fillVertical = 0.3;
                }
            }
            NEW eastContainer {
                childConstraints = TO THE BOTTOM;
                ADD r.receiptSaleActionGroup {
                    childConstraints = TO THE BOTTOM;
                    PROPERTY(searchSkuSaleReceipt) { focusable = FALSE; font = 'Tahoma bold 24'; }
                    ADD PROPERTY(barcodeSale) { caption = 'Ввести штрих-код'; panelLabelAbove = TRUE; focusable = FALSE; font = 'Tahoma bold 24'; editKey = 'F4'; showEditKey = TRUE; }
                }
                ADD r.receiptReturnActionGroup {
                    childConstraints = TO THE BOTTOM;
                    PROPERTY(searchSkuReturnReceipt) { focusable = FALSE; font = 'Tahoma bold 24'; }
                    PROPERTY(scanBarcodeReturnReceipt) { focusable = FALSE; font = 'Tahoma bold 24'; }
                }
                ADD r.receiptActionGroup {
                    childConstraints = TO THE BOTTOM;
                    PROPERTY(postReceipt) { focusable = FALSE; font = 'Tahoma bold 24'; }
                    PROPERTY(postCardReceipt) { focusable = FALSE; font = 'Tahoma bold 24'; }
                    PROPERTY(postCashReceipt) { focusable = FALSE; font = 'Tahoma bold 24'; }
                    PROPERTY(cancelReceipt) { focusable = FALSE; font = 'Tahoma bold 24'; }
                }
                ADD r.receiptDiscountGroup {
                    childConstraints = TO THE BOTTOM;
                    PROPERTY(numberDiscountCardReceipt) { panelLabelAbove = TRUE; focusable = FALSE; font = 'Tahoma bold 24'; }
                    PROPERTY(nameClientDiscountCardReceipt) { panelLabelAbove = TRUE; focusable = FALSE; font = 'Tahoma bold 24'; }
                    NEW buttonDiscountContainer {
                        childConstraints = TO THE RIGHT;
                        ADD PROPERTY(inputDiscountCardNumberReceipt) { focusable = FALSE; font = 'Tahoma bold 14'; }
                        ADD PROPERTY(clearDiscountCardReceipt) { focusable = FALSE; font = 'Tahoma bold 14'; }
                    }
                }
                NEW sumContainer {
                    title = 'Итого';
                    childConstraints = TO THE BOTTOM;
                    ADD PROPERTY(discountSumReceiptDetailReceipt) { panelLabelAbove = TRUE; focusable = FALSE; font = 'Tahoma bold 72'; }
                    ADD PROPERTY(sumReceiptDetailReceipt) { panelLabelAbove = TRUE; focusable = FALSE; font = 'Tahoma bold 72'; }
                }
            }
        }

        NEW southContainer {
            childConstraints = TO THE RIGHT;
            NEW currentContainer {
                title = 'Текущие значения';
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(formRefreshAction) { focusable = FALSE; }
                ADD PROPERTY(hostnameCurrentComputer) { focusable = FALSE; }
                ADD PROPERTY(currentDateTime) { focusable = FALSE; }
                ADD PROPERTY(currentUserName) { focusable = FALSE; }
            }
            NEW actionsContainer {
                title = 'Действия с кассой';
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(fiscalAdvancePaper) { focusable = FALSE; }
                ADD PROPERTY(fiscalCutReceipt) { focusable = FALSE; }
            }
        }
    }
}

NAVIGATOR {
    NEW POSNavigator 'Касса' {
        ADD POS;
        ADD zReports;
        ADD operationCashRegister;
        ADD groupsCashRegister;
        ADD cashRegistersModels;
    }
}

// ----------------------------------------- Кассовые операции ---------------------------------------------- //
fiscalZReport 'Закрыть Z-отчёт' = ABSTRACT ACTION() CONFIRM;
fiscalZReport() += closeCurrentZReport() IF NOT sidModelCurrentCashRegister();

currentZReportDepartmentStore() = currentZReport() AND departmentStoreCurrentCashRegister();
closedCurrentZReportDepartmentStore() = closedCurrentZReport() AND departmentStoreCurrentCashRegister();

EXTEND FORM operationCashRegister
    PROPERTIES() READONLY numberCurrentZReport SHOWIF departmentStoreCurrentCashRegister()
    PROPERTIES() fiscalZReport SHOWIF currentZReportDepartmentStore()
    PROPERTIES() openCurrentZReport SHOWIF closedCurrentZReportDepartmentStore()
;

EXTEND DESIGN operationCashRegister{
    actions {
        ADD PROPERTY(fiscalZReport) { font = 'Tahoma 24'; }
        ADD PROPERTY(openCurrentZReport) { font = 'Tahoma 24'; };
        ADD PROPERTY(numberCurrentZReport) { font = 'Tahoma 24'; };
    }
}

// ----------------------------------------------- Стандартные значения ------------------------------------- //

loadDefaultPaymentType 'Добавить тип оплаты' = ACTION(string, paymentMeans) {
    ADDOBJ paymentType;
    FOR p == addedObject() DO {
        SET name(p) <- string AS STRING[110];
        SET paymentMeansPaymentType(p) <- paymentMeans AS paymentMeans;
    }
}

loadDefaultPaymentTypes 'Загрузить стандартные типы оплаты' = ACTION(){
    EXEC loadDefaultPaymentType('Наличные', paymentMeans.paymentMeansCash);
    EXEC loadDefaultPaymentType('Visa', paymentMeans.paymentMeansCard);
    EXEC loadDefaultPaymentType('Mastercard', paymentMeans.paymentMeansCard);
} IN loadDefaultGroup;

@implementLoadDefaultData(loadDefaultPaymentTypes);

