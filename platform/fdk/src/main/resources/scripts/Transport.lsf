MODULE Transport;

REQUIRE System,
        Utils,
        Employee,
        LegalEntity,
        Stock;

// Модели машины

CLASS mark 'Модель': named;

FORM mark 'Модель машины'
    OBJECTS m=mark FIXED PANEL
    PROPERTIES (m) name

    EDIT mark OBJECT m
;

FORM marks 'Модели машин'
    OBJECTS m=mark
    PROPERTIES(m) READONLY name
    PROPERTIES(m) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    DIALOG mark OBJECT m
;

// Машины

CLASS truck 'Машина';

sidTruck 'Номер' = DATA STRING[20](truck);
markTruck = DATA mark(truck);
nameMarkTruck 'Марка' (truck) = name(markTruck(truck));

nameTruck 'Наименование' (truck) = [FORMULA STRING[70] ' CAST($1 AS TEXT) || \' \' || CAST($2 AS TEXT)'](nameMarkTruck(truck), sidTruck(truck));

trailerTruck 'Прицеп' = DATA STRING[100](truck);
weightTruck 'Грузоподьёмность (тонн)' = DATA NUMERIC[14,2](truck);
ownerTruck 'Владелец' = DATA STRING[100](truck);

FORM truck 'Машина'
    OBJECTS t=truck FIXED PANEL
    PROPERTIES (t) sidTruck, nameMarkTruck, nameTruck, trailerTruck, ownerTruck, weightTruck

    EDIT truck OBJECT t
;

FORM trucks 'Машины'
    OBJECTS t=truck
    PROPERTIES(t) READONLY sidTruck, nameMarkTruck, nameTruck, trailerTruck, ownerTruck, weightTruck
    PROPERTIES(t) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    DIALOG truck OBJECT t
;

// Водители

CLASS driver 'Водитель': employee;

sidDriver 'Табельный номер' = DATA STRING[10](driver);
typeDriver 'Класс' = DATA STRING[10](driver);

FORM driver 'Водитель'
    OBJECTS d=driver FIXED PANEL
    PROPERTIES (d) name, userFirstName, userLastName, commonName, sidDriver, typeDriver

    EDIT driver OBJECT d
;

FORM drivers 'Водители'
    OBJECTS d=driver
    PROPERTIES(d) READONLY name, userFirstName, userLastName, commonName, sidDriver, typeDriver
    PROPERTIES(d) ADDFORM, EDITFORM, delete FORCE PANEL DRAWTOTOOLBAR

    DIALOG driver OBJECT d
;

NAVIGATOR {
    NEW transportNavigator 'Управление транспортом' {
        NEW transportDocuments 'Документы';
        NEW transportCatalog 'Справочники' {
            ADD drivers;
            ADD trucks;
            ADD marks;
        }
    }
}