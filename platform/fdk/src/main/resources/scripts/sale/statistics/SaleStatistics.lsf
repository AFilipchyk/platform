MODULE SaleStatistics;

REQUIRE System, Utils, Historizable, Stock, LegalEntity, SaleLedger, Sale, Integration;

NAMESPACE Sale;

// ----------------------------------- Статистический классификатор ------------------------------------------ //
CLASS StatisticGroupType 'Тип классификатора статистических групп' : Named;

idStatisticGroupType 'Идентификатор' = DATA STRING[20] (StatisticGroupType) MINCHARWIDTH 3 MAXCHARWIDTH 10 PREFCHARWIDTH 7;

typeIdStatisticGroupType (string1) = GROUP UNIQUE type
    BY idStatisticGroupType(type) WHERE type IS StatisticGroupType;

FORM statisticGroupType 'Тип классификатора статистических групп'
    OBJECTS t = StatisticGroupType FIXED PANEL
    PROPERTIES(t) name, idStatisticGroupType
    EDIT StatisticGroupType OBJECT t
;

FORM dialogStatisticGroupType 'Тип классификатора статистических групп'
    OBJECTS t = StatisticGroupType
    PROPERTIES(t) READONLY name, idStatisticGroupType
    DIALOG StatisticGroupType OBJECT t
;

FORM statisticGroupTypes 'Типы классификаторов статистических групп'
    OBJECTS t = StatisticGroupType
    PROPERTIES(t) READONLY name, idStatisticGroupType
    PROPERTIES(t) ADDFORM, EDITFORM, delete
;

CLASS StatisticGroup 'Статистическая группа' : Named, Externalizable;
TABLE statisticGroup(StatisticGroup);

CLASS GroupStatic 'Ед. изм.' {
    statisticLiter 'Литр',
    statisticThing 'Штука',
    statisticPounds 'Килограмм',
    statisticSum 'Валюта',
    statisticLinearMeters 'Пог. м',
    statisticPair 'Пара',
    statisticConditionalPiece 'Усл. кус.',
    statisticSquareMeters 'Квадратный м',
    statisticCubicMeters 'Кубический м'
} : Named;

TABLE statisticGroupStatisticGroup(StatisticGroup, StatisticGroup);
@defineHierarchy(statisticGroup);

groupTypeStatisticGroup = DATA StatisticGroupType (StatisticGroup) AUTOSET;
nameGroupTypeStatisticGroup 'Тип классификатора' (group) = name(groupTypeStatisticGroup(group));

countryStatisticGroupType = DATA Country (StatisticGroupType);
nameCountryStatisticGroupType 'Страна' (type)= name(countryStatisticGroupType(type)) IN base;

countryStatisticGroup = countryStatisticGroupType(groupTypeStatisticGroup(group));
nameCountryStatisticGroup 'Страна' (statisticGroup)= name(countryStatisticGroup(statisticGroup)) IN base;

unitMeasureStatisticGroup 'Ед. изм. ИД' (statisticGroup) = DATA GroupStatic (StatisticGroup);
nameUOMStatisticGroup 'Ед. изм.' (statisticGroup) = name(unitMeasureStatisticGroup(statisticGroup)) MINCHARWIDTH 12 MAXCHARWIDTH 12 PREFCHARWIDTH 12;

sidStatisticGroup 'Код группы' (statisticGroup) = DATA STRING[12] (StatisticGroup)  MINCHARWIDTH 12 MAXCHARWIDTH 12 PREFCHARWIDTH 12;
canonicalNumberStatisticGroup 'Канонический код' (statisticGroup) = toString255(
                           [GROUP CONCAT sidStatisticGroup(parent), ' / ' BY child ORDER DESC levelStatisticGroupStatisticGroup(child, parent)](statisticGroup))
                           MINCHARWIDTH 50 MAXCHARWIDTH 100 PREFCHARWIDTH 100 PERSISTENT;

canonicalNumberNameStatisticGroup 'Канонический код-название' (statisticGroup) = [FORMULA STRING[100] '$1 || \' / \' || $2'](
    canonicalNumberStatisticGroup(statisticGroup), name(statisticGroup));

groupIdTypeIdGroup (string1, string2) = GROUP UNIQUE group
    BY idStatisticGroupType(groupTypeStatisticGroup(group)), sidStatisticGroup(group) WHERE group IS StatisticGroup;

sidParentStatisticGroup 'Код родительского объекта' (statisticGroup) = sidStatisticGroup(parentStatisticGroup(statisticGroup));

conversionFactorStatisticGroup 'Коэффициент перевода' (statisticGroup) = DATA NUMERIC[14,5] (StatisticGroup);

FORM statisticGroup 'Статистическая группа'
    OBJECTS g=StatisticGroup FIXED PANEL
    PROPERTIES(g)   nameGroupTypeStatisticGroup, nameStat=name, nameParentStatisticGroup, sidParentStatisticGroup,
                    sidStatisticGroup, nameUOMStatisticGroup,conversionFactorStatisticGroup

    EDIT StatisticGroup OBJECT g
;

addStatisticGroupType 'Добавить' = ACTION (group, type) NEWSESSION {
    FOR ADDOBJ g = StatisticGroup DO {
        SET parentStatisticGroup(g) <- group;
        SET groupTypeStatisticGroup(g) <- type;
        FORM statisticGroup OBJECTS g = g MODAL;
        IF formResult() == FormResult.ok THEN
            EXEC apply();
    };
} TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

addStatisticGroup 'Добавить' (group)= addStatisticGroupType(group, groupTypeStatisticGroup(group)) TOOLBAR IMAGE 'add.png' EDITKEY 'INSERT';

statisticGroupTypeSku = ABSTRACT StatisticGroup (StatisticGroupType,Sku);
nameStatisticGroupTypeSku 'Статистическая группа' = name(statisticGroupTypeSku(type, sku));

TABLE statisticGroupStatisticGroupTypeSku(StatisticGroup, StatisticGroupType, Sku);
isParentStatisticGroupSku (group, sku) = isParentStatisticGroupStatisticGroup(statisticGroupTypeSku(groupTypeStatisticGroup(group),sku), group) PERSISTENT;
isParentLeafStatisticGroupSku (group, sku) = isParentLeafStatisticGroupStatisticGroup(statisticGroupTypeSku(groupTypeStatisticGroup(group),sku), group) PERSISTENT;
parentStatisticTypeSku (type, sku) = parentStatisticGroup(statisticGroupTypeSku(type,sku)) PERSISTENT;

residentCountryCountry 'Отечественный производитель' =  DATA BOOLEAN (Country, Country);
residentStatisticGroupSku (group, sku) = residentCountryCountry(countryStatisticGroupType(groupTypeStatisticGroup(group)), countrySku(sku));

FORM statisticGroups 'Статистические группы'

    OBJECTS ss=Country FIXED PANEL
    PROPERTIES(ss) SELECTOR name

    OBJECTS c=StatisticGroupType FIXED PANEL
    PROPERTIES(c) SELECTOR name
    FILTERS countryStatisticGroupType(c) == ss

    TREE treeGroups g=StatisticGroup PARENT parentStatisticGroup
    PROPERTIES READONLY name(g), sidStatisticGroup(g)
    PROPERTIES(g,c)       addStatisticGroupType
    PROPERTIES(g) EDITFORM
    ORDER BY sidStatisticGroup

    OBJECTS cg=StatisticGroup
    PROPERTIES(cg)     READONLY canonicalNumberNameStatisticGroup, sidStatisticGroup, nameUOMStatisticGroup,
                       conversionFactorStatisticGroup
    PROPERTIES(cg)  delete

    FILTERS groupTypeStatisticGroup(g) == c,
            groupTypeStatisticGroup(cg) == c

    OBJECTS s = Country

    PROPERTIES(s) READONLY name, nameOriginCountry, sidCountry, sidOrigin2Country, sidOrigin3Country,
                           nameCurrencyCountry, nameLanguageCountry
    PROPERTIES(ss,s) residentCountryCountry
    FILTERGROUP filter
        FILTER 'Страны явл. отечественными' 'F11' residentCountryCountry(ss, s) DEFAULT

    FILTERGROUP filters
        FILTER 'Все листья' 'F10' isParentLeafStatisticGroupStatisticGroup(cg, g) DEFAULT
        FILTER 'Всех потомков' 'F9' isParentStatisticGroupStatisticGroup(cg, g)
        FILTER 'Только непосредственных потомков' 'F8' parentStatisticGroup(cg) == g
;

DESIGN statisticGroups FROM DEFAULT {
    main {
        childConstraints = TO THE BOTTOM;
        NEW header {
            childConstraints = TO THE RIGHT;
            ADD ss.box;
            ADD c.box;
        }
        NEW specification.box {
            type = TABBED;
            NEW topContainer{
                title = 'Статистические группы';
                childConstraints = TO THE RIGHT;
                type = SPLITH;

                ADD treeGroups.tree.box;

                ADD cg.box{
                    fillHorizontal = 4;
                }
            }
            ADD s.box {title = 'Страны являющиеся отечественными';}
        }
    }

    ADD functions.box;
}

FORM statisticGroupDialog 'Статистические группы'
    OBJECTS c=StatisticGroupType FIXED PANEL
    PROPERTIES(c) SELECTOR name

    TREE treeGroups g=StatisticGroup PARENT parentStatisticGroup
    PROPERTIES(g) READONLY name, sidStatisticGroup, canonicalNumberStatisticGroup
    PROPERTIES(g) ADDFORM, EDITFORM, delete
    FILTERS groupTypeStatisticGroup(g) == c
    ORDER BY canonicalNumberStatisticGroup

    DIALOG StatisticGroup OBJECT g
;

//---------------------------------------------- Формы стат. отчетов -------------------------------------//

//  12 ТОРГ ПРОДАЖИ

netWeightSoldSkuStockDateFromTo 'Вес проданного товара, кг' (sku, stock, dateFrom, dateTo) =
    quantitySoldSkuStockDateFromTo(sku, stock, dateFrom, dateTo) *
    (UNION OVERRIDE 1 IF sku IS Sku, netWeightSku(sku) AND NOT isWeightSku(sku));
volumeSoldSkuStockDateFromTo 'Объем проданного товара, л' (sku, stock, dateFrom, dateTo) =
    quantitySoldSkuStockDateFromTo(sku, stock, dateFrom, dateTo) * volumeSku(sku);
// sumSoldSkuStockDateFromTo 'Продано, сумма' (sku, stock, dateFrom, dateTo)   - рубли
//quantitySoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo)                      - шт.

                            //с коэффициентом пересчета за период//
//всего
quantitySoldByStatisticGroupDateFromTo 'Продано, шт' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM quantitySoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
netWeightSoldByStatisticGroupDateFromTo 'Продано, кг' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM netWeightSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
volumeSoldByStatisticGroupDateFromTo 'Продано, л' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM volumeSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
retailSumSoldByStatisticGroupDateFromTo 'Продано, сум.' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM sumSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

//отечеств. пр-ва
quantitySoldResidentByStatisticGroupDateFromTo 'Продано, шт' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM quantitySoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF  residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
netWeightSoldResidentByStatisticGroupDateFromTo 'Продано, кг' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM netWeightSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
volumeSoldResidentByStatisticGroupDateFromTo 'Продано, л' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM volumeSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
retailSumSoldResidentByStatisticGroupDateFromTo 'Продано, сум.' (statisticGroup, stock, dateFrom, dateTo)=
    [GROUP SUM sumSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateFrom, dateTo](statisticGroup, stock, dateFrom, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

netWeightBalanceASkuLedgerDate 'Вес товара на конец , кг' (sku, stock, dateTo) =
    balanceASkuStockDate (sku, stock, dateTo) * (UNION OVERRIDE 1 IF sku IS Sku, netWeightSku(sku) AND NOT isWeightSku(sku));
volumeBalanceASkuLedgerDate 'Объем товара на конец , л' (sku, stock, dateTo) =
    balanceASkuStockDate (sku, stock, dateTo) * volumeSku(sku);

//sumASkuStockDate (sku, stock, dateTo) -  сумма на конец

//retailSumBalanceASkuLedgerDate 'Сумма товара на конец , сум.' (sku, stock, dateTo) =
//    retailPriceASkuStockDate (sku, stock, dateTo) * balanceASkuStockDate(sku, stock, dateTo);
//balanceASkuStockDate (sku, stock, dateTo)    - шт.

                                   //с коэффициентом пересчета на коцен//
// всего
balanceByStatisticGroupDateTo 'Остаток на конец' (statisticGroup, stock, dateTo)=
    [GROUP SUM balanceASkuStockDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
netWeightByStatisticGroupDateTo 'Вес на конец, кг' (statisticGroup, stock, dateTo)=
    [GROUP SUM netWeightBalanceASkuLedgerDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
volumeByStatisticGroupDateTo 'Объем на конец, л' (statisticGroup, stock, dateTo)=
    [GROUP SUM volumeBalanceASkuLedgerDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
retailSumByStatisticGroupDateTo 'Сумма на конец, сум.' (statisticGroup, stock, dateTo)=
    [GROUP SUM sumASkuStockDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

// отечеств. пр-ва
balanceResidentByStatisticGroupDateTo 'Остаток на конец' (statisticGroup, stock, dateTo)=
    [GROUP SUM balanceASkuStockDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
netWeightResidentByStatisticGroupDateTo 'Вес на конец, кг' (statisticGroup, stock, dateTo)=
    [GROUP SUM netWeightBalanceASkuLedgerDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
volumeResidentByStatisticGroupDateTo 'Объем на конец, л' (statisticGroup, stock, dateTo)=
    [GROUP SUM volumeBalanceASkuLedgerDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
retailSumResidentByStatisticGroupDateTo 'Сумма на конец, сум.' (statisticGroup, stock, dateTo)=
    [GROUP SUM sumASkuStockDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, stock, dateTo](statisticGroup, stock, dateTo)/
        (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

valueByStatisticGroupDateFromTo 'Продано' (statisticGroup, stock, dateFrom, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN quantitySoldByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightSoldByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeSoldByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumSoldByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        DEFAULT 0 AND statisticGroup IS StatisticGroup AND stock IS Stock AND dateFrom IS DATE AND dateTo IS DATE
    END;
valueResidentByStatisticGroupDateFromTo 'Продано, бел. пр-ва' (statisticGroup, stock, dateFrom, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN quantitySoldResidentByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightSoldResidentByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeSoldResidentByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumSoldResidentByStatisticGroupDateFromTo(statisticGroup, stock, dateFrom, dateTo)
        DEFAULT 0 AND statisticGroup IS StatisticGroup AND stock IS Stock AND dateFrom IS DATE AND dateTo IS DATE
    END;

valueByStatisticGroupDateTo 'Остаток на конец' (statisticGroup, stock, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN balanceByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        DEFAULT 0 IF statisticGroup IS StatisticGroup AND stock IS Stock AND dateTo IS DATE
    END;
valueResidentByStatisticGroupDateTo 'Остаток на конец, бел. пр-ва' (statisticGroup, stock, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN balanceResidentByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightResidentByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeResidentByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumResidentByStatisticGroupDateTo(statisticGroup, stock, dateTo)
        DEFAULT 0 IF statisticGroup IS StatisticGroup AND stock IS Stock AND dateTo IS DATE
    END;

FORM statisticalReport 'Статистика (продажи) склад/группа/sku'

    OBJECTS dates = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES OBJVALUE(dFrom), OBJVALUE(dTo)
    OBJECTS c = StatisticGroupType FIXED PANEL
    PROPERTIES SELECTOR name(c)

    TREE stockTree a = STRING[3], sg = StockGroup PARENT parentStockGroup, ts = Stock
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg), name(ts)
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a),
            stockGroupStock(ts) == sg

    TREE treeGroups g=StatisticGroup PARENT parentStatisticGroup
    PROPERTIES READONLY gname = name(g),  sidStatisticGroup(g), nameUOMStatisticGroup(g)
    ORDER BY gname
    FILTERS groupTypeStatisticGroup(g) ==c

    OBJECTS           sts=(st=Stock, s=Sku)
    PROPERTIES        READONLY nameS=nameSku(s), name(st)
    FILTERS           st == ts AND sg IS StockGroup OR isParentStockGroupStock(sg, st) AND NOT ts OR st IS Stock AND NOT sg AND NOT ts

    ORDER BY          nameS
    FILTERGROUP filters
        FILTER 'Все листья' 'F10' isParentLeafStatisticGroupSku(g, s) DEFAULT
        FILTER 'Всех потомков' 'F9' isParentStatisticGroupSku(g, s)
        FILTER 'Только непосредственных потомков' 'F8' parentStatisticTypeSku(c,s) == g

    PROPERTIES        balanceBSkuStockDate(s, st, dFrom),
                      quantitySoldSkuStockDateFromTo(s, st, dFrom, dTo),
                      balanceASkuStockDate(s, st, dTo)
    PROPERTIES  FORCE PANEL valueByStatisticGroupDateFromTo(g, ts, dFrom, dTo), valueResidentByStatisticGroupDateFromTo(g, ts, dFrom, dTo)
    PROPERTIES  FORCE PANEL valueByStatisticGroupDateTo(g, ts, dTo), valueResidentByStatisticGroupDateTo(g, ts, dTo)
;
@extendFormFilterStockAccess(Stock, st, statisticalReport);
@extendFormFilterStockGroupAccess(Stock, ts, statisticalReport, accessEmployeeEmployeeDivision);
@extendFormFilterStockGroupAccess(StockGroup, sg, statisticalReport, countAccessEmployeeEmployeeDivisionGroup);

DESIGN statisticalReport FROM DEFAULT {

    main{

        NEW topContainer{

            type = SPLITH;
            childConstraints = TO THE RIGHT;

            NEW firstCase {

                type = SPLITV;
                childConstraints = TO THE BOTTOM;

                ADD stockTree.tree.box { title = 'Склады'; }
                ADD treeGroups.tree.box { title = 'Статистические группы'; }
            }

            NEW secondCase {

                type = SPLITV;
                fillHorizontal = 2;
                childConstraints = TO THE BOTTOM;

                NEW wor {
                    childConstraints = TO THE RIGHT;
                    ADD dates.box;
                    ADD c.box {title = 'Тип классификатора статистических групп';};
                }
                ADD sts.box {fillVertical = 2;
                    title = 'Показатели по продажам в номинальных единицах';
                }
            }
        }

        NEW row {
            title = 'Суммы';
            childConstraints = TO THE BOTTOM;
            NEW row1 {
            title = 'По отдел-группа, с учетом ед. изм. и коэфф. перевода';
            ADD PROPERTY(valueByStatisticGroupDateFromTo);
            ADD PROPERTY(valueResidentByStatisticGroupDateFromTo);
            ADD PROPERTY(valueByStatisticGroupDateTo);
            ADD PROPERTY(valueResidentByStatisticGroupDateTo);
            }
        }
    }

    ADD functions.box;
}

                ///////////////--------по регионам и компании----------/////////////////

                           //c коэффициентом пересчета за период//
// всего
quantitySoldByStatisticGroupRegionDateFromTo 'Продано, шт' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM quantitySoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

netWeightSoldByStatisticGroupRegionDateFromTo 'Продано, кг' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM netWeightSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

volumeSoldByStatisticGroupRegionDateFromTo 'Продано, л' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM volumeSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

retailSumSoldByStatisticGroupRegionDateFromTo 'Продано, руб.' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM sumSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

// отечест. производствава
quantitySoldResidentByStatisticGroupRegionDateFromTo 'Продано, шт' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM quantitySoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

netWeightSoldResidentByStatisticGroupRegionDateFromTo 'Продано, кг' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM netWeightSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

volumeSoldResidentByStatisticGroupRegionDateFromTo 'Продано, л' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM volumeSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

retailSumSoldResidentByStatisticGroupRegionDateFromTo 'Продано, руб.' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    [GROUP SUM sumSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo]
        (statisticGroup, legalEntity, region, dateFrom, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

                             //с коэффициентом пересчета на коцен//
// всего
balanceByStatisticGroupRegionDateTo 'Остаток на конец' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM balanceASkuStockDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup,legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

netWeightByStatisticGroupRegionDateTo 'Вес на конец, кг' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM netWeightBalanceASkuLedgerDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

volumeByStatisticGroupRegionDateTo 'Объем на конец, л' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM volumeBalanceASkuLedgerDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

retailSumByStatisticGroupRegionDateTo 'Сумма на конец, руб' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM sumASkuStockDate (sku, stock, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));
// белорусского производства
balanceResidentByStatisticGroupRegionDateTo 'Остаток на конец' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM balanceASkuStockDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

netWeightResidentByStatisticGroupRegionDateTo 'Вес на конец, кг' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM netWeightBalanceASkuLedgerDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

volumeResidentByStatisticGroupRegionDateTo 'Объем на конец, л' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM volumeBalanceASkuLedgerDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

retailSumResidentByStatisticGroupRegionDateTo 'Сумма на конец, руб' (statisticGroup, legalEntity, region, dateTo)=
    [GROUP SUM sumASkuStockDate (sku, stock, dateTo) IF residentStatisticGroupSku (statisticGroup, sku) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateTo]
        (statisticGroup, legalEntity, region, dateTo) / (UNION OVERRIDE 1 AND statisticGroup IS StatisticGroup, conversionFactorStatisticGroup(statisticGroup));

valueByStatisticGroupLegalEntityRegionDateFromTo 'Продано всего' (statisticGroup, legalEntity, region, dateFrom, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN quantitySoldByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightSoldByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeSoldByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumSoldByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        DEFAULT 0 AND statisticGroup IS StatisticGroup AND legalEntity IS LegalEntity AND region IS Region AND dateFrom IS DATE AND dateTo IS DATE
    END;

valueByStatisticGroupLegalEntityRegionDateTo 'Остаток на конец всего' (statisticGroup, legalEntity, region, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN balanceByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        DEFAULT 0 IF statisticGroup IS StatisticGroup AND legalEntity IS LegalEntity AND region IS Region AND dateTo IS DATE
    END;

valueResidentByStatisticGroupLegalEntityRegionDateFromTo 'Продано отеч. пр-ва' (statisticGroup, legalEntity, region, dateFrom, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN quantitySoldResidentByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightSoldResidentByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeSoldResidentByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumSoldResidentByStatisticGroupRegionDateFromTo(statisticGroup, legalEntity, region, dateFrom, dateTo)
        DEFAULT 0 AND statisticGroup IS StatisticGroup AND legalEntity IS LegalEntity AND region IS Region AND dateFrom IS DATE AND dateTo IS DATE
    END;

valueResidentByStatisticGroupLegalEntityRegionDateTo 'Остаток на конец отеч. пр-ва' (statisticGroup, legalEntity, region, dateTo) =
    CASE
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticThing
            THEN balanceResidentByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticPounds
            THEN netWeightResidentByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticLiter
            THEN volumeResidentByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        WHEN unitMeasureStatisticGroup(statisticGroup)==GroupStatic.statisticSum
            THEN retailSumResidentByStatisticGroupRegionDateTo(statisticGroup, legalEntity, region, dateTo)
        DEFAULT 0 IF statisticGroup IS StatisticGroup AND legalEntity IS LegalEntity AND region IS Region AND dateTo IS DATE
    END;

totalRetailSumSoldByStatisticGroupRegionDateFromTo 'Итого продано, руб.' (statisticGroup, legalEntity, region, dateFrom, dateTo)=
    GROUP SUM sumSoldSkuStockDateFromTo (sku, stock, dateFrom, dateTo) AND isParentStatisticGroupSku(statisticGroup, sku)
    BY statisticGroup, legalEntityStock(stock), regionStock(stock), dateFrom, dateTo;

FORM torgSales 'Статистика (продажи) организация/регион/группа'

    OBJECTS params = (dFrom = DATE, dTo = DATE) FIXED PANEL
    PROPERTIES objFrom = OBJVALUE(dFrom), objTo = OBJVALUE(dTo)

    OBJECTS c = StatisticGroupType FIXED PANEL
    PROPERTIES SELECTOR name(c)

    OBJECTS l=LegalEntity  FIXED PANEL
    PROPERTIES(l) SELECTOR name

    OBJECTS r=Region  FIXED PANEL
    PROPERTIES(r) SELECTOR name

    TREE treeGroups g=StatisticGroup PARENT parentStatisticGroup
    PROPERTIES READONLY gname = name(g), sidStatisticGroup(g), nameUOMStatisticGroup(g)
    ORDER BY sidStatisticGroup

    FILTERS groupTypeStatisticGroup(g) == c

    PROPERTIES(g, l, r, dFrom, dTo) valueByStatisticGroupLegalEntityRegionDateFromTo, valueResidentByStatisticGroupLegalEntityRegionDateFromTo
    PROPERTIES(g, l, r, dTo) valueByStatisticGroupLegalEntityRegionDateTo, valueResidentByStatisticGroupLegalEntityRegionDateTo
    PROPERTIES(g, l, r, dFrom, dTo) totalRetailSumSoldByStatisticGroupRegionDateFromTo
;

DESIGN torgSales FROM DEFAULT {

    main{
        ADD treeGroups.tree.box  BEFORE functions.box {
            title = 'Показатели по стат. группам с учетом ед. изм. и коэфф. перевода';
        }
        NEW firstCase BEFORE treeGroups.tree.box {
            title = 'Параметры отчета';
            childConstraints = TO THE RIGHTBOTTOM;

            ADD c.box;
            NEW dateCase {
                title = 'Даты';
                childConstraints = TO THE RIGHT;
                ADD PROPERTY(objFrom) {
                    caption = 'Дата (с)';
                }
                ADD PROPERTY(objTo) {
                    caption = 'Дата (по)';
                }
            }
            ADD l.box;
            ADD r.box;
        }
        ADD functions.box;
    }
}

//---------------------------------------------- Автоматическое заполнение -----------------------------------------------------//

loadDefaultStatisticGroupType 'Добавить значение классификатора стат.групп' = ACTION (iname, isid, isidCountry) {
    FOR ADDOBJ t = StatisticGroupType DO {
        SET name(t) <- iname;
        SET idStatisticGroupType (t) <- isid;
        SET countryStatisticGroupType(t) <- countrySID(isidCountry);
    }
}

loadDefaultStatisticGroup 'Добавить значение статистические группы' = ACTION (sidType, sidParent, iname, isid, groupStatic, value) {
    FOR ADDOBJ g = StatisticGroup DO {
        SET groupTypeStatisticGroup(g) <- typeIdStatisticGroupType(sidType);
        SET parentStatisticGroup(g) <- groupIdTypeIdGroup(sidType, sidParent);

        SET name(g) <- iname;
        SET sidStatisticGroup(g) <- isid;
        SET unitMeasureStatisticGroup(g) <- groupStatic AS GroupStatic;
        SET conversionFactorStatisticGroup(g) <- value;
    }
}

loadDefaultResidentCountry 'Добавить отеч. производ' = ACTION (idCountry, idCountry2)  {
    SET residentCountryCountry(a,b) <- TRUE WHERE a == countrySID(idCountry) AND b == countrySID(idCountry2) AND a IS Country AND b IS Country ;
}

loadDefaultStatisticGroups 'Загрузить стандартные статистические группы и классификатор' () = ABSTRACT ACTION ()  IN loadDefaultGroup;
@implementLoadDefaultData(loadDefaultStatisticGroups);

NAVIGATOR {
    saleNavigator {
        NEW statisticsNavigator 'Статистика' BEFORE saleMasterData {
            ADD statisticGroupTypes;
            ADD statisticGroups;
            ADD statisticalReport;
            ADD torgSales;
        }
    }
}