MODULE Barcode;

REQUIRE System, Stock, Utils;

CLASS barcode 'Штрих код';
TABLE barcode (barcode);
TABLE stringDate (STRING[14], DATE);

idBarcode 'Штрих код' = DATA STRING[14] (barcode) IN recognizeGroup FIXEDCHARWIDTH 14 INDEXED;

dataDateBarcode 'Дата действия' = DATA DATE (barcode);
dateBarcode 'Дата действия' = (2001_01_01 IF barcode IS barcode) OR dataDateBarcode(barcode) IN baseGroup PERSISTENT;

dataBarcodeIdDate (id, date) = GROUP UNIQUE barcode BY idBarcode(barcode), dateBarcode(barcode) WHERE barcode IS barcode PERSISTENT;
orderBarcodeIdDate 'Дата действия' (id, date) = GROUP MAX LIST(dateIn, dataBarcodeIdDate(id, dateIn)) IF dateIn <= (date AS DATE) BY id, date IN baseGroup;
barcodeIdDate (id, date) = orderBarcodeIdDate(id, date)[2];

barcodeId (id) = barcodeIdDate(id, currentDate());
activeBarcode 'Активный' (barcode) = barcodeId(idBarcode(barcode)) == barcode;

dataAmountBarcode 'Множитель' = DATA NUMERIC[14,3] (barcode);
amountBarcode 'Множитель' = (1.0 IF barcode IS barcode) OR dataAmountBarcode (barcode) PERSISTENT;

// Sku
skuBarcode = DATA sku(barcode) IN baseGroup;
nameSkuBarcode 'Наименование' (barcode) = nameSku(skuBarcode(barcode)) IN recognizeGroup;

skuBarcodeIdDate (id, date) = skuBarcode(barcodeIdDate(id, date));

dataBarcodeSku = DATA barcode (sku) IN idGroup;
CONSTRAINT skuBarcode(dataBarcodeSku(item)) != item CHECKED BY dataBarcodeSku MESSAGE 'Выбран неверный sku для штрих-кода';

barcodeSku(sku) = [GROUP MIN barcode BY skuBarcode(barcode)](sku) OR dataBarcodeSku(sku) PERSISTENT;

primaryBarcode 'Основной' (barcode) = barcodeSku(skuBarcode(barcode)) == barcode;

idBarcodeSku 'Штрих-код' = idBarcode(barcodeSku(sku)) IN baseGroup PERSISTENT;
idBarcodeSkuBatch 'Штрих-код' (batch) = idBarcodeSku(skuBatch(batch));

// UOM

dataUOMBarcode = DATA UOM (barcode);
UOMBarcode = UOMSku(skuBarcode(barcode)) OR dataUOMBarcode(barcode);
shortNameUOMBarcode 'Единица измерения' (barcode) = shortName(UOMBarcode(barcode));

// ----------------------------------- Форма по вводу штрих-кода ---------------------------- //

FORM barcodeInput 'Ввод штрих-кода'
    OBJECTS             barcode=STRING[14] FIXED PANEL
    PROPERTIES(barcode) objValue = OBJVALUE
;

DESIGN barcodeInput FROM DEFAULT {
    PROPERTY (objValue) {
        caption = 'Штрих-код';
        font = 'Tahoma bold 64';
        panelLabelAbove = TRUE;
    }
}

// ------------------------------------ Добавление сканированием строк в документ ----------------------//
META defineAddDetailDialogBarcode(object, skuProp)
    detail###object##Sku(object, sku) = GROUP MAX detail BY object###object##Detail(detail), skuProp###object##Detail(detail);
    addDetailDialogBarcode###object##Detail###object 'Ввод штрих-кода' = ACTION (object) {
        FORM barcodeInput MODAL;

        IF formResult() == formResult.ok THEN {
            LOCAL dialogBarcodeSku = sku();
            SET dialogBarcodeSku() <- skuBarcodeIdDate(chosenString('barcode'), date###object(object));

            IF dialogBarcodeSku() IS sku THEN {
                LOCAL dialogBarcodeDetail = object##Detail();
                SET dialogBarcodeDetail() <- detail###object##Sku(object, dialogBarcodeSku());
                IF dialogBarcodeDetail() IS object##Detail THEN {
                    SET quantity###object##Detail(detail) IF detail == dialogBarcodeDetail() <-
                        quantity###object##Detail(detail) (+) (1.0 IF detail IS object###Detail);
                } ELSE {
                    ADDOBJ object##Detail;
                    FOR w == addedObject() DO {
                        SET object###object##Detail(w) <- object AS object;
                        SET skuProp###object##Detail(w) <- dialogBarcodeSku();
                        SET quantity###object##Detail(w) <- 1.0;
                    }
                }
            } ELSE
                MESSAGE 'Не найден штрих-код';
        }
    } TOOLBAR EDITKEY 'F4';
END


// ----------------------------------- Расширение подбора товаров ---------------------------- //

META defineDialogSkuBarcode (form)
    EXTEND FORM form
        PROPERTIES READONLY idBarcodeSku(s), idBarcodeSku(si)
    ;
END

@defineDialogSkuBarcode(dialogSku);

EXTEND FORM currentBalanceSkuStock
    PROPERTIES(s) READONLY idBarcodeSku
;