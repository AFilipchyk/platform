MODULE Barcode;

REQUIRE System, Stock, Utils, Numerator;

CLASS barcode 'Штрих код';
TABLE barcode (barcode);
TABLE stringDate (STRING[14], DATE);

idBarcode 'Штрих код' = DATA STRING[14] (barcode) IN recognizeGroup FIXEDCHARWIDTH 14 AGGPROP INDEXED;

dataDateBarcode 'Дата действия' = DATA DATE (barcode);
dateBarcode 'Дата действия' = (2001_01_01 IF barcode IS barcode) OR dataDateBarcode(barcode) IN baseGroup PERSISTENT;

dataBarcodeIdDate (id, date) = GROUP UNIQUE barcode BY idBarcode(barcode), dateBarcode(barcode) WHERE barcode IS barcode PERSISTENT;
orderBarcodeIdDate 'Дата действия' (id, date) = GROUP MAX LIST(dateIn, dataBarcodeIdDate(id, dateIn)) IF dateIn <= (date AS DATE) BY id, date IN baseGroup;
barcodeIdDate (id, date) = orderBarcodeIdDate(id, date)[2];

barcodeId (id) = barcodeIdDate(id, currentDate());
activeBarcode 'Активный' (barcode) = barcodeId(idBarcode(barcode)) == barcode;

dataAmountBarcode 'Множитель' = DATA NUMERIC[14,3] (barcode);
amountBarcode 'Множитель' = (1.0 IF barcode IS barcode) OR dataAmountBarcode (barcode) PERSISTENT;

completeBarcode = FORMULA STRING[13] 'completeBarcode($1)';

// Sku
skuBarcode = DATA sku(barcode) IN baseGroup NOT NULL DELETE;
nameSkuBarcode 'Наименование' (barcode) = nameSku(skuBarcode(barcode)) IN recognizeGroup;

skuBarcodeIdDate (id, date) = skuBarcode(barcodeIdDate(id, date));

dataBarcodeSku = DATA barcode (sku);
CONSTRAINT skuBarcode(dataBarcodeSku(item)) != item CHECKED BY dataBarcodeSku MESSAGE 'Выбран неверный sku для штрих-кода';

barcodeSku(sku) = [GROUP MIN barcode BY skuBarcode(barcode)](sku) OR dataBarcodeSku(sku) PERSISTENT;

primaryBarcode 'Основной' (barcode) = barcodeSku(skuBarcode(barcode)) == barcode;
changePrimaryBarcodeSku = ACTION (barcode, sku) {
    REQUEST BOOLEAN INPUT;
    IF requestedLogical() THEN {
        SET dataBarcodeSku(sku) <- barcode;
    }
}

idBarcodeSku 'Штрих-код' (sku) = idBarcode(barcodeSku(sku)) IN baseGroup AGGPROP PERSISTENT;
idBarcodeSkuBatch 'Штрих-код' (batch) = idBarcodeSku(skuBatch(batch));
idBarcodeSkuSkuLedger 'Штрих-код' (skuLedger) = idBarcodeSku(skuSkuLedger(skuLedger));

dataUOMBarcode = DATA UOM (barcode);
UOMBarcode = UOMSku(skuBarcode(barcode)) OR dataUOMBarcode(barcode) PERSISTENT;
shortNameUOMBarcode 'Единица измерения' (barcode) = shortName(UOMBarcode(barcode));

// Группа

skuGroupBarcode (barcode) = skuGroupSku(skuBarcode(barcode));

// Генерация штрих-кодов

barcodeNumeratorSku = ABSTRACT numerator (sku);

generateBarcodeSku 'Сгенерировать штрих-код' = ACTION (sku) {
    ADDOBJ barcode;
    FOR b == addedObject() DO {
        SET skuBarcode(b) <- sku;
        SET idBarcode(b) <- completeBarcode(curStringValueNumerator(barcodeNumeratorSku(sku)));
        EXEC incrementValueNumerator(barcodeNumeratorSku(sku));
    };
} TOOLBAR CONFIRM;

FORM barcodeDialog 'Штрих код'
    OBJECTS b = barcode
    PROPERTIES (b) READONLY idBarcode, nameSkuBarcode, dateBarcode, shortNameUOMBarcode, amountBarcode, activeBarcode, primaryBarcode//, ordersBarcode
    PROPERTIES (b) ADDSESSIONFORM, EDITSESSIONFORM, delete
    DIALOG barcode OBJECT b
;

// ----------------------------------- Форма по вводу штрих-кода ---------------------------- //

barcodeInput 'Штрих-код' = SESSION DATA STRING[13] () EVENTID 'SCANNER';
changeBarcodeInput = ACTION () {
    REQUEST STRING[13] INPUT;
    SET barcodeInput() <- requestedString();
    EXEC formOkAction();
}
FORM barcodeInput 'Ввод штрих-кода'
    PROPERTIES() barcodeInput ON CHANGE EXEC changeBarcodeInput()
;

DESIGN barcodeInput FROM DEFAULT {
    PROPERTY (barcodeInput) {
        caption = 'Штрих-код';
        font = 'Tahoma bold 64';
        panelLabelAbove = TRUE;
        editKey = 'F4';
        focusable = TRUE;
    }
}

// ------------------------------------ Добавление сканированием строк в документ ----------------------//

META defineAddDetailDialogBarcodeCustom(object, idetail, skuProp)
    idetail###object##Sku(object, sku) = GROUP MAX detail BY object###idetail(detail), skuProp###idetail(detail);

    addDetailBarcode###idetail###object = ACTION (object, barcode) {
        LOCAL dialogBarcodeSku = sku();
        SET dialogBarcodeSku() <- skuBarcodeIdDate(barcode, date###object(object));

        IF dialogBarcodeSku() IS sku THEN {
            LOCAL dialogBarcodeDetail = idetail();
            SET dialogBarcodeDetail() <- idetail###object##Sku(object, dialogBarcodeSku());
            IF dialogBarcodeDetail() IS idetail THEN {
                SET quantity###idetail(detail) IF detail == dialogBarcodeDetail() <-
                    quantity###idetail(detail) (+) (1.0 IF detail IS idetail);
            } ELSE {
                FOR ADDOBJ w = idetail DO {
                    SET object###idetail(w) <- object;
                    SET skuProp###idetail(w) <- dialogBarcodeSku();
                    SET quantity###idetail(w) <- 1.0;
                }
            }
        } ELSE
            MESSAGE 'Не найден штрих-код';
    }

    addDetailDialogBarcode###idetail###object 'Ввод штрих-кода' = ACTION (object) {
        FORM barcodeInput MODAL;

        IF formResult() == formResult.ok THEN
            EXEC addDetailBarcode###idetail###object(object, barcodeInput());
    } TOOLBAR EDITKEY 'F4';

    changeAddDetailInputBarcode###idetail###object = ACTION (object) {
        REQUEST STRING[14] INPUT;
        EXEC addDetailBarcode###idetail###object(object, requestedString());
    };
    addDetailInputBarcode###idetail###object 'Ввод штрих-кода :' (object) = SESSION DATA STRING[14] (object) TOOLBAR EDITKEY 'F4' EVENTID 'SCANNER'
                                                                            ON CHANGE EXEC changeAddDetailInputBarcode###idetail###object(object);
END

META defineAddDetailDialogBarcode(object, skuProp)
    @defineAddDetailDialogBarcodeCustom(object, object##Detail, skuProp);
END

// ----------------------------------- Расширение подбора товаров ---------------------------- //

META defineDialogSkuBarcode (form)
    EXTEND FORM form
        PROPERTIES READONLY idBarcodeSku(s), idBarcodeSku(si)
    ;
END

@defineDialogSkuBarcode(dialogSku);

EXTEND FORM skus
    PROPERTIES(s) READONLY idBarcodeSku AFTER nameSku
;

EXTEND FORM currentBalanceSkuStock
    PROPERTIES(s) READONLY idBarcodeSku AFTER nameSku
;

EXTEND FORM currentBalanceBatchStock
    PROPERTIES(bt) READONLY idBarcodeSkuBatch AFTER nameSkuBatch
;

EXTEND FORM costSkuLedger
    PROPERTIES(bil) READONLY idBarcodeSkuSkuLedger AFTER nameSkuSkuLedger
;
EXTEND FORM batchDialog
    PROPERTIES(bt) READONLY idBarcodeSkuBatch AFTER nameSkuBatch
;
EXTEND FORM dialogBatch
    PROPERTIES(bt) READONLY idBarcodeSkuBatch AFTER nameSkuBatch
;

// Barcode
META defineDocumentDetailBarcodeCustomPrefix (detail, skuProp, prop, prefixB, prefixS)
    prefixB###prop###detail = DATA barcode (detail);
    prefixB###id###prop###detail 'Штрих-код упаковки' (detail) = idBarcode(prefixB###prop###detail(detail)) IN recognizeGroup;
    prefixB###shortNameUOM###prop###detail 'Ед. изм. упаковки' = shortNameUOMBarcode(prefixB###prop###detail(detail)) IN recognizeGroup;
    prefixB###amount###prop###detail 'Кол-во в упаковке' = DATA NUMERIC[14,3] (detail) IN recognizeGroup;

    prefixB###amount###prop###detail(detail) <- amountBarcode(prefixB###prop###detail(detail)) WHEN CHANGED(prefixB###prop###detail(detail));

    CONSTRAINT skuBarcode(prefixB###prop###detail(detail)) != prefixS###skuProp###detail(detail) CHECKED BY prefixB###prop###detail
        MESSAGE 'Выбран штрих-кода не соответствующий sku';
END
META defineDocumentAbstractDetailBarcodeCustomPrefix (detail, prop, prefixB)
    prefixB###prop###detail = ABSTRACT barcode (detail);
    prefixB###id###prop###detail 'Штрих-код упаковки' (detail) = idBarcode(prefixB###prop###detail(detail)) IN recognizeGroup;
    prefixB###shortNameUOM###prop###detail 'Ед. изм. упаковки' = shortNameUOMBarcode(prefixB###prop###detail(detail)) IN recognizeGroup;
    prefixB###amount###prop###detail 'Кол-во в упаковке' = ABSTRACT NUMERIC[14,3] (detail) IN recognizeGroup;
END
META defineDocumentInterfaceBarcodeSkuCustomPrefix (detail, skuProp, prop, prefixB, prefixS)
    @defineDocumentAbstractDetailBarcodeCustomPrefix(detail, prop, prefixB);
    @defineDocumentDetailBarcodeCustomPrefix(user###detail, skuProp, prop, prefixB, prefixS);
    prefixB###prop###detail(detail) += prefixB###prop###user###detail(detail);
    prefixB###amount###prop###detail(detail) += prefixB###amount###prop###user###detail(detail);
END

META defineDocumentInterfaceBarcodeSkuPrefix (object, skuProp, prop, prefixB, prefixS)
    @defineDocumentInterfaceBarcodeSkuCustomPrefix(object###Detail, skuProp, prop, prefixB, prefixS);
END
META defineDocumentInterfaceBarcodeSkuPrefix (object, skuProp, prop, prefixB)
    @defineDocumentInterfaceBarcodeSkuPrefix(object, skuProp, prop, prefixB, );
END