MODULE Document;

REQUIRE System, Numerator;

// Логика проведения документов

CLASS ABSTRACT postedObject 'Объект с возможностью закрытия';
TABLE postedObject (postedObject);

GROUP postedGroup 'Информация о закрытии' : baseGroup;

timeClosed 'Время закрытия' = DATA DATETIME (postedObject) IN postedGroup;
userClosed 'Проведен пользователем' = DATA customUser (postedObject) IN idGroup;
computerClosed 'Проведен на компьютере' = DATA computer (postedObject) IN idGroup;

nameUserClosed 'Проведен пользователем' (postedObject) = commonName(userClosed(postedObject)) IN postedGroup;
hostnameComputerClosed 'Проведен на компьютере' (postedObject) = hostname(computerClosed(postedObject)) IN postedGroup;

timeClosed(postedObject) <- currentDateTime() WHEN ASSIGNED(postedObject IS postedObject);
userClosed(postedObject) <- currentUser() WHEN ASSIGNED(postedObject IS postedObject);
computerClosed(postedObject) <- currentComputer() WHEN ASSIGNED(postedObject IS postedObject);

// Логика нумерирования документов

CLASS ABSTRACT numeratedDocument 'Нумерируемый документ' : numeratedObject;
@defineNumeratedObject(numeratedDocument, 'Нумератор для документов', 'ДК');

// Стандартные группы
GROUP documentHeaderGroup 'Шапка документа' : baseGroup;
GROUP documentSumGroup 'Суммы документа': publicGroup;
GROUP documentPrmGroup 'Параметры документа': publicGroup;

// Макросы по созданию документов

// ----- Tables ----- //

META defineDocumentTables (object)
    TABLE object (object);
    TABLE object##Detail (object##Detail);
END
META defineDocumentAbstractTables (object)
    @defineDocumentTables(user###object);
END
META defineDocumentInterfaceTables (object)
    @defineDocumentAbstractTables(object);
    @defineDocumentTables(object);
END

// ----- Relation ----- //
META defineDocumentRelation (object, detail, caption)
    object###detail = DATA object (detail) NOT NULL DELETE;
    in###object###detail(iobject, idetail) = object###detail(idetail) == iobject;

    @defineDocumentHeaderCount(object, detail);
END
META defineDocumentAbstractRelation(object, detail, caption)
    object###detail = ABSTRACT object (detail) PERSISTENT;
    in###object###detail(iobject, idetail) = object###detail(idetail) == iobject;

    @defineDocumentHeaderCount(object, detail);
END
META defineDocumentInterfaceRelation(object, detail, caption)
    @defineDocumentAbstractRelation(object, detail, caption);
    @defineDocumentRelation(user###object, user###detail, caption);
    object###detail(idetail) += user###object##User###detail(idetail);
END

META defineDocumentRelation (object, detail)
    @defineDocumentRelation(object, detail, );
END
META defineDocumentAbstractRelation (object, detail)
    @defineDocumentAbstractRelation(object, detail, );
END
META defineDocumentInterfaceRelation (object, detail)
    @defineDocumentInterfaceRelation(object, detail, );
END

META defineDocumentRelation (object)
    @defineDocumentRelation(object, object##Detail);
END
META defineDocumentAbstractRelation (object)
    @defineDocumentAbstractRelation(object, object##Detail);
END
META defineDocumentInterfaceRelation (object)
    @defineDocumentInterfaceRelation(object, object##Detail);
END

// Header
META defineDocumentHeaderCount (object, detail, caption)
    count###detail###object 'Количество строк в документе'###caption (iobject) =
        GROUP SUM 1 IF object###detail(idetail) == iobject BY iobject PERSISTENT IN documentSumGroup;
END

META defineDocumentHeaderCount (object, detail)
    @defineDocumentHeaderCount(object, detail, );
END

META defineDocumentHeaderCount (object)
    @defineDocumentHeaderCount(object, object##Detail);
END

// ----- Time ----- //
META defineDocumentHeaderTime (object)
    date###object 'Дата документа' = DATA DATE (object) IN documentHeaderGroup;
    date###object (object) <- currentDate() WHEN ASSIGNED(object AS object);

    time###object 'Время документа' = DATA TIME (object) IN documentHeaderGroup;
    time###object (object) <- currentTime() WHEN ASSIGNED(object AS object);

    dateTime###object 'Дата/время документа' (object) = toDateTime(date###object(object), time###object(object)) PERSISTENT;
END
META defineDocumentAbstractHeaderTime (object)
    date###object 'Дата документа' = ABSTRACT DATE (object) IN documentHeaderGroup PERSISTENT;
    time###object 'Время документа' = ABSTRACT TIME (object) IN documentHeaderGroup PERSISTENT;
    dateTime###object 'Дата/время документа' (object) = toDateTime(date###object(object), time###object(object)) PERSISTENT;
END
META defineDocumentInterfaceHeaderTime(object)
    @defineDocumentAbstractHeaderTime(object);
    @defineDocumentHeaderTime(user###object);
    date###object(object) += date##User###object(object);
    time###object(object) += time##User###object(object);
END

META defineDocumentDetailTime (object, detail)
    dateTime###detail 'Дата/время документа' (detail) = dateTime###object(object###detail(detail));
    date###detail 'Дата документа' = date###object(object###detail(detail));
    time###detail 'Время документа' = time###object(object###detail(detail));
END
META defineDocumentInterfaceDetailTime (object, detail)
    @defineDocumentDetailTime(object, detail);
    @defineDocumentDetailTime(user###object, user###detail);
END

META defineDocumentDetailTime (object)
    @defineDocumentDetailTime(object, object##Detail);
END
META defineDocumentInterfaceDetailTime (object)
    @defineDocumentInterfaceDetailTime(object, object##Detail);
END

META defineDocumentTime (object)
    @defineDocumentHeaderTime(object);
    @defineDocumentDetailTime(object);
END
META defineDocumentAbstractTime (object)
    @defineDocumentAbstractHeaderTime(object);
    @defineDocumentDetailTime(object);
END
META defineDocumentInterfaceTime (object)
    @defineDocumentInterfaceHeaderTime(object);
    @defineDocumentInterfaceDetailTime(object);
END

// Примечание

META defineDocumentHeaderNote (object)
    note###object 'Примечание' = DATA STRING[100] (object) IN additionalInfo MINCHARWIDTH 30 PREFCHARWIDTH 80 IN documentPrmGroup;
END
META defineDocumentAbstractHeaderNote (object)
    note###object 'Примечание' = ABSTRACT STRING[100] (object) IN additionalInfo MINCHARWIDTH 30 PREFCHARWIDTH 80 IN documentPrmGroup;
END
META defineDocumentInterfaceHeaderNote(object)
    @defineDocumentAbstractHeaderNote(object);
    @defineDocumentHeaderNote(user###object);
    note###object(object) += note##User###object(object);
END

META defineDocumentDetailNote (object)
    note###object##Detail 'Примечание' = DATA STRING[100] (object##Detail) MINCHARWIDTH 30 PREFCHARWIDTH 80;
END
META defineDocumentAbstractDetailNote (object)
    note###object##Detail 'Примечание' = ABSTRACT STRING[100] (object##Detail) MINCHARWIDTH 30 PREFCHARWIDTH 80;
END
META defineDocumentInterfaceDetailNote(object)
    @defineDocumentAbstractDetailNote(object);
    @defineDocumentDetailNote(user###object);
    note###object##Detail(object) += note##User###object##Detail(object);
END

// Posted

META defineDocumentHeaderPosted (object)
    isDraft###object 'Не проведен' (object) = object IS object AND NOT object IS object##Posted PERSISTENT;
    isPosted###object 'Проведен' (object) = object IS object##Posted PERSISTENT;

    prePost###object 'Провести' (object) = [ACTION CHANGECLASS object TO object##Posted ] (object)
                                    IF object IS object AND NOT object IS object##Posted;

    post###object 'Провести' (object) = [ACTION (object) NEWSESSION AUTOAPPLY { CHANGECLASS object TO object##Posted; } ] (object)
                                    IF object IS object AND NOT object IS object##Posted TOOLBAR CONFIRM;

    unpost###object 'Распровести' (object) = [ACTION (object) NEWSESSION AUTOAPPLY { CHANGECLASS object TO object; } ] (object)
                                      IF object IS object##Posted TOOLBAR CONFIRM;
END
META defineDocumentAbstractHeaderPosted (object)
    isPosted###object 'Проведен' (object) = ABSTRACT BOOLEAN(object) PERSISTENT;
    isDraft###object 'Не проведен' (object) = object IS object AND isPosted###object(object) PERSISTENT;
END
META defineDocumentInterfaceHeaderPosted (object)
    @defineDocumentAbstractHeaderPosted(object);
    @defineDocumentHeaderPosted(user###object);
    isPosted###object(object) += isPosted###user###object(object);
END

META defineDocumentDetailPosted (object, detail)
    isPosted###detail 'Проведен' (idetail) = isPosted###object(object###detail(idetail));
    isNotPosted###detail 'Не проведен' (idetail) = idetail IS detail AND NOT isPosted###detail(idetail);
END

META defineDocumentDetailPosted (object)
    @defineDocumentDetailPosted(object, object##Detail);
END

META defineDocumentPosted (object)
    @defineDocumentHeaderPosted(object);
    @defineDocumentDetailPosted(object);
END
META defineDocumentAbstractPosted (object)
    @defineDocumentAbstractHeaderPosted(object);
    @defineDocumentDetailPosted(object);
END
META defineDocumentInterfacePosted (object)
    @defineDocumentInterfaceHeaderPosted(object);
    @defineDocumentDetailPosted(object);
END

// Index

META defineDocumentDetailIndex (object, detail)
    index###detail 'Номер строки' (idetail) =
        PARTITION SUM 1 IF idetail IS detail BY object###detail(idetail)
        ORDER idetail IN recognizeGroup MINCHARWIDTH 4 PREFCHARWIDTH 4 IN recognizeGroup;
END
META defineDocumentInterfaceDetailIndex (object, detail)
    @defineDocumentDetailIndex(object, detail);
    @defineDocumentDetailIndex(user###object, user###detail);
END

META defineDocumentDetailIndex (object)
    @defineDocumentDetailIndex(object, object##Detail);
END
META defineDocumentInterfaceDetailIndex (object)
    @defineDocumentInterfaceDetailIndex(object, object##Detail);
END

META defineDocumentDetailActionsCustom (object, idetail)
    delete###idetail###object 'Очистить' = ACTION (object) {
        FOR object###idetail (detail) == object DO {
            EXEC delete(detail AS idetail);
        };
    } IN documentPrmGroup TOOLBAR CONFIRM;
END

META defineDocumentDetailActions (object)
    delete###object##Detail###object 'Очистить' = ACTION (object) {
        FOR object###object##Detail (detail) == object DO {
            EXEC delete(detail AS object##Detail);
        };
    } IN documentPrmGroup TOOLBAR CONFIRM;
END

// ------- Number ----- //

META defineDocumentAbstractHeaderNumber(object)
    number###object 'Номер' = ABSTRACT STRING[18] (object) IN numberedGroup MINCHARWIDTH 7;
    series###object 'Серия' = ABSTRACT STRING[2] (object) IN numberedGroup FIXEDCHARWIDTH 3;
    seriesNumber###object 'Серия/Номер' (object) = ustring2(series###object(object), number###object(object)) MINCHARWIDTH 7 PREFCHARWIDTH 10 MAXCHARWIDTH 20 PERSISTENT;
END
META defineDocumentInterfaceHeaderNumber(object)
    @defineDocumentAbstractHeaderNumber(object);
    number###object(object) += numberObject(object) IF object IS user###object;
    series###object(object) += seriesObject(object) IF object IS user###object;
END

META defineDocumentDetailNumber (object, idetail)
    number###idetail 'Номер' (detail) = numberObject(object###idetail(detail));
    series###idetail 'Серия' (detail) = seriesObject(object###idetail(detail));
    seriesNumber###idetail 'Серия/номер' (detail) = seriesNumberObject(object###idetail(detail));
END

META defineDocumentDetailNumber (object)
    @defineDocumentDetailNumber(object, object##Detail);
END

META defineDocumentAbstractNumber(object, detail)
    @defineDocumentAbstractHeaderNumber(object);
    @defineDocumentDetailNumber(object, detail);
END
META defineDocumentInterfaceNumber(object, detail)
    @defineDocumentInterfaceHeaderNumber(object);
    @defineDocumentDetailNumber(object, detail);
END

META defineDocumentAbstractNumber(object)
    @defineDocumentAbstractNumber(object, object##Detail);
END
META defineDocumentInterfaceNumber(object)
    @defineDocumentInterfaceNumber(object, object##Detail);
END

// ----- Description ----- //

META defineDocumentHeaderDescription(object, numberProp, caption)
    description###object 'Название документа' (object) =
        [FORMULA STRING[200]  ' CAST($1 AS TEXT) || \' № \' || CAST($2 AS TEXT) || \' от \' || CAST($3 AS TEXT)'](
        caption, numberProp(object), date###object(object)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
END
META defineDocumentAbstractHeaderDescription(object)
    description###object 'Название документа' (object) = ABSTRACT STRING[200] (object) MINCHARWIDTH 30 PREFCHARWIDTH 50 PERSISTENT;
END
META defineDocumentInterfaceHeaderDescription(object, numberProp, caption)
    @defineDocumentAbstractHeaderDescription(object);
    @defineDocumentHeaderDescription(user###object, numberProp, caption);
    description###object (object) += description###user###object(object);
END

META defineDocumentHeaderDescription(object, caption)
    @defineDocumentHeaderDescription(object, seriesNumberObject, caption);
END
META defineDocumentInterfaceHeaderDescription(object, caption)
    @defineDocumentInterfaceHeaderDescription(object, seriesNumberObject, caption);
END

META defineDocumentDetailDescription(object, detail)
    description###detail 'Название документа' (detail) = description###object(object###detail(detail)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
END

META defineDocumentDescription(object, detail, numberProp, caption)
    @defineDocumentHeaderDescription(object, numberProp, caption);
    @defineDocumentDetailDescription(object, detail);
END
META defineDocumentAbstractDescription(object, detail)
    @defineDocumentAbstractHeaderDescription(object);
    @defineDocumentDetailDescription(object, detail);
END
META defineDocumentInterfaceDescription(object, detail, numberProp, caption)
    @defineDocumentInterfaceHeaderDescription(object, numberProp, caption);
    @defineDocumentDetailDescription(object, detail);
END

META defineDocumentDescription (object, caption)
    @defineDocumentDescription(object, object##Detail, seriesNumberObject, caption);
END
META defineDocumentAbstractDescription (object)
    @defineDocumentAbstractDescription(object, object##Detail);
END
META defineDocumentInterfaceDescription (object, caption)
    @defineDocumentInterfaceDescription(object, object##Detail, seriesNumberObject, caption);
END

// ----- Is ----- //
META defineDocumentIs(object, detail)
    is###object (iobject) = iobject IS object;
    is###detail (idetail) = idetail IS detail;
END
META defineDocumentInterfaceIs(object, detail)
    @defineDocumentIs(object, detail);
    @defineDocumentIs(user###object, user###detail);
END

META defineDocumentIs(object)
    @defineDocumentIs(object, object##Detail);
END
META defineDocumentInterfaceIs(object)
    @defineDocumentInterfaceIs(object, object##Detail);
END

META defineDocument (object)
    @defineDocumentTables(object);

    @defineDocumentRelation(object);
    @defineDocumentDetailIndex(object);

    @defineDocumentTime(object);
    @defineDocumentHeaderNote(object);

    @defineDocumentIs(object);

    @defineDocumentDetailActions(object);
END
META defineDocumentAbstract (object)
    @defineDocumentAbstractTables(object);

    @defineDocumentAbstractRelation(object);
    @defineDocumentDetailIndex(object);

    @defineDocumentAbstractTime(object);
    @defineDocumentAbstractHeaderNote(object);

    @defineDocumentIs(object);
END
META defineDocumentInterface (object)
    @defineDocumentInterfaceTables(object);

    @defineDocumentInterfaceRelation(object);
    @defineDocumentInterfaceDetailIndex(object);

    @defineDocumentInterfaceTime(object);
    @defineDocumentInterfaceHeaderNote(object);

    @defineDocumentInterfaceIs(object);

    @defineDocumentDetailActions(user###object);
END

// ----------------------------- количество упаковок и вес товаров ----------------------- //
META defineDocumentDetailPackWeightSku (object, skuProp)
    packQuantity###object##Detail 'Количество грузовых мест' (idetail) = round0(quantity###object##Detail(idetail)/
        UNION OVERRIDE 1 IF idetail IS object##Detail, quantityPack###skuProp(skuProp###object##Detail(idetail)));

    grossWeight###object##Detail 'Масса груза, т.' (idetail) = round6(
        netWeight###skuProp(skuProp###object##Detail(idetail))*quantity###object##Detail(idetail)/1000);
END
META defineDocumentDetailPackWeightSku (object)
    @defineDocumentDetailPackWeightSku (object, sku);
END
//----------- Изменение кол-ва для строки при изменении кол-ва для товара---------------//
META defineDocumentSkuStockCustom(object, detail, skuProp, stockProp)
    detail###Sku###object###Stock (sku, object, stock) =  GROUP MAX detail
        BY skuProp###detail(detail), object###detail(detail), stockProp###detail(detail);
    detail###Sku###object (sku, object) = detail###Sku###object###Stock(sku, object, stockProp###object(object));
    
    quantitySku###object###Stock 'Кол-во товара в документе' (sku, object, stock) = GROUP SUM quantity###detail(detail)
        BY skuProp###detail(detail), object###detail(detail), stockProp###detail(detail);
    
    priceSkuStock###object 'Цена учетная' (sku, stock, object) = priceSkuStockDateTime(sku, stock, dateTime###object(object));
    
    
    changeQuantitySku###object###Stock = ACTION (sku, object, stock) {
        REQUEST NUMERIC[14,3] INPUT;
        IF detail###Sku###object###Stock(sku, object, stock) THEN {
            IF requestedNumeric() THEN {
                SET quantity###detail(detail) <- requestedNumeric() WHERE detail == detail###Sku###object###Stock(sku, object, stock);
            } ELSE {
                FOR object###detail(detail) == object AND stockProp###detail(detail) == stock AND skuProp###detail(detail) == sku DO {
                    EXEC delete(detail);
                }
            }
        } ELSE {
            ADDOBJ detail;
            FOR d == addedObject() DO {
               SET object###detail(d) <- object;
               SET stockProp###detail (d) <- stock;
               SET skuProp###detail(d) <- sku;
               SET quantity###detail (d) <- requestedNumeric();
            }
        }
    }
END
META defineDocumentSkuStock(object, skuProp, stockProp)
    @defineDocumentSkuStockCustom(object, object##Detail, skuProp, stockProp);
END
META defineDocumentSkuStock(object)
    @defineDocumentSkuStock(object, sku, stock);
END

META extendDocumentFormSkuStockCustom(object, form, concrete, stockProp)

    EXTEND FORM form

        TREE stockTree sg = stockGroup PARENT parentStockGroup, ts = stock
        PROPERTIES READONLY sgTreeName = name(sg), tsTreeName = name(ts)
        FILTERS stockGroupStock(ts) == sg

        TREE skuTree sk = skuGroup PARENT parentSkuGroup
        PROPERTIES READONLY skuTreeName = name(sk)
        ORDER BY skuTreeName

        OBJECTS           sts=(st=stock, s=sku)
        PROPERTIES        READONLY nameSku(s), stockName = name(st), idBarcodeSku(s), shortNameUOMSku(s)

        FILTERS           isParentSkuGroupSku(sk, s),
                          st == ts AND sg IS stockGroup AND NOT stock###object(concrete) OR
                          isParentStockGroupStock(sg, st) AND NOT ts AND NOT stock###object(concrete) OR
                          st == ts AND sg IS stockGroup AND stock###object(concrete)== st

        ORDER BY          nameSku, stockName

        PROPERTIES        currentBalanceSkuStock(s, st) READONLY, priceSkuStock###object(s, st, concrete) READONLY, quantitySku###object###Stock(s, concrete, st) ON CHANGE EXEC changeQuantitySku###object###Stock(s, concrete, st)

        FILTERGROUP filter
            FILTER 'Товары с остатком ' 'F10' currentBalanceSkuStock(s, st) DEFAULT
            FILTER 'Товары в заказе ' 'F9' quantitySku###object###Stock(s, concrete, st)
    ;

    EXTEND DESIGN form {
        specification.box {
            type = TABBED;
            NEW itemBox {
                childConstraints = TO THE RIGHT;
                title = 'Товар';
                type = SPLITH;

                NEW firstBottom {
                    type = SPLITV;
                    ADD stockTree.tree.box {title = 'Склады'; fillVertical = 1;};
                    ADD skuTree.tree.box {title = 'Группы SKU'; fillVertical = 1;};
                    fillHorizontal = 1;
                }
                ADD sts.box {
                    fillHorizontal = 2;
                }
            }
        }
    }
END
META extendDocumentFormSkuStock(object, form, concrete)
    @extendDocumentFormSkuStockCustom(object, form, concrete, stock);
END

//--

META defineDocumentSkuCustom(object, detail, skuProp, stockProp)
    detail###Sku###object###Stock (sku, object, stock) =  GROUP MAX detail
        BY skuProp###detail(detail), object###detail(detail), stockProp###detail(detail);
    detail###Sku###object (sku, object) = detail###Sku###object###Stock(sku, object, stockProp###object(object));

    currentBalanceSku###object 'Остаток' (sku, object) = currentBalanceSkuStock(sku, stockProp###object(object));
    priceSku###object 'Цена учетная' (sku, object) = priceSkuStockDateTime(sku, stockProp###object(object), dateTime###object(object));

    changeQuantitySku###object = ACTION (sku, object) {
        REQUEST NUMERIC[14,3] INPUT;
        IF detail###Sku###object(sku, object) THEN {
            IF requestedNumeric() THEN {
                SET quantity###detail(detail) <- requestedNumeric() WHERE detail == detail###Sku###object(sku, object);
            } ELSE {
                FOR object###detail(detail) == object AND stockProp###detail(detail) == stockProp###object(object) AND skuProp###detail(detail) == sku DO {
                    EXEC delete(detail);
                }
            }
        } ELSE {
            ADDOBJ detail;
            FOR d == addedObject() DO {
               SET object###detail(d) <- object;
               SET stockProp###detail (d) <- stockProp###object(object);
               SET skuProp###detail(d) <- sku;
               SET quantity###detail (d) <- requestedNumeric();
            }
        }
    }
END
META defineDocumentSku(object, skuProp, stockProp)
    @defineDocumentSkuCustom(object, object##Detail, skuProp, stockProp);
END
META defineDocumentSku(object)
    @defineDocumentSku(object, sku, stock);
END

META extendDocumentFormSkuCustom(object, form, concrete, skuProp, detail)

    EXTEND FORM form

        TREE skuTree sk = skuGroup PARENT parentSkuGroup
        PROPERTIES READONLY skuTreeName = name(sk)
        ORDER BY skuTreeName

        OBJECTS s=sku
        PROPERTIES             READONLY    inputName = nameSku(s)
        PROPERTIES(s)          READONLY idBarcodeSku, shortNameUOMSku
        PROPERTIES(s, concrete)       currentBalanceSku###object READONLY, priceSku###object READONLY, quantity###detail###skuProp###object ON CHANGE EXEC changeQuantitySku###object(s, concrete)

        FILTERS                isParentSkuGroupSku(sk, s)
        ORDER BY inputName

        FILTERGROUP filter
            FILTER 'Товары с остатком ' 'F10' currentBalanceSku###object(s, concrete) DEFAULT
            FILTER 'Товары в заказе ' 'F9' quantity###detail###skuProp###object(s, concrete)
    ;

    EXTEND DESIGN form {
        specification.box {
            type = TABBED;
            NEW itemBox {
                childConstraints = TO THE RIGHT;
                title = 'Товар';
                type = SPLITH;

                ADD skuTree.tree.box {
                    title = 'Группы SKU';
                    fillHorizontal = 1;
                }
                ADD s.box {
                    fillHorizontal = 2;
                }
            }
        }
    }
END
META extendDocumentFormSku(object, form, concrete)
    @extendDocumentFormSkuCustom(object, form, concrete, sku, object##Detail);
END

// ------------------------- Основной - агрегированный документы ---------------------------------- //

META defineDocumentAggregationHeader(primObject, aggrObject, aggrProperty)
    @defineAggregation (primObject, aggrObject, aggrProperty);
    @defineDocumentAggregationHeaderNote(primObject, aggrObject);
    @defineDocumentAggregationHeaderTime(primObject, aggrObject);
END

META defineDocumentAggregationDetail (primObject, aggrObject, aggrProperty)
    @defineAggregation(primObject##Detail, aggrObject##Detail, aggrProperty##Detail);
    aggrObject###aggrObject##Detail (detail) = aggrObject###primObject(primObject###primObject##Detail(primObject##Detail###aggrObject##Detail(detail)));

    @defineDocumentAggregationDetailTime(primObject, aggrObject);
END

META defineDocumentAggregation (primObject, aggrObject, aggrProperty)
    @defineDocumentAggregationHeader(primObject, aggrObject, aggrProperty);
    @defineDocumentAggregationDetail(primObject, aggrObject, aggrProperty);
    @defineDocumentHeaderCount(aggrObject);
END

// Time
META defineDocumentAggregationHeaderTime (primObject, aggrObject)
    date###aggrObject 'Дата' (object) = date###primObject(primObject###aggrObject(object));
    time###aggrObject 'Время' (object) = time###primObject(primObject###aggrObject(object));
    dateTime###aggrObject 'Дата/время' (object) = dateTime###primObject(primObject###aggrObject(object));
END

META defineDocumentAggregationDetailTime (primObject, aggrObject)
    date###aggrObject##Detail 'Дата' (detail) = date###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
    time###aggrObject##Detail 'Время' (detail) = time###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
    dateTime###aggrObject##Detail 'Дата/время' (detail) = dateTime###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
END

META defineDocumentAggregationTime (primObject, aggrObject)
    @defineDocumentAggregationHeaderTime(primObject, aggrObject);
    @defineDocumentAggregationDetailTime(primObject, aggrObject);
END

// Posted
META defineDocumentAggregationHeaderPosted (primObject, aggrObject)
    isPosted###aggrObject 'Проведен' (object) = isPosted###primObject(primObject###aggrObject(object));
END

META defineDocumentAggregationDetailPosted (primObject, aggrObject)
    isPosted###aggrObject##Detail 'Проведен' (detail) = isPosted###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
END

META defineDocumentAggregationPosted (primObject, aggrObject)
    @defineDocumentAggregationHeaderPosted(primObject, aggrObject);
    @defineDocumentAggregationDetailPosted(primObject, aggrObject);
END

// Note
META defineDocumentAggregationHeaderNote (primObject, aggrObject)
    note###aggrObject 'Примечание' (object) = note###primObject(primObject###aggrObject(object));
END

// Number
META defineDocumentAggregationHeaderNumberCustom (primObject, aggrObject, suffix)
    number###aggrObject 'Номер документа' (object) = number###suffix(primObject###aggrObject(object));
    series###aggrObject 'Серия документа' (object) = series###suffix(primObject###aggrObject(object));
    seriesNumber###aggrObject 'Серия/номер документ' (object) = seriesNumber###suffix(primObject###aggrObject(object));
END

META defineDocumentAggregationHeaderNumber (primObject, aggrObject)
    @defineDocumentAggregationHeaderNumberCustom (primObject, aggrObject, Object);
END

META deriveDocumentAggregationHeaderNumber (primObject, aggrObject)
    initValueNumberObject (aggrObject) += [PREV(numberObject(aggrObject))](primObject###aggrObject(aggrObject));
    initValueSeriesObject (aggrObject) += [PREV(seriesObject(aggrObject))](primObject###aggrObject(aggrObject));

    initWhenNumberedObject(aggrObject) += CHANGED(primObject###aggrObject(aggrObject));
END

// Description
META defineDocumentAggregationHeaderDescription (primObject, aggrObject)
    description###aggrObject 'Название документа' (object) = description###primObject(primObject###aggrObject(object));
END
