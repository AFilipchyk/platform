MODULE Document;

REQUIRE System;

CLASS ABSTRACT postedObject 'Объект с возможностью закрытия';
TABLE postedObject (postedObject);

GROUP postedGroup 'Информация о закрытии' : baseGroup;

timeClosed 'Время закрытия' = DATA DATETIME (postedObject) IN postedGroup;
userClosed 'Закрыт пользователем' = DATA customUser (postedObject) IN idGroup;
computerClosed 'Закрыт на компьютере' = DATA computer (postedObject) IN idGroup;

nameUserClosed 'Закрыт пользователем' (postedObject) = commonName(userClosed(postedObject)) IN postedGroup;
hostnameComputerClosed 'Закрыт на компьютере' (postedObject) = hostname(computerClosed(postedObject)) IN postedGroup;

timeClosed(postedObject) <- currentDateTime() WHEN ASSIGNED(postedObject IS postedObject);
userClosed(postedObject) <- currentUser() WHEN ASSIGNED(postedObject IS postedObject);
computerClosed(postedObject) <- currentComputer() WHEN ASSIGNED(postedObject IS postedObject);

GROUP documentHeaderGroup 'Шапка документа' : baseGroup;
GROUP documentSumGroup 'Суммы документа': publicGroup;
GROUP documentPrmGroup 'Параметры документа': publicGroup;

META defineDocumentTables (object)
    TABLE object (object);
    TABLE object##Detail (object##Detail);
END

META defineDocumentRelationCustom (object, detail)
    object###detail = DATA object (detail) IN idGroup;
    in###object###detail(iobject, idetail) = object###detail(idetail) == iobject;

    @defineDocumentHeaderCountCustom(object, detail);
END

META defineDocumentRelation (object)
    @defineDocumentRelationCustom(object, object##Detail);
END

META defineDocumentHeaderCountCustom (object, detail)
    count###detail###object 'Количество строк в документе' (iobject) =
        GROUP SUM 1 IF object###detail(idetail) == iobject BY iobject PERSISTENT IN documentSumGroup;
END

META defineDocumentHeaderCount (object)
    @defineDocumentHeaderCountCustom(object, object##Detail);
END
// Время

META defineDocumentHeaderTime (object)
    date###object 'Дата документа' = DATA DATE (object) IN documentHeaderGroup;
    date###object (object) <- currentDate() WHEN ASSIGNED(object AS object);

    time###object 'Время документа' = DATA TIME (object) IN documentHeaderGroup;
    time###object (object) <- currentTime() WHEN ASSIGNED(object AS object);

    dateTime###object 'Дата/время документа' (object) = toDateTime(date###object(object), time###object(object));
END

META defineDocumentDetailTimeCustom (object, detail)
    dateTime###detail 'Дата/время документа' (detail) = dateTime###object(object###detail(detail));
    date###detail 'Дата документа' = date###object(object###detail(detail));
    time###detail 'Время документа' = time###object(object###detail(detail));
END

META defineDocumentDetailTime (object)
    @defineDocumentDetailTimeCustom(object, object##Detail);
END

META defineDocumentTime (object)
    @defineDocumentHeaderTime(object);
    @defineDocumentDetailTime(object);
END

// Отдел магазина

META defineDocumentHeaderStock (object, stockClass, stockCaption)
    stockClass###object = DATA stockClass(object) AUTOSET;
    name###stockClass###object stockCaption (object) = name(stockClass###object(object)) IN documentHeaderGroup
            MINCHARWIDTH 20 PREFCHARWIDTH 40;;
END

META defineDocumentDetailStockCustom (object, detail, stockClass, stockCaption)
    stockClass###detail (idetail) = stockClass###object(object###detail(idetail));
    name###stockClass###detail stockCaption (idetail) = name(stockClass###detail(idetail)) MINCHARWIDTH 20 PREFCHARWIDTH 20;
END
META defineDocumentDetailStock (object, stockClass, stockCaption)
    @defineDocumentDetailStockCustom (object, object##Detail, stockClass, stockCaption);
END

META defineDocumentStock (object, stockClass, stockCaption)
    @defineDocumentHeaderStock(object, stockClass, stockCaption);
    @defineDocumentDetailStock(object, stockClass, stockCaption);
END

// Примечание

META defineDocumentHeaderNote (object)
    note###object 'Примечание' = DATA STRING[100] (object) IN additionalInfo MINCHARWIDTH 30 PREFCHARWIDTH 80 IN documentPrmGroup;
END

META defineDocumentDetailNote (object)
    note###object##Detail 'Примечание' = DATA STRING[100] (object##Detail) MINCHARWIDTH 30 PREFCHARWIDTH 80;
END

// Проведение

META defineDocumentHeaderPosted (object)

    isDraft###object 'Открыт' (object) = object IS object AND NOT object IS object##Posted PERSISTENT;
    isPosted###object 'Закрыт' (object) = object IS object##Posted PERSISTENT;

    post###object 'Провести' (object) = [ACTION (object) NEWSESSION AUTOAPPLY { CHANGECLASS object TO object##Posted; } ] (object)
                                    IF object IS object AND NOT object IS object##Posted TOOLBAR CONFIRM;

    unpost###object 'Распровести' (object) = [ACTION (object) NEWSESSION AUTOAPPLY { CHANGECLASS object TO object; } ] (object)
                                      IF object IS object##Posted TOOLBAR CONFIRM;
END

META defineDocumentDetailPostedCustom (object, detail)
    isPosted###detail 'Закрыт' (idetail) = isPosted###object(object###detail(idetail));
    isNotPosted###detail 'Открыт' (idetail) = idetail IS detail AND NOT isPosted###detail(idetail);
END

META defineDocumentDetailPosted (object)
    @defineDocumentDetailPostedCustom(object, object##Detail);
END

META defineDocumentPosted (object)
    @defineDocumentHeaderPosted(object);
    @defineDocumentDetailPosted(object);
END

META defineDocumentDetailIndexCustom (object, detail)
    index###detail 'Номер строки' (idetail) =
        PARTITION SUM 1 IF idetail IS detail BY object###detail(idetail)
        ORDER idetail IN recognizeGroup MINCHARWIDTH 3 PREFCHARWIDTH 3;
END

META defineDocumentDetailIndex (object)
    @defineDocumentDetailIndexCustom(object, object##Detail);
END

META defineDocumentDetailActions (object)
    delete###object##Detail###object 'Очистить' = ACTION (object) {
        FOR object###object##Detail (detail) == object DO {
            EXEC delete(detail AS object##Detail);
        };
    } IN documentPrmGroup TOOLBAR CONFIRM;
END

META defineDocumentDetailNumbered (object)
    number###object##Detail 'Номер' (detail) = numberObject(object###object##Detail(detail));
    series###object##Detail 'Серия' (detail) = seriesObject(object###object##Detail(detail));
    seriesNumber###object##Detail 'Серия/номер' (detail) = seriesNumberObject(object###object##Detail(detail));
END

META defineDocumentAggregation (primObject, aggrObject, aggrProperty)
    @defineAggregation(primObject, aggrObject, aggrProperty);
    @defineAggregation(primObject##Detail, aggrObject##Detail, aggrProperty##Detail);

    aggrObject###aggrObject##Detail (detail) = unique###aggrObject(primObject###primObject##Detail(primObject##Detail###aggrObject##Detail(detail)));
END

META defineDocumentDetailQuantityPrefix (object, prefix, caption)
    prefix###quantity###object##Detail 'Кол-во'###caption = DATA NUMERIC[14,3] (object##Detail);
END
META defineDocumentDetailQuantity (object)
    @defineDocumentDetailQuantityPrefix(object, , );
END

META defineDocumentHeaderQuantityCustomPrefix (object, detail, prefix, caption)
    prefix###quantity###detail###object 'Кол-во (всего)'###caption (object) = GROUP SUM prefix###quantity###detail(idetail) BY object###detail(idetail) IN documentSumGroup PERSISTENT;
END
META defineDocumentHeaderQuantityCustom (object, detail)
    @defineDocumentHeaderQuantityCustomPrefix(object, detail, , );
END
META defineDocumentHeaderQuantityPrefix (object, prefix, caption)
    @defineDocumentHeaderQuantityCustomPrefix(object, object##Detail, prefix, caption);
END
META defineDocumentHeaderQuantity (object)
    @defineDocumentHeaderQuantityCustomPrefix(object, object##Detail, , );
END

META defineDocumentBase (object)
    @defineDocumentTables(object);

    @defineDocumentRelation(object);
    @defineDocumentDetailIndex(object);

    @defineDocumentTime(object);
    @defineDocumentHeaderNote(object);

    @defineDocumentDetailActions(object);
END

META defineDocumentBasePosted (object)
    @defineDocumentBase(object);

    @defineDocumentPosted(object);
END

// ------------------------- Основной - агрегированный документы ---------------------------------- //

META defineDocumentHeaderAggregationNumberCustom (primObject, aggrObject, suffix)
    number###aggrObject 'Номер документа' (object) = number###suffix(primObject###aggrObject(object));
    series###aggrObject 'Серия документа' (object) = series###suffix(primObject###aggrObject(object));
    seriesNumber###aggrObject 'Серия/номер документ' (object) = seriesNumber###suffix(primObject###aggrObject(object));
END

META defineDocumentHeaderAggregationNumber (primObject, aggrObject)
    @defineDocumentHeaderAggregationNumberCustom (primObject, aggrObject, Object);
END

META defineDocumentHeaderAggregationDescription (primObject, aggrObject)
    description###aggrObject 'Название документа' (object) = description###primObject(primObject###aggrObject(object));
END

META defineDocumentHeaderAggregation (primObject, aggrObject)
    date###aggrObject 'Дата' (object) = date###primObject(primObject###aggrObject(object));
    time###aggrObject 'Время' (object) = time###primObject(primObject###aggrObject(object));
    dateTime###aggrObject 'Дата/время' (object) = dateTime###primObject(primObject###aggrObject(object));

    isPosted###aggrObject 'Закрыт' (object) = isPosted###primObject(primObject###aggrObject(object));
END

META defineDocumentDetailAggregationHeader (primObject, aggrObject)
    isPosted###aggrObject##Detail 'Закрыт' (detail) = isPosted###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));

    date###aggrObject##Detail 'Дата' (detail) = date###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
    dateTime###aggrObject##Detail 'Дата/время' (detail) = dateTime###primObject##Detail(primObject##Detail###aggrObject##Detail(detail));
END
