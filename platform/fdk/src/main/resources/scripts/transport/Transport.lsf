MODULE Transport;

REQUIRE System,
        Utils,
        Employee,
        LegalEntity,
        Stock;

// Модели машины
CLASS truckModel 'Модель': named;

FORM truckModel 'Модель машины'
    OBJECTS m=truckModel FIXED PANEL
    PROPERTIES (m) name

    EDIT truckModel OBJECT m
;

FORM truckModels 'Модели машин'
    OBJECTS m=truckModel
    PROPERTIES(m) READONLY name
    PROPERTIES(m) ADDFORM, EDITFORM, delete FORCE PANEL TOOLBAR

    DIALOG truckModel OBJECT m
;

// Машины
CLASS truck 'Машина';
TABLE truck(truck);

sidTruck 'Номер' = DATA STRING[20](truck);
truckModelTruck = DATA truckModel(truck);
nameTruckModelTruck 'Марка' (truck) = name(truckModelTruck(truck));

nameTruck 'Наименование' (truck) = [FORMULA STRING[70] ' CAST($1 AS TEXT) || \' \' || CAST($2 AS TEXT)'](nameTruckModelTruck(truck), sidTruck(truck));

trailerTruck 'Прицеп' = DATA STRING[100](truck);
weightTruck 'Грузоподьёмность (тонн)' = DATA NUMERIC[14,2](truck);
ownerTruck 'Владелец' = DATA STRING[100](truck);

FORM truck 'Машина'
    OBJECTS t=truck FIXED PANEL
    PROPERTIES (t) sidTruck, nameTruckModelTruck, nameTruck, trailerTruck, ownerTruck, weightTruck

    EDIT truck OBJECT t
;

FORM trucks 'Машины'
    OBJECTS t=truck
    PROPERTIES(t) READONLY sidTruck, nameTruckModelTruck, nameTruck, trailerTruck, ownerTruck, weightTruck
    PROPERTIES(t) ADDFORM, EDITFORM, delete FORCE PANEL TOOLBAR

    DIALOG truck OBJECT t
;

// Водители

CLASS driver 'Водитель': employee;

sidDriver 'Табельный номер' = DATA STRING[10](driver);
typeDriver 'Класс' = DATA STRING[10](driver);

FORM driver 'Водитель'
    OBJECTS d=driver FIXED PANEL
    PROPERTIES (d) name, userFirstName, userLastName, commonName, sidDriver, typeDriver

    EDIT driver OBJECT d
;

FORM drivers 'Водители'
    OBJECTS d=driver
    PROPERTIES(d) READONLY name, userFirstName, userLastName, commonName, sidDriver, typeDriver
    PROPERTIES(d) ADDFORM, EDITFORM, delete FORCE PANEL TOOLBAR

    DIALOG driver OBJECT d
;

NAVIGATOR {
    NEW transportNavigator 'Транспорт' BEFORE administration TO toolbar IMAGE '/images/truck.png' {
        NEW transportDocuments 'Документы';
        NEW transportMasterData 'Справочники' {
            ADD drivers;
            ADD trucks;
            ADD truckModels;
        }
    }
}