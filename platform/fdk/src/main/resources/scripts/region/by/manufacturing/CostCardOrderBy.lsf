MODULE  CostCardOrderBy;

REQUIRE PricingProductionOutput;
NAMESPACE Production;

// ----------------  Калькуляционная карта ------------------- //

calcMultiplierSkuOrder(sku, order)= (1.000 IF sku IS sku AND order IS order) OR  multiplierSkuOrder(sku, order);

quantityBOMSkuOrder (BOM, sku, order) = [GROUP SUM recBruttoNodeQuantityBOMSkuDate (BOM, sku, dateOrder(order)) * calcMultiplierSkuOrder(sku, order)
    BY BOM, sku, order](BOM, sku, order) AND bruttoQuantitySkuOrder(sku, order);
quantityProductDetailSku 'Норма' (productDetail, sku) = quantityBOMSkuOrder(BOMProduct(productProductDetail(productDetail)), sku, orderProductDetail(productDetail));

totalPriceSkuOrder (sku, order) = priceSkuOrder(sku,order) OR priceSubstitutesSkuOrder(sku,order);
totalPremiumPriceSkuOrder 'Цена компонента с надбавкой'(sku, order) = totalPriceSkuOrder(sku,order)*multiplierCalcSkuOrder(sku, order);
//--
quantityProductSkuOrder = [GROUP SUM recPricePercentProductSkuDate (product, sku, dateOrder(order))* calcMultiplierSkuOrder(sku, order)/100 BY product, sku, order](product, sku, order) AND bruttoQuantitySkuOrder(sku, order);
quantityProductProductDetailSku 'Норма с учетом коэфф.цены'(productDetail, sku) = quantityProductSkuOrder(productProductDetail(productDetail), sku, orderProductDetail(productDetail))*quantityProductDetail(productDetail);
totalSumProductProductDetailSku 'Сумма компонента с надбавками' (productDetail, sku)= quantityProductProductDetailSku (productDetail, sku)*totalPriceSkuOrder (sku, orderProductDetail(productDetail))*multiplierCalcSkuOrder(sku, orderProductDetail(productDetail));

componentsPriceProductDetailSku 'Цена расчетная (с/с)' (productDetail,sku)=  quantityProductProductDetailSku(productDetail, sku) *totalPriceSkuOrder (sku, orderProductDetail(productDetail));

//--
totalVATSumProductDetailSku 'Сумма НДС' (productDetail, sku)=  totalSumProductProductDetailSku (productDetail, sku)*valueVATSkuOrder(sku, orderProductDetail(productDetail))/(100+valueVATSkuOrder(sku, orderProductDetail(productDetail)));
VATSumProductDetail 'Сумма НДС компонентов' (productDetail) = GROUP SUM totalVATSumProductDetailSku (productDetail, sku) BY productDetail;
//--
descriptionBOMProductDetail 'Спецификация'(productDetail) = descriptionBOM(BOMProduct(productProductDetail(productDetail)));
quantityProductProductDetail 'Кол-во в спец.'(productDetail)= quantityProduct(productProductDetail(productDetail));
shortNameUOMProductProductDetail 'Ед.изм. в спец.'(productDetail)= shortNameUOMProduct(productProductDetail(productDetail));

//    numberCostCardProductDetail 'Номер кальк.карты' (productDetail) =
//        [FORMULA STRING[200]  ' CAST($1 AS TEXT) || \' № \' || CAST($2 AS TEXT) || \' от \' || CAST($3 AS TEXT)'](
//        seriesNumberObject(orderProductDetail(productDetail)), date###object(object)) MINCHARWIDTH 30 PREFCHARWIDTH 50;
//--
calcPremiumComponentsSumProductDetail(productDetail) = quantityProductDetail(productDetail)*calcPremiumComponentsPriceProductDetail(productDetail);
calcVATComponentsSumProductDetail(productDetail) = quantityProductDetail(productDetail)*VATSumProductDetail(productDetail);
//--
retailMarkupPriceProductDetail 'Цена надбавки' (productDetail)= retailMarkupSumProductDetail(productDetail)/quantityProductDetail(productDetail);
retailVATPriceProductDetail 'Цена НДС' (productDetail)= retailVATSumProductDetail(productDetail)/quantityProductDetail(productDetail);

componentsSumProductProductDetail(productDetail)= componentsPriceProductDetail(productDetail)*quantityProductDetail(productDetail);

FORM costCardOrderBy 'Калькуляционная карта' PRINT

    OBJECTS o = order FIXED PANEL
    PROPERTIES(o) objectClassName, nameGoodsLegalEntityStockOrder,
                  nameComponentsStockOrder, nameGoodsStockOrder, nameNumeratorObject, numberObject, seriesObject, seriesNumberObject,
                  dateOrder, timeOrder, fromDateOrder, toDateOrder, noteOrder, quantityProductDetailOrder, productsSumProductDetailOrder, overQuantityComponentDetailOrder
    PROPERTIES(o) nameCalcPriceListTypeOrder

    OBJECTS pd=productDetail
    PROPERTIES(pd) indexProductDetail, idBarcodeSkuProductDetail, descriptionBOMProductDetail, quantityProductProductDetail, shortNameUOMProductProductDetail, nameSkuProductDetail,  nameProductProductDetail, shortNameUOMProductDetail, quantityProductDetail,
                   componentsPriceProductDetail, componentsSumProductProductDetail
    PROPERTIES(pd) markupProductDetail, productsPriceProductDetail, productsSumProductDetail, densityPremiumVATComponentsPriceProductDetail,
                   VATSumProductDetail, calcPremiumVATComponentsPriceProductDetail, calcPremiumComponentsPriceProductDetail, calcPremiumComponentsSumProductDetail,
                   calcVATComponentsSumProductDetail,
                   retailMarkupPriceProductDetail, retailMarkupSumProductDetail, retailVATPriceProductDetail, retailVATSumProductDetail

    FILTERS orderProductDetail(pd)==o

    OBJECTS s = sku
    PROPERTIES(pd,s)quantityProductDetailSku, quantityProductProductDetailSku, componentsPriceProductDetailSku, totalSumProductProductDetailSku, totalVATSumProductDetailSku
    PROPERTIES(s,o) nameSkuSkuOrder, idBarcodeSkuSkuOrder, shortNameUOMSkuSkuOrder, totalPriceSkuOrder, valueVATSkuOrder, markupSkuOrder, totalPremiumPriceSkuOrder

    FILTERS quantityProductDetailSku(pd,s)

;

printCostCardOrder 'Калькуляционная карта' (order) = ACTION FORM costCardOrderBy OBJECTS o IMAGE 'print.png' IN printGroup;
EXTEND FORM orders
    PROPERTIES(o) FORCE PANEL printCostCardOrder //SHOWIF createPricingOrder(o)
;
EXTEND DESIGN orders {printContainer { ADD o.printGroup;}}

NAVIGATOR {
    manufacturingDocuments {
        ADD costCardOrderBy;
    }
}
