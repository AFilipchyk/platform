MODULE Ware;

REQUIRE System, Historizable, Tax, Stock;

// ------------------------------------------ Глобальные настройки --------------------------------- //
showWare 'Показывать стеклопосуду' = DATA BOOLEAN ();
EXTEND FORM options
    PROPERTIES() showWare
;
EXTEND DESIGN options {
    commons {
        ADD PROPERTY(showWare);
    }
}

// ----------------------------------------- Группа посуды ----------------------------- //
CLASS wareGroup 'Группа посуды' : named, externalizable, skuGroup;

@defineHierarchy(wareGroup);

parentSkuGroup (wareGroup) += parentWareGroup(wareGroup);

// ------------------------------------------ Посуда ----------------------------------------------- //
CLASS ware 'Посуда' : named, externalizable, sku;
TABLE ware(ware);

wareGroupWare = DATA wareGroup(ware) AUTOSET;
skuGroupSku(ware) += wareGroupWare(ware);

nameSku(ware) += name(ware) IF ware IS ware;

nameWareGroupWare 'Группа посуды' (ware) = name(wareGroupWare(ware));
canonicalNameWareGroupWare 'Группа посуды' (ware) = canonicalNameWareGroup(wareGroupWare(ware)) IN base;

TABLE wareGroupWare (wareGroup, ware);

isParentWareGroupware (wareGroup, ware) = isParentWareGroupWareGroup(wareGroupWare(ware), wareGroup) PERSISTENT;

TABLE wareDate(ware, DATE);

@defineHistorizable(warePrice, 'Цена', NUMERIC[14,2], ware, name, base);

@defineHistorizableCustom(rangeWare, 'НДС', range, numberRange, ware, name, base);

CONSTRAINT taxRange(dataRangeWareDate(ware, date)) != tax.taxVAT CHECKED BY dataRangeWareDate MESSAGE 'ошибка: Шкала должна соответствовать шкале НДС';

dataValueRangeWareDate 'НДС, %' (ware, date) = valueRateRangeDate(dataRangeWareDate(ware, date), date) IN base;
valueCurrentRateRangeWare 'НДС, %' (ware) = valueCurrentRateRange(rangeWare(ware)) IN base;

EXTEND FORM addRangeWare
    PROPERTIES READONLY dataValueRangeWareDate(a, d)
;
EXTEND FORM dialogRangeWare
    PROPERTIES dataValueRangeWareDate(a, d)
;

FORM ware 'Посуда'
    OBJECTS w=ware FIXED PANEL
    PROPERTIES(w) name, canonicalNameWareGroupWare, warePrice, dialogWarePrice
    PROPERTIES(w) numberRangeRangeWare, valueCurrentRateRangeWare, dialogRangeWare

    EDIT ware OBJECT w
;

FORM wares 'Посуда'
    OBJECTS w=ware
    PROPERTIES(w) READONLY name, canonicalNameWareGroupWare, warePrice, dialogWarePrice, numberRangeRangeWare, valueCurrentRateRangeWare
    PROPERTIES(w)          ADDFORM, EDITFORM, delete

    DIALOG ware OBJECT w
;

NAVIGATOR {
    skuNavigator {
        ADD wares AFTER UOMs;
    }
}