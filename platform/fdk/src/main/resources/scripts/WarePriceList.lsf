MODULE WarePriceList;

REQUIRE PriceList, Ware, WareItem;

NAMESPACE PriceList;

CLASS warePriceListType 'Цена cо стеклопосудой' : priceListType;

basePriceListTypeWarePriceListType(type) = DATA basePriceListType (warePriceListType);
nameBasePriceListTypeWarePriceListType 'Базовый вид цены' (type) = name(basePriceListTypeWarePriceListType(type));

includeVATPriceListType(type) += TRUE IF type IS warePriceListType;

@defineDocumentHeaderCurrency(warePriceListType);
currencyPriceListType(warePriceListType) += currencyWarePriceListType(warePriceListType);

TABLE warePriceListTypePriceGroup (warePriceListType, priceGroup);

wareSku (sku) = ABSTRACT ware (sku);
wareSku (sku) += wareItem(sku);

priceWarePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) =
    priceBasePriceListTypeSkuStockDateTime(basePriceListTypeWarePriceListType(type), sku, stock, dateTime) (+) (warePriceDate(wareSku(sku), toDate(dateTime)) AND dateTime IS DATETIME AND type IS warePriceListType AND stock IS stock);
priceWarePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) =
    priceBasePriceListTypeBatchStockDateTime(basePriceListTypeWarePriceListType(type), batch, stock, dateTime) (+) (warePriceDate(wareSku(skuBatch(batch)), toDate(dateTime)) AND dateTime IS DATETIME AND type IS warePriceListType AND stock IS stock);

pricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) += priceWarePriceListTypeSkuStockDateTime(type, sku, stock, dateTime);
pricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) += priceWarePriceListTypeBatchStockDateTime(type, batch, stock, dateTime);

FORM warePriceListType 'Цена cо стеклопосудой'
    OBJECTS w = warePriceListType FIXED PANEL
    PROPERTIES(w) name, includeVATPriceListType, nameCurrencyWarePriceListType, nameBasePriceListTypeWarePriceListType

    EDIT warePriceListType OBJECT w
;

DESIGN warePriceListType FROM DEFAULT {
    w.box{
        ADD PROPERTY(name);
        ADD PROPERTY(includeVATPriceListType);
        ADD PROPERTY(nameCurrencyWarePriceListType);
        ADD PROPERTY(nameBasePriceListTypeWarePriceListType);
    }
}

editWarePriceListType 'Редактировать' = ACTION EDITFORM warePriceListType;
editPriceListType(priceListType) += editWarePriceListType(priceListType);