MODULE Agreement;

REQUIRE Currency,
        Document,
        LegalEntity,
        Numerator,
        PriceList;

CLASS agreement 'Соглашение' : named, numeratedObject;

TABLE agreement (agreement);

@defineNumeratedObjectDefault(agreement, 'Нумератор для договоров', 'ТД');

@defineDocumentHeaderTime(agreement);
@defineDocumentHeaderTimePrefix(agreement, from, ' с');

@defineDocumentHeaderTimePrefix(agreement, to, ' по');
toDateAgreement(agreement) <- NULL WHEN ASSIGNED(agreement IS agreement);
toTimeAgreement(agreement) <- NULL WHEN ASSIGNED(agreement IS agreement);

@defineDocumentHeaderNote(agreement);
@defineDocumentHeaderCurrency(agreement);
@defineDocumentHeaderDescription(agreement, seriesNumberObject, 'Соглашение');

inCustomerAgreement 'Отм' = DATA BOOLEAN (agreement);

TABLE agreementLegalEntityGroup (agreement, legalEntityGroup);
inAgreementCustomerGroup 'Отм' = DATA BOOLEAN (agreement, legalEntityGroup);

TABLE agreementLegalEntity(agreement, legalEntity);
inAgreementCustomer 'Отм.' (agreement, legalEntity) = DATA BOOLEAN (agreement, legalEntity);

levelParentAgreementCustomerGroup (agreement, legalEntityGroup) =
    GROUP MIN levelLegalEntityGroupLegalEntityGroup(legalEntityGroup, parent) IF inAgreementCustomerGroup(agreement, parent)
          BY agreement, legalEntityGroup PERSISTENT;
nearestParentLegalEntityGroup (agreement, legalEntityGroup) =
    legalEntityGroupLegalEntityGroupLevel(legalEntityGroup, levelParentAgreementCustomerGroup(agreement, legalEntityGroup));
nearestInAgreementCustomerGroup (agreement, legalEntityGroup) =
    inAgreementCustomerGroup(agreement, nearestParentLegalEntityGroup(agreement, legalEntityGroup));

inAgreementCustomerGroupOver 'Отм' (agreement, legalEntityGroup) =
    UNION OVERRIDE inCustomerAgreement(agreement) AND legalEntityGroup IS legalEntityGroup,
                   nearestInAgreementCustomerGroup(agreement, legalEntityGroup),
                   inAgreementCustomerGroup(agreement, legalEntityGroup);

inAgreementCustomerOver 'Отм' (agreement, customer) =
    UNION OVERRIDE inCustomerAgreement(agreement) AND customer IS legalEntity,
                   nearestInAgreementCustomerGroup(agreement, legalEntityGroupLegalEntity(customer)),
                   inAgreementCustomer(agreement, customer);

customerAgreement 'Покупатели' (agreement) = GROUP CONCAT name(customer) IF inAgreementCustomerOver(agreement, customer) , ', '
                                                 BY agreement
                                                 ORDER customer MINCHARWIDTH 100 PERSISTENT;

supplierAgreement = DATA legalEntity (agreement) NOT NULL;
nameSupplierAgreement 'Поставщик' (agreement) = name(supplierAgreement(agreement));

CONSTRAINT supplierAgreement(agreement) AND NOT isSellerLegalEntity(supplierAgreement(agreement))
    CHECKED BY supplierAgreement MESSAGE 'Для соглашения выбрано в качестве поставщика организация, не являющеяся поставщиком или компанией';

priceListTypeAgreement 'Вид цены' (agreement) = DATA priceListType (agreement) NOT NULL;
namePriceListTypeAgreement 'Вид цены' (agreement) = name(priceListTypeAgreement(agreement));

RRPPriceListTypeAgreement 'Вид цены (RRP)' (agreement) = DATA priceListType (agreement);
nameRRPPriceListTypeAgreement 'Вид цены (RRP)' (agreement) = name(RRPPriceListTypeAgreement(agreement));
nameCurrencyRRPPriceListTypeAgreement 'Валюта (RRP)' (agreement)= name(currencyPriceListType(RRPPriceListTypeAgreement(agreement))) MINCHARWIDTH 10 PREFCHARWIDTH 20;

RRPTypeExchangeAgreement = DATA typeExchange (agreement);
nameRRPTypeExchangeAgreement 'Тип обмена (RRP)' (agreement) = name(RRPTypeExchangeAgreement(agreement)) MINCHARWIDTH 10 PREFCHARWIDTH 20;

CONSTRAINT currencyPriceListType(RRPPriceListTypeAgreement(agreement)) != currencyTypeExchange(RRPTypeExchangeAgreement(agreement))
    CHECKED BY RRPTypeExchangeAgreement MESSAGE 'Для соглашения валюта типа обмена RRP не соавпадает с валютой вида цены RRP';


groupTypeAgreement = DATA groupType (agreement);
nameGroupTypeAgreement 'Тип классификатора' (agreement) = name(groupTypeAgreement(agreement));

TABLE agreementGroup(agreement, group);
dataPriceListTypeAgreementGroup (agreement, group) = DATA priceListType (agreement, group);
nameDataPriceListTypeAgreementGroup 'Вид цены' (agreement, group) = name(dataPriceListTypeAgreementGroup(agreement, group));

levelParentPriceListTypeAgreementGroup (agreement, group) =
    GROUP MIN levelGroupGroup(group, parent) IF dataPriceListTypeAgreementGroup(agreement, parent)
    BY agreement, group PERSISTENT;
nearestParentGroup (agreement, group) = groupGroupLevel(group, levelParentPriceListTypeAgreementGroup (agreement, group));
nearestPriceListTypeAgreementGroup (agreement, group) =
   dataPriceListTypeAgreementGroup(agreement, nearestParentGroup(agreement, group)) PERSISTENT;
nameNearestPriceListTypeAgreementGroup 'Вид цены' (agreement, group) =
    name(nearestPriceListTypeAgreementGroup (agreement, group));

priceListTypeAgreementGroup 'Вид цены' (agreement, group) =
    UNION OVERRIDE nearestPriceListTypeAgreementGroup(agreement, group), dataPriceListTypeAgreementGroup(agreement, group) PERSISTENT;

TABLE agreementSku(agreement, sku);
dataPriceListTypeAgreementSku (agreement, sku) = DATA priceListType (agreement, sku);
nameDataPriceListTypeAgreementSku 'Вид цены' (agreement, sku) = name(dataPriceListTypeAgreementSku(agreement, sku));

priceListTypeAgreementSku (agreement, sku) =
    UNION OVERRIDE priceListTypeAgreementGroup(agreement, groupGroupTypeSku(groupTypeAgreement(agreement), sku)),
                   dataPriceListTypeAgreementSku (agreement, sku);
namePriceListTypeAgreementSku 'Вид цены' (agreement, sku) = name(priceListTypeAgreementSku(agreement, sku));

CONSTRAINT currencyAgreement(agreement) != currencyPriceListType(priceListTypeAgreement(agreement))
           CHECKED BY priceListTypeAgreement
           MESSAGE 'Валюта соглашения должна совпадать с валютой вида цены';

CONSTRAINT currencyAgreement(agreement) != currencyPriceListType(priceListTypeAgreementGroup(agreement, group))
           CHECKED BY priceListTypeAgreementGroup
           MESSAGE 'Валюта соглашения должна совпадать с валютой вида цены';

CONSTRAINT currencyAgreement(agreement) != currencyPriceListType(priceListTypeAgreementSku(agreement, sku))
           CHECKED BY priceListTypeAgreementSku
           MESSAGE 'Валюта соглашения должна совпадать с валютой вида цены';

priceListTypeAgreementSkuOver (agreement, sku) =
    UNION OVERRIDE priceListTypeAgreement(agreement) AND sku IS sku,
                   priceListTypeAgreementGroup(agreement, groupGroupTypeSku(groupTypeAgreement(agreement),sku)),
                   priceListTypeAgreementSku(agreement, sku);

namePriceListTypeAgreementSkuOver 'Вид цены' = name(priceListTypeAgreementSkuOver(agreement, sku));

userDefaultAgreementSupplierCustomer = DATA agreement (legalEntity, legalEntity);
userDefaultAgreementSupplierCustomer(supplier, customer) => inAgreementCustomerOver(userDefaultAgreementSupplierCustomer(supplier, customer), customer) RESOLVE FALSE;

calcDefaultAgreementSupplierCustomer(supplier, customer) =
    GROUP MAX agreement IF inAgreementCustomerOver(agreement, customer)
    BY supplierAgreement(agreement), customer;

defaultAgreementSupplierCustomer(supplier, customer) = UNION OVERRIDE calcDefaultAgreementSupplierCustomer(supplier, customer), userDefaultAgreementSupplierCustomer(supplier, customer) PERSISTENT;
nameDefaultAgreementSupplierCustomer 'Соглашение по умолчанию' (supplier, customer) = name(defaultAgreementSupplierCustomer(supplier, customer));

isDefaultAgreementSupplierCustomer 'Соглашение по умолчанию' (agreement, supplier, customer) = defaultAgreementSupplierCustomer(supplier, customer) == agreement;

META defineDocumentAbstractHeaderAgreement (object)
    agreement###object (object) = ABSTRACT agreement (object) PERSISTENT;
    nameAgreement###object 'Соглашение' (object)= name(agreement###object(object)) IN documentPrmGroup MINCHARWIDTH 5 PREFCHARWIDTH 10;
END
META defineDocumentHeaderAgreement (object)
    agreement###object (object) = DATA agreement (object);
    nameAgreement###object 'Соглашение' (object)= name(agreement###object(object)) IN documentPrmGroup MINCHARWIDTH 5 PREFCHARWIDTH 10;
END
META defineDocumentInterfaceHeaderAgreement (object)
    @defineDocumentAbstractHeaderAgreement(object);
    @defineDocumentHeaderAgreement(user###object);
    agreement###object(object) += agreement###user###object(object);

END

META deriveDocumentHeaderAgreement (object, supplier, customer)

    agreement###object (object) <- defaultAgreementSupplierCustomer(supplier(object), customer(object))
        IF currency###object(object) == currencyAgreement(defaultAgreementSupplierCustomer(supplier(object), customer(object)))
    WHEN CHANGED(currency###object(object)) OR
         CHANGED(customer(object)) OR
         CHANGED(supplier(object));

    CONSTRAINT currency###object(object) != currencyAgreement(agreement###object(object))
               CHECKED BY agreement###object
               MESSAGE 'Валюта документа должны совпадать с валютой соглашения';

    CONSTRAINT supplierAgreement(agreement###object(object)) != supplier(object)
               CHECKED BY agreement###object
               MESSAGE 'Соглашение должно быть между заданными организациями';

    CONSTRAINT agreement###object(object)
               AND NOT inAgreementCustomerOver(agreement###object(object), customer(object))
               CHECKED BY agreement###object
               MESSAGE 'Соглашение должно быть между заданными организациями';
END



META deriveDocumentDetailPriceListTypeAgreementCustom (object, detail)
       priceListType###user###detail(detail) <- IF agreement###user###object(object###detail(detail))
                                                THEN priceListTypeAgreementSkuOver(agreement###user###object(object###detail(detail)), sku###user###detail(detail))
                                                ELSE priceListType###user###object(object###detail(detail))
       WHEN CHANGED(agreement###user###object(object###detail(detail))) OR
            CHANGED(priceListType###user###object(object###detail(detail)));
   END
   META deriveDocumentDetailPriceListTypeAgreement (object)
       @deriveDocumentDetailPriceListTypeAgreementCustom(object, object###detail);
   END

//--------------------------------------- Для двух объектов (object1 - документ)----------------------------------------------------------//
META defineDocumentDoubleHeaderAgreement (object1, object2)
    agreement###object1###object2 (object1, object2) = DATA agreement (object1, object2);
    nameAgreement###object1###object2 'Соглашение' (object1, object2)= name(agreement###object1###object2 (object1, object2)) IN documentPrmGroup MINCHARWIDTH 5 PREFCHARWIDTH 10;
    defaultPriceListType###object1###object2 (object1, object2) = priceListTypeAgreement(agreement###object1###object2 (object1, object2));
    priceListType###object1###object2 (object1, object2) =
        UNION OVERRIDE defaultPriceListType###object1###object2(object1, object2),
                       userPriceListType###object1###object2(object1, object2);
    namePriceListType###object1###object2 'Вид цен' (object1, object2)= name(priceListType###object1###object2(object1, object2)) IN documentPrmGroup MINCHARWIDTH 5 PREFCHARWIDTH 10;
END

META defineDocumentDoubleInterfaceHeaderAgreement (object1, object2, supplierProp, customerProp)
    @defineDocumentDoubleHeaderAgreement(object1, object2);
    agreement###object1###object2 (object1, object2) <- agreement###object1(object1)
        IF currency###object1(object1) == currencyAgreement(agreement###object1(object1))
        AND in###object1###object2(object1, object2)
    WHEN CHANGED(currency###object1(object1)) AND object2 IS object2 OR
         CHANGED(agreement###object1(object1)) AND object2 IS object2 OR
         CHANGED(in###object1###object2(object1, object2));

    CONSTRAINT currency###object1(object1) != currencyAgreement(agreement###object1###object2(object1, object2))
               CHECKED BY agreement###object1###object2
               MESSAGE 'Валюта документа должны совпадать с валютой соглашения';

    CONSTRAINT supplierAgreement(agreement###object1###object2(object1, object2)) != supplierProp###object1(object1)
               CHECKED BY agreement###object1###object2
               MESSAGE 'Соглашение должно быть между заданными организациями';

    CONSTRAINT agreement###object1###object2(object1, object2)
               AND NOT inAgreementCustomerOver(agreement###object1###object2(object1, object2), customerProp###object1(object1))
               CHECKED BY agreement###object1###object2
               MESSAGE 'Соглашение должно быть между заданными организациями';
END

FORM agreementsCustomer 'Соглашения'

    OBJECTS s=legalEntity FIXED PANEL
    PROPERTIES(s) name

    OBJECTS c=legalEntity FIXED PANEL
    PROPERTIES(c) name

    OBJECTS ag=agreement
    PROPERTIES(ag) READONLY name, numberObject, seriesObject, dateAgreement, timeAgreement,
                            fromDateAgreement, fromTimeAgreement, toDateAgreement, toTimeAgreement,
                            nameCurrencyAgreement, namePriceListTypeAgreement, noteAgreement,
                            nameRRPPriceListTypeAgreement, nameCurrencyRRPPriceListTypeAgreement READONLY,
                            nameRRPTypeExchangeAgreement
    PROPERTIES inAgreementCustomerOver(ag, s)
;

DESIGN agreementsCustomer FROM DEFAULT{
    NEW mainContainer{
        REMOVE s.box;
        REMOVE c.box;
        ADD ag.box;
    }
    ADD functions.box;
}

addAgreementsSupplierCustomer 'Присоединить' = ACTION (supplier, customer) {

    FORM agreementsCustomer OBJECTS s=supplier, c=customer MODAL;
    IF formResult() == formResult.ok THEN {
        EXEC apply();
    }
} TOOLBAR;

FORM agreement 'Соглашение'

    OBJECTS a = agreement FIXED PANEL
    PROPERTIES(a) name, nameNumeratorObject, numberObject, seriesObject, dateAgreement, timeAgreement,
                  fromDateAgreement, fromTimeAgreement, toDateAgreement, toTimeAgreement, nameSupplierAgreement,
                  nameCurrencyAgreement, namePriceListTypeAgreement, nameGroupTypeAgreement, noteAgreement,
                  nameRRPPriceListTypeAgreement, nameCurrencyRRPPriceListTypeAgreement READONLY, nameRRPTypeExchangeAgreement

    TREE legalEntityGroupTree b=STRING[3], lg = legalEntityGroup PARENT parentLegalEntityGroup
    PROPERTIES READONLY OBJVALUE(b), lgTreeName = name(lg)
    FILTERS stringEqualsAll(b)
    PROPERTIES(a) inCustomerAgreement TODRAW b FORCE GRID
    PROPERTIES(a, lg) inAgreementCustomerGroupOver

    OBJECTS c = legalEntity
    PROPERTIES(c) READONLY name
    PROPERTIES(a, c) inAgreementCustomerOver
    FILTERS inLegalEntityGroupLegalEntity(lg, c),
            supplierAgreement(a) != c OR (c IS legalEntity AND NOT supplierAgreement(a))

    FILTERGROUP filters
        FILTER 'Только отмеченные' 'F10' inAgreementCustomerOver (a, c)

    TREE groupTree g=group PARENT parentGroup
    PROPERTIES READONLY name(g)
    FILTERS groupTypeGroup(g) == groupTypeAgreement(a)
    ORDER BY name(g)
    PROPERTIES(a, g) nameDataPriceListTypeAgreementGroup, nameNearestPriceListTypeAgreementGroup

    TREE skuGroupTree sg=skuGroup PARENT parentSkuGroup
    PROPERTIES READONLY name(sg)
    ORDER BY name(sg)

    OBJECTS s = sku
    PROPERTIES(s) READONLY nameSku
    PROPERTIES(a, s) nameDataPriceListTypeAgreementSku, namePriceListTypeAgreementSku
    FILTERS isParentSkuGroupSku(sg, s)

    EDIT agreement OBJECT a
;

DESIGN agreement FROM DEFAULT {

    NEW topContainer{
        childConstraints = TO THE BOTTOM;
        NEW headContainer {
            caption = 'Шапка документа';
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY (name);
            ADD PROPERTY (nameNumeratorObject);
            ADD PROPERTY (numberObject);
            ADD PROPERTY (seriesObject);
            ADD PROPERTY (dateAgreement);
            ADD PROPERTY (timeAgreement);

        }
        NEW firstContainer{
            childConstraints = TO THE BOTTOM;
            NEW timeContainer{
                caption = 'Период действия';
                childConstraints = TO THE RIGHTBOTTOM;
                ADD PROPERTY (fromDateAgreement);
                ADD PROPERTY (fromTimeAgreement);
                ADD PROPERTY (toDateAgreement);
                ADD PROPERTY (toTimeAgreement);
            }
            NEW propContainer{
                caption = 'Параметры документа';
                childConstraints = TO THE RIGHTBOTTOM;
                ADD PROPERTY(nameSupplierAgreement);
                ADD PROPERTY(nameCurrencyAgreement);
                ADD PROPERTY(namePriceListTypeAgreement);
                ADD PROPERTY(noteAgreement);
            }
            NEW rrpContainer{
                caption = 'RRP';
                childConstraints = TO THE RIGHTBOTTOM;
                ADD PROPERTY(nameRRPPriceListTypeAgreement);
                ADD PROPERTY(nameCurrencyRRPPriceListTypeAgreement);
                ADD PROPERTY(nameRRPTypeExchangeAgreement);
            }
        }
            NEW tabContainer{
                type = TABBED;
                NEW customerBox{
                    caption = 'Контрагенты';
                    childConstraints = TO THE RIGHT;
                    type = SPLITH;
                    ADD legalEntityGroupTree.tree.box;
                    ADD c.box{
                        fillHorizontal = 2.0;
                    }
                }
                NEW groupPane {
                    childConstraints = TO THE BOTTOM;
                    caption = 'Уточнение цен по ценовым группам';
                    ADD PROPERTY(nameGroupTypeAgreement);
                    ADD groupTree.tree.box;
                }
                NEW skuContainer{
                    childConstraints = TO THE RIGHT;
                    caption = 'Уточнение цен по товарам';
                    type = SPLITH;
                    ADD skuGroupTree.tree.box;
                    ADD s.box{fillHorizontal = 3.0;}
                }
            }
    }
    ADD functions.box;
}

createAgreementSupplierCustomer 'Добавить' = ACTION (supplier, customer) {

    FOR ADDOBJ ag = agreement DO {
        SET inAgreementCustomer(ag, customer) <- TRUE;
        SET supplierAgreement(ag) <- (supplier AS legalEntity);
        FORM agreement OBJECTS a = ag DOCKED_MODAL;
        IF TRUE AND NOT formResult() == formResult.ok THEN {
            EXEC delete(ag);
        }
    }
} IMAGE 'add.png' EDITKEY 'INSERT' HIDE TOOLBAR;

copySessionAgreement 'Копировать' = ACTION (agreement) {
    FOR ADDOBJ a = agreement DO {
        SET dateAgreement(a) <- dateAgreement(agreement);
        SET timeAgreement(a) <- timeAgreement(agreement);
        SET fromDateAgreement(a) <- fromDateAgreement(agreement);
        SET fromTimeAgreement(a) <- fromTimeAgreement(agreement);
        SET toDateAgreement(a) <- toDateAgreement(agreement);
        SET toTimeAgreement(a) <- toTimeAgreement(agreement);
        SET supplierAgreement(a) <- supplierAgreement(agreement);
        SET currencyAgreement(a) <- currencyAgreement(agreement);
        SET priceListTypeAgreement(a) <- priceListTypeAgreement(agreement);
        SET noteAgreement(a) <- noteAgreement(agreement);
        SET groupTypeAgreement(a) <- groupTypeAgreement(agreement);
        SET inCustomerAgreement(a) <- inCustomerAgreement(agreement);

        SET inAgreementCustomer(a, customer) <- inAgreementCustomer (agreement, customer);
        SET priceListTypeAgreementGroup(a, group) <- priceListTypeAgreementGroup(agreement, group);
        SET priceListTypeAgreementSku(a, sku) <- priceListTypeAgreementSku(agreement, sku);

        FORM agreement OBJECTS a = a DOCKED_MODAL;

        IF TRUE AND NOT formResult() == formResult.ok THEN {
            EXEC delete(a);
        }
    };
} TOOLBAR;


FORM agreements 'Соглашения'

    OBJECTS a = agreement
    PROPERTIES(a) READONLY name, seriesNumberObject, dateTimeAgreement,
                           fromDateTimeAgreement, toDateTimeAgreement, nameSupplierAgreement,
                           nameCurrencyAgreement, namePriceListTypeAgreement, customerAgreement, noteAgreement,
                           nameRRPPriceListTypeAgreement, nameCurrencyRRPPriceListTypeAgreement READONLY,
                           nameRRPTypeExchangeAgreement

    PROPERTIES(a) ADDFORM, EDITFORM, delete
;

relationSupplierCustomer 'Связь' (supplier, customer)= GROUP SUM 1 IF inAgreementCustomerOver(agreement, customer) BY supplierAgreement(agreement), customer;

EXTEND FORM legalEntity

    OBJECTS ags=agreement
    PROPERTIES(ags, s, l) isDefaultAgreementSupplierCustomer
    PROPERTIES(ags, l) inAgreementCustomerOver
    PROPERTIES(ags) READONLY name, numberObject, seriesObject, dateAgreement, timeAgreement,
                             fromDateAgreement, fromTimeAgreement, toDateAgreement, toTimeAgreement,
                             nameCurrencyAgreement, namePriceListTypeAgreement, noteAgreement,
                             nameRRPPriceListTypeAgreement, nameCurrencyRRPPriceListTypeAgreement READONLY,
                             nameRRPTypeExchangeAgreement
    PROPERTIES(ags) delete, EDITSESSIONFORM, copySessionAgreement
    PROPERTIES createAgreementSupplierCustomer(s, l) TODRAW ags FORCE PANEL DRAWTOTOOLBAR, addAgreementsSupplierCustomer(s, l) TODRAW ags FORCE PANEL DRAWTOTOOLBAR
    FILTERS inAgreementCustomerOver(ags, l),
            supplierAgreement(ags) == s,
            isSellerLegalEntity(s),
            l != s

    FILTERGROUP filtersSupplier
        FILTER 'Есть соглашение' 'F11' relationSupplierCustomer(s, l)

    OBJECTS agc=agreement
    PROPERTIES(agc, c) READONLY inAgreementCustomerOver
    PROPERTIES(agc, l, c) isDefaultAgreementSupplierCustomer
    PROPERTIES(agc) READONLY name, numberObject, seriesObject, dateAgreement, timeAgreement,
                             fromDateAgreement, fromTimeAgreement, toDateAgreement, toTimeAgreement,
                             nameCurrencyAgreement, namePriceListTypeAgreement, noteAgreement,
                             nameRRPPriceListTypeAgreement, nameCurrencyRRPPriceListTypeAgreement READONLY, nameRRPTypeExchangeAgreement
    PROPERTIES(agc) delete, EDITSESSIONFORM, copySessionAgreement

    PROPERTIES createAgreementSupplierCustomer(l, c) TODRAW agc FORCE PANEL DRAWTOTOOLBAR, addAgreementsSupplierCustomer (l, c) TODRAW agc FORCE PANEL DRAWTOTOOLBAR
    FILTERS inAgreementCustomerOver(agc, c),
            supplierAgreement(agc) == l,
            isBuyerLegalEntity(c),
            c != l

    FILTERGROUP filters3
        FILTER 'Есть соглашение' 'F9' relationSupplierCustomer(l, c)

;

EXTEND DESIGN legalEntity {
    extendContainer {
        docContainer{
            ADD ags.box;
        }

        docContainer2{
            ADD agc.box;
        }
    }
}

NAVIGATOR {
    priceListDocuments {
        ADD agreements;
    }
}