MODULE PriceList;

REQUIRE System, Document, Numerator, Stock, Barcode, PriceRound;

// --------------------------- Виды цен ------------------------- //

CLASS ABSTRACT PriceListType 'Вид цены' : Named;
TABLE priceListType(PriceListType);

includeVATPriceListType 'Цена с НДС' = ABSTRACT BOOLEAN (PriceListType) PERSISTENT;

pricePriceListTypeSkuStockDateTime 'Цена' = ABSTRACT NUMERIC[14,2] (PriceListType, Sku, Stock, DATETIME);
pricePriceListTypeBatchStockDateTime 'Цена' = ABSTRACT NUMERIC[14,2] (PriceListType, Batch, Stock, DATETIME);

prevPricePriceListTypeSkuStockDateTime 'Цена (пред.)' (type, sku, stock, dateTime) = PREV(pricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime));
prevPricePriceListTypeBatchStockDateTime 'Цена (пред.)' (type, batch, stock, dateTime) = PREV(pricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime));

prevPriceMVATPriceListTypeSkuStockDateTimeVAT 'Цена без НДС (пред.)' (type, sku, stock, dateTime, VAT) =
    IF includeVATPriceListType(type)
        THEN
            prevPricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) * 100 /(100 + VAT)
        ELSE
            prevPricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) AND VAT IS NUMERIC[10,5];

prevPricePVATPriceListTypeSkuStockDateTimeVAT 'Цена с НДС (пред.)' (type, sku, stock, dateTime, VAT) =
    IF includeVATPriceListType(type)
        THEN
            prevPricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) AND VAT IS NUMERIC[10,5]
        ELSE
            prevPricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) * (100 + VAT)/100;

prevPriceMVATPriceListTypeBatchStockDateTimeVAT 'Цена без НДС (пред.)' (type, batch, stock, dateTime, VAT) =
    IF includeVATPriceListType(type)
        THEN
            prevPricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) * 100 /(100 + VAT)
        ELSE
            prevPricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) AND VAT IS NUMERIC[10,5];

prevPricePVATPriceListTypeBatchStockDateTimeVAT 'Цена с НДС (пред.)' (type, batch, stock, dateTime, VAT) =
    IF includeVATPriceListType(type)
        THEN
            prevPricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) AND VAT IS NUMERIC[10,5]
        ELSE
            prevPricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) * (100 + VAT)/100;

@defineDocumentAbstractHeaderCurrency(priceListType);

CLASS ABSTRACT BasePriceListType 'Базовый вид цены' : PriceListType;

priceBasePriceListTypeSkuStockDateTime (type, sku, stock, dateTime) = ABSTRACT NUMERIC[14,2] (BasePriceListType, Sku, Stock, DATETIME);
priceBasePriceListTypeBatchStockDateTime (type, sku, stock, dateTime) = ABSTRACT NUMERIC[14,2] (BasePriceListType, Batch, Stock, DATETIME);

includeVATBasePriceListType 'Цена с НДС' = ABSTRACT BOOLEAN (BasePriceListType);
includeVATPriceListType(type) += includeVATBasePriceListType(type);

pricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) += priceBasePriceListTypeSkuStockDateTime(type, sku, stock, dateTime);
pricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) += priceBasePriceListTypeBatchStockDateTime(type, batch, stock, dateTime);

// --------------------------- Системные виды цен ------------------------- //
CLASS SystemPriceListType 'Системный вид цены' : BasePriceListType;

priceSystemPriceListTypeSkuStockDateTime (type, sku, stock, dateTime) = ABSTRACT NUMERIC[14,3] (SystemPriceListType, Sku, Stock, DATETIME) EXCLUSIVE;
priceSystemPriceListTypeBatchStockDateTime (type, sku, stock, dateTime) = ABSTRACT NUMERIC[14,3] (SystemPriceListType, Batch, Stock, DATETIME) EXCLUSIVE;

includeVATSystemPriceListType 'Цена с НДС' = ABSTRACT BOOLEAN (SystemPriceListType);
includeVATBasePriceListType(type) += includeVATSystemPriceListType(type);

priceBasePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) += priceSystemPriceListTypeSkuStockDateTime(type, sku, stock, dateTime);
priceBasePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) += priceSystemPriceListTypeBatchStockDateTime(type, batch, stock, dateTime);

//----------------------------- Ценовые группы --------------------------//
CLASS PriceStockGroup 'Ценовые группы' : Named;
TABLE priceStockGroup(PriceStockGroup);

priceStockGroupStock = ABSTRACT PriceStockGroup (Stock);
namePriceStockGroupStock 'Ценовая группа' (stock)= name(priceStockGroupStock(stock));

FORM priceStockGroups 'Ценовые группы'

    OBJECTS g = PriceStockGroup
    PROPERTIES(g) READONLY objectClassName, name
    PROPERTIES(g) ADDFORM, EDITFORM, delete

    OBJECTS s = Stock
    PROPERTIES(s) READONLY objectClassName, name

    FILTERS priceStockGroupStock(s) == g

    DIALOG PriceList OBJECT g
;

// ------------------------------------- Виды цен, формируемые ledger'ами ----------- //

CLASS ABSTRACT LedgerPriceListType 'Регистровый вид цены' : BasePriceListType;

batchLedgerPriceListType 'Использовать для партий свои цены' = ABSTRACT BOOLEAN (PriceListType) EXCLUSIVE PERSISTENT;

CLASS ABSTRACT PriceListLedger 'Изменение цены';
TABLE priceListLedger(PriceListLedger);

fromDateTimePriceListLedger = ABSTRACT DATETIME (PriceListLedger) PERSISTENT INDEXED;
toDateTimePriceListLedger = ABSTRACT DATETIME (PriceListLedger) PERSISTENT INDEXED;

isPostedPriceListLedger 'Проведен' (ledger) = ABSTRACT BOOLEAN (PriceListLedger) PERSISTENT;
skipPriceListLedger 'Не изменять значение' (ledger) = ABSTRACT BOOLEAN (PriceListLedger) PERSISTENT;

activePriceListLedger 'Активен' (ledger) = isPostedPriceListLedger(ledger) AND NOT skipPriceListLedger(ledger) PERSISTENT;

skuPriceListLedger = ABSTRACT Sku (PriceListLedger) PERSISTENT INDEXED;
batchPriceListLedger = ABSTRACT Batch (PriceListLedger) PERSISTENT INDEXED;

descriptionPriceListLedger = ABSTRACT STRING[200] (PriceListLedger) PERSISTENT;

companyPriceListLedger = ABSTRACT LegalEntity (PriceListLedger) PERSISTENT;

TABLE priceListLedgerLedgerPriceListType(PriceListLedger, LedgerPriceListType);
pricePriceListLedgerLedgerPriceListType = ABSTRACT NUMERIC[14,2] (PriceListLedger, LedgerPriceListType) PERSISTENT;

TABLE priceListLedgerStock(PriceListLedger, Stock);
inPriceListLedgerStock (ledger, stock) = ABSTRACT BOOLEAN (PriceListLedger, Stock);

orderPriceListLedger(ledger) = STRUCT(fromDateTimePriceListLedger(ledger), ledger) AND activePriceListLedger(ledger) PERSISTENT;

TABLE priceListLedgerLedgerPriceListTypeStock (PriceListLedger, LedgerPriceListType, Stock);
orderPriceListLedgerLedgerPriceListTypeStock(ledger, type, stock) =
    orderPriceListLedger(ledger) IF pricePriceListLedgerLedgerPriceListType(ledger, type) AND inPriceListLedgerStock(ledger, stock) PERSISTENT INDEXED;

concatLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime) =
    GROUP MAX orderPriceListLedgerLedgerPriceListTypeStock(ledger, type, stock)
              IF fromDateTimePriceListLedger(ledger) < (dateTimeIn AS DATETIME) AND NOT toDateTimePriceListLedger(ledger) <= (dateTimeIn AS DATETIME)
          BY type, skuPriceListLedger(ledger), stock, dateTimeIn;
priceListLedgerLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime) =
    concatLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime)[2];

concatLedgerPriceListTypeBatchStockDateTime(type, batch, stock, dateTime) =
    GROUP MAX orderPriceListLedgerLedgerPriceListTypeStock(ledger, type, stock)
              IF fromDateTimePriceListLedger(ledger) < (dateTimeIn AS DATETIME) AND NOT toDateTimePriceListLedger(ledger) <= (dateTimeIn AS DATETIME)
          BY type, batchPriceListLedger(ledger), stock, dateTimeIn;
priceListLedgerLedgerPriceListTypeBatchStockDateTime(type, batch, stock, dateTime) =
    IF batchLedgerPriceListType(type) THEN concatLedgerPriceListTypeBatchStockDateTime(type, batch, stock, dateTime)[2]
                                      ELSE priceListLedgerLedgerPriceListTypeSkuStockDateTime(type, skuBatch(batch), stock, dateTime);

priceLedgerPriceListTypeSkuStockDateTime 'Цена' (type, sku, stock, dateTime) =
    pricePriceListLedgerLedgerPriceListType(priceListLedgerLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime), type);
priceLedgerPriceListTypeBatchStockDateTime 'Цена' (type, batch, stock, dateTime) =
    pricePriceListLedgerLedgerPriceListType(priceListLedgerLedgerPriceListTypeBatchStockDateTime(type, batch, stock, dateTime), type);

companyLedgerPriceListTypeSkuStockDateTime (type, sku, stock, dateTime) =
    companyPriceListLedger(priceListLedgerLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime));
nameCompanyLedgerPriceListTypeSkuStockDateTime 'Компания прайса' (type, sku, stock, dateTime) =
    name(companyLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime));
companyLedgerPriceListTypeBatchStockDateTime (type, batch, stock, dateTime) =
    companyPriceListLedger(priceListLedgerLedgerPriceListTypeSkuStockDateTime(type, skuBatch(batch), stock, dateTime));
nameCompanyLedgerPriceListTypeBatchStockDateTime 'Компания прайса' (type, batch, stock, dateTime) =
    name(companyLedgerPriceListTypeBatchStockDateTime(type, batch, stock, dateTime));

descriptionLedgerPriceListTypeSkuStockDateTime 'Описание' (type, sku, stock, dateTime) =
    descriptionPriceListLedger(priceListLedgerLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime));
descriptionLedgerPriceListTypeBatchStockDateTime 'Описание' (type, batch, stock, dateTime) =
    descriptionPriceListLedger(priceListLedgerLedgerPriceListTypeBatchStockDateTime(type, batch, stock, dateTime));

includeVATLedgerPriceListType 'Цена с НДС' = ABSTRACT BOOLEAN (LedgerPriceListType);

priceBasePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) += priceLedgerPriceListTypeSkuStockDateTime(type, sku, stock, dateTime);
priceBasePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) += priceLedgerPriceListTypeBatchStockDateTime(type, batch, stock, dateTime);

includeVATBasePriceListType(type) += includeVATLedgerPriceListType(type);

// ------ оптимизация для ассортиментов
ledgerPriceListTypePriceListType = ABSTRACT LedgerPriceListType (PriceListType) PERSISTENT;

ledgerPriceListTypePriceListType(type) += type AS LedgerPriceListType;

// --------------------------- Системные виды цен, на основе Ledger'ов ------------------ //

CLASS SystemLedgerPriceListType 'Системный вид цены (регистр)' {
    accountPriceListType 'Учетная'
}: LedgerPriceListType;

batchLedgerPriceListType(type) += type == SystemLedgerPriceListType.accountPriceListType;

includeVATSystemLedgerPriceListType 'Цена с НДС' = ABSTRACT BOOLEAN (SystemLedgerPriceListType) EXCLUSIVE;
includeVATLedgerPriceListType (type) += includeVATSystemLedgerPriceListType(type);

pricePriceListLedgerSystemLedgerPriceListType = ABSTRACT NUMERIC[14,2] (PriceListLedger, SystemLedgerPriceListType) EXCLUSIVE PERSISTENT;
pricePriceListLedgerLedgerPriceListType(ledger, type) += pricePriceListLedgerSystemLedgerPriceListType(ledger, type);

META implementSystemLedgerPriceListType (type, object, stockProp, prefix)
    @implementSystemLedgerPriceListTypeInner (type, object, ###object, stockProp, prefix);
END

META implementSystemLedgerPriceListTypeInner (type, object, class, stockProp, prefix)
    EXTEND CLASS class : PriceListLedger;

    fromDateTimePriceListLedger (ledger) += dateTime###object(ledger);

    isPostedPriceListLedger(ledger) += isPosted###object(ledger);

    skuPriceListLedger (ledger) += sku###object(ledger);

    descriptionPriceListLedger (ledger) += description###object(ledger);

    pricePriceListLedgerSystemLedgerPriceListType (ledger, type) += prefix###price###object(ledger) WHEN CLASS(prefix###price###object(ledger)) AND type == SystemLedgerPriceListType.##type##PriceListType;
    inPriceListLedgerStock (ledger, stock) += stockProp###object(ledger) == stock;
END

META implementSystemLedgerPriceListType (type, object, stockProp)
    @implementSystemLedgerPriceListType(type, object, stockProp, );
END

META implementSystemLedgerPriceListTypeBatch (type, object, stockProp)
    @implementSystemLedgerPriceListTypeBatchInner(type, object, ###object, stockProp);
END

META implementSystemLedgerPriceListTypeBatchInner (type, object, class, stockProp)
    @implementSystemLedgerPriceListType(type, object, stockProp);
    batchPriceListLedger (ledger) += ledger AS class;
END
// --------------------------- Пользовательские виды цен ------------------------- //

CLASS DataPriceListType 'Пользовательский вид цены' : LedgerPriceListType;
batchLedgerPriceListType (priceListType) += priceListType IS DataPriceListType;

includeVATDataPriceListType 'Цена с НДС' = DATA BOOLEAN (DataPriceListType);
includeVATLedgerPriceListType (priceListType) += includeVATDataPriceListType(priceListType);

@defineDocumentHeaderCurrency(dataPriceListType);
currencyPriceListType(dataPriceListType) += currencyDataPriceListType(dataPriceListType);

CLASS ABSTRACT PriceList 'Прайс';
CLASS ABSTRACT PriceListDetail 'Строка прайса';

CLASS UserPriceList 'Прайс (польз.)' : PriceList, Historizable, NumeratedObject;
CLASS UserPriceListDetail 'Строка прайса (польз.)' : PriceListDetail;
CLASS UserPriceListPosted 'Проведенный прайс (польз.)' : UserPriceList, PostedObject;

@defineDocumentInterface(priceList);

@defineDocumentInterfaceTimePrefix(priceList, from, ' c');
//@defineDocumentInterfaceTimePrefix(priceList, to, ' по');

toDatePriceList 'Дата по' = ABSTRACT DATE (PriceList) IN documentHeaderGroup PERSISTENT;
toTimePriceList 'Время по' = ABSTRACT TIME (PriceList) IN documentHeaderGroup PERSISTENT;
toDateTimePriceList 'Дата/время по'(priceList) = dateTimeToDateTime(toDatePriceList(priceList), toTimePriceList(priceList)) PERSISTENT;

toDateUserPriceList 'Дата по' = DATA DATE(UserPriceList);
toTimeUserPriceList 'Время по' = DATA TIME(UserPriceList);
toDateTimeUserPriceList 'Дата/время'(userPriceList) = dateTimeToDateTime(toDateUserPriceList(userPriceList), toTimeUserPriceList(userPriceList));

toDatePriceList(userPriceList) += toDateUserPriceList(userPriceList);
toTimePriceList(userPriceList) += toTimeUserPriceList(userPriceList);

@defineDocumentInterfaceNumber(priceList);
@defineNumeratedObjectDefault(PriceList, 'Нумератор для прайсов', 'ПЛ');

//@defineDocumentInterfaceDataStock(priceList, stock, 'Склад');
@defineDocumentInterfacePosted(priceList);

@defineDocumentInterfaceDescription(priceList, 'Прайс');

@defineDocumentInterfaceCurrency(priceList);

@defineDocumentInterfaceDetailSku(priceList, sku);

@defineDocumentInterfaceLegalEntity(priceList, company, 'Организация');
@defineDocumentInterfaceDetailQuantity(priceList);
@defineAddDetailDialogBarcode(userPriceList, sku);

@defineDocumentInterfaceDetailTimePrefix(priceList, to, 'по');

//WHEN ASSIGNED(u AS UserPriceList) DO {
//    IF u IS UserPriceList AND toDateUserPriceList(u) THEN SET toDateUserPriceList(u) <- NULL;
//    IF u IS UserPriceList AND toTimeUserPriceList(u) THEN SET toTimeUserPriceList(u) <- NULL;
//} SESSION;

userPriceListLegalEntity (legalEntity) = GROUP MAX userPriceList BY companyPriceList(userPriceList) WHERE userPriceList IS UserPriceList;

pricePriceListDetailDataPriceListType 'Цена (новая)' = ABSTRACT NUMERIC[14,2] (PriceListDetail, DataPriceListType) PERSISTENT;
priceUserPriceListDetailDataPriceListType 'Цена (новая)' = DATA NUMERIC[14,2] (PriceListDetail, DataPriceListType);
pricePriceListDetailDataPriceListType (priceListDetail, dataPriceListType) += priceUserPriceListDetailDataPriceListType(priceListDetail, dataPriceListType);

TABLE priceListPriceListType (PriceList, PriceListType);
TABLE priceListDetailPriceListType (PriceListDetail, PriceListType);

TABLE priceListDataPriceListType (PriceList, DataPriceListType);
TABLE priceListDetailDataPriceListType (PriceListDetail, DataPriceListType);

inPriceListDataPriceListType 'Изменять цены' = ABSTRACT BOOLEAN (PriceList, DataPriceListType) PERSISTENT;
inUserPriceListDataPriceListType 'Изменять цены' = DATA BOOLEAN (UserPriceList, DataPriceListType);
inPriceListDataPriceListType (priceList, dataPriceListType) += inUserPriceListDataPriceListType(priceList, dataPriceListType);

showPriceListPriceListType 'Показывать цены' = ABSTRACT BOOLEAN (PriceList, PriceListType) PERSISTENT;
showUserPriceListPriceListType 'Показывать цены' = DATA BOOLEAN (UserPriceList, PriceListType);
showPriceListPriceListType (priceList, priceListType) += showUserPriceListPriceListType(priceList, priceListType);

inShowPriceListPriceListType (priceList, priceListType) = inPriceListDataPriceListType (priceList, priceListType) AND
                                                          showPriceListPriceListType (priceList, priceListType);

priceListTypesPriceList 'Цены' (priceList) = GROUP CONCAT name(priceListType) IF inPriceListDataPriceListType(priceList, priceListType) , ', '
                                               BY priceList
                                               ORDER priceListType MINCHARWIDTH 50 PERSISTENT;

inPriceListDetailPriceListType (detail, dataPriceListType) = inPriceListDataPriceListType(priceListPriceListDetail(detail), dataPriceListType) PERSISTENT;

pricePriceListDetailDataPriceListType(detail, dataPriceListType) => inPriceListDetailPriceListType (detail, dataPriceListType) RESOLVE FALSE;

inPriceList 'Отм' = ABSTRACT BOOLEAN (PriceList) PERSISTENT;
inUserPriceList 'Отм' = DATA BOOLEAN (UserPriceList);
inUserPriceList(userPriceList) <- TRUE WHEN ASSIGNED(userPriceList IS UserPriceList);
inPriceList (priceList) += inUserPriceList(priceList);

TABLE priceListStockGroup(PriceList, StockGroup);
inPriceListStockGroup 'Отм' = ABSTRACT BOOLEAN (PriceList, StockGroup) PERSISTENT;
inUserPriceListStockGroup 'Отм' = DATA BOOLEAN (UserPriceList, StockGroup);
inPriceListStockGroup (priceList, stockGroup) += inUserPriceListStockGroup(priceList, stockGroup);

TABLE priceListStock(PriceList, Stock);
inPriceListStock 'Отм' = ABSTRACT BOOLEAN (PriceList, Stock) PERSISTENT;
inUserPriceListStock 'Отм' = DATA BOOLEAN (UserPriceList, Stock);
inPriceListStock (priceList, stock) += inUserPriceListStock(priceList, stock);

TABLE priceListDetailStock(PriceListDetail, Stock);
dataPriceListDetailStock (detail, stock) = inPriceListStock(priceListPriceListDetail(detail), stock) PERSISTENT;

levelParentPriceListStockGroup (priceList, stockGroup) = GROUP MIN levelStockGroupStockGroup(stockGroup, parent) IF inPriceListStockGroup(priceList, parent)
                                                               BY priceList, stockGroup PERSISTENT;
nearestParentStockGroup (priceList, stockGroup) = stockGroupStockGroupLevel(stockGroup, levelParentPriceListStockGroup(priceList, stockGroup));
nearestInPriceListStockGroup (priceList, stockGroup) =
    inPriceListStockGroup(priceList, nearestParentStockGroup(priceList, stockGroup)) PERSISTENT;

inPriceListStockGroupOver 'Отм' (priceList, stockGroup) =
    UNION OVERRIDE inPriceList(priceList) AND stockGroup IS StockGroup,
                   nearestInPriceListStockGroup(priceList, stockGroup),
                   inPriceListStockGroup(priceList, stockGroup);

inPriceListStockOver 'Отм' (priceList, stock) =
    (UNION OVERRIDE inPriceList(priceList) AND stock IS Stock,
                   nearestInPriceListStockGroup(priceList, stockGroupStock(stock)),
                   inPriceListStock(priceList, stock)) AND isCompanyStock(stock);

stocksPriceList 'Склады' (priceList) = GROUP CONCAT name(stock) IF inPriceListStockOver(priceList, stock) , ', '
                                       BY priceList
                                       ORDER stock MINCHARWIDTH 100 PERSISTENT;

currentPricePriceListDetailDataPriceListTypeStock 'Действующая цена' (priceListDetail, dataPriceListType, stock) =
    prevPricePriceListTypeSkuStockDateTime(dataPriceListType, skuPriceListDetail(priceListDetail), stock, fromDateTimePriceListDetail(priceListDetail));

currentPriceSkuPriceListDataPriceListTypeStock 'Действующая цена' (sku, priceList, dataPriceListType, stock) =
    prevPricePriceListTypeSkuStockDateTime(dataPriceListType, sku, stock, fromDateTimePriceList(priceList));

markUpPricePriceListDetailDataPriceListTypeStock 'Изменение, %' (priceListDetail, dataPriceListType, stock) =
    pricePriceListDetailDataPriceListType(priceListDetail, dataPriceListType) * 100.0 /
    currentPricePriceListDetailDataPriceListTypeStock (priceListDetail, dataPriceListType, stock) - 100.0;

extendNameCurrentDataPriceListType (userPriceList, dataPriceListType) =
    [FORMULA STRING[50]  ' CAST($1 AS TEXT) || \' (старая) \''](
    name(dataPriceListType)) IF showPriceListPriceListType(userPriceList, dataPriceListType) MINCHARWIDTH 30 MAXCHARWIDTH 50;

extendNameMarkUpDataPriceListType (userPriceList, dataPriceListType) =
    [FORMULA STRING[50]  'CAST($1 AS TEXT) || \' (изменение, %) \''](
    name(dataPriceListType)) IF inShowPriceListPriceListType(userPriceList, dataPriceListType) MINCHARWIDTH 30 MAXCHARWIDTH 50;

extendNameDataPriceListType (userPriceList, dataPriceListType) =
    [FORMULA STRING[50]  ' CAST($1 AS TEXT) || \' (новая) \''](
    name(dataPriceListType)) IF inPriceListDataPriceListType(userPriceList, dataPriceListType) MINCHARWIDTH 30 MAXCHARWIDTH 50;

backgroundCurrentDataPriceListType 'Цвет' (dataPriceListType) = RGB(255,238,165) AND dataPriceListType IS DataPriceListType;
backgroundMarkUpDataPriceListType 'Цвет' (dataPriceListType) = RGB(232,184,146) AND dataPriceListType IS DataPriceListType;
backgroundDataPriceListType 'Цвет' (dataPriceListType) = RGB(213,249,185) AND dataPriceListType IS  DataPriceListType;

inStockGroupStock (stockGroup, stock) = UNION OVERRIDE stock IS Stock AND NOT stockGroup IS StockGroup, isParentStockGroupStock(stockGroup, stock);

detailSkuUserPriceList(sku, price) = GROUP SUM 1 IF sku == skuUserPriceListDetail(detail) AND price == userPriceListUserPriceListDetail(detail)
                                           BY skuUserPriceListDetail(detail), userPriceListUserPriceListDetail(detail);

inSkuUserPriceList 'Отм.' (sku, price) = TRUE IF detailSkuUserPriceList(sku, price);

changeInSkuUserPriceList = ACTION (sku, price) {
    REQUEST BOOLEAN INPUT;
    IF TRUE AND NOT requestedLogical() THEN {
        IF detailSkuUserPriceList(sku, price) THEN {
            FOR sku == skuUserPriceListDetail(detail) AND price == userPriceListUserPriceListDetail(detail) DO {
                EXEC delete(detail);
            }
        }
    } ELSE {
        IF requestedLogical() THEN {
            FOR ADDOBJ d = UserPriceListDetail DO {
               SET userPriceListUserPriceListDetail(d) <- price;
               SET skuUserPriceListDetail(d) <- sku;
            }
        }
    }
}

isSelectedPriceListDetail 'Отм' = DATA SESSION BOOLEAN (PriceListDetail);
sumSelectedPriceList (priceList) =
    GROUP SUM 1 IF isSelectedPriceListDetail(priceListDetail)
          BY priceListPriceListDetail(priceListDetail);

FORM chooseMarkUpSkuUserPriceList 'Наценка'

    OBJECTS p = UserPriceList FIXED PANEL

    OBJECTS t = PriceListType FIXED PANEL
    PROPERTIES(t) SELECTOR name

    OBJECTS n = NUMERIC[20,7] FIXED PANEL
    PROPERTIES(n) objValue = OBJVALUE

    OBJECTS t2 = DataPriceListType FIXED PANEL
    PROPERTIES(t2) SELECTOR name
    FILTERS inPriceListDataPriceListType(p, t2)
;

DESIGN chooseMarkUpSkuUserPriceList FROM DEFAULT {

    REMOVE p.box;
    NEW topContainer {
        childConstraints = TO THE BOTTOM;
        ADD PROPERTY(name(t));
        PROPERTY(name(t)){caption = 'Старая цена';}
        ADD PROPERTY(objValue);
        PROPERTY(objValue){caption = 'Изменение, %';}
        ADD PROPERTY(name(t2));
        PROPERTY(name(t2)){caption = 'Новая цена';}
    }
    ADD functions.box;
}

sessionDataPriceListType 'Вид цены' = DATA SESSION DataPriceListType();

changeMarkUpSkuUserPriceList 'Изменить наценку' = ACTION (userPriceList, stock){

    FORM chooseMarkUpSkuUserPriceList OBJECTS p = userPriceList MODAL;
    IF formResult() == FormResult.ok THEN {
        IF TRUE AND NOT sumSelectedPriceList(userPriceList) THEN MESSAGE 'Выберите хотя бы одну позицию' ELSE
        SET priceUserPriceListDetailDataPriceListType(detail, t) <-
                currentPricePriceListDetailDataPriceListTypeStock(detail, chosenObject('t'), stock) * (chosenNumeric('n') + 100.0) / 100.0
                WHERE isSelectedPriceListDetail(detail) AND userPriceListUserPriceListDetail(detail) == userPriceList AND t == chosenObject('t2');
    }
}

FORM userPriceList 'Прайс'
    TREE skuTree sk = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = name(sk)
    ORDER BY skuTreeName

    OBJECTS p = UserPriceList FIXED PANEL
    PROPERTIES(p) objectClassName, nameNumeratorObject, numberObject, seriesObject,
                  dateUserPriceList, timeUserPriceList,
                  fromDateUserPriceList, fromTimeUserPriceList, toDateUserPriceList, toTimeUserPriceList,
                  nameCurrencyUserPriceList, nameCompanyUserPriceList, noteUserPriceList, includeVATLedgerPriceListType

    OBJECTS t = PriceListType
    PROPERTIES(t) READONLY name
    PROPERTIES(p, t) inPriceListDataPriceListType TODRAW t FORCE GRID, showPriceListPriceListType
    FILTERS (currencyUserPriceList(p) == currencyPriceListType(t)) OR (p IS UserPriceList AND t IS PriceListType AND NOT currencyPriceListType(t))

    OBJECTS d = UserPriceListDetail
    PROPERTIES(d) isSelectedPriceListDetail, indexUserPriceListDetail, idBarcodeSkuUserPriceListDetail, nameSkuUserPriceListDetail,
                  shortNameUOMSkuUserPriceListDetail, ADDOBJ, delete
    PROPERTIES(d, t) pricePriceListDetailDataPriceListType COLUMNS (t) HEADER extendNameDataPriceListType(p, t) BACKGROUND backgroundDataPriceListType(t) TODRAW d FORCE GRID
    PROPERTIES(p) TODRAW d FORCE PANEL TOOLBAR addDetailInputBarcodeUserPriceListDetailUserPriceList
    FILTERS userPriceListUserPriceListDetail(d) == p,
            isParentSkuGroupSku(sk, skuPriceListDetail(d)) OR (sk IS SkuGroup AND d IS PriceListDetail AND NOT skuPriceListDetail(d))


    TREE stockTree a=STRING[3], sg = StockGroup PARENT parentStockGroup
    PROPERTIES READONLY OBJVALUE(a), sgTreeName = name(sg)
    PROPERTIES(p) inPriceList TODRAW a FORCE GRID
    PROPERTIES(p, sg) inPriceListStockGroupOver
    ORDER BY sgTreeName
    FILTERS stringEqualsAll(a)
    FILTERS quantityStocksStockGroup(sg)

    OBJECTS ts = Stock
    PROPERTIES READONLY  name(ts)
    PROPERTIES(p, ts)    inPriceListStockOver
    PROPERTIES(d, t, ts) currentPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER extendNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t)
    PROPERTIES(d, t, ts) markUpPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER extendNameMarkUpDataPriceListType(p, t) BACKGROUND backgroundMarkUpDataPriceListType(t)
    FILTERS inStockGroupStock(sg, ts),
            isCompanyStock(ts)
    FILTERGROUP filters
            FILTER 'Выбранные склады' 'F10' inPriceListStockOver(p, ts) DEFAULT

    OBJECTS tt = Stock FIXED PANEL
    PROPERTIES SELECTOR stockName = name(tt) FORCE PANEL
    PROPERTIES(d, t, tt) currentPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER extendNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t) TODRAW d FORCE GRID
    PROPERTIES(d, t, tt) markUpPricePriceListDetailDataPriceListTypeStock COLUMNS (t) HEADER extendNameMarkUpDataPriceListType(p, t) BACKGROUND backgroundMarkUpDataPriceListType(t) TODRAW d FORCE GRID
    PROPERTIES(p, tt)    changeMarkUpSkuUserPriceList TODRAW d FORCE PANEL TOOLBAR
    FILTERS isCompanyStock(tt)

    TREE skuTree2 sk2 = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName2 = name(sk2)
    ORDER BY skuTreeName2

    OBJECTS ts2 = Stock FIXED PANEL
    PROPERTIES(ts2) SELECTOR name
    FILTERS inPriceListStockOver(p, ts2),
            isCompanyStock(ts2)

    OBJECTS t2 = PriceListType FIXED PANEL
    PROPERTIES(t2) SELECTOR name

    OBJECTS s2=Sku
    PROPERTIES inSkuUserPriceList(s2, p) ON CHANGE changeInSkuUserPriceList(s2, p)
    PROPERTIES READONLY inputName2 = nameSku(s2), idBarcodeSku(s2), currentBalanceSkuStock(s2, ts2)
    PROPERTIES READONLY currentPriceSkuPriceListDataPriceListTypeStock(s2, p, t, ts2) COLUMNS (t) HEADER extendNameCurrentDataPriceListType(p, t) BACKGROUND backgroundCurrentDataPriceListType(t)
    FILTERGROUP filters
        FILTER 'С действующей ценой' 'F10' currentPriceSkuPriceListDataPriceListTypeStock(s2, p, t2, ts2) DEFAULT

    FILTERGROUP filters2
        FILTER 'С остатком' 'F11' currentBalanceSkuStock(s2, ts2)

    FILTERS isParentSkuGroupSku(sk2, s2)
    ORDER BY inputName2

    EVENTS
        ON OK prePostUserPriceList(p)

    EDIT UserPriceList OBJECT p
;

DESIGN userPriceList FROM DEFAULT {
//    REMOVE st.box;
    NEW topContainer{
        NEW headContainer {
            caption = 'Шапка документа';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY (objectClassName);
            ADD PROPERTY (nameNumeratorObject);
            ADD PROPERTY (numberObject);
            ADD PROPERTY (seriesObject);
            ADD PROPERTY (dateUserPriceList);
            ADD PROPERTY (timeUserPriceList);

        }
        NEW timeContainer{
            caption = 'Период действия';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY (fromDateUserPriceList);
            ADD PROPERTY (fromTimeUserPriceList);
            ADD PROPERTY (toDateUserPriceList);
            ADD PROPERTY (toTimeUserPriceList);
        }
        NEW propContainer{
            caption = 'Параметры документа';
            childConstraints = TO THE RIGHT;
            ADD PROPERTY(nameCurrencyUserPriceList);
            ADD PROPERTY(nameCompanyUserPriceList);
            ADD PROPERTY(noteUserPriceList);
        }
        POSITION propContainer TO THE RIGHT timeContainer;
        NEW detailContainer{
            childConstraints = TO THE RIGHT;
            type = TABBED;
            NEW firstContainer{
                caption = 'Параметры';
                type = SPLITH;
                NEW firstSecondContainer{
                    type = SPLITV;
                    ADD t.box;
                    ADD stockTree.tree.box{caption = 'Группы складов';}
                }
                ADD ts.box{fillHorizontal = 2.0;}
            }
            NEW secondContainer {
                caption = 'Спецификация';
                childConstraints = TO THE BOTTOM;

                ADD tt.box; // PROPERTY(stockName);
                NEW secondSecondContainer{
                    type = SPLITH;
                    childConstraints =  TO THE RIGHT;
                    ADD skuTree.tree.box {caption = 'Товары';}
                    ADD d.box{fillHorizontal = 3.0;}
                }
            }
            NEW thirdContainer{
                caption = 'Подбор';
                NEW topThirdContainer{
                    childConstraints = TO THE RIGHT;
                    ADD ts2.box;
                    ADD t2.box;
                }
                NEW detailThirdContainer{
                    childConstraints = TO THE RIGHT;
                    type = SPLITH;
                    ADD skuTree2.tree.box;
                    ADD s2.box{fillHorizontal = 3.0;}
                }
           }
        }
        PROPERTY(formOk) {
            caption = 'Провести';
        }
    }

    ADD functions.box;
}

addUserPriceList 'Добавить' = ACTION ADDFORM UserPriceList;
editUserPriceList 'Редактировать' = ACTION EDITFORM UserPriceList;
editPriceList 'Редактировать' = ABSTRACT ACTION (PriceList) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;
editPriceList (p) += editUserPriceList(p);

copyPriceList 'Копировать' = ACTION (priceList) NEWSESSION {

    FOR ADDOBJ u = UserPriceList DO {
        SET currencyUserPriceList(u) <- currencyUserPriceList(priceList);
        SET inUserPriceListStock(u, stock) <- inUserPriceListStock(priceList, stock);
        SET inUserPriceListStockGroup(u, stockGroup) <- inUserPriceListStockGroup(priceList, stockGroup);
        SET inUserPriceList(u) <- inUserPriceList(priceList);
        SET inPriceListDataPriceListType(u, priceListType) <- inPriceListDataPriceListType(priceList, priceListType);
        FOR userPriceListUserPriceListDetail(detail) == priceList DO {
            FOR ADDOBJ d = UserPriceListDetail DO {
                SET userPriceListUserPriceListDetail(d) <- u;
                SET skuUserPriceListDetail(d) <- skuUserPriceListDetail(detail);
            }
        }
        FORM userPriceList OBJECTS p = u MANAGESESSION DOCKEDMODAL;
    }
}

FORM priceLists 'Прайсы'

    OBJECTS p = PriceList
    PROPERTIES(p) READONLY isPostedPriceList FORCE GRID, numberObject, seriesObject, datePriceList, timePriceList,
                           fromDatePriceList, fromTimePriceList, toDatePriceList, toTimePriceList,
                           nameCurrencyPriceList, nameCompanyPriceList, priceListTypesPriceList, stocksPriceList,
                           notePriceList, objectClassName

    PROPERTIES ()  addUserPriceList TODRAW p
    PROPERTIES (p) editPriceList, copyPriceList FORCE PANEL TOOLBAR
    PROPERTIES (p) delete FORCE PANEL TOOLBAR SHOWIF isUserPriceList(p)

    OBJECTS t = DataPriceListType

    OBJECTS d = UserPriceListDetail
    PROPERTIES(d) READONLY indexUserPriceListDetail, idBarcodeSkuUserPriceListDetail, nameSkuUserPriceListDetail,
                           shortNameUOMSkuUserPriceListDetail

    OBJECTS ts = Stock
    PROPERTIES(ts) READONLY name
    PROPERTIES(p, ts) READONLY inPriceListStockOver

    PROPERTIES(d, t) READONLY pricePriceListDetailDataPriceListType COLUMNS (t) HEADER name(t)

    FILTERS userPriceListUserPriceListDetail(d) == p,
            inPriceListDataPriceListType(p, t),
            inPriceListStockOver(p, ts),
            currencyUserPriceList(p) == currencyDataPriceListType(t),
            isCompanyStock(ts)

    DIALOG PriceList OBJECT p
;

DESIGN priceLists FROM DEFAULT{

    REMOVE t.box;
    NEW topContainer{
        type = SPLITV;
        ADD p.box{fillVertical = 3.0;}
        NEW detailContainer{
            childConstraints = TO THE RIGHT;
            type = SPLITH;
            fillVertical = 2.0;
            NEW firstContainer{
                childConstraints = TO THE RIGHT;
                type = SPLITH;
                ADD d.box{fillHorizontal = 4.0;}
                ADD ts.box{fillHorizontal = 1.0;}
            }
        }
    }
    ADD functions.box;
}

// Заполнение на основании

addUnderPriceListUserPriceList 'Заполнить на основании' = ACTION (userPriceList){

    FORM priceLists MODAL;
    IF formResult() == FormResult.ok THEN {
        FOR priceListPriceListDetail(detail) == chosenObject('p') DO {
            ADDOBJ UserPriceListDetail;
            FOR d == addedObject() DO {
                SET userPriceListUserPriceListDetail(d) <- userPriceList;
                SET skuUserPriceListDetail(d) <- skuPriceListDetail(detail);
            }
        }
    }
}

EXTEND FORM userPriceList
    PROPERTIES(p) TODRAW d FORCE PANEL TOOLBAR addUnderPriceListUserPriceList
;

// ------- Проведение по priceListLedger ----- //

EXTEND CLASS PriceListDetail : PriceListLedger;

fromDateTimePriceListLedger (ledger) += fromDateTimePriceListDetail(ledger);
toDateTimePriceListLedger (ledger) += toDateTimePriceListDetail(ledger);

isPostedPriceListLedger(ledger) += isPostedPriceListDetail(ledger);

skuPriceListLedger (ledger) += skuPriceListDetail(ledger);

companyPriceListLedger (ledger) += companyPriceList(priceListPriceListDetail(ledger));

descriptionPriceListLedger (ledger) += descriptionPriceList(priceListPriceListDetail(ledger));

pricePriceListLedgerLedgerPriceListType (ledger, type) += pricePriceListDetailDataPriceListType(ledger, type);
inPriceListLedgerStock (ledger, stock) += inPriceListStockOver(priceListPriceListDetail(ledger), stock);

// --------------------------- Расчетные виды цен ------------------------- //

CLASS CalcPriceListType 'Расчетный вид цены' : PriceListType;

basePriceListTypeCalcPriceListType(type) = DATA BasePriceListType (CalcPriceListType);
nameBasePriceListTypeCalcPriceListType 'Базовый вид цены' (type) = name(basePriceListTypeCalcPriceListType(type));

ledgerPriceListTypePriceListType(type) += basePriceListTypeCalcPriceListType(type) AS LedgerPriceListType;

includeVATCalcPriceListType 'Цена с НДС' = DATA BOOLEAN (CalcPriceListType);

includeVATPriceListType(type) += includeVATCalcPriceListType(type);

@defineDocumentHeaderCurrency(calcPriceListType);
currencyPriceListType(calcPriceListType) += currencyCalcPriceListType(calcPriceListType);

groupTypeCalcPriceListType = DATA GroupType (CalcPriceListType);
nameGroupTypeCalcPriceListType 'Тип классификатора' (type) = name(groupTypeCalcPriceListType(type));

TABLE calcPriceListTypeGroup (CalcPriceListType, Group);
markupCalcPriceListTypeGroup 'Надбавка, %' = DATA NUMERIC[8,2] (CalcPriceListType, Group);

TABLE calcPriceListTypeSku (CalcPriceListType, Sku);
dataMarkupCalcPriceListTypeSku 'Надбавка, %' = DATA NUMERIC[8,2] (CalcPriceListType, Sku);

roundConditionCalcPriceListType 'Условие округления' = DATA RoundCondition (CalcPriceListType);
nameRoundConditionCalcPriceListType 'Условие округления' (calcPriceListType) = name(roundConditionCalcPriceListType(calcPriceListType));

CONSTRAINT currencyRoundCondition(roundConditionCalcPriceListType(calcPriceListType)) != currencyCalcPriceListType(calcPriceListType)
           CHECKED BY roundConditionCalcPriceListType
           MESSAGE 'Валюта документа должна совпадать с валютой условия округления цены';

levelParentMarkupGroup (calcPriceListType, group) = GROUP MIN levelGroupGroup(group, parent) IF markupCalcPriceListTypeGroup(calcPriceListType, parent)
                                                              BY calcPriceListType, group PERSISTENT;
nearestParentGroup (calcPriceListType, group) = groupGroupLevel(group, levelParentMarkupGroup(calcPriceListType, group));
nearestMarkupCalcPriceListTypeGroup 'Расчетная надбавка, %' (calcPriceListType, group) =
    markupCalcPriceListTypeGroup(calcPriceListType, nearestParentGroup(calcPriceListType, group)) PERSISTENT;

markupCalcPriceListTypeGroupOver 'Надбавка, %' (type, group) =
    UNION OVERRIDE nearestMarkupCalcPriceListTypeGroup(type, group), markupCalcPriceListTypeGroup(type, group) PERSISTENT;

markupCalcPriceListTypeSku 'Надбавка, %' (type, sku) =
    UNION OVERRIDE markupCalcPriceListTypeGroupOver(type, groupGroupTypeSku(groupTypeCalcPriceListType(type), sku)),
                   dataMarkupCalcPriceListTypeSku(type, sku);

multiplierCalcPriceListTypeSku (type, sku) = (100 + markupCalcPriceListTypeSku(type, sku)) / 100 PERSISTENT;

priceCalcPriceListTypeSkuStockDateTime(type, sku, stock, dateTime) =
    roundPriceRoundCondition(priceBasePriceListTypeSkuStockDateTime(basePriceListTypeCalcPriceListType(type), sku, stock, dateTime) * multiplierCalcPriceListTypeSku(type, sku), roundConditionCalcPriceListType(type));
priceCalcPriceListTypeBatchStockDateTime(type, batch, stock, dateTime) =
    roundPriceRoundCondition(priceBasePriceListTypeBatchStockDateTime(basePriceListTypeCalcPriceListType(type), batch, stock, dateTime) * multiplierCalcPriceListTypeSku(type, skuBatch(batch)), roundConditionCalcPriceListType(type));

pricePriceListTypeSkuStockDateTime(type, sku, stock, dateTime) += priceCalcPriceListTypeSkuStockDateTime(type, sku, stock, dateTime);
pricePriceListTypeBatchStockDateTime(type, batch, stock, dateTime) += priceCalcPriceListTypeBatchStockDateTime(type, batch, stock, dateTime);

backgroundMarkup 'Цвет' (calcPriceListType, group) = RGB(255,160,160) AND markupCalcPriceListTypeGroup(calcPriceListType, group);

// --------------------------- Формы ------------------------- //

editPriceListType 'Редактировать' = ABSTRACT ACTION (priceListType) IMAGE 'edit.png' EDITKEY 'BACK_SPACE' HIDE TOOLBAR;

FORM dataPriceListType 'Пользовательский вид цен'
    OBJECTS d = DataPriceListType FIXED PANEL
    PROPERTIES(d) name, includeVATPriceListType, nameCurrencyDataPriceListType

    EDIT DataPriceListType OBJECT d
;

DESIGN dataPriceListType FROM DEFAULT {
    d.box{
        ADD PROPERTY(name);
        ADD PROPERTY(includeVATPriceListType);
        ADD PROPERTY(nameCurrencyDataPriceListType);
    }
}

addDataPriceListType 'Добавить пользовательскую цену' = ACTION ADDFORM DataPriceListType;
editDataPriceListType 'Редактировать' = ACTION EDITFORM DataPriceListType;
editPriceListType(priceListType) += editDataPriceListType(priceListType);

FORM calcPriceListType 'Расчетный вид цен'
    OBJECTS c = CalcPriceListType FIXED PANEL
    PROPERTIES(c) name, includeVATPriceListType, nameCurrencyCalcPriceListType, nameBasePriceListTypeCalcPriceListType,
                  nameRoundConditionCalcPriceListType, nameGroupTypeCalcPriceListType

    TREE treeGroup g=Group PARENT parentGroup
    PROPERTIES READONLY name(g)
    FILTERS groupTypeGroup(g) == groupTypeCalcPriceListType(c)
    ORDER BY name(g)
    PROPERTIES(c, g) markupCalcPriceListTypeGroup , nearestMarkupCalcPriceListTypeGroup

    OBJECTS s = Sku
    PROPERTIES(s) READONLY nameSku, idBarcodeSku
    FILTERS isParentGroupSku(g, s) OR s IS Sku AND NOT g
    PROPERTIES(c, s) dataMarkupCalcPriceListTypeSku, markupCalcPriceListTypeSku

    EDIT CalcPriceListType OBJECT c
;

DESIGN calcPriceListType FROM DEFAULT{
    NEW mainContainer{
        childConstraints = TO THE BOTTOM;
        ADD c.box{
            fillHorizontal = 3.0;
            childConstraints = TO THE RIGHTBOTTOM;
            ADD PROPERTY(nameCurrencyCalcPriceListType);
        }
        NEW groupContainer {
            childConstraints = TO THE BOTTOM;
            ADD PROPERTY(nameGroupTypeCalcPriceListType);
            NEW treeContainer{
                type = SPLITH;
                childConstraints = TO THE RIGHT;
                ADD treeGroup.tree.box { fillHorizontal = 2.0; }
                ADD s.box { fillHorizontal = 2.0; }
            }
        }
    }
    ADD functions.box;
}

addCalcPriceListType 'Добавить расчетную цену' = ACTION ADDFORM CalcPriceListType;
editCalcPriceListType 'Редактировать' = ACTION EDITFORM CalcPriceListType;
editPriceListType(priceListType) += editCalcPriceListType(priceListType);

FORM priceListType 'Вид цены'
    OBJECTS p = PriceListType
    PROPERTIES(p) READONLY name, nameCurrencyPriceListType
    PROPERTIES(p) includeVATPriceListType

    DIALOG PriceListType OBJECT p
;

FORM priceListTypes 'Виды цен'
    OBJECTS dt = DATETIME FIXED PANEL
    PROPERTIES(dt) objValue = OBJVALUE

    OBJECTS s = Stock FIXED PANEL
    PROPERTIES(s) SELECTOR name

    OBJECTS pt = PriceListType
    PROPERTIES(pt) READONLY objectClassName, name, nameCurrencyPriceListType
    PROPERTIES() TODRAW pt FORCE PANEL addDataPriceListType, addCalcPriceListType
    PROPERTIES(pt) editPriceListType, delete
    ORDER BY objectClassName

    OBJECTS p = UserPriceList
    PROPERTIES(p) READONLY isPostedUserPriceList FORCE GRID, numberObject, seriesObject, dateUserPriceList, timeUserPriceList,
                           fromDateUserPriceList, fromTimeUserPriceList, toDateUserPriceList, toTimeUserPriceList,
                           nameCurrencyUserPriceList, nameCompanyUserPriceList, priceListTypesPriceList, stocksPriceList,
                           noteUserPriceList, objectClassName
    PROPERTIES(p) EDITFORM, delete
    ORDER BY fromDateUserPriceList, fromTimeUserPriceList
    FILTERS inPriceListDataPriceListType(p, pt),
            inPriceListStockOver(p, s)

    TREE skuTree sg = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName = name(sg)
    ORDER BY skuTreeName

    OBJECTS sk = Sku
    PROPERTIES(sk) READONLY nameSku, idBarcodeSku
    PROPERTIES(sk, s, dt) READONLY balanceASkuStockDateTime
    ORDER BY nameSku
    FILTERS isParentSkuGroupSku(sg, sk)

    TREE skuTree2 sg2 = SkuGroup PARENT parentSkuGroup
    PROPERTIES READONLY skuTreeName2 = name(sg2)
    ORDER BY skuTreeName2

    OBJECTS b = Batch
    PROPERTIES(b) READONLY isPostedBatch, dateBatch, timeBatch, nameSkuBatch, nameStockBatch, descriptionBatch, costBatch
    PROPERTIES(b, s, dt) READONLY balanceABatchStockDateTime
    ORDER BY dateBatch, timeBatch
    FILTERGROUP filters
        FILTER 'Проведенные партии' 'F10' isPostedBatch(b) DEFAULT
    FILTERS isParentSkuGroupBatch(sg2, b)

    PROPERTIES(pt, sk, s, dt) READONLY pricePriceListTypeSkuStockDateTime, nameCompanyLedgerPriceListTypeSkuStockDateTime, descriptionLedgerPriceListTypeSkuStockDateTime
    PROPERTIES(pt, b, s, dt) READONLY pricePriceListTypeBatchStockDateTime, nameCompanyLedgerPriceListTypeBatchStockDateTime, descriptionLedgerPriceListTypeBatchStockDateTime
    FILTERS pricePriceListTypeSkuStockDateTime(pt, sk, s, dt),
            pricePriceListTypeBatchStockDateTime(pt, b, s, dt)
;
@extendFormFilterStockAccess(Stock, s, priceListTypes);

DESIGN priceListTypes FROM DEFAULT{
    NEW mainContainer{
        type = SPLITV;
        ADD pt.box;
        NEW firstContainer{
            fillVertical = 2.0;
            ADD s.box;
            NEW tabContainer{
                type = TABBED;
                ADD p.box{
                    caption = 'Прайсы';
                }
                NEW priceContainer{
                    caption = 'Текущие цены';
                    childConstraints = TO THE BOTTOM;
                    ADD dt.box;
                    NEW currentPriceContainer{
                        type = TABBED;
                        NEW skuContainer{
                            caption = 'Товары';
                            childConstraints = TO THE RIGHT;
                            type = SPLITH;
                            ADD skuTree.tree.box;
                            ADD sk.box{fillHorizontal = 2.0;}
                        }
                        NEW batchContainer{
                            caption = 'Партии';
                            childConstraints = TO THE RIGHT;
                            type = SPLITH;
                            ADD skuTree2.tree.box;
                            ADD b.box{fillHorizontal = 2.0;}
                        }
                    }
                }
            }
        }
    }
    ADD functions.box;
}

addUserPriceListLegalEntity 'Добавить' = ACTION (legalEntity) {

    FOR ADDOBJ u = UserPriceList DO {
        SET companyUserPriceList(u) <- legalEntity;
        FORM userPriceList OBJECTS p = u DOCKEDMODAL;
        IF TRUE AND NOT formResult() == FormResult.ok THEN {
            EXEC delete(u);
        }
    }
} TOOLBAR;

EXTEND FORM legalEntity

    OBJECTS p = UserPriceList
    PROPERTIES(p) READONLY objectClassName, numberObject, seriesObject, fromDateTimeUserPriceList, toDateTimeUserPriceList,
                           nameCurrencyUserPriceList, nameCompanyUserPriceList, noteUserPriceList, priceListTypesPriceList,
                           stocksPriceList
    PROPERTIES addUserPriceListLegalEntity(l) TODRAW p FORCE PANEL
    PROPERTIES(p) EDITSESSIONFORM, delete FORCE PANEL TOOLBAR
    FILTERS companyUserPriceList(p) == l
;

EXTEND DESIGN legalEntity{

    extendContainer {
        ADD p.box{caption = 'Прайсы';}
    }
}


NAVIGATOR {
    NEW priceListNavigator 'Цены' BEFORE administration TO toolbar IMAGE '/images/label.png' {
        NEW priceListDocuments 'Документы' {
            ADD priceLists;
        }
        NEW priceListMasterData 'Справочники' {
            ADD priceListTypes;
            ADD priceIntervals;
            ADD priceStockGroups;
        }
    }
}

// ---------------------- Макросы по добавлению видов цен в документы ------------------------------------ //

META defineDocumentHeaderPriceListType (object, type, prefix, caption)
    @defineDocumentHeaderPriceListTypeInner (object, type, ###type, prefix, caption);
END

META defineDocumentHeaderPriceListTypeInner (object, type, typeClass, prefix, caption)
    prefix###type###object (object) = DATA typeClass (###object);
    name###prefix###type###object 'Вид цен'###caption (object)= name(prefix###type###object(object)) IN documentPrmGroup MINCHARWIDTH 5 PREFCHARWIDTH 10;
END

META defineDocumentAbstractHeaderPriceListType (object, type, prefix, caption)
    @defineDocumentAbstractHeaderPriceListTypeInner (object, type, ###type, prefix, caption);
END

META defineDocumentAbstractHeaderPriceListTypeInner (object, type, typeClass, prefix, caption)
    prefix###type###object (object) = ABSTRACT typeClass (###object) PERSISTENT;
    name###prefix###type###object 'Вид цен'###caption (object)= name(prefix###type###object(object)) IN documentPrmGroup MINCHARWIDTH 5 PREFCHARWIDTH 10;
END
META defineDocumentInterfaceHeaderPriceListType (object, type, prefix, caption)
    @defineDocumentAbstractHeaderPriceListType(object, type, prefix, caption);
    @defineDocumentHeaderPriceListType(user###object, type, prefix, caption);
    prefix###type###object (object) += prefix###type###user###object(object);
END

META defineDocumentHeaderPriceListType (object)
    @defineDocumentHeaderPriceListType(object, priceListType, ,);
END
META defineDocumentAbstractHeaderPriceListType (object)
    @defineDocumentAbstractHeaderPriceListType(object, priceListType, ,);
END
META defineDocumentInterfaceHeaderPriceListType (object)
    @defineDocumentInterfaceHeaderPriceListType(object, priceListType, ,);
END

//--
META defineDocumentDetailPriceListTypePrefix (object, detail, type, prefix, caption)
    prefix###type###detail = prefix###type###object(object###detail (detail));
    name###prefix###type###detail 'Вид цен'###caption (idetail) = name(prefix###type###detail(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;
END
//--

META defineDocumentDetailPriceListType (object, detail, type, prefix, caption)
    @defineDocumentDetailPriceListTypeInner (object, detail, type, ###type, prefix, caption);
END

META defineDocumentDetailPriceListTypeInner (object, detail, type, typeClass, prefix, caption)
    prefix###type###detail =  DATA typeClass (###detail);
    name###prefix###type###detail 'Вид цен'###caption (idetail) = name(prefix###type###detail(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;
END

META defineDocumentAbstractDetailPriceListType (object, detail, type, prefix, caption)
    @defineDocumentAbstractDetailPriceListTypeInner(object, detail, type, ###type, prefix, caption);
END

META defineDocumentAbstractDetailPriceListTypeInner (object, detail, type, typeClass, prefix, caption)
    prefix###type###detail =  ABSTRACT typeClass (###detail) PERSISTENT;
    name###prefix###type###detail 'Вид цен'###caption (idetail) = name(prefix###type###detail(idetail)) MINCHARWIDTH 10 PREFCHARWIDTH 20;
END

META defineDocumentPriceListType (object, detail, type, prefix, caption)
    @defineDocumentHeaderPriceListType(object, type, prefix, caption);
    @defineDocumentDetailPriceListType(object, detail, type, prefix, caption);
END
META defineDocumentPriceListType (object, type, prefix, caption)
    @defineDocumentPriceListType(object, object##Detail, type, prefix, caption);
END

META defineDocumentInterfaceDetailPriceListType (object, detail, type, prefix, caption)
    @defineDocumentAbstractDetailPriceListType(object, detail, type, prefix, caption);
    @defineDocumentDetailPriceListType(user###object, user###detail, type, prefix, caption);
    prefix###type###detail (detail) += prefix###type###user###detail (detail);
END

META defineDocumentInterfaceDetailPriceListType (object, type, prefix, caption)
    @defineDocumentInterfaceDetailPriceListType(object, object##Detail, type, prefix, caption);
END

META defineDocumentInterfacePriceListType (object, detail, type, prefix, caption)
    @defineDocumentInterfaceHeaderPriceListType(object, type, prefix, caption);
    @defineDocumentInterfaceDetailPriceListType(object, detail, type, prefix, caption);

    CONSTRAINT currency###user###object(object) != currencyPriceListType(prefix###type###user###object(object))
               CHECKED BY prefix###type###user###object
               MESSAGE 'Валюта документа должна совпадать с валютой вида цены';

    CONSTRAINT currency###user###detail(detail) != currencyPriceListType(prefix###type###user###detail(detail))
               CHECKED BY prefix###type###user###detail
               MESSAGE 'Валюта документа должна совпадать с валютой вида цены';
END
META defineDocumentInterfacePriceListType (object, type, prefix, caption)
    @defineDocumentInterfacePriceListType(object, object##Detail, type, prefix, caption);
END

//--
META defineDocumentDetailPriceListType (object, detail)
    @defineDocumentDetailPriceListType(object, detail, priceListType, , );
END

META defineDocumentAbstractDetailPriceListType (object, detail)
    @defineDocumentAbstractDetailPriceListType(object, detail, priceListType, , );
END

META defineDocumentPriceListType (object, detail)
    @defineDocumentPriceListType (object, detail, priceListType, , );
END
META defineDocumentPriceListType (object)
    @defineDocumentPriceListType(object, object##Detail);
END

META defineDocumentInterfaceDetailPriceListType (object, detail)
    @defineDocumentInterfaceDetailPriceListType (object, detail, priceListType, , );
END

META defineDocumentInterfaceDetailPriceListType (object)
    @defineDocumentInterfaceDetailPriceListType(object, object##Detail);
END

META defineDocumentInterfacePriceListType (object, detail)
    @defineDocumentInterfacePriceListType (object, detail, priceListType, , );
END
META defineDocumentInterfacePriceListType (object)
    @defineDocumentInterfacePriceListType(object, object##Detail);
END

// ----------------------------------------------- Автоматическое проставление

META deriveDocumentDetailPricePriceListTypeCustom (detail, stockProp)
    price###detail(detail) <- prevPricePriceListTypeSkuStockDateTime(priceListType###detail(detail),
                                                                     sku###detail(detail),
                                                                     stockProp###detail(detail),
                                                                     dateTime###detail(detail))
                                    WHEN CHANGED(priceListType###detail(detail)) OR
                                         CHANGED(sku###detail(detail)) OR
                                         CHANGED(stockProp###detail(detail)) OR
                                         CHANGED(dateTime###detail(detail));
END

META deriveDocumentDetailPricePriceListType (object, stockProp)
    @deriveDocumentDetailPricePriceListTypeCustom(object##Detail, stockProp);
END

META deriveDocumentDetailPricePriceListTypeVATCustom (detail, stockProp)
    prevListSkuPrice###detail (detail) = prevPricePriceListTypeSkuStockDateTime(priceListType###detail(detail),
                                                                                sku###detail(detail),
                                                                                stockProp###detail(detail),
                                                                                dateTime###detail(detail));

    prevListSkuPriceMVAT###detail (detail) =
        IF includeVATPriceListType(priceListType###detail(detail))
            THEN
                prevListSkuPrice###detail(detail) * 100.0 /(100.0 + valueVAT###detail(detail))
            ELSE
                prevListSkuPrice###detail(detail);

    prevListSkuPricePVAT###detail (detail) =
        IF includeVATPriceListType(priceListType###detail(detail))
            THEN
                prevListSkuPrice###detail(detail)
            ELSE
                prevListSkuPrice###detail(detail) * (100.0 + valueVAT###detail(detail)) / 100.0;

    price###detail(detail) <- prevListSkuPriceMVAT###detail (detail)
                            WHEN priceListType###detail(detail) AND
                                 (CHANGED(priceListType###detail(detail)) OR
                                 CHANGED(sku###detail(detail)) OR
                                 CHANGED(stockProp###detail(detail)) OR
                                 CHANGED(dateTime###detail(detail)));

    invoice###price###detail(detail) <- prevListSkuPricePVAT###detail (detail)
                            WHEN priceListType###detail(detail) AND
                                 (CHANGED(priceListType###detail(detail)) OR
                                 CHANGED(sku###detail(detail)) OR
                                 CHANGED(stockProp###detail(detail)) OR
                                 CHANGED(dateTime###detail(detail)));

END

META deriveDocumentDetailPricePriceListTypeVAT (object, stockProp)
    @deriveDocumentDetailPricePriceListTypeVATCustom (object##Detail, stockProp);
END

META deriveDocumentDetailPriceBatchPriceListTypeCustom (detail, stockProp)
    price###detail(detail) <- IF batch###detail(detail) THEN
                                    prevPricePriceListTypeBatchStockDateTime(priceListType###detail(detail),
                                                                             batch###detail(detail),
                                                                             stockProp###detail(detail),
                                                                             dateTime###detail(detail))
                              ELSE
                                    prevPricePriceListTypeSkuStockDateTime(priceListType###detail(detail),
                                                                           sku###detail(detail),
                                                                           stockProp###detail(detail),
                                                                           dateTime###detail(detail))
                                    WHEN priceListType###detail(detail) AND
                                        (CHANGED(batch###detail(detail)) OR
                                         CHANGED(priceListType###detail(detail)) OR
                                         CHANGED(sku###detail(detail)) OR
                                         CHANGED(stockProp###detail(detail)) OR
                                         CHANGED(dateTime###detail(detail)));
END

META deriveDocumentDetailPriceBatchPriceListType (object, stockProp)
    @deriveDocumentDetailPriceBatchPriceListTypeCustom(object##Detail, stockProp);
END

META deriveDocumentDetailPricePriceListTypeVATBatchCustom (detail, stockProp)
    prevListSkuPrice###detail (priceListType, detail) = prevPricePriceListTypeSkuStockDateTime(priceListType,
                                                                                               sku###detail(detail),
                                                                                               stockProp###detail(detail),
                                                                                               dateTime###detail(detail));
    prevListBatchPrice###detail (priceListType, detail) = prevPricePriceListTypeBatchStockDateTime(priceListType,
                                                                                                   batch###detail(detail),
                                                                                                   stockProp###detail(detail),
                                                                                                   dateTime###detail(detail));
    prevListPrice###detail (priceListType, detail) = IF batch###detail(detail) THEN prevListBatchPrice###detail(priceListType, detail) ELSE prevListSkuPrice###detail (priceListType, detail);

    prevListPriceMVAT###detail (priceListType, detail) =
        IF includeVATPriceListType(priceListType)
            THEN
                prevListPrice###detail(priceListType, detail) * 100.0 /(100.0 + valueVAT###detail(detail))
            ELSE
                prevListPrice###detail(priceListType, detail);

    prevListPricePVAT###detail (priceListType, detail) =
        IF includeVATPriceListType(priceListType)
            THEN
                prevListPrice###detail(priceListType, detail)
            ELSE
                prevListPrice###detail(priceListType, detail) * (100.0 + valueVAT###detail(detail)) / 100.0;

//    price###detail(detail) <- prevListPriceMVAT###detail (detail)
//                            WHEN priceListType###detail(detail) AND
//                                (CHANGED(priceListType###detail(detail)) OR
//                                 CHANGED(batch###detail(detail)) OR
//                                 CHANGED(sku###detail(detail)) OR
//                                 CHANGED(stockProp###detail(detail)) OR
//                                 CHANGED(dateTime###detail(detail)) OR
//                                 CHANGED(valueVAT###detail(detail)));
//
//    invoicePrice###detail(detail) <- prevListPricePVAT###detail (detail)
//                            WHEN priceListType###detail(detail) AND
//                                (CHANGED(priceListType###detail(detail)) OR
//                                 CHANGED(batch###detail(detail)) OR
//                                 CHANGED(sku###detail(detail)) OR
//                                 CHANGED(stockProp###detail(detail)) OR
//                                 CHANGED(dateTime###detail(detail)) OR
//                                 CHANGED(valueVAT###detail(detail)));

    ON SESSION FORMS userInvoice PREVSTART pricePriceListTypeSkuStockDateTime, pricePriceListTypeBatchStockDateTime { // цены берем на начало сессии, а не этого event'а
        LOCAL changedPriceListType = BOOLEAN (###detail);
        SET changedPriceListType(detail) <- TRUE IF priceListType###detail(detail) AND
                                                    skuUserInvoiceDetail(detail) AND
                                            (CHANGED(priceListType###detail(detail)) OR
                                             CHANGED(batch###detail(detail)) OR
                                             CHANGED(sku###detail(detail)) OR
                                             CHANGED(stockProp###detail(detail)) OR
                                             CHANGED(dateTime###detail(detail)));

        FOR [GROUP MAX(changedPriceListType(detail)) BY priceListType###detail(detail)](priceListType) NOINLINE DO {
            SET price###detail(detail) <- prevListPriceMVAT###detail (priceListType, detail)
                WHERE changedPriceListType(detail) AND priceListType###detail(detail) == priceListType;
            SET invoicePrice###detail(detail) <- prevListPricePVAT###detail (priceListType, detail)
                WHERE changedPriceListType(detail) AND priceListType###detail(detail) == priceListType;
        }
    }

END

META deriveDocumentDetailPricePriceListTypeVATBatch (object, stockProp)
    @deriveDocumentDetailPricePriceListTypeVATBatchCustom (object##Detail, stockProp);
END

// Системные ledger'ы
META deriveDocumentDetailPriceSystemLedgerPriceListType (concrete, priceListTypeProp, prefixP, skuProp, stockProp)
    prefixP###price###concrete##Detail (detail)  <- prevPricePriceListTypeSkuStockDateTime(SystemLedgerPriceListType.##priceListTypeProp,
                                                                                           skuProp###concrete##Detail(detail),
                                                                                           stockProp###concrete##Detail(detail),
                                                                                           dateTime###concrete##Detail(detail))
                                                    WHEN CHANGED(skuProp###concrete##Detail(detail)) OR
                                                         CHANGED(stockProp###concrete##Detail(detail)) OR
                                                         CHANGED(dateTime###concrete##Detail(detail));
END

META deriveDocumentDetailPriceSystemLedgerPriceListTypeBatch (concrete, priceListTypeProp, prefixP, prefixB, skuProp, stockProp)
    prefixP###price###concrete##Detail (detail)  <- IF prefixB###batch###concrete##Detail(detail)

                                                    THEN prevPricePriceListTypeBatchStockDateTime (SystemLedgerPriceListType.##priceListTypeProp,
                                                                                               prefixB###batch###concrete##Detail(detail),
                                                                                               stockProp###concrete##Detail(detail),
                                                                                               dateTime###concrete##Detail(detail))
                                                    ELSE prevPricePriceListTypeSkuStockDateTime(SystemLedgerPriceListType.##priceListTypeProp,
                                                                                           prefixB###skuProp###concrete##Detail(detail),
                                                                                           stockProp###concrete##Detail(detail),
                                                                                           dateTime###concrete##Detail(detail))
                                                    WHEN CHANGED(prefixB###skuProp###concrete##Detail(detail)) OR
                                                         CHANGED(stockProp###concrete##Detail(detail)) OR
                                                         CHANGED(dateTime###concrete##Detail(detail)) OR
                                                         CHANGED(prefixB###batch###concrete##Detail(detail));
END

META deriveDocumentDetailPriceSystemLedgerPriceListTypeBatch (concrete, priceListTypeProp, prefixP, skuProp, stockProp)
    @deriveDocumentDetailPriceSystemLedgerPriceListTypeBatch (concrete, priceListTypeProp, prefixP, , skuProp, stockProp);
END

// ----------------------------- Action'ы для ON CHANGE ------------------------- //

// Изменение цены без НДС
META defineDocumentDetailPriceVATOnChangeCustom (detail)
    changePrice###detail (detail) = ACTION (detail) {
        REQUEST NUMERIC[14,2] INPUT;
        IF requestedNumeric() THEN {
            IF rangeTypeRange(VAT###detail(detail)) == RangeType.units THEN {
                SET price###detail (detail) <- requestedNumeric();
                SET invoicePrice###detail (detail) <- price###detail (detail) * 100 /(100 - valueVAT###detail(detail));
            } ELSE {
                SET price###detail (detail) <- requestedNumeric();
                SET invoicePrice###detail (detail) <- price###detail (detail) * (100 + valueVAT###detail(detail))/100;
            }
        } ELSE {
            SET price###detail (detail) <- NULL;
            SET invoicePrice###detail (detail) <- NULL;
        }
    }
END

META defineDocumentDetailPriceVATOnChange (object)
    @defineDocumentDetailPriceVATOnChangeCustom(object###Detail);
END

// Изменение щкалы НДС
META defineDocumentDetailNumberVATOnChangeCustom (detail, stockProp)
    changeNumberVAT###detail (detail) = ACTION (detail) {
        REQUEST OBJECT r
            FORM rangeDialog OBJECTS c = country###stockProp###detail(detail) MODAL SHOWDROP;
        IF formResult() == FormResult.ok THEN {
            IF rangeTypeRange(chosenObject('r')) == RangeType.units THEN {
                SET VAT###detail (detail) <- requestedObject();
                SET invoicePrice###detail (detail) <- price###detail (detail) * 100 /(100 - valueVAT###detail(detail));
            } ELSE {
                SET VAT###detail (detail) <- requestedObject();
                SET invoicePrice###detail (detail) <- price###detail (detail) * (100 + valueVAT###detail(detail))/100;
            }
        } ELSE IF formResult() == FormResult.drop THEN {
            SET VAT###detail(detail) <- NULL;
            SET invoicePrice###detail(detail) <- price###detail (detail);
        }
    }
END

META defineDocumentDetailNumberVATOnChange (object, stockProp)
    @defineDocumentDetailNumberVATOnChangeCustom(object###Detail, stockProp);
END

// Изменение ставки НДС
META defineDocumentDetailValueVATOnChangeCustom (detail)
    changeValueVAT###detail (detail) = ACTION (detail) {
        REQUEST NUMERIC[10,5] INPUT;
        IF requestedNumeric() THEN {
            IF rangeTypeRange(VAT###detail(detail)) == RangeType.units THEN {
                SET valueVAT###detail (detail) <- requestedNumeric();
                SET invoicePrice###detail (detail) <- price###detail (detail) * 100 /(100 - requestedNumeric());
            } ELSE {
                SET valueVAT###detail (detail) <- requestedNumeric();
                SET invoicePrice###detail (detail) <- price###detail (detail) * (100 + requestedNumeric())/100;
            }
        }
    }
END

META defineDocumentDetailValueVATOnChange (object)
    @defineDocumentDetailValueVATOnChangeCustom(object###Detail);
END

// Изменение цены с НДС
META defineDocumentDetailInvoicePriceVATOnChangeCustom (detail)
    changeInvoicePrice###detail (detail) = ACTION (detail) {
        REQUEST NUMERIC[14,2] INPUT;
        IF requestedNumeric() THEN {
            IF rangeTypeRange(VAT###detail(detail)) == RangeType.units THEN {
                SET invoicePrice###detail (detail) <- requestedNumeric();
                SET price###detail (detail) <- invoicePrice###detail (detail) * (100-valueVAT###detail(detail))/100;
            } ELSE {
                SET invoicePrice###detail (detail) <- requestedNumeric();
                SET price###detail (detail) <- invoicePrice###detail (detail) * 100/(100 + valueVAT###detail(detail));
            }
        } ELSE {
            SET price###detail (detail) <- NULL;
            SET invoicePrice###detail (detail) <- NULL;
        }
    }
END

META defineDocumentDetailInvoicePriceVATOnChange (object)
    @defineDocumentDetailInvoicePriceVATOnChangeCustom(object###Detail);
END

// ----------------------------------------- Операции --------------------------------------- //

META defineOperationPriceListType(dumb)

    TABLE priceListType###operation(PriceListType, Operation);
    isPriceListType###operation 'Отм.' = DATA BOOLEAN (PriceListType, Operation);

    EXTEND FORM operation
        OBJECTS pt = PriceListType
        PROPERTIES(pt, o) isPriceListType###operation
        PROPERTIES(pt) READONLY objectClassName, name, nameCurrencyPriceListType
        FILTERGROUP filters
            FILTER 'Показывать отмеченные' 'F9' isPriceListType###operation(pt, o)
    ;

    EXTEND DESIGN operation {
        tabContainer {
            ADD pt.box;
        }
    }

END

META defineDocumentOperationPriceListType(object)
    CONSTRAINT operation###object(object) AND priceListType###object(object)
               AND NOT isPriceListType###operation(priceListType###object(object), operation###object(object))
        CHECKED BY priceListType###object
        MESSAGE 'Вид цены для документа должен совпадать с отмеченными в операции';

    CONSTRAINT operation###object##Detail(detail)
               AND NOT isPriceListType###operation(priceListType###object##Detail(detail), operation###object##Detail(detail))
        CHECKED BY priceListType###object##Detail
        MESSAGE 'Вид цены для строки документа должен совпадать с отмеченными в операции';
END
