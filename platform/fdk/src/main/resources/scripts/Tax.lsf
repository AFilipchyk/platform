MODULE Tax;

REQUIRE System, Utils, Hierarchy, Country;

// ---------------------------------- Налог --------------------------------------- //

CLASS STATIC tax 'Налоги'
{
    taxVAT 'НДС',
    taxNP 'Налог с продаж'
};

nameTax 'Наименование налога' = DATA STRING[200] (tax) IN baseGroup;

// ---------------------------------- Шкала налога --------------------------------------- //

CLASS range 'Шкала';
TABLE range(range);
numberRange 'Шкала' = DATA INTEGER (range) AGGPROP IN recognizeGroup;

taxRange = DATA tax (range) NOT NULL;
nameTaxRange 'Налог' (range) = name(taxRange(range)) IN recognizeGroup;

countryRange = DATA country (range) NOT NULL AUTOSET;
nameCountryRange 'Страна' (range) = name(countryRange(range)) IN recognizeGroup;

countRangeTaxCountry 'Кол-во шкал' (tax, country) = GROUP SUM 1 IF range IS range BY taxRange(range), countryRange(range);

maxDataRangeTax(tax, country) = UNION OVERRIDE 0 IF tax IS tax AND country IS country, [GROUP MAX numberRange(range) BY taxRange(range), countryRange(range)](tax, country);
addNewRange 'Добавить шкалу' = ACTION (tax, country) {
    ADDOBJ range;
    SET numberRange(r) IF r == addedObject() <- maxDataRangeTax(tax, country) + 1;
} TOOLBAR IMAGE 'add.png';

CLASS STATIC rangeType 'Для каких подразделений действует шкала'
{
    common 'Общий',
    units 'Для подразделений'
};
rangeTypeRange = DATA rangeType (range) NOT NULL;
nameRangeTypeRange 'Тип шкалы' = name(rangeTypeRange(range)) IN baseGroup;

isUnitsTypeRange(range) = rangeTypeRange(range) == rangeType.units;

rangeTypeRange(range) <- rangeType.common WHEN ASSIGNED(range IS range);     /// потом согласовать, какое значение должно быть по-умолчанию ///

// ----------------------------- Разбиение шкалы для подразделений ------------------------------------- //

CLASS ABSTRACT taxUnitGroup 'Группа налогового субъекта' : named;
@defineHierarchyAbstract(taxUnitGroup);

CLASS ABSTRACT taxUnit 'Налоговый субъект';
TABLE taxUnit(taxUnit);

taxUnitGroupTaxUnit = ABSTRACT taxUnitGroup (taxUnit) PERSISTENT;
descriptionTaxUnit 'Название налогового субъекта' = ABSTRACT STRING[255] (taxUnit) PERSISTENT;

TABLE rangeTaxUnit (range, taxUnit);
inRangeTaxUnit 'Вкл.' (range, taxUnit) = DATA BOOLEAN (range, taxUnit);
inRangeTaxUnit (range, taxUnit) => isUnitsTypeRange(range) RESOLVE FALSE;

rangeVATTaxTaxUnit 'Шкала по налогу и субъекту' (tax, taxUnit) = GROUP UNIQUE range BY taxRange(range), taxUnit
                                                                       WHERE inRangeTaxUnit(range, taxUnit) AND tax.taxVAT == taxRange(range);


// ---------------------------------- История изменений шкалы --------------------------------------- //

CLASS rate 'История изменений шкалы' : historyObject;
TABLE rate(rate);

rangeRate = DATA range (rate) IN idGroup;
quantityRateRange 'Количество историй' (range) = GROUP SUM 1 IF rate BY rangeRate(rate);

valueRate 'Значение ставки' = DATA NUMERIC[10,5] (rate) IN recognizeGroup NOT NULL;
dateFromRate 'Действует с' = DATA DATE (rate) IN baseGroup;
dateToRate 'Действует по' = DATA DATE (rate) IN baseGroup;

rateRangeDate(range, date) = GROUP MAX rate IF dateFromRate(rate) <= date AND NOT dateToRate(rate) < date AND date AS DATE
                                         BY rangeRate(rate), date;
valueRateRangeDate 'На дату' (range, date) = valueRate(rateRangeDate(range, date));

currentRateRange (range) = rateRangeDate(range, currentDate()) IN idGroup PERSISTENT;
valueCurrentRateRange 'Действующая ставка' (range) = valueRate(currentRateRange(range)) IN baseGroup;

dateFromActingRateRange 'Действует с' (range) = dateFromRate(currentRateRange(range)) IN baseGroup;
dateToActingRateRange 'Действует по' (range) = dateToRate(currentRateRange(range)) IN baseGroup;
nameUserCreatedActingRateRange 'Создан пользователем' (range) = nameUserCreated(currentRateRange(range)) IN baseGroup;

CONSTRAINT rangeRate(rt1) == rangeRate(rt2) AND NOT (dateFromRate(rt2) <= dateFromRate(rt1) OR dateToRate(rt1) < dateFromRate(rt2))  MESSAGE 'ошибка: Одновременно не может быть несколько действующих ставок / или не задано: действует с';

// для импорта
valueCurrentVATCountryValue 'Шкала НДС по значению' (country, doubleValue) = GROUP UNIQUE range BY countryRange(range), valueCurrentRateRange (range);

valueCurrentVATDefaultValue 'Шкала по значению' (doubleValue) = valueCurrentVATCountryValue(defaultCountry(), doubleValue);

// --------------------------------------- Формы ------------------------------------- //

FORM tax 'Налог'
    OBJECTS           t = tax FIXED PANEL
    PROPERTIES(t)     name, nameTax

    OBJECTS c = country FIXED PANEL
    PROPERTIES(c) SELECTOR    name

    OBJECTS           ra = range
    PROPERTIES(ra)    numberRange, nameRangeTypeRange, delete
    PROPERTIES(ra)    READONLY valueCurrentRateRange, dateFromActingRateRange, dateToActingRateRange
    PROPERTIES(t, c)  addNewRange TODRAW ra
    FILTERS           taxRange(ra) == t
    FILTERS           countryRange(ra) == c

    OBJECTS           rt=rate
    PROPERTIES(rt)    valueRate, dateFromRate, dateToRate, timeCreated, nameUserCreated, ADDOBJ, delete
    FILTERS           rangeRate(rt) == ra

    TREE treeTaxUnit  a=STRING[3], ug = taxUnitGroup PARENT parentTaxUnitGroup
    PROPERTIES READONLY OBJVALUE(a), name(ug)
    FILTERS           stringEqualsAll(a)

    OBJECTS           u=taxUnit
    PROPERTIES(u) READONLY objectClassName, descriptionTaxUnit
    PROPERTIES(ra, u)      inRangeTaxUnit SHOWIF isUnitsTypeRange(ra)
    FILTERS           taxUnitGroupTaxUnit(u) == ug OR u IS taxUnit AND NOT ug

    FILTERGROUP filters
        FILTER 'Показывать магазины только для данного налога' 'F10' inRangeTaxUnit(ra, u)

    EDIT tax OBJECT t
;

DESIGN tax FROM DEFAULT {
    main {
        NEW row1 BEFORE functions.box {
            childConstraints = TO THE RIGHT;
            ADD t.box {
                t.panel { childConstraints = TO THE RIGHT; }
            }
            ADD c.box;
        }
        NEW secondCase BEFORE functions.box {
            ADD u.box {
                fillHorizontal = 3.8;
            };
            ADD treeTaxUnit.tree.box {
                title = 'Налоговые субъекты';
                fillHorizontal = 1.2;
            };
            POSITION treeTaxUnit.tree.box TO THE LEFT u.box;
        }
        NEW firstCase BEFORE secondCase {
            ADD ra.box {
                fillHorizontal = 2.5;
            };
            ADD rt.box {
                fillHorizontal = 2.5;
            };
            POSITION ra.box TO THE LEFT rt.box;
        }
    }
}

FORM taxes 'Налоги'
    OBJECTS           t=tax
    PROPERTIES(t)     READONLY name, nameTax
    PROPERTIES(t)     EDITFORM, delete

    OBJECTS           c=country
    PROPERTIES(c)     name
    FILTERS           countRangeTaxCountry(t, c)

    OBJECTS ra=range
    PROPERTIES(ra) READONLY numberRange, valueCurrentRateRange, dateFromActingRateRange, dateToActingRateRange, nameUserCreatedActingRateRange, nameRangeTypeRange, quantityRateRange
    FILTERS                 taxRange(ra) == t,
                            countryRange(ra) == c

    OBJECTS u=taxUnit
    PROPERTIES(u)     READONLY descriptionTaxUnit
    FILTERS                    inRangeTaxUnit(ra, u)
;

DESIGN taxes FROM DEFAULT {

    NEW topContainer{

        type = SPLITV;
        childConstraints = TO THE BOTTOM;

        NEW row1 {
            type = SPLITH;
            childConstraints = TO THE RIGHT;

            ADD t.box;
            ADD c.box;
        }
        NEW firstCase {

            type = SPLITV;

            ADD ra.box;
            ADD u.box;
        }
    }
    ADD functions.box;
}

// ----------------------------------------- Макросы по объявлению налогов в документах ---------------------------- //

META defineDocumentDetailVATCustom(detail, countryProp)
    VAT###detail(detail) = DATA range (detail);
    numberVAT###detail 'НДС, номер' (detail) = numberRange(VAT###detail(detail));
    valueVAT###detail 'НДС, %' (detail) = valueRateRangeDate(VAT###detail(detail), date###detail(detail));

    CONSTRAINT taxRange(VAT###detail(detail)) != tax.taxVAT OR
               countryRange(VAT###detail(detail)) != countryProp###detail(detail)
               CHECKED BY VAT###detail
               MESSAGE 'ошибка: Шкала и страна строки должна соответствовать шкале и строке НДС';
END
META defineDocumentAbstractDetailVATCustom(detail, countryProp)
    VAT###detail(detail) = ABSTRACT range (detail) PERSISTENT;
    numberVAT###detail 'НДС, номер' (detail) = numberRange(VAT###detail(detail));
    valueVAT###detail 'НДС, %' (detail) = valueRateRangeDate(VAT###detail(detail), date###detail(detail));
END
META defineDocumentInterfaceDetailVATCustom(detail, countryProp)
    @defineDocumentAbstractDetailVATCustom(detail, countryProp);
    @defineDocumentDetailVATCustom(user###detail, countryProp);
    VAT###detail(detail) += VAT###user###detail(detail);
END

META defineDocumentDetailVAT(object, countryProp)
    @defineDocumentDetailVATCustom(object##Detail, countryProp);
END
META defineDocumentAbstractDetailVAT(object, countryProp)
    @defineDocumentAbstractDetailVATCustom(object##Detail, countryProp);
END
META defineDocumentInterfaceDetailVAT(object, countryProp)
    @defineDocumentInterfaceDetailVATCustom(object##Detail, countryProp);
END

META defineDocumentDetailVATSumCustom(detail, prefix, currencyProp)
    VATSum###detail 'Сумма НДС' (detail) = roundCurrency((sum###detail(detail) * valueVAT###detail (detail) / 100), currencyProp###detail(detail));
    prefix###sum###detail 'Сумма с НДС' (detail) = sum###detail(detail) (+) VATSum###detail(detail);
END
META defineDocumentInterfaceDetailVATSumCustom(detail, prefix, currencyProp)
    @defineDocumentDetailVATSumCustom(detail, prefix, currencyProp);
    @defineDocumentDetailVATSumCustom(user###detail, prefix, currencyProp);
END

META defineDocumentDetailVATSum(object, currencyProp)
    @defineDocumentDetailVATSumCustom(object##Detail, object, currencyProp);
END
META defineDocumentInterfaceDetailVATSum(object, currencyProp)
    @defineDocumentInterfaceDetailVATSumCustom(object##Detail, object, currencyProp);
END

META defineDocumentDetailVATSum(object)
    @defineDocumentDetailVATSum(object, currency);
END
META defineDocumentInterfaceDetailVATSum(object)
    @defineDocumentInterfaceDetailVATSum(object, currency);
END

META defineDocumentDetailVATPrefixCustom(detail, prefix, countryProp, caption)
    prefix##VAT###detail(detail) = DATA range (detail);
    number###prefix##VAT###detail 'НДС, номер'###caption (detail) = numberRange(prefix##VAT###detail(detail));
    value###prefix##VAT###detail 'НДС, %'###caption (detail) = valueRateRangeDate(prefix##VAT###detail(detail), date###detail(detail));

    CONSTRAINT taxRange(prefix##VAT###detail(detail)) != tax.taxVAT OR
               countryRange(prefix##VAT###detail(detail)) != countryProp###detail(detail)
               CHECKED BY prefix##VAT###detail
               MESSAGE 'ошибка: Шкала и страна строки должна соответствовать шкале и строке НДС';
END
META defineDocumentAbstractDetailVATPrefixCustom(detail, prefix, countryProp, caption)
    prefix##VAT###detail(detail) = ABSTRACT range (detail) PERSISTENT;
    number###prefix##VAT###detail 'НДС, номер'###caption (detail) = numberRange(prefix##VAT###detail(detail));
    value###prefix##VAT###detail 'НДС, %'###caption (detail) = valueRateRangeDate(prefix##VAT###detail(detail), date###detail(detail));
END
META defineDocumentInterfaceDetailVATPrefixCustom(detail, prefix, countryProp, caption)
    @defineDocumentAbstractDetailVATPrefixCustom(detail, prefix, countryProp, caption);
    @defineDocumentDetailVATPrefixCustom(user###detail, prefix, countryProp, caption);
    prefix##VAT###detail(detail) += prefix##VAT###user###detail(detail);
END

META defineDocumentDetailVATPrefix(object, prefix, countryProp, caption)
    @defineDocumentDetailVATPrefixCustom(object##Detail, prefix, countryProp, caption);
END
META defineDocumentAbstractDetailVATPrefix(object, prefix, countryProp, caption)
    @defineDocumentAbstractDetailVATPrefixCustom(object##Detail, prefix, countryProp, caption);
END
META defineDocumentInterfaceDetailVATPrefix(object, prefix, countryProp, caption)
    @defineDocumentInterfaceDetailVATPrefixCustom(object##Detail, prefix, countryProp, caption);
END

//-- Сумма НДС в detail
META defineDocumentDetailVATDataSumCustom (detail, prefix)
    VATSum###detail 'Сумма НДС' (detail) = DATA NUMERIC[16,2] (detail);
    prefix###sum###detail 'Сумма с НДС' (detail) = sum###detail(detail) (+) VATSum###detail(detail);
END
META defineDocumentAbstractDetailVATDataSumCustom (detail, prefix)
    VATSum###detail 'Сумма НДС' (detail) = ABSTRACT NUMERIC[16,2] (detail);
    prefix###sum###detail 'Сумма с НДС' (detail) = sum###detail(detail) (+) VATSum###detail(detail);
END

META defineDocumentInterfaceDetailVATDataSumCustom (detail, prefix)
    @defineDocumentAbstractDetailVATDataSumCustom(detail, prefix);
    @defineDocumentDetailVATDataSumCustom(user###detail, prefix);
    VATSum###detail (detail) += VATSum###user###detail(detail);
END
META defineDocumentInterfaceDetailVATDataSum (object)
    @defineDocumentInterfaceDetailVATDataSumCustom(object##Detail, object);
END

META deriveDocumentDetailVATSumCustom(detail, currencyProp)
    VATSum###detail (detail)  <- roundCurrency((sum###detail(detail) * valueVAT###detail (detail) / 100), currencyProp###detail(detail))
                    WHEN CHANGED(sum###detail(detail)) OR CHANGED (valueVAT###detail (detail));
END
META deriveDocumentDetailVATSum(object, currencyProp)
    @deriveDocumentDetailVATSumCustom(object##Detail, currencyProp);
END
META deriveDocumentDetailVATSum(object)
    @deriveDocumentDetailVATSum(object, currency);
END

//-- Сумма НДС в документе
META defineDocumentHeaderVATSumPrefix (object, detail, prefix)
    VATSum###detail###object 'Сумма НДС' (object) = GROUP SUM VATSum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;
    prefix###sum###detail###object 'Сумма с НДС' (object) = GROUP SUM prefix###sum###detail(idetail) BY object###detail(idetail) IN documentSumGroup;
END
META defineDocumentInterfaceHeaderVATSumPrefix (object, detail, prefix)
    @defineDocumentHeaderVATSumPrefix(object, detail, prefix);
    @defineDocumentHeaderVATSumPrefix(user###object, user###detail, prefix);
END
META defineDocumentInterfaceHeaderVATSum(object)
    @defineDocumentInterfaceHeaderVATSumPrefix(object, object##Detail, object);
END

// ------------------------------------------------------- Стандартные данные ----------------------------------- //

loadDefaultVAT 'Добавить значение НДС' = ACTION (idCountry, number, value) {
    ADDOBJ range;
    FOR rn == addedObject() DO {
       SET rangeTypeRange(rn) <- rangeType.common;
       SET taxRange(rn) <- tax.taxVAT;
       SET countryRange(rn) <- countrySID(idCountry);
       SET numberRange(rn) <- number AS INTEGER;
       ADDOBJ rate;
       FOR ra == addedObject() DO {
          SET rangeRate(ra) <- rn AS range;
          SET valueRate(ra) <- value AS NUMERIC[10,5];
          SET dateFromRate(ra) <- 2001_01_01;
       };
    };
};

loadDefaultVATs 'Загрузить стандартные НДС' = ACTION () {
    EXEC loadDefaultVAT(112, 1, 0);
    EXEC loadDefaultVAT(112, 2, 9.09);
    EXEC loadDefaultVAT(112, 3, 16.67);
    EXEC loadDefaultVAT(112, 4, 10);
    EXEC loadDefaultVAT(112, 5, 20);
    EXEC loadDefaultVAT(112, 6, 24);

    EXEC loadDefaultVAT(804, 1, 0);
    EXEC loadDefaultVAT(804, 2, 20);
} IN loadDefaultGroup;

@implementLoadDefaultData(loadDefaultVATs);
